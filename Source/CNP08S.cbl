000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.  CNP08S.
000030*****************************************************************
000040*         ******* ASSIGNED JOBS SHIFT TIE-UP/CALL *****
000050*****************************************************************
000060*  DATE   INITIAL  LOG#    DESCRIPTION
000070*-------- ------- ------  ---------------------------------------
000080*02/07/91   GLM           CICS CONVERSION.
000090*12/20/93   LMB           UNAVAILABLE STATUS MODIFICATIONS.
000100*04/20/94   GER           TIMEZONE UPGRADES
000110*06/22/95   PLS           FOR FASTSLOW SPAREBOARDS, USE THE
000120*                         BOARD SIDE RETURNED FROM DECISION
000130*                         TABLE PROCESSING.
000140*07/11/95   FHK           GO TRAIN CHANGES.
000150*07/25/95   FHK           CHANGE SVC-FI AND SPCL-QUAL-FI TO SE
000160*09/20/95   MMM           NEW RESTRICTION CONTROL PROCESSING
000170*                         ADDED.  CNC0009.
000180*12/18/95   TJP           BUG C251 SHIFT REPORTED NOT BEING
000190*                         GENERATED WHEN SHIFT CALLED.
000200*12/18/95   PLS  CNC0108  NEW REJECT FUNCTION - RJNW.
000210*12/22/95   PLS  CNC0110  DISPLAY MTOD PREV DUTY.
000220*01/08/96   PLS  CNC0106  ADD CLERICAL CRAFT CODE PROCESSING
000230*03/19/96   FLW           BUG C268 - CHANGED "OR" TO "AND"
000240*03/19/96   SRP  BUG C272 BUG C272 -- CORRECT CALL TO '947.
000250*04/01/96   TGJ           CURSOR CORRECTION IN P5000.
000260*05/01/96   FLW  BUG C268 ELIMINATE VACANCIES NOT YET EFFECTIVE *
000270*05/08/96   PLS  BUG C192 USE AJ-SCH-DAY/TIME TO FURTHER CHECK
000280*                         IF JOB WAS CALLED TODAY.
000290*05/14/96   FLW  CNC0006  DEFINITION OF WORK WEEK
000300*06/27/96   FLW  BUG C309 REMOVE SHIFT CALL WHEN DISPLAYING     *
000310*                         VACANCIES                             *
000320*07/16/96   PLS    A-173  REQUIRE ECC CODE WHEN LAYING OFF ON   *
000330*                         CALL FOR COMPANY BUSINESS.
000340*09/04/96   RCW  CNC0006  ADD CNTL-VERIFY-STARTS CHECK BEFORE
000350*                         DISQUALIFYING AN EMPLOYEE FOR # STARTS.
000360*09/25/96   RCW           NORTHERN QUEBEC SPAREBOARD - PHASE 1
000370*10/10/96   FLW  CNCXXXX  NORTHERN QUEBEC SPAREBOARD - PHASE 2
000380*01/10/97   PLS  YEAR2000 ADJUST COMPARISON OF QUAL-DATE.
000390*04/03/97   JES  CNC0113  CHANGED RUN MILES SOURCE TO TABLE 525
000400*08/07/97   RDH           YR 2000 DATE LOGIC UPDATE TO
000410*                         WK-JSK1-EXP-DATE.
000420*03/10/98   LMB  CNC0134  QUICK TIE; ADD PHONE FUNCTION.
000430*04/06/98   GBJ  Y2K      REMOVE WRITING TO WSHIST WITH A CALL
000440*                         TO P943.
000450*04/09/98   AMF           ADDED HELP LOGIC
000460*05/22/98   AMF           FIXED '5 START WARNING' BUG.
000470*06/03/98   LMB           CHECK FOR DECISION SERVICE CODE OVERRIDE
000480*07/21/98   LMB  C-425    PASS CRAFT CODE TO P922.
000490*09/18/98   WLC  C-371    FIX FILL PROCESS
000500*12/03/98   AMF           YEAR 2000 SWEEP.
000510*04/02/99   LMB  CNC0208  INITIATE TRACKING FOR REGULAR EMPLOYEE.
000520*                         TRACK FOR RJH.
000530*05/20/99   NJB           ASGN SEN
000540*12/15/99   GER  CNC0259  ADD IC CALL FUNCTIONS
000550*01/15/99   MOO  CNC0183  ADD RJHS FUNCTIONS.
000560*03/25/00   MOO  CNC0183  ADD FROZEN LOGIC CHANGES.
000570*04/24/00   MOO  CNC0183  DISPLAY FROZEN IN YELLOW.
000580*04/28/00   GER  CNC0283  DECISION DH-LEAD TIMES.
000590*11/21/00   PLS           ALLOW RJNR, BEFORE FILL, WHEN EMPLOYEE
000600*                         IS NOT RESTED.
000610*11/28/00   PLS  BUG      ALLOW SPREAD TIME TO BE UP TO 24 HOURS
000620*                         IN THE FUTURE.
000630*08/10/01   AJK  C-461    FIX TRACKING BUG FOR EMPLOYEES MISS
000640*                         CALLED ON RELIEF JOBS.
000650*10/08/01   AJK  CNC0331  MANDATORY REST/HOURS OF SERVICE ENH.
000660*07/23/02   AJK  C525     FIX CALL BUG FOR ASSIGNED JOBS WHOSE
000670*                         RELIEF XB ASSIGNMENT IS VACANT.
000680*02/27/03   SAM  C537     ADDED READ TO MISSED CALL TABLE
000690*03/03/03   SAM  C539     FIXED ERROR WHEN DOING FILL ON RELIEF
000700*                         JOBS, WAS NOT RECOGNIZING ON DUTY JOBS.
000710*03/13/03   JAJ CNC0331B  MTOD/HOS ADDENDUM B.
000720*07/29/03   AJK  C541     GO-TRAIN BUG FIX - HOLD EMPLOYEE ON
000730*                         RELIF ASSIGNMENT UNTIL 2ND TOUR, NOT
000740*                         ON REGULAR ASSIGNMENT (WHEN APPLICABLE).
000750*08/07/03   ERW  C561     DEADHEAD TIME ON REJECT
000760*09/25/03   GER  C513     TRACKING BUG, ON RELIEF JOBS.
000770*01/02/04   AJK  C564     RELIEF JOB TYPE BUG FIX.
000780*01/27/04   AJK           DELETE ALL TRACKED EMPLOYEES WHEN JOB
000790*                         IS ANNULLED.
000800*02/03/04   AJK  C570     MTOD TIMEZONE BUG.
000810*02/24/04   AJK  C574     POPULATE P921-ASGN-PARM WITH SCHEDULED
000820*                         XB ASSIGNMENT, INSTEAD OF THE ASSIGNMENT
000830*                         IT'S RELIEVING, WHEN APPLICABLE.
000840*03/25/04   AJK  CNC0331G MANDATORY REST/HOS ENH. - ADD CALLBACK
000850*                         LOGIC.
000860*08/27/04   TRK  C584     OWNER OF A GO TRAIN IS NOT ENTITLED TO
000870*                         WORK SECOND SHIFT IF HE DID NOT WORK
000880*                         THE FIRST SHIFT
000890*09/27/04   TRK  CNC0387  COMMUTER HOS RULES
000900*10/13/04   KJS  CNC0386A ADD PERSONAL REST TIME/DAY TO REST
000910*                         FUNCTION DISPLAY INFO
000920*04/21/05   AJK  CNC0386E RECOMPILE FOR COPYBOOK CHANGES.
000930*05/06/01   AJK           CALCULATE HOS-APPLICABLE ON-DUTY TIME
000940*                         BEFORE DETERMINING REST STATUS.
000950*                         POPULATE P922-JOB-TYPE FOR YARD ASGNS.
000960*07/21/05   AJK           DO NOT FACTOR IN LEAD TIME FOR REGULAR
000970*                         OWNERS OF GO-TRAINS (COMMUTERS)
000980*10/05/05   AJK  C634     CONSIDER THE 'HELP' FUNCTION AS A
000990*                         VACANCY FILL FUNCTION FOR HOS CALC
001000*                         PURPOSES (SET P925-VACANCY-FILL TO TRUE)
001010*01/30/06   BLD  C630     WHEN CHECKING FOR 2ND SHIFT ON GO TRAIN,
001020*                         CHECK TRAIN ID.
001030*02/09/06   BLD  C632     CHECKING FOR 2ND SHIFT BEFORE SETTING
001040*                         P925-ROUTE-DESGN.
001050*02/17/06   AJK  C648     GO TRAIN LEAD TIME: CONSIDER AN EMPLOYEE
001060*                         AS WORKING THEIR OWN ASSIGNMENT, FOR
001070*                         LEAD TIME PROCESSING PURPOSES, AS LONG
001080*                         AS HE'S BEING CALLED ON ANY CRAFT WITHIN
001090*                         HIS ASSIGNMENT (I.E. EMP X OWNS THE AC
001100*                         POSITION ON THE OGOC01, BUT IS BEING
001110*                         CALLED ON THE CO POSITION FOR THAT SAME
001120*                         JOB).
001130*05/15/07   AJK  CNC0413  RECALCULATE THE REST STATUS FOR ANY
001140*                         EMPLOYEES WHOSE TRACKING WAS INITIATED
001150*                         VIA A DEADHEAD CALL.  STAMP THIS STATUS
001160*                         IN THE TRACKING RECORD FOR USE IN THE
001170*                         'VIRTUAL HOS' PROCESSING DOWNSTREAM.
001180*05/30/07   RAL  CNC0331E IMPOSED REST UPON TIEUP CHANGES
001190*06/19/07   RAL  CNC0331E IGNORE EMP-MTOD RESTRICTIONS FOR
001200*                         YARDMASTERS WORKING YARDMASTER JOBS.
001210*08/29/07   RAL  CNC0331E IMPOSED REST STARTS AT 6401, NOT 6400
001220*09/06/07   AJK  CNC0331E IF THE EMPLOYEE IS NOT 'GREEN' AND IS
001230*                         INVOLVED IN A CALLBACK, DISPLAY 'CBCK'
001240*                         IN THE FUNCTION COLUMN FIRST, THEN
001250*                         SHOW 'HOUR' ONCE THE CALLBACK TASK HAS
001260*                         BEEN WRITTEN.
001270*09/14/07   KAD  C731     IN P3020-...
001280*                         IF A POS WAS VACANT, AND FILL EMP WAS
001290*                         CALLED BEFORE ORDER DT/TM, THEN SCREEN
001300*                         STILL SHOWS VACANCY. PROBLEM WAS AHMISC
001310*                         READ LOOKED FOR ORDER DT/TM & ON-DUTY
001320*                         DT/TM.  INSTEAD, START BROWSE ONLY FOR
001330*                         ORDER DT/TM  AND NON 'VACT' FUNCTION.
001340*                         THIS WILL CONFIRM POS DID WORK ALREADY.
001350*11/02/07   AJK  C731     FOLLOW-UP FIX: AN ASSIGNMENT WILL NOT
001360*                         BE CONSIDERED A VACANCY IF AN AHM
001370*                         RECORD IS FOUND FOR IT, REGARDLESS IF
001380*                         IT WAS VACATED OR CALLED. (P3020)
001390*02/26/08   SXO  CNC0457  ACCESS THE HOST MESSAGE ROUTING CONTROL
001400*                         BY NEW KEY FORMAT.
001410*03/14/08   AJK  C774     IF THE CURRENT TV OWNER OF AN ASSIGNMENT
001420*                         IS ALSO TV'D ONTO A SUBSEQUENT XB ASGN,
001430*                         DON'T PRESENT THEM AS THE REGULAR OWNER
001440*                         OF THE ASSIGNMENT.  PRESENT IT AS A
001450*                         VACANCY
001460*03/27/08   AJK  C777     P5240 - WHEN ESTABLISHING THE SCHEDULE
001470*                         WINDOW, USE THE CALL DATE/TIME AS THE
001480*                         REFERENCE POINT, NOT LOCAL DATE TIME.
001490*05/15/08   AJK  C783     AFTER READING MASTER RECORD IN P8505,
001500*                         WE NEED TO CONVERT THE EMPLOYEE'S MTOD,
001510*                         US REST, AND PERS REST TO THE PROFILE'S
001520*                         TIMEZONE.
001530*12/23/08   JES  CNC0436B REMOVE PAGER-NO
001540*02/03/09   JES  CNC0436B REPLACE MSTR3-LEAD-TIME WITH TELE-REQ-
001550*                         LEAD-TIME VIA LINK TO CNPS52
001560*05/04/09   RZP  CNC0484  CALL PS94 TO GET THE TOTAL HOS AND LIMBO
001570*                         TIME
001580*05/22/09   AJK  CNC0484  ADDED PS94-TIME-ZONE LOGIC
001590*06/12/09   JES  CNC0436B ADDED 6 NEW WS-FORMAT-FUNCS
001600*06/26/09   SZV  CNC0484  FIX TO CHECK ALL THE EMPS HOS DETAILS.
001610*                         IF EXCEEDED FLAG EMPS WITH YELLOW COLOR
001620*                         AND LAY OFF THE EMPLOYEE IF EMPLOYEE HAS
001630*                         EXCEEDED THE THRESHOLDS DURING TIE UP
001640*07/07/09   BLD  CNC0436B ADDED IVR FUNCTIONS
001650*07/11/09   AJK  CNC0484  HR 2095 - PASS US REST STATUS TO CALLING
001660*                         SUBROUTINES.
001670*07/15/09   JES  CNC0436B SHORT CALL DISPLAY.
001680*07/27/09   RXS  CNC0484  PHASE II CHANGES.
001690*08/28/09  AJK   C389     DON'T REFERENCE TRAIN-CRAFT IN REST
001700*                         REQUIREMENT CHECK (FOR US HOS PROCESS)
001710*                         INSTEAD, CHECK FOR 'NOT ENGINE-CRAFT'.
001720*                         THIS WILL PICK UP YARDMASTERS AND
001730*                         OTHER CRAFTS THAT ARE NOT PART OF
001740*                         THE ENGINE-CRAFT / TRAIN-CRAFT GROUPS.
001750*09/08/09   JES  CNC0436E USE PAGER-NO IN MSTR FOR DISPLAY
001760*09/21/09   JES  CNC0436B #779 ADD SHORT-CALL-FLAG PASS TO 0ES.
001770*09/24/09   JES  CNC0436B COMMENT PS52-DISPLAY-ONLY
001780*09/29/09   JES  CNC0436B #779 CHANGE FLAG TO SHORT-CALL-REQ.
001790*09/30/09   RXS  CNC0484  INCLUDE SHORT LEAD FUNCTIONALITY FOR US
001800*                         NEW FRA REPORTING. CHANGES IN P3038A-DET
001810*10/07/09   BLD  CNC0436B #801 WHEN EMPLOYEE HAS BOTH "QTIE" AND
001820*                         "CBCK" DISPLAY "QTIE IN THE FUNCTION.
001830*                         ALSO, INITIALIZE THE FUNCTION AFTER A
001840*                         "SENT"/"SENR" IS PROCESSED.
001850*10/08/09   JES  CNC0436B #846 IVR-CALL ALLOWED WITH NOT-NOTIFIED
001860*                         (LAYOFF-CODE-2 = '1')
001870*10/16/09   CXJ  CNC0484  POPULATE HST KEY WITH THE CURRENT ASGN.
001880*10/19/09   JES  CNC0436B #892 ADDED P08SCA-RESUB-FLAG AND SENT TO
001890*                         CNP0ES THRU P0ESCOMM.
001900*10/30/09   JES  CNC0436B FIXED VRUINFO UPDATE WHEN SUBSEQUENT EMP
001910*                         GET VCLL'D. ALSO FIXED TASK TO UPDATE,
001920*                         NOT CREATE, WHEN TWO VACANCIES ON SAME
001930*                         ASSIGNMENT.
001940*11/03/09   JES  #957     CHANGE CALLBACK MSG WHEN BOTH QTIE AND
001950*                         CBCK EXIST.
001960*11/04/09   RXS  C841     POPULATE THE MASTER DIST/SUB DISTRICT
001970*                         IF EMP IS TO-PLACE FOR HST KEY
001980*11/09/09   JES  CNC0436B COMMENT EDIT FOR DUP EMPS ON SCREEN -
001990*                         ASSURE VRUINFO WILL NOT RECORD DUPS.
002000*11/23/09   JES  CNC0436B STORE VRUINFO-KEY IN AHM3 FOR DELETE AT
002010*                         TIEUP/CANCEL TIME.
002020*01/05/10   JES  C846     CHANGE READ OF TASK-VIA-STATION TO
002030*                         BROWSE UNTIL SURE OF RECORD FOUND.
002040*02/22/10   JES  PRODFIX  VRUINFO UPDATE MAY BE NECESSARY WHEN
002050*                         DOING SCAL, ALSO CHECK FOR CREW COMPLE-
002060*                         TION TO UPDATE/DELETE THE TASK RECORD.
002070*02/26/10   JES  PRODFIX  NEED TO THINK IN QUAD SCREEN TERMS -
002080*                         WHEN THEY DON'T REFRESH 08S AFTER DOING
002090*                         ORDER VX CALLS FROM ANOTHER SCREEN, WE
002100*                         NEED TO REEVALUATE EACH 08S FUNCTION
002110*                         TO SEE IF IT HAS BEEN AFFECTED VIA 08
002120*                         ON ANOTHER SCREEN. (SEE P6167)
002130*03/11/10   JES           CHANGE INITIAL CONSTRUCT OF VRUINFO REC
002140*04/27/10   JES  #1277    COUNT NON-REQUIRED CRAFTS WHEN IN IVR-
002150*                         UNSUCCESSFUL STATUS.
002160*05/03/10   JES  #1290    TIE-UP TIME - NEED TO CHECK THAT EMPS
002170*                         ARE NOT IN B8 STATUS.
002180*05/05/10   RXS  CNC0496  US HOS ENHANCEMENT
002190*06/01/10   JES  CNC0436M USE OF VCLL WILL NOT BE ALLOWED WHEN
002200*                         EMPLOYEE UNDER UDR/MTOD.
002210*09/29/10   BLD  #1368    DISPLAY MSG WHEN IVR MASTER SWITCH OFF
002220*11/02/10   JES  #1474    IF CALLBACK (CBCK), UPDATE VRUINFO WITH
002230*                         VRU-SHORT-CALLED.
002240*11/11/10   JES  #1489    IF HOLIDAY, AHM-OD-FUNCTION = '?AO*',
002250*                         TEST FOR THIS DURING VRUINFO UPDATE.
002260*11/17/10   JES  #1490    IF CALL TIME IS CHANGED WITH VCLL,
002270*                         FIX THE TIME WHEN POPULATING VRIUNFO
002280*                         WITH THE AHIST KEY.
002290*11/30/10   JES  #1494    SET FLAG WITHIN TASK RECORD WHEN TASK
002300*                         CREATED DUE TO VCLL.
002310*02/04/11   JES  #1529    EDIT VCLL WITH SHORT CALL AND NOT-RESTED
002320*                         INCLUDE PERSONAL REST.
002330*05/12/11   JES  C907     RESET PERSONAL REST TO YES WHEN REJECT.
002340*08/01/11   BXS  C919     FIX ABEND AT P3020TZ
002350*11/02/12   KXD  CNC0531  INCLUDED YARDMASTER CRAFT CODES TO
002360*                         CONVERT TO THE MASTER CRAFT CODE.
002370*04/09/13   AXK  CNC0520  ADDED TRANSFER TO NEW SCREEN FOR QUAL
002380*                         FUNCTION
002390*06/18/13   AXK  C986     PASSED THE VALUE OF ROUTE DESIGNATION
002400*                         INTO P921-ROUTE-DESIGNATION
002410*07/15/14   KXB  CNC0555  DO NOT INITIATE THE REPORT GENERATOR
002420*                         CNP906 WHEN THE PRINTER STATUS ON THE
002430*                         HOST MESSAGE ROUTING CONTROL IS SET TO
002440*                         "ACTIVE" AND THE PRINTER ID IS INVALID.
002450*10/07/14   BXS  C1056    SHOW P081 MESSAGE WHEN PRINTER ID IS
002460*                         INVALID (EX. XXXXX).
002470*06/01/15   BXS  CNC0562  CHECK AHIST FOR OTHER SERVICE TYPE.
002480*09/29/15   MFO  CNC0572  ADDITIONAL SPCL QUALIFICATIONS
002490*08/11/16   RXB  CNC0578  ADD LOGIC TO WRITE "NO" TYPE WORK HIST.
002500*08/31/16   MFO  CNC0578-01  INCLUDE DECISION IN 'NO' RECORD
002510*10/04/16   MFO  C1143    ABEND WHEN 'NO' REDUCED CREW
002520*03/16/17   MFO  CNC0579B PASS REPORT STATION ON DECISION TABLE
002530*10/09/17   AXK  C1174    CORRECTED THE EMPLOYEE TRACKING ISSUE.
002540*01/29/18   MFO  C1184    F11 COMPLETING SHIFT CALLS BEFORE ALL
002550*                         VACANCIES FILLED.
002560*04/18/18   MFO  C1193    REMOVE 64/7 EDIT SETTING YELLOW STATUS
002570*04/23/19   MFO  COURTESY INCREASE TSQ MAX FROM 22 TO 55 TO ALLOW
002580*                         SCROLL UP TO 5 SCREENS.
002590*02/27/23   SXP  CNC0598  WRR PH-1: NO NEED OF MTOY AND MTOR
002600*03/28/23   SXP  CNC0600  WRR PH-2: WITH NEW WRR, RESTRICT RJHS
002610*04/28/23   RKW  CNC0600  COLORS NOT SHIFTING ON SENT AND SENR
002620*05/02/23   RZS  CNC0600  CNLD-226 'RED' IN MTOD, AND FIX ABEND
002630*05/04/23   RKW  CNC0600  CNLD-226B SHORTEN REST UNTIL MESSAGE
002640*26/04/23   RRS  CNLD-220 CHANGE EMP DISPLAY TO WHITE
002650*05/05/23   DXC  CNLD-207 PASSON LEADTIME
002660*05/12/23   RKW  CNC0600  CNLD-226 SHOW EMP NOT VACANCY WHEN RED.
002670*05/12/23   SXP  CNLD-230 DISPLAY DIFFERENT ERROR MSG FOR 7DAY
002680*                         EDITS IN P925
      *06/12/23   RJA  CNLD-246 YARDMASTER TRAINEES ON YARDMASTER JOBS,
      *                         LIKE YARDMASTERS, ARE HOS EXEMPT.
      *06/20/23   DXC  CNLD-257 7/28 DAYS MESSAGE CHANGE
      *06/29/23   RJA  CNLD-259 E95 DUEBACK IS END OF MSTR3-SYSTEM-UD2,
      *                         BUT EMP CANNOT BE PUT ON-DUTY UNTIL
      *                         AFTER MSTR3-SYSTEM-RESET-BRK.
      *07/17/23   SXP  CNLD-265 EDIT FOR ORDER-LEAD > PERS REST
      *07/17/23   SXP  CNLD-245 DISPLAY UNDISTURBED REST MSG FOR USHR
      *                         > LOCAL TIME CONDITION
      *07/26/23   RJA  CNLD-269 USING DECISION RULE FLAGS TO IDENTIFY
      *                         PROMOTED(BOOSTED) EMPLOYEE
      *11/01/23   RJA  CNLD-285 INITIALIZE WS-EMP-UNDST-REST-FLAG
      *               /CNLD-286            WS-REST-US-STATUS-FLAG
      *                                    WS-SAVE-UD-DTTM
      *07/11/23   RJA  CNLD-330 REMOVED WS-REST-US-STATUS-FLAG
      *07/22/24   RJA  CNLD-302 IMPOSED REST IS OBSOLETE PER NEW WRR.
      *                         REMOVING RELATED CODE.
009000*08/16/24   RJA  CNLD-309 MTOY AND MTOR ARE OBSOLETE.
002690*****************************************************************
002700 ENVIRONMENT DIVISION.
002710 CONFIGURATION SECTION.
002720 SOURCE-COMPUTER.  IBM-9370.
002730 OBJECT-COMPUTER.  IBM-9370.
002740 DATA DIVISION.
002750 WORKING-STORAGE SECTION.
002760 01  FILLER                          PIC X(009) VALUE 'PGM08S WS'.
002770
002780 01  WS-FLAGS.
002790     05  SCREEN-FLAG                 PIC X(001) VALUE '0'.
002800         88  CONTINUE-SCREEN                    VALUE '0'.
002810         88  CREATE-SCREEN                      VALUE '1'.
002820         88  SEND-BUFFER                        VALUE '2'.
002830     05  DONE-CODE                   PIC 9(001) VALUE ZEROS.
002840         88  DONE                               VALUE 1.
002850     05  ASGN-DONE-CODE              PIC 9(001) VALUE ZEROS.
002860         88  ASGN-DONE                          VALUE 1.
002870     05  DAILY-MARKUP-YARD-FLAG      PIC X(001) VALUE '0'.
002880         88  THIS-IS-A-DAILY-MARKUP-YARD        VALUE '1'.
002890     05  REST-OK-FLAG                PIC X(001) VALUE '0'.
002900         88  REST-OK                            VALUE '0'.
002910         88  REST-NOT-OK                        VALUE '1'.
002920     05  ERRORS-FOUND-CODE           PIC X(001) VALUE '0'.
002930         88  ERRORS-FOUND                       VALUE '1'.
002940     05  SCHED-DONE-CODE             PIC 9(001) VALUE ZEROS.
002950         88  SCHED-DONE                         VALUE 1.
002960     05  SCHED-DONE-2-CODE           PIC 9(001) VALUE ZEROS.
002970         88  SCHED-DONE-2                       VALUE 1.
002980     05  SCHED-2ND-CODE              PIC 9(001) VALUE ZEROS.
002990         88  SCHED-2ND                          VALUE 1.
003000     05  SCHED-1ST-CODE              PIC 9(001) VALUE ZEROS.
003010         88  ON-SCHED-1ST                       VALUE ZEROS.
003020         88  NOT-ON-SCHED-1ST                   VALUE 1.
003030     05  SCHED-LST-CODE              PIC 9(001) VALUE ZEROS.
003040         88  SCHED-LST                          VALUE 1.
003050     05  ONE-TOUR-FLAG               PIC X(001) VALUE '1'.
003060         88  ONE-TOUR                           VALUE '1'.
003070         88  TWO-TOURS                          VALUE '2'.
003080     05  SCHED-FOUND-CODE            PIC 9(001) VALUE ZEROS.
003090         88  SCHED-FOUND                        VALUE 1.
003100     05  WS-EMP-DISQUALIFIED         PIC X(001) VALUE SPACES.
003110         88  EMP-DISQUALIFIED                   VALUE 'Y'.
003120     05  WS-QTIE-DISQUALIFIED        PIC X(001) VALUE SPACES.
003130         88  QTIE-DISQUALIFIED                  VALUE 'Y'.
003140     05  WS-ALLOW-QUICK-TIE-FLAG     PIC X(001) VALUE SPACES.
003150         88  ALLOW-QUICK-TIEUP                  VALUES 'Y' 'B'.
003160     05  WS-SVC-OVERRIDE-FLAG        PIC X(001) VALUE 'N'.
003170         88  USE-SVC-OVERRIDE                   VALUE 'Y'.
003180         88  DONT-USE-SVC-OVERRIDE              VALUE 'N'.
003190     05  WS-TRK-REASON               PIC X(002).
003200         88  WS-TRK-RJH                         VALUE '05'.
003210         88  WS-TRK-ON-DUTY                     VALUE '20'.
003220         88  WS-TRK-NOT-NOTIFIED                VALUE '25'.
003230         88  WS-TRK-REQUIRED-REST               VALUE '30'.
003240         88  WS-TRK-UNAVAIL                     VALUE '35'.
003250         88  WS-TRK-PERS-REST                   VALUE '40'.
003260         88  WS-TRK-NOT-QUALIFIED               VALUE '45'.
003270         88  WS-TRK-QUICK-TIE                   VALUE '50'.
003280         88  WS-TRK-STARTS                      VALUE '55'.
003290         88  WS-TRK-DONT-TRACK                  VALUE '88'.
003300         88  WS-TRK-UNKNOWN                     VALUE '99'.
003310     05  WS-ASGN-TYPE                PIC X(001) VALUE '0'.
003320         88  UFP-ASGN                           VALUE '1'.
003330         88  AJ-ASGN                            VALUE '2'.
003340     05  FIND-MC-CODE-FLAG           PIC X(001) VALUE 'Y'.
003350         88  FIND-MC-CODE                       VALUE 'Y'.
003360         88  DONT-FIND-MC-CODE                  VALUE 'N'.
003370     05  RELIEF-ONDUTY-FLAG          PIC X(001) VALUE ' '.
003380         88  RELIEF-ONDUTY                      VALUE '1'.
003390     05  WS-TIME-FLAG                PIC  X(001) VALUE '0'.
003400         88  TIME-NOT-DONE                       VALUE '0'.
003410         88  TIME-DONE                           VALUE '1'.
003420     05  WS-VACANCY-FILL-FLAG        PIC  X(001) VALUE '0'.
003430         88  NOT-VACANCY-FILLED                  VALUE '0'.
003440         88  VACANCY-FILLED                      VALUE '1'.
003440*07/26/23 RJA B CNC0600 CNLD-269
003420     05  WS-PROMOTING-EMP-FLAG       PIC  X(001) VALUE '0'.
003430         88  NOT-PROMOTING-EMP                   VALUE '0'.
003440         88  PROMOTING-EMP                       VALUE '1'.
003440*07/26/23 RJA E
003450     05  CONTINUE-VACANCY-CHECK-FL   PIC  X(001) VALUE '0'.
003460         88  CONTINUE-VACANCY-CHECK              VALUE '1'.
003470     05  WS-OWNER-NOT-ELGIBLE-FLAG   PIC  X(001) VALUE '0'.
003480         88  NOT-OWNER-ELIGIBLE                  VALUE '0'.
003490         88  OWNER-ELIGIBLE                      VALUE '1'.
003500     05  WS-AHMISC-CODE              PIC  X(001) VALUE '0'.
003510         88  AHMISC-NOT-DONE                     VALUE '0'.
003520         88  AHMISC-DONE                         VALUE '1'.
003530     05  WS-AHMISC-FOUND-FLAG        PIC  X(001) VALUE '0'.
003540         88  AHMISC-NOT-FOUND                    VALUE '0'.
003550         88  AHMISC-FOUND                        VALUE '1'.
003560     05  WS-LEAD-TIME-FLAG           PIC  X(001) VALUE 'Y'.
003570         88  WS-INCLUDE-LEAD-TIME                VALUE 'Y'.
003580         88  WS-IGNORE-LEAD-TIME                 VALUE 'N'.
003590     05  WS-ETRK-DONE-FLAG           PIC  X(001) VALUE 'N'.
003600         88  ETRK-NOT-DONE                       VALUE 'N'.
003610         88  ETRK-DONE                           VALUE 'Y'.
003620     05  CALC-TRK-HOS-FLAG           PIC  X(001) VALUE 'N'.
003630         88  CALC-TRK-HOS-NO                     VALUE 'N'.
003640         88  CALC-TRK-HOS                        VALUE 'Y'.
003650     05  HOS-EXEMPT-FLAG             PIC  X(001) VALUE SPACE.
003660         88  NOT-HOS-EXEMPT                      VALUE SPACE.
003670         88  HOS-EXEMPT                          VALUE 'Y'.
003680     05  WS-US-DOM-FLAG              PIC X(001) VALUE SPACES.
003690         88  WS-US-DOM-YES                      VALUE 'Y'.
003700     05  WS-HS-US-ACTIVATE-FL        PIC X(001) VALUE SPACES.
003710         88 WS-HS-US-ACTIVATE                   VALUE 'Y'.
003720     05  WS-EMP-HOS-FL               PIC X(001) VALUE SPACES.
003730         88 EMP-HOS-EXD                         VALUE 'Y'.
003740     05  EMP-IMPOSED-REST-US-FL      PIC X(001) VALUE SPACES.
003750         88 EMP-IMPOSED-REST-US-YES             VALUE 'Y'.
003760     05  WS-CBCK-FL                  PIC X(001) VALUE SPACES.
003770         88 WS-CBCK                             VALUE 'Y'.
003780     05  TASK-DONE-CODE              PIC X(001) VALUE 'N'.
003790         88  TASK-NOT-DONE                      VALUE 'N'.
003800         88  TASK-DONE                          VALUE 'Y'.
003810     05  WS-TASK-FOUND-FLAG          PIC X(001) VALUE SPACES.
003820         88  WS-TASK-FOUND                      VALUE 'Y'.
003830     05  WS-ALREADY-VINFOD-FL        PIC X(001) VALUE SPACES.
003840         88  WS-ALREADY-VINFOD                  VALUE 'Y'.
003850     05  WS-VRUINFO-PASS-FL          PIC X(001) VALUE SPACES.
003860         88  WS-VRUINFO-PASS                    VALUE 'Y'.
003870     05  WS-SHORT-FLAG               PIC X(001) VALUE SPACES.
003880         88  WS-SHORT-LEAD                      VALUE 'L'.
003890         88  WS-SHORT-CALL                      VALUE 'C'.
003900     05  WS-OD-TIME-CHANGED-FL       PIC X(001) VALUE SPACES.
003910         88  WS-OD-TIME-CHANGED                 VALUE 'Y'.
003920     05  WS-QUAL-FLAG                PIC X(001) VALUE SPACE.
003930         88  QUAL-MSG-YES                       VALUE 'Y'.
003940         88  QUAL-MSG-NO                        VALUE 'N'.
003950     05  DONE-CODE1                  PIC X(001) VALUE 'N'.
003960         88  DONE1                              VALUE 'Y'.
003970         88  NOT-DONE1                          VALUE 'N'.
003980     05  WS-CALL-FLAG                PIC X(001) VALUE SPACE.
003990         88  WS-CALL-YES                        VALUE 'Y'.
004000         88  WS-CALL-NO                         VALUE 'N'.
004010*CNC0598-B
004020     05  WS-CAN-WRR-FLAG             PIC X(001) VALUE ' '.
004030         88  WS-CAN-WRR-NEW                     VALUE 'N'.
004040*CNC0598-E
004050*CNLD-230-B
004060     05  WS-REASON-FOR-RED           PIC X(001) VALUE ' '.
      *CNLD-259 B CNC0600
               88  WS-RFR-LEAD-IN-SYS-UD              VALUE 'U'.
               88  WS-RFR-OD-IN-SYS-RB                VALUE 'S'.
      *CNLD-259 E
004070         88  WS-RFR-7DAY-RND-ERT                VALUE 'R'.
004080         88  WS-RFR-7DAY-ERT                    VALUE 'E'.
004090         88  WS-RFR-7DAY-HRS                    VALUE 'H'.
      *CNLD-257-DXC-B
004100*        88  WS-RFR-60-192-ERT                  VALUE 'L'.
004110*        88  WS-RFR-60-192-HRS                  VALUE '6'.
               88  WS-RFR-60-ERT                      VALUE 'L'.
               88  WS-RFR-192-ERT                     VALUE 'N'.
               88  WS-RFR-60-HRS                      VALUE '6'.
               88  WS-RFR-192-HRS                     VALUE '7'.
      *CNLD-257-DXC-E
004120         88  WS-RFR-MTOD                        VALUE 'M'.
      *CNLD-265-B
               88  WS-RFR-PERS                        VALUE 'P'.
      *CNLD-265-E
004130*CNLD-230-E
004140*CNLD-236 - B CNC0600
004150     05  WS-EMP-UNDST-REST-FLAG      PIC X(001) VALUE 'N'.
004160         88  WS-EMP-UNDST-REST                  VALUE 'Y'.
004170     05  WS-MSTR3-FOUND-FLAG         PIC X(001) VALUE 'N'.
004180         88  WS-MSTR3-REC-FOUND                 VALUE 'Y'.
004190         88  WS-MSTR3-NOT-FOUND                 VALUE 'N'.
004200
004210 01  WS-UD-MESG.
004220     05 WS-TEXT                      PIC X(38)
004230        VALUE 'EMPLOYEE IS ON UNDISTURBED REST UNTIL '.
004240     05 WS-TIME1.
004250        06 WS-UD-HH                  PIC X(02).
004260        06 FILLER                    PIC X(01)   VALUE ':'.
004270        06 WS-UD-MM                  PIC X(02).
004280
004290 01  WS-RB-1-MESG.
004300     05 WS-RB-1-TEXT                 PIC X(41)
004310        VALUE 'EMP WILL RESET SHORTLY, CAN BE CALLED AT '.
004320     05 WS-RB-1-TIME.
004330        06 WS-RB-1-HH                PIC X(02)   VALUE SPACES.
004340        06 FILLER                    PIC X(01)   VALUE ':'.
004350        06 WS-RB-1-MM                PIC X(02)   VALUE SPACES.
004360*CNLD-236 - E CNC0600
004370
004380 01  WS-DATA-ARRAY.
004390     05 WS-DATA-CURRENT OCCURS 100 TIMES.
004400        10 WS-TASK-KEY.
004410          20 WS-LINE-SEG             PIC X(08) VALUE SPACES.
004420          20 WS-QUAL-CODE            PIC X(04) VALUE SPACES.
004430          20 WS-LSD-TRIPS            PIC 9(02) VALUE ZEROES.
004440          20 WS-LSD-INIT-DAYS        PIC X(04) VALUE SPACES.
004450          20 WS-EXP-TS               PIC X(26) VALUE SPACES.
004460 01  WK-LINE-SEG-ARRAY.
004470     05 WK-LINE-SEG-O   OCCURS 100 TIMES.
004480        10 WK-LINE-SEG               PIC X(08) VALUE SPACES.
004490        10 WK-EXP-TS                 PIC X(26) VALUE SPACES.
004500 01  WS-ASGN-QUAL-ARRAY.
004510*CNC0572 - BEG
004520***  05 WS-ASGN-QUAL    OCCURS 6 TIMES PIC X(004).
004530     05 WS-ASGN-QUAL    OCCURS 8 TIMES PIC X(004).
004540*CNC0572 - BEG
004550 01  WS-TERR-DATE-AREA.
004560     05 WS-TERR-EMP-NO               PIC X(09) VALUE SPACES.
004570     05 WS-TERR-EMP-CC               PIC X(02) VALUE SPACES.
004580
004590 01  WS-MISC-2.
004600     02  HOLD-WK-JSK1-EXP-DATE.
004610         03  HOLD-JSK1-CE        PIC 9(2)  VALUE ZEROES.
004620         03  HOLD-JSK1-YYMMDD.
004630             04  HOLD-JSK1-YY    PIC 9(2)  VALUE ZEROES.
004640             04  HOLD-JSK1-MM    PIC 9(2)  VALUE ZEROES.
004650             04  HOLD-JSK1-DD    PIC 9(2)  VALUE ZEROES.
004660
004670     02  COUNT1                  PIC  S9(04) COMP VALUE ZERO.
004680     02  COUNT2                  PIC  S9(04) COMP VALUE ZERO.
004690     02  COUNT3                  PIC  S9(04) COMP VALUE ZERO.
004700     02  COUNTMAX                PIC  S9(04) COMP VALUE ZERO.
004710     02  WS-TEMP-QUAL            PIC  X(004) VALUE SPACES.
004720 01  WS-SUBSCRIPTS.
004730     05  T                           PIC S9(04) COMP VALUE +0.
004740     05  SUB8                        PIC S9(04) COMP VALUE +0.
004750     05  SUBV                        PIC S9(04) COMP VALUE +0.
004760     05  I                           PIC 9(002) VALUE ZEROS.
004770     05  J                           PIC 9(002) VALUE ZEROS.
004780     05  SUBA                        PIC 9(002) VALUE ZEROS.
004790*CNC0572 - BEG
004800     05  SUBB                        PIC 9(002) VALUE ZEROS.
004810*CNC0572 - END
004820     05  SUB1                        PIC 9(003) VALUE ZEROS.
004830     05  HOLD-SUB1                   PIC 9(003) VALUE ZEROS.
004840     05  SUB2                        PIC 9(003) VALUE ZEROS.
004850     05  HOLD-SUB2                   PIC 9(003) VALUE ZEROS.
004860     05  SUB3                        PIC 9(003) VALUE ZEROS.
004870     05  FUNC-SUB                    PIC 9(003) VALUE ZEROS.
004880     05  FUNC-SUB2                   PIC 9(003) VALUE ZEROS.
004890     05  SUBQ                        PIC 9(003) VALUE ZEROS.
004900     05  FUNC-ARRAY-MAX              PIC 9(002) VALUE 13.
004910     05  ARRAY-MAX                   PIC 9(002) VALUE 11.
004920*C1174-BEG
004930*04/23/19 MFO - BEG
004940***  05  TSQ-MAX                     PIC 9(002) VALUE 22.
004950     05  TSQ-MAX                     PIC 9(002) VALUE 55.
004960*04/23/19 MFO - END
004970     05  SUB7                        PIC 9(002) VALUE ZEROS.
004980*C1174-END
004990     05  SUB4                        PIC 9(002) VALUE ZEROS.
005000     05  SUB5                        PIC 9(002) VALUE ZEROS.
005010     05  SUB6                        PIC 9(002) VALUE ZEROS.
005020     05  QUAL                        PIC 9(002) VALUE ZEROS.
005030
005040 01  WS-PERS-REST.
005050     05  WS-PERS-REST-TIME       PIC X(4) VALUE SPACES.
005060     05  FILLER                  PIC X(1) VALUE SPACES.
005070     05  WS-PERS-REST-DAY        PIC X(2) VALUE SPACES.
005080
005090 01  WS-MISC.
005100     05  WS-HR-MN.
005110         10  WS-HR                   PIC 9(002) VALUE ZEROS.
005120         10  WS-MN                   PIC 9(002) VALUE ZEROS.
005130     05  WS-MIN1                     PIC 9(004) VALUE ZEROS.
005140     05  WS-MIN2                     PIC 9(004) VALUE ZEROS.
005150     05  WS-TIME-ZONE                PIC X(001) VALUE SPACES.
005160     05  WS-EMP-TIME-ZONE            PIC X(001) VALUE SPACES.
005170     05  WS-TOT-JOBS                 PIC 9(004) VALUE ZEROS.
005180     05  WS-ANNUL-FROM-DATE-CENT.
005190         10  WS-ANNUL-FROM-CE        PIC X(002) VALUE SPACES.
005200         10  WS-ANNUL-FROM-DATE      PIC X(006) VALUE SPACES.
005210     05  WS-ANNUL-TO-DATE-CENT.
005220         10  WS-ANNUL-TO-CE          PIC X(002) VALUE SPACES.
005230         10  WS-ANNUL-TO-DATE        PIC X(006) VALUE SPACES.
005240     05  WS-STARTS-DISQUALIFY        PIC 9(002) VALUE 04.
005250     05  WS-STARTS-TOTAL             PIC 9(003) VALUE ZEROS.
005260     05  WS-STARTS-DISQUALIFIED      PIC X(001) VALUE SPACES.
005270         88  STARTS-DISQUALIFIED                VALUE 'Y'.
005280     05  WS-JS-CRAFT                 PIC X(002) VALUE SPACES.
005290     05  WS-HOLD-CC                  PIC X(002) VALUE SPACES.
005300     05  SAVE-ONDUTY-FLAG            PIC X(001) VALUE SPACES.
005310     05  WORK-TIME.
005320         10  WORK-HR                 PIC  9(002) VALUE ZEROS.
005330             88 VALID-HR                         VALUE 0 THRU 23.
005340         10  WORK-MN                 PIC  9(002) VALUE ZEROS.
005350             88 VALID-MN                         VALUE 0 THRU 59.
005360     05  WS-PREP-TIME                PIC  9(004) VALUE ZEROS.
005370     05  WS-EARLIEST-CENT-DATE-TIME.
005380         10  WS-EARLIEST-CENT        PIC  X(002) VALUE SPACES.
005390         10  WS-EARLIEST-DATE        PIC  X(006) VALUE SPACES.
005400         10  WS-EARLIEST-TIME        PIC  X(004) VALUE SPACES.
005410     05  WS-EARLI-US-CENT-DATE-TIME.
005420         10  WS-EARLI-US-CENT        PIC  X(002) VALUE SPACES.
005430         10  WS-EARLI-US-DATE        PIC  X(006) VALUE SPACES.
005440         10  WS-EARLI-US-TIME        PIC  X(004) VALUE SPACES.
005450     05  WS-EMP-OD-CENT-DATE-TIME.
005460         10  WS-EMP-OD-CENT          PIC  X(002) VALUE SPACES.
005470         10  WS-EMP-OD-DATE          PIC  X(006) VALUE SPACES.
005480         10  WS-EMP-OD-TIME          PIC  X(004) VALUE SPACES.
005490     05  WS-TOTAL-HOURS-X.
005500         10  WS-TOTAL-HOURS          PIC 9(05)   VALUE ZEROES.
005510     05  WS-TOTAL-MINUTES-X.
005520         10  WS-TOTAL-MINUTES        PIC 9(03)   VALUE ZEROES.
005530     05  WS-64-TOT-TIME-ON-DUTY      PIC 9(007)  VALUE ZEROES.
005540     05  FILLER REDEFINES WS-64-TOT-TIME-ON-DUTY.
005550         10  WS-64-TOT-HRS-ON-DUTY   PIC 9(005).
005560         10  WS-64-TOT-MNS-ON-DUTY   PIC 9(002).
005570     05  WS-EMPLOYEE-ZONE            PIC X(001) VALUE SPACES.
005580     05  HOLD-SCHED-2ND-CODE         PIC 9(001) VALUE ZEROES.
005590
005600     05  WS-TELEPHONE-NBRS.
005610         10  WS-TELEPHONE-ENTRY OCCURS 3.
005620             15  WS-TELEPHONE-NBR.
005630                 20  WS-TELEPHONE-AREA     PIC X(3).
005640                 20  FILLER                PIC X.
005650                 20  WS-TELEPHONE-PREFIX   PIC X(3).
005660                 20  FILLER                PIC X.
005670                 20  WS-TELEPHONE-SUFFIX   PIC X(4).
005680                 20  FILLER                PIC X.
005690             15  WS-TELEPHONE-CODE-AREA.
005700                 20  WS-TELEPHONE-CODE     PIC X(01).
005710                 20  FILLER                PIC X.
005720             15  WS-TELEPHONE-COMMENT-AREA.
005730                 20  WS-TELEPHONE-COMMENTS PIC X(10).
005740                 20  FILLER                PIC X.
005750         10  WS-TELEPHONE-FILLER           PIC X(02).
005760     05  SAVE-TIME-ZONE              PIC X(01)  VALUE SPACES.
005770     05  WS-AFHT-FAC-CODE            PIC X(03)  VALUE SPACES.
005780     05  WS-AFHT-FAC-NAME            PIC X(25)  VALUE SPACES.
005790     05  WS-AFHT-ROOM-NBR            PIC X(03)  VALUE SPACES.
005800     05  WS-IVR-CALL-PROF            PIC X(08)  VALUE SPACES.
005810     05  HOLD-TASK-KEY               PIC X(41)  VALUE SPACES.
005820     05  WS-AHIST-KEY                PIC X(26)  VALUE SPACES.
005830     05  WS-AHM-KEY                  PIC X(43)  VALUE SPACES.
005840     05  WS-MISC-LIMBO-TM.
005850         10 WS-LIMBO-TM1             PIC X(1)   VALUE SPACES.
005860         10 WS-LIMBO-TM2             PIC X(5)   VALUE SPACES.
005870     05  WS-MISC-TOT-TM.
005880         10 WS-TOT-TM1               PIC X(1)   VALUE SPACES.
005890         10 WS-TOT-TM2               PIC X(5)   VALUE SPACES.
005900     05  WS-MISC-CONSEC-STARTS.
005910         10 WS-CONSEC-STARTS         PIC X(2)   VALUE SPACES.
005920     05  WS-SHORT-CALL-REQ           PIC X(004) VALUE SPACES.
005930     05  WS-SAVE-JSK2-ASGN           PIC X(008) VALUE SPACES.
005940     05  WS-SAVE-JSK2-STIME          PIC X(004) VALUE SPACES.
005950     05  CNT-UNDER-VRU-CONTROL       PIC 9(1) VALUE ZEROES.
005960     05  CNT-CMA-NOT-CALLED          PIC 9(1) VALUE ZEROES.
005970     05  CNT-VRU-UNSUCCESSFUL        PIC 9(1) VALUE ZEROES.
005980     05  SAVE-JOB-SCHEDULE           PIC X(128) VALUE ZEROES.
005990*CNLD-230-B
006000     05 WS-RFR-MSG                   PIC  X(004) VALUE SPACES.
006010*CNLD-230-E
      *CNLD-245-B
006000     05 WS-SAVE-UD-DTTM              PIC 9(010) VALUE ZEROS.
      *CNLD-330-B COMMENTING
006000*    05 WS-REST-US-STATUS-FLAG       PIC X(001) VALUE SPACES.
014400*       88  WS-EMP-US-STATUS-RED        VALUE 'R'.
014500*       88  WS-EMP-US-STATUS-YELLOW     VALUE 'Y'.
014600*       88  WS-EMP-US-STATUS-GREEN      VALUE 'G'.
      *CNLD-330-E
      *CNLD-245-E
006020
006030 01  WORK-HIST-TIMEX.
006040     05  WORK-HIST-TIME              PIC 9(012) VALUE ZEROS.
006050     05  WORK-HIST-TIME-TIE          PIC 9(002) VALUE ZEROS.
006060
006070 01  24HRS-AGO-DT-TM-CE                         VALUE SPACES.
006080     05  24HRS-AGO-CENT              PIC X(002).
006090     05  24HRS-AGO-DATE              PIC X(006).
006100     05  24HRS-AGO-TIME              PIC X(004).
006110
006120 01  6HRS-AHEAD-DT-TM-CE                        VALUE SPACES.
006130     05  6HRS-AHEAD-CENT             PIC X(002).
006140     05  6HRS-AHEAD-DATE             PIC X(006).
006150     05  6HRS-AHEAD-TIME             PIC X(004).
006160
006170 01  WS-COMPARE-DT-TM-CE                        VALUE SPACES.
006180     05  WS-COMPARE-CENT             PIC X(002).
006190     05  WS-COMPARE-DATE             PIC X(006).
006200     05  WS-COMPARE-TIME             PIC X(004).
006210
006220 01  WS-2B4-SYSTEM-DATE-CENT.
006230     05  WS-2B4-SYSTEM-CENT          PIC X(002) VALUE SPACES.
006240     05  WS-2B4-SYSTEM-DATE          PIC X(006) VALUE SPACES.
006250
006260 01  WS-OFFSET-TIME.
006270     05  FILLER                      PIC 9(001) VALUE ZEROS.
006280     05  WS-HR-MINS                  PIC 9(003).
006290
006300 01  WS-EMP-MTOD-CENT.
006310     05  WS-EMP-MTOD-DATE-CENT.
006320         10  WS-EMP-MTOD-CE          PIC 9(002) VALUE ZEROS.
006330         10  WS-EMP-MTOD-DATE        PIC 9(006) VALUE ZEROS.
006340     05  WS-EMP-MTOD-HRMN            PIC 9(004) VALUE ZEROS.
006350
006360 01  WS-EMP-US-RSTD-CENT.
006370     05  WS-EMP-US-RSTD-DATE-CENT.
006380         10  WS-EMP-US-RSTD-CE       PIC 9(002) VALUE ZEROS.
006390         10  WS-EMP-US-RSTD-DATE     PIC 9(006) VALUE ZEROS.
006400     05  WS-EMP-US-RSTD-HRMN         PIC 9(004) VALUE ZEROS.
006410
006420 01  WS-EMP-PERS-REST-CENT.
006430     05  WS-EMP-PERS-REST-DATE-CENT.
006440         10  WS-EMP-PERS-REST-CE     PIC 9(002) VALUE ZEROS.
006450         10  WS-EMP-PERS-REST-DATE   PIC 9(006) VALUE ZEROS.
006460     05  WS-EMP-PERS-REST-HRMN       PIC 9(004) VALUE ZEROS.
006470
006480 01  WS-LAST-TIE-UP-TIME-CENT.
006490     05  WS-LAST-TUT-DATE-CENT.
006500         10  WS-LAST-TUT-CENT        PIC 9(002) VALUE ZEROS.
006510         10  WS-LAST-TUT-DATE        PIC 9(006) VALUE ZEROS.
006520     05  WS-LAST-TUT-HRMN            PIC 9(004) VALUE ZEROS.
006530
006540 01  WS-CALL-TIME-AREA.
006550     05  CALL-DAY                    PIC 9(002) VALUE ZEROS.
006560     05  WK-CALL-DT-TM-CE.
006570         10  WK-CALL-CENT            PIC 9(002) VALUE ZEROS.
006580         10  WK-CALL-DATE-TIME.
006590             15  WK-CALL-DATE.
006600                 20  WK-CALL-YR      PIC 9(002) VALUE ZEROS.
006610                 20  WK-CALL-MO      PIC 9(002) VALUE ZEROS.
006620                 20  WK-CALL-DY      PIC 9(002) VALUE ZEROS.
006630             15  WK-CALL-TIME.
006640                 20  WK-CALL-HR      PIC 9(002) VALUE ZEROS.
006650                 20  WK-CALL-MN      PIC 9(002) VALUE ZEROS.
006660         10  WK-CALL-DATE-TIME-NUM REDEFINES
006670             WK-CALL-DATE-TIME       PIC 9(010).
006680     05  FILLER REDEFINES WK-CALL-DT-TM-CE.
006690         10  WK-CALL-DATE-CENT       PIC 9(008).
006700         10  FILLER                  PIC 9(004).
006710     05  WK-CALL-MINUS-LTIME                    VALUE SPACES.
006720         10  WK-CALL-MINUS-LEAD.
006730             15  WK-CALL-ML-CENT     PIC X(002).
006740             15  WK-CALL-ML-DATE     PIC X(006).
006750             15  WK-CALL-ML-TIME     PIC X(004).
006760     05  WK-LEAD-TIME.
006770         10  WK-LEAD-TIME-HR         PIC 9(002) VALUE ZEROS.
006780         10  WK-LEAD-TIME-MN         PIC 9(002) VALUE ZEROS.
006790     05  WK-LEAD-TIME-MN3            PIC 9(003) VALUE ZEROS.
006800     05  WS-CALL-AREA-1.
006810         10  CALL-DAY1               PIC 9(002) VALUE ZEROS.
006820         10  START-DATE-TIME-1                  VALUE SPACES.
006830             15  START-DATE-1        PIC X(006).
006840             15  START-TIME-1        PIC X(004).
006850         10  END-DATE-TIME-1                    VALUE SPACES.
006860             15  END-DATE-1          PIC X(006).
006870             15  END-TIME-1          PIC X(004).
006880     05  WS-CALL-AREA-2.
006890         10  CALL-DAY2               PIC 9(002) VALUE ZEROS.
006900         10  START-DATE-TIME-2                  VALUE SPACES.
006910             15  START-DATE-2        PIC X(006).
006920             15  START-TIME-2        PIC X(004).
006930         10  END-DATE-TIME-2                    VALUE SPACES.
006940             15  END-DATE-2          PIC X(006).
006950             15  END-TIME-2          PIC X(004).
006960     05  WS-CENT-CALL-DATE           PIC X(008) VALUE SPACES.
006970     05  WS-CENT-QUAL-DATE           PIC X(008) VALUE SPACES.
006980     05  WS-9300-CALL-DATE-TIME.
006990         10  WS-9300-CALL-DATE       PIC X(006) VALUE SPACES.
007000         10  WS-9300-CALL-TIME       PIC X(004) VALUE SPACES.
007010
007020 01  WS-HOS-EMP-NBR                  PIC 9(009) VALUE ZEROS.
007030
007040 01  WS-ASGN-MERG-CLASS              PIC XX.
007050
007060 01  WS-MERG-KEY.
007070     02  WS-MERG-DIST                PIC XX.
007080     02  WS-MERG-SUB                 PIC XX.
007090     02  WS-MERG-REST.
007100         04  WS-MERG-POOL            PIC XX.
007110         04  FILLER                  PIC X(4).
007120     02  FILLER                      PIC XX.
007130
007140 01  WS-START-DAY-TIME.
007150     05  WS-START-DAY                PIC 9(003) VALUE ZEROS.
007160     05  WS-START-DATE               PIC 9(006) VALUE ZEROS.
007170     05  WS-START-TIME               PIC 9(004) VALUE ZEROS.
007180     05  WS-END-TIME                 PIC 9(004) VALUE ZEROS.
007190
007200 01  WS-ADJUSTED-DATE-TIME.
007210     05  WS-ADJUSTED-CC              PIC X(002) VALUE SPACES.
007220     05  WS-ADJUSTED-DATE            PIC X(006) VALUE SPACES.
007230     05  WS-ADJUSTED-TIME.
007240         10  WS-ADJUSTED-HR          PIC X(002) VALUE SPACES.
007250         10  WS-ADJUSTED-MN          PIC X(002) VALUE SPACES.
007260
007270 01  WS-SHORT-CALL-DATE-TIME.
007280     05  WS-SHORT-CALL-CC            PIC X(002) VALUE SPACES.
007290     05  WS-SHORT-CALL-DATE          PIC X(006) VALUE SPACES.
007300     05  WS-SHORT-CALL-TIME.
007310         10  WS-SHORT-CALL-HR        PIC X(002) VALUE SPACES.
007320         10  WS-SHORT-CALL-MN        PIC X(002) VALUE SPACES.
007330
007340 01  WS-OFF-DUTY-DT-TM-CE                       VALUE SPACES.
007350     05  WS-OFF-DUTY-CENT            PIC X(002).
007360     05  WS-OFF-DUTY-DATE-TIME.
007370         10  WS-OFF-DUTY-DATE        PIC X(006).
007380         10  WS-OFF-DUTY-TIME        PIC X(004).
007390
007400 01  SAVE-JOB-ON-DUTY.
007410     05  SAVE-JOB-OD-DATE            PIC X(006).
007420     05  SAVE-JOB-OD-TIME            PIC X(004).
007430
007440 01  WS-FIRST-SHIFT-TIME             PIC X(004).
007450 01  WS-POOL                         PIC X(002).
007460 01  WS-CRAFT-SEQ                    PIC X(002).
007470
007480 01  WORK-CNTLKEY                               VALUE SPACES.
007490     05  WK-CNTL-REC-TYPE            PIC X(002).
007500     05  WK-CNTL-DIST                PIC X(002).
007510     05  WK-CNTL-SUB-DIST            PIC X(002).
007520     05  WK-CNTL-ASSIGN.
007530         10  WK-CNTL-AJ-JOB-NO.
007540             15  WK-CNTL-AJ-1-4      PIC X(004).
007550             15  WK-CNTL-AJ-5-6      PIC X(002).
007560         10  FILLER                  PIC X(003).
007570     05  WK-CNTL-MSG-ROUTE REDEFINES
007580         WK-CNTL-ASSIGN.
007590         10  WK-CNTL-MR-POOL         PIC X(002).
007600         10  WK-CNTL-MR-I-O          PIC X(001).
007610         10  WK-CNTL-MR-TYPE         PIC X(001).
007620         10  WK-CNTL-MR-SEQ          PIC X(002).
007630         10  FILLER                  PIC X(003).
007640     05  WK-CNTL-XB REDEFINES
007650         WK-CNTL-ASSIGN.
007660         10  WK-CNTL-XB-CODE         PIC X(002).
007670         10  FILLER                  PIC X(007).
007680     05  WK-CNTL-UFP REDEFINES
007690         WK-CNTL-ASSIGN.
007700         10  WK-CNTL-POOL            PIC X(002).
007710         10  WK-CNTL-POOL-MISC.
007720             15  WK-CNTL-POOL-TYPE   PIC X(001).
007730             15  FILLER              PIC X(006).
007740         10  WK-CNTL-POOL-SEN
007750             REDEFINES WK-CNTL-POOL-MISC.
007760             15  WK-CNTL-POOL-TURN.
007770                 20  WK-CNTL-POOL-TURN-PREFIX PIC X(002).
007780                 20  WK-CNTL-POOL-TURN-NO     PIC X(002).
007790             15  WK-CNTL-POOL-CC              PIC X(002).
007800             15  FILLER                       PIC X(001).
007810     05  WK-CNTL-ASSIGN-QUAL
007820         REDEFINES WK-CNTL-ASSIGN.
007830         10  WK-CNTL-ASGN-QUAL-TYPE      PIC X(001).
007840         10  WK-CNTL-ASGN-QUAL.
007850             15  WK-CNTL-ASGN-QUAL-1-2   PIC X(002).
007860             15  WK-CNTL-ASGN-QUAL-3-4   PIC X(002).
007870             15  WK-CNTL-ASGN-QUAL-5-6   PIC X(002).
007880             15  WK-CNTL-ASGN-QUAL-7-8   PIC X(002).
007890     05  FILLER                          PIC X(005).
007900
007910 01  WK-CNTL-MC-KEY REDEFINES
007920     WORK-CNTLKEY.
007930     05  WK-CNTL-MC-REC-TYPE     PIC  X(002).
007940     05  WK-CNTL-MC-CODE         PIC  X(004).
007950     05  WK-CNTL-MC-TYPE-SVC     PIC  X(002).
007960     05  WK-CNTL-MC-DIST         PIC  X(002).
007970     05  WK-CNTL-MC-SDIST        PIC  X(002).
007980     05  WK-CNTL-MC-XB           PIC  X(002).
007990     05  WK-CNTL-MC-CONS         PIC  X(002).
008000     05  FILLER                  PIC  X(004).
008010
008020 01  WORKING-FIELD.
008030     05  FILLER                      PIC X(008) VALUE 'WORKING '.
008040     05  WORKING-JOB-NO              PIC X(008).
008050     05  WORK-DATE.
008060         10  WORK-YR                 PIC X(002).
008070         10  FILLER                  PIC X(004).
008080     05  WS-ROUTE-DESIGNATION        PIC X(005).
008090
008100 01  SHIFT-ERRORMSG.
008110     05  FILLER                      PIC X(043)
008120         VALUE 'ABOVE ERRORS ENCOUNTERED - YARD NOT CALLED '.
008130     05  FILLER                      PIC X(030)
008140         VALUE ' - RE-ENTER IF ACCEPTABLE     '.
008150
008160 01  SAVE-AJ-FILE                    PIC X(256) VALUE SPACES.
008170 01  SAVE-ASGN-AREA                  PIC X(128) VALUE SPACES.
008180 01  SAVE-AJTRAINKEY-AREA            PIC X(016) VALUE SPACES.
008190 01  WS-HOLD-JOB-SCHED               PIC X(128) VALUE SPACES.
008200 01  SAVE-TRCN-FILE                  PIC X(350) VALUE SPACES.
008210 01  SAVE-2A-CNTL-FILE               PIC X(256) VALUE SPACES.
008220 01  SAVE-02-CNTL-FILE               PIC X(256) VALUE SPACES.
008230 01  SAVE-CNTL-FILE                  PIC X(256) VALUE SPACES.
008240
008250 01  WS-SCH-EXP-DATE                 PIC X(006) VALUE SPACES.
008260 01  WS-SCH-DATE-TIME                           VALUE SPACES.
008270     05  WS-SCH-DATE                 PIC X(006).
008280     05  WS-SCH-TIME                 PIC X(004).
008290 01  WS-SCH-DAY-TIME-FROM.
008300     05  WS-SCH-DAY-FROM             PIC 9(003) VALUE ZEROS.
008310     05  WS-SCH-TIME-FROM            PIC 9(004) VALUE ZEROS.
008320     05  WS-SCH-DATE-FROM            PIC X(006) VALUE SPACES.
008330 01  WS-SCH-DAY-TIME-TO.
008340     05  WS-SCH-DAY-TO               PIC 9(003) VALUE ZEROS.
008350     05  WS-SCH-TIME-TO              PIC 9(004) VALUE ZEROS.
008360     05  WS-SCH-DATE-TO              PIC X(006) VALUE SPACES.
008370 01  WS-SCH-DAY-TIME.
008380     05  WS-SCH-DAY                  PIC 9(003) VALUE ZEROS.
008390     05  WS-SCH-START-TIME           PIC 9(004) VALUE ZEROS.
008400     05  WS-SCH-END-TIME             PIC 9(004) VALUE ZEROS.
008410
008420 01  GET-FIRST-TEMP-EMP-CODE         PIC X(001) VALUE '0'.
008430     88  GET-FIRST-TEMP-EMP                     VALUE '1'.
008440
008450 01  XXXX-ASGNKEY1                              VALUE SPACES.
008460     05  XX-ASGN-JOB-TYPE            PIC X(001).
008470     05  XX-ASGN-DIST                PIC X(002).
008480     05  XX-ASGN-SUB-DIST            PIC X(002).
008490     05  XX-ASGN-JOB                 PIC X(008).
008500     05  XX-ASGN-REC-TYPE            PIC X(001).
008510     05  XX-ASGN-DATE-TIME           PIC X(010).
008520 01  XXXX-ASGNKEY2.
008530     05  XX-ASGN-EMP                 PIC 9(009) VALUE ZEROS.
008540     05  XX-ASGN2-REC-TYPE           PIC X(001) VALUE SPACES.
008550     05  XX-ASGN2-DATE-TIME          PIC 9(010) VALUE ZEROS.
008560
008570 01  NORMAL-ASGNMT-FLAG              PIC X(001) VALUE SPACES.
008580     88  NORM-ASGN-UFP                          VALUE 'U'.
008590     88  NORM-ASGN-XB                           VALUE 'X'.
008600     88  NORM-ASGN-AJ                           VALUE 'A'.
008610 01  NORMAL-ASGNMT                              VALUE SPACES.
008620     05  NA-DIST                     PIC X(002).
008630     05  NA-SUB-DIST                 PIC X(002).
008640     05  NA-AREA.
008650         10  NA-1                    PIC X(006).
008660         10  NA-2 REDEFINES NA-1.
008670             15  NA-POOL             PIC X(002).
008680             15  NA-TURN             PIC X(004).
008690         10  NA-3 REDEFINES NA-1.
008700             15  NA-FILLER           PIC X(002).
008710             15  NA-XB-TURN          PIC X(004).
008720         10  NA-CC                   PIC X(002).
008730
008740 01  TEMPORARY-ASGNMT-FLAG           PIC X(001) VALUE SPACES.
008750     88  TEMP-UFP                               VALUE 'U'.
008760     88  TEMP-AJ                                VALUE 'A'.
008770     88  TEMP-ASGN-XB                           VALUE 'X'.
008780 01  TEMPORARY-ASGNMT                           VALUE SPACES.
008790     05  TA-DIST                     PIC X(002).
008800     05  TA-SUB-DIST                 PIC X(002).
008810     05  TA-AREA.
008820         10  TA-1                    PIC X(006).
008830         10  TA-2 REDEFINES TA-1.
008840             15  TA-POOL             PIC X(002).
008850             15  TA-TURN             PIC X(004).
008860         10  TA-3 REDEFINES TA-1.
008870             15  TA-XB               PIC X(002).
008880             15  TA-XB-TURN          PIC X(004).
008890         10  TA-CC                   PIC X(002).
008900
008910 01  ON-DUTY-ASGNMT-FLAG             PIC X(001) VALUE SPACES.
008920     88  ON-DUTY-UFP                            VALUE 'U'.
008930     88  ON-DUTY-AJ                             VALUE 'A'.
008940 01  ON-DUTY-OUT-TOWN-CODE           PIC X(10) VALUE '9999999999'.
008950     88  NOT-OUT-OF-TOWN                       VALUE '9999999999'.
008960     88  OUT-TOWN                              VALUE '0000000000'.
008970 01  ON-DUTY-ASGNMT                             VALUE SPACES.
008980     05  OD-DIST                     PIC X(002).
008990     05  OD-SUB-DIST                 PIC X(002).
009000     05  OD-AREA.
009010         10  OD-1                    PIC X(006).
009020         10  OD-2 REDEFINES OD-1.
009030             15  OD-POOL             PIC X(002).
009040             15  OD-TURN             PIC X(004).
009050         10  OD-CC                   PIC X(002).
009060
009070 01  ON-DUTY-EMP                     PIC 9(009) VALUE ZEROS.
009080     88 VALID-ONDUTY-EMP               VALUES 1 THRU 999999995.
009090 01  AJ-EMP-NBR                      PIC 9(009) VALUE ZEROS.
009100 01  AJ-TEMP-EMP-NBR                 PIC 9(009) VALUE ZEROS.
009110 01  WS-CURR-OWNER                   PIC 9(009) VALUE ZEROS.
009120
009130 01  SAVE-CNTL-DIST                  PIC X(2)   VALUE SPACES.
009140 01  SAVE-CNTL-SDIST                 PIC X(2)   VALUE SPACES.
009150 01  SAVE-CNTL-POOL                  PIC X(2)   VALUE SPACES.
009160
009170 01  WS-VRU-SCAL-TABLE.
009180     05  WS-VRU-SCAL-TAB OCCURS 100 TIMES.
009190         10  WS-VRU-SCAL-PARM.
009200             15  WS-VRU-SCAL-DIST    PIC X(02).
009210             15  WS-VRU-SCAL-SDIST   PIC X(02).
009220             15  WS-VRU-SCAL-ASSIGN.
009230                 20  WS-VRU-SCAL-ASGN PIC X(06).
009240                 20  WS-VRU-SCAL-CC  PIC X(02).
009250         10  WS-VRU-SCAL-EMP         PIC X(09).
009260
009270 01  WS-FUNCTION-TABLE-SIZE          PIC S9(004) COMP VALUE +38.
009280 01  WS-FUNCTION-TABLE.
009290     05  FILLER                      PIC X(027)
009300         VALUE 'CALL A Y NORMAL CALL       '.
009310     05  FILLER                      PIC X(027)
009320         VALUE 'CAOT Y Y CALL OVERTIME     '.
009330     05  FILLER                      PIC X(027)
009340         VALUE 'FILL A Y 1ST VACANCY FILL  '.
009350     05  FILLER                      PIC X(027)
009360         VALUE 'OFF  A Y LAYOFF            '.
009370     05  FILLER                      PIC X(027)
009380         VALUE 'RJSU A Y REJECT EMP SUPV   '.
009390     05  FILLER                      PIC X(027)
009400         VALUE 'RJNW A Y REJECT NO WORK    '.
009410     05  FILLER                      PIC X(027)
009420         VALUE 'RJNR A Y REJECT EMP NOT RST'.
009430     05  FILLER                      PIC X(027)
009440         VALUE 'RJRT A Y REJECT RESTRICTION'.
009450     05  FILLER                      PIC X(027)
009460         VALUE 'RJNQ A Y REJECT EMP NO QUAL'.
009470     05  FILLER                      PIC X(027)
009480         VALUE 'RJH  A Y REJECT/HOLD       '.
009490     05  FILLER                      PIC X(027)
009500         VALUE 'RJHS A Y REJECT HRS SERVICE'.
009510     05  FILLER                      PIC X(027)
009520         VALUE 'VACT A Y VACATE ASGN       '.
009530     05  FILLER                      PIC X(027)
009540         VALUE 'ANNU A Y ANNULL ASSIGN     '.
009550     05  FILLER                      PIC X(027)
009560         VALUE 'YES  A N CREW CONSIST YES  '.
009570     05  FILLER                      PIC X(027)
009580         VALUE 'NO   A N CREW CONSIST NO   '.
009590     05  FILLER                      PIC X(027)
009600         VALUE 'SENT A Y SENIOR TARGET     '.
009610     05  FILLER                      PIC X(027)
009620         VALUE 'SENR A Y SENIOR MAN        '.
009630     05  FILLER                      PIC X(027)
009640         VALUE 'CREW A Y DISPLAY CREW      '.
009650     05  FILLER                      PIC X(027)
009660         VALUE 'HELP A Y MANUAL OVERRIDE   '.
009670     05  FILLER                      PIC X(027)
009680         VALUE 'PHON A Y PHONE NUMBERS     '.
009690     05  FILLER                      PIC X(027)
009700         VALUE 'QTIE A N QUICK TIE         '.
009710     05  FILLER                      PIC X(027)
009720         VALUE 'ATNA A Y ATTEMPT NO ANSWER '.
009730     05  FILLER                      PIC X(027)
009740         VALUE 'ATBU A Y ATTEMPT BUSY      '.
009750     05  FILLER                      PIC X(027)
009760         VALUE 'CDSP A Y CALL DH SEP PVT AU'.
009770     05  FILLER                      PIC X(027)
009780         VALUE 'CDSC A Y CALL DH SEP CAB   '.
009790     05  FILLER                      PIC X(027)
009800         VALUE 'CDCP A Y COMBINE/DH/SVC PVT'.
009810     05  FILLER                      PIC X(027)
009820         VALUE 'CDCC A Y COMBINE/DH/SVC CAB'.
009830     05  FILLER                      PIC X(027)
009840         VALUE 'REST A Y DISPLAY REST INFO '.
009850     05  FILLER                      PIC X(027)
009860         VALUE 'CBCK A N CALLBACK          '.
009870     05  FILLER                      PIC X(027)
009880         VALUE 'VCLL B Y IVR NORMAL CALL   '.
009890     05  FILLER                      PIC X(027)
009900         VALUE 'VAOT Z Y IVR CALL OVERTIME '.
009910     05  FILLER                      PIC X(027)
009920         VALUE 'VDSP B Y IVR-CLL DH SEP PVT'.
009930     05  FILLER                      PIC X(027)
009940         VALUE 'VDSC B Y IVR-CLL DH SEP CAB'.
009950     05  FILLER                      PIC X(027)
009960         VALUE 'VDCP B Y IVR-COMB/DH/SV PVT'.
009970     05  FILLER                      PIC X(027)
009980         VALUE 'VDCC B Y IVR-COMB/DH/SV CAB'.
009990     05  FILLER                      PIC X(027)
010000         VALUE 'HOS  A Y DISPLAY HOS INFO  '.
010010     05  FILLER                      PIC X(027)
010020         VALUE 'QUAL A Y DISPLAY QUAL INFO '.
010030     05  FILLER                      PIC X(027)
010040         VALUE '                           '.
010050 01  FILLER REDEFINES WS-FUNCTION-TABLE.
010060     05  WS-FUNC-ARRAY                          OCCURS 38 TIMES.
010070         10  WS-FUNC-CODE            PIC X(004).
010080         10  FILLER                  PIC X(001).
010090         10  WS-FUNC-USAGE           PIC X(001).
010100             88  YARD-USAGE                     VALUE 'A' 'Y'.
010110             88  LOCAL-USAGE                    VALUE 'A' 'O' 'R'.
010120             88  ALL-USAGE                      VALUE 'A'.
010130             88  IVR-YARD-USAGE                 VALUE 'B' 'Z'.
010140             88  IVR-LOCAL-USAGE                VALUE 'B' 'P' 'S'.
010150             88  IVR-ALL-USAGE                  VALUE 'B'.
010160         10  FILLER                  PIC X(001).
010170         10  WS-FUNC-DISPLAY-FLAG    PIC X(001).
010180             88  WS-FUNC-IS-DISPLAYED           VALUE 'Y'.
010190         10  FILLER                  PIC X(001).
010200         10  WS-FUNC-DESC            PIC X(018).
010210
010220 01  WS-FORMAT-FUNCTIONS                        VALUE SPACES.
010230     05  WS-FORMAT-FUNC-DETAIL                  OCCURS 3 TIMES.
010240         10  WS-FORMAT-FUNC-ARRAY               OCCURS 13 TIMES.
010250             15  WS-FORMAT-FUNC      PIC X(004).
010260             15  WS-FORMAT-DASH      PIC X(001).
010270
010280 01  WS-SENIOR-EMP-NO                PIC X(009) VALUE SPACES.
010290 01  WS-SENIOR-QTIE-DISQUAL-FLAG     PIC X(001) VALUE SPACES.
010300 01  WS-SENIOR-DEC-KEY               PIC X(020) VALUE SPACES.
010310 01  WS-SENIOR-SEN.
010320     02  WS-SENIOR-LVL               PIC X(02) VALUE SPACE.
010330     02  WS-SENIOR-SEN-DATE          PIC X(08) VALUE SPACE.
010340 01  WS-COMPARE-SEN.
010350     02  WS-COMPARE-LVL              PIC X(02) VALUE SPACE.
010360     02  WS-COMPARE-SEN-DATE         PIC X(08) VALUE SPACES.
010370 01  WS-SENIOR-SUB1                  PIC 9(003) VALUE ZEROS.
010380 01  WS-SENIOR-SUB2                  PIC 9(003) VALUE ZEROS.
010390
010400 01  P08S-TSQ-ID.
010410     05  P08S-TSQ-TRAN               PIC X(004) VALUE 'P08S'.
010420     05  P08S-TSQ-TERM               PIC X(004) VALUE SPACES.
010430*CNC0572 - INCREASE LENGTH TO 5530
010440*01  P08S-TSQ-LGTH                   PIC S9(04) COMP VALUE +5354.
010450*CNC0579B - BEG
010460*01  P08S-TSQ-LGTH                   PIC S9(04) COMP VALUE +5530.
010470*04/23/19 MFO - BEG
010480*01  P08S-TSQ-LGTH                   PIC S9(04) COMP VALUE +5640.
010490*04/21/23 RKW - B
010500 01  P08S-TSQ-LGTH                PIC S9(04) COMP-5 VALUE +13923.
010510*01  P08S-TSQ-LGTH                  PIC S9(05) COMP VALUE +13923.
010520*04/21/23 RKW - E
010530*04/23/19 MFO - END
010540*CNC0579B - END
010550 01  P08S-TSQ-ITEM                   PIC S9(04) COMP VALUE +1.
010560 01  P08S-TSQ2-ITEM                  PIC S9(04) COMP VALUE +2.
010570 01  P08S-TSQ2-LGTH                  PIC S9(04) COMP VALUE +4170.
010580
010590*04/23/19 MFO - BEG
010600*01  P08S-TSQ-MAX                          PIC 9(003) VALUE 022.
010610 01  P08S-TSQ-MAX                          PIC 9(003) VALUE 055.
010620*04/23/19 MFO - END
010630 01  P08S-TSQ-AREA.
010640     05  P08S-TSQ-CNT                      PIC 9(003).
010650     05  P08S-TSQ-CNT-PICX REDEFINES
010660         P08S-TSQ-CNT                      PIC X(003).
010670     05  P08S-TSQ-ACT-CNT                  PIC 9(003).
010680     05  P08S-TSQ-ACT-CNT-PICX REDEFINES
010690         P08S-TSQ-ACT-CNT                  PIC X(003).
010700     05  FILLER                            PIC X(012).
010710     05  P08S-TSQ-ARRAY-AREA.
010720*04/23/19 MFO - BEG
010730***      10  P08S-TSQ-ARRAY                    OCCURS 022 TIMES.
010740         10  P08S-TSQ-ARRAY                    OCCURS 055 TIMES.
010750*04/23/19 MFO - END
010760             15  P08S-TSQ-JSKEY2           PIC X(030).
010770             15  P08S-TSQ-OWNER            PIC X(009).
010780             15  P08S-TSQ-OWNER-NAME       PIC X(026).
010790             15  P08S-TSQ-RJHS-EMPNO       PIC X(009).
010800             15  P08S-TSQ-COLOR            PIC X(001).
010810             15  P08S-TSQ-QUAL-AREA.
010820*CNC0572 - INCREASE TO OCCUR 8
010830***              20  P08S-TSQ-QUAL             OCCURS 6 TIMES.
010840                 20  P08S-TSQ-QUAL             OCCURS 8 TIMES.
010850                     25  FILLER            PIC X(001).
010860                         88  P08S-TSQ-QUAL-RSTR VALUE '%'.
010870                     25  FILLER            PIC X(003).
010880             15  P08S-TSQ-CALL-DAY         PIC X(002).
010890             15  P08S-TSQ-CALL-DATE-TIME.
010900                 20  P08S-TSQ-CALL-DATE    PIC X(006).
010910                 20  P08S-TSQ-CALL-TIME    PIC X(004).
010920             15  P08S-TSQ-JOB-DATE-TIME.
010930                 20  P08S-TSQ-JOB-DATE     PIC X(006).
010940                 20  P08S-TSQ-JOB-TIME     PIC X(004).
010950             15  P08S-TSQ-LEAD-TIME        PIC X(004).
010960             15  P08S-TSQ-REST-CODE        PIC X(001).
010970                 88  P08S-TSQ-CALL-NRML               VALUE '1'.
010980                 88  P08S-TSQ-CALL-MINUS-LD           VALUE '2'.
010990             15  P08S-TSQ-DECKEY           PIC X(020).
011000             15  P08S-TSQ-SENR-RESTRICT-FLAG   PIC X(001).
011010                 88  P08S-TSQ-SENR-RESTRICTED         VALUE 'Y'.
011020             15  P08S-TSQ-LAST-SEN         PIC X(008).
011030             15  P08S-TSQ-LAST-POS         PIC X(028).
011040             15  P08S-TSQ-LAST-STARTS      PIC X(002).
011050*
011060*  THE FOLLOWING FIELD IS NO LONGER USED. I'VE REPLACED IT WITH
011070*  A 1-BYTE FLAG AND 1 BYTE OF FILLER.
011080*  AJK - 10/05/05
011090*
011100**           15  P08S-TSQ-PRIORITY-ONLY.
011110**               20  P08S-TSQ-PRIOR-ONLY   PIC 9(002).
011120*
011130             15  P08S-USER-OVERRIDE-FLAG   PIC X(001).
011140                 88  P08S-USER-OVERRIDE        VALUE 'Y'.
011150             15  P08S-TSQ-REST-STATUS      PIC X(001).
011160                 88  P08S-TSQ-REST-GREEN            VALUE 'G'.
011170                 88  P08S-TSQ-REST-YELLOW           VALUE 'Y'.
011180                 88  P08S-TSQ-REST-RED              VALUE 'R'.
011190             15  P08S-TSQ-EMP-SEL          PIC X(009).
011200             15  P08S-TSQ-EMP-NAME         PIC X(026).
011210             15  P08S-TSQ-NO-CALL-CODE     PIC X(001).
011220                 88  P08S-TSQ-NO-CALL               VALUE '1'.
011230                 88  P08S-TSQ-NOT-QUAL              VALUE '2'.
011240                 88  P08S-TSQ-RESTRICTED            VALUE '3'.
011250             15  P08S-TSQ-CALLED-CODE      PIC X(004).
011260             15  P08S-TSQ-COLOR-OVRD       PIC X(001).
011270             15  P08S-TSQ-FASTSLOW-BOARD   PIC X(001).
011280             15  P08S-TSQ-STARTS-VACANCY-FLAG PIC X(001).
011290                 88  P08S-TSQ-STARTS-VACANCY        VALUE 'Y'.
011300             15  P08S-TSQ-STARTS-DISQUAL-FLAG PIC X(001).
011310                 88  P08S-TSQ-STARTS-DISQUALIFIED   VALUE 'Y'.
011320             15  P08S-TSQ-STARTS-TOTAL        PIC X(003).
011330             15  P08S-TSQ-QTIE-DISQUAL-FLAG   PIC X(001).
011340                 88  P08S-TSQ-QTIE-DISQUALIFIED     VALUE 'Y'.
011350             15  P08S-TSQ-TRK-REASON          PIC X(002).
011360                 88  P08S-TSQ-TRK-RJH               VALUE '05'.
011370                 88  P08S-TSQ-TRK-ON-DUTY           VALUE '20'.
011380                 88  P08S-TSQ-TRK-NOT-NOTIFIED      VALUE '25'.
011390                 88  P08S-TSQ-TRK-REQUIRED-REST     VALUE '30'.
011400                 88  P08S-TSQ-TRK-UNAVAIL           VALUE '35'.
011410                 88  P08S-TSQ-TRK-PERS-REST         VALUE '40'.
011420                 88  P08S-TSQ-TRK-NOT-QUALIFIED     VALUE '45'.
011430                 88  P08S-TSQ-TRK-QUICK-TIE         VALUE '50'.
011440                 88  P08S-TSQ-TRK-STARTS            VALUE '55'.
011450                 88  P08S-TSQ-TRK-DONT-TRACK        VALUE '88'.
011460                 88  P08S-TSQ-TRK-UNKNOWN           VALUE '99'.
011470             15  P08S-TSQ-SAVE-CBCK-FLAG      PIC X(001).
011480*CNC0579B - BEG
011490             15  P08S-TSQ-DEC-RPT-LOCATION    PIC X(005).
011500*CNC0579B - END
011510         10  P08S-TSQ-SAVE-VARIABLE-AREA      PIC X(100).
011520 01  P08S-TSQ2-AREA.
011530     05  P08S-TSQ-SCREEN-AREA              PIC X(4000).
011540     05  P08S-TSQ-COMMAREA                 PIC X(170).
011550
011560 01  P08S-TSQ3-ID.
011570     05  P08S-TSQ3-TRAN              PIC X(004) VALUE 'PS08'.
011580     05  P08S-TSQ3-TERM              PIC X(004) VALUE SPACES.
011590 01  P08S-TSQ3-LGTH                  PIC S9(04) COMP VALUE +5546.
011600 01  P08S-TSQ3-ITEM                  PIC S9(04) COMP VALUE +1.
011610 01  P08S-TSQ3-AREA.
011620     05  P08S-TSQ3-DATA              PIC X(3000) VALUE SPACES.
011630     05  P08S-TSQ3-PSTCOMM-AREA      PIC X(0170) VALUE SPACES.
011640     05  P08S-TSQ3-SCREEN-AREA       PIC X(2374) VALUE SPACES.
011650     05  P08S-TSQ3-FUNC-SUB          PIC 9(0002) VALUE ZEROES.
011660
011670 01  P08S-TSQ4-ID.
011680     05  P08S-TSQ4-TRAN              PIC X(004) VALUE 'P8S4'.
011690     05  P08S-TSQ4-TERM              PIC X(004) VALUE SPACES.
011700 01  P08S-TSQ4-LGTH                  PIC S9(04) COMP VALUE +2116.
011710 01  P08S-TSQ4-ITEM                  PIC S9(04) COMP VALUE +1.
011720 01  P08S-TSQ4-AREA.
011730     05  P08S-TSQ4-DATA              PIC X(2100) VALUE SPACES.
011740
011750 01  WORK-PASS-KEY.
011760     05  WORK-NAME                   PIC X(26)   VALUE SPACES.
011770     05  WORK-NUMBER                 PIC X(09)   VALUE SPACES.
011780 01  WS-CNTL-PRNTR-FL                PIC X(01)   VALUE SPACES.
011790 01  WS-PRINTER-ID                   PIC X(008).
011800     88 WS-INVALID-PRNTR-ID                      VALUE '    XXXX',
011810                                                       '   XXXXX',
011820                                                       '  XXXXXX',
011830                                                       ' XXXXXXX',
011840                                                       'XXXXXXXX',
011850                                                       'XXXXXXX ',
011860                                                       'XXXXXX  ',
011870                                                       'XXXXX   ',
011880                                                       'XXXX    '.
011890*****************************************************************
011900***                       JCL AREA
011910*****************************************************************
011920 01  JCL-INPUT-CARD.
011930     05  FILLER                      PIC X(001) VALUE QUOTE.
011940     05  JCL-IC-DIST                 PIC X(002) VALUE SPACES.
011950     05  JCL-IC-SUB-DIST             PIC X(002) VALUE SPACES.
011960     05  JCL-IC-CALL-AREA-1          PIC X(022) VALUE SPACES.
011970     05  JCL-IC-CALL-AREA-2          PIC X(022) VALUE SPACES.
011980     05  FILLER                      PIC X(001) VALUE QUOTE.
011990******************************************************************
012000***                  TEMPORARY STORAGE QUEUE                   ***
012010******************************************************************
012020 01  P08STSQ-QUEUE-ITEM         PIC S9(4)  COMP VALUE +1.
012030 01  P08STSQ-MAP-QUEUE-ID.
012040     05  P08STSQ-MAP-QUEUE      PIC X(4)   VALUE '08SM'.
012050     05  P08STSQ-MAP-TERM-ID    PIC X(4)   VALUE SPACES.
012060 01  P08STSQ-CA-QUEUE-ID.
012070     05  P08STSQ-CA-QUEUE       PIC X(4)   VALUE '08SC'.
012080     05  P08STSQ-CA-TERM-ID     PIC X(4)   VALUE SPACES.
012090 01  P08STSQ-QLGTH              PIC S9(4)  COMP VALUE +1.
012100*****************************************************************
012110***                 I/O STATUS CHECK FIELDS
012120*****************************************************************
012130 01  WS-RESPONSE                     PIC S9(008) COMP VALUE ZEROS.
012140 01  FILE-STATUS                     PIC  9(004) VALUE ZEROS.
012150     COPY IOCODES.
012160*****************************************************************
012170***                    COMMAREA COPYBOOKS
012180*****************************************************************
012190     COPY PSTCOMM.
012200     COPY P08DCOMM.
012210     COPY P997COMM.
012220     COPY P998COMM.
012230     COPY P08SCOMM.
012240     COPY P12COMM.
012250     COPY PS94COMM.
012260*****************************************************************
012270***                     MAP AREA COPYBOOK
012280*****************************************************************
012290     COPY PSM08SRE.
012300*****************************************************************
012310***                  PROGRAM NAMES COPYBOOKS
012320*****************************************************************
012330     COPY PSTCB08S.
012340     COPY PSTCB03.
012350     COPY PSTCB12.
012360     COPY PSTBATCH.
012370     COPY PSTCB997.
012380     COPY PSTCB998.
012390     COPY PSTCB08D.
012400*****************************************************************
012410***                 CALLED ROUTINES COPYBOOKS
012420*****************************************************************
012430     COPY PSTERAR.
012440     COPY PS08COMM.
012450     COPY PS52COMM.
012460     COPY P902COMM.
012470     COPY P903COMM.
012480     COPY P906COMM.
012490     COPY P917COMM.
012500     COPY P921COMM.
012510     COPY P922COMM.
012520     COPY P923COMM.
012530     COPY P925COMM.
012540     COPY P927COMM.
012550     COPY P931COMM.
012560     COPY P938COMM.
012570     COPY P942COMM.
012580     COPY P943COMM.
012590     COPY P947COMM.
012600     COPY P980COMM.
012610     COPY P0ESCOMM.
012620*****************************************************************
012630***                     FILE COPYBOOKS
012640*****************************************************************
012650     COPY WSAHIST.
012660     COPY WSAHMISC.
012670     COPY WSAJ.
012680     COPY WSASGN.
012690     COPY WSCNTL.
012700     COPY WSDEC.
012710     COPY WSEMPTRK.
012720     COPY WSJS.
012730     COPY WSMSTR.
012740     COPY WSMSTR3.
012750     COPY WSQUAL.
012760     COPY WSSEN.
012770     COPY WSSWASGN.
012780     COPY WSTASK.
012790     COPY WSTKCR.
012800     COPY WSTRCN.
012810     COPY WSVRUINF.
012820
012830     EXEC SQL INCLUDE LVPV     END-EXEC.
012840     EXEC SQL INCLUDE LVPWS    END-EXEC.
012850
012860     EXEC SQL INCLUDE LVEV     END-EXEC.
012870     EXEC SQL INCLUDE LVEWS    END-EXEC.
012880
012890     EXEC SQL INCLUDE HSTV     END-EXEC.
012900     EXEC SQL INCLUDE HSTWS    END-EXEC.
012910     EXEC SQL INCLUDE LSPV     END-EXEC.
012920     EXEC SQL INCLUDE LSPWS    END-EXEC.
012930
012940     EXEC SQL INCLUDE LSDV     END-EXEC.
012950     EXEC SQL INCLUDE LSDWS    END-EXEC.
012960
012970     EXEC SQL INCLUDE TRRV     END-EXEC.
012980     EXEC SQL INCLUDE TRRWS    END-EXEC.
012990
013000*****************************************************************
013010***                     MISC. COPYBOOKS
013020*****************************************************************
013030     COPY PSTKEYS.
013040     COPY PSTATTR.
013050     COPY WSLOCODE.
013060     COPY WSZONE.
013070     COPY WSMSG.
013080     COPY PSTCCRFT.
013090     COPY WSEDDATE.
013100     COPY WSEDTIME.
013110     COPY WSAJDEFN.
013120     COPY WSSYDTTM.
013130     COPY WSMSCALL.
013140     COPY WSBUFFER.
013150     COPY CALLFUNC.
013160     EXEC SQL INCLUDE SQLCA    END-EXEC.
013170     EXEC SQL INCLUDE WSCOMMON END-EXEC.
013180
013190 LINKAGE SECTION.
013200 01  DFHCOMMAREA.
013210     05  FILLER                    PIC X(170).
013220
013230 PROCEDURE DIVISION.
013240*
013250 P0000-MAINLINE.
013260*
013270     MOVE EIBTRMID                  TO P08S-TSQ-TERM
013280     MOVE EIBTRMID                  TO P08S-TSQ4-TERM
013290     EXEC CICS IGNORE CONDITION
013300               ERROR
013310     END-EXEC
013320     EXEC CICS HANDLE ABEND
013330               LABEL(P9999-GOT-PROBLEM)
013340     END-EXEC
013350     COPY ABSTIME.
013360     IF EIBCALEN = ZERO
013370        PERFORM P9990-CLEAR-SCREEN
013380     END-IF
013390     MOVE DFHCOMMAREA TO PSTCOMM-AREA
013400     IF EIBTRNID NOT = P08S-TRAN
013410        SET CREATE-SCREEN      TO TRUE
013420        IF EIBTRNID = P998-TRAN
013430           MOVE LOW-VALUES     TO PSTS08S
013440           MOVE P998CA-CURSOR-POS TO EIBCPOSN
013450           PERFORM P7010-READ-TSQUEUE
013460           PERFORM P9000-SEND-MAP-AND-RETURN
013470        END-IF
013480
013490        IF EIBTRNID = P997-TRAN
013500           PERFORM P9600-READ-TSQUEUE3
013510           MOVE P08S-TSQ3-DATA          TO PSTS08S
013520           EXEC CICS DELETEQ TS
013530                     QUEUE(P08S-TSQ3-ID)
013540           END-EXEC
013550           IF P997CA-RECORD-SELECTED
013560              MOVE P997CA-PASS-KEY        TO WORK-PASS-KEY
013570              MOVE P08S-TSQ3-PSTCOMM-AREA TO PSTCOMM-AREA
013580              PERFORM P9050-RESET-ATTRIBUTES
013590              MOVE 'HELP'       TO SCR08S-FUNC(P08S-TSQ3-FUNC-SUB)
013600              MOVE REV-VIDEO TO SCR08S-FUNC-HI(P08S-TSQ3-FUNC-SUB)
013610              MOVE WORK-NAME    TO SCR08S-NAME(P08S-TSQ3-FUNC-SUB)
013620              MOVE -1    TO SCR08S-FUNC-CURSOR(P08S-TSQ3-FUNC-SUB)
013630*                  'PRESS ENTER TO PROCESS HELP'
013640              MOVE 'P167'                TO MSGLOG-CODE
013650              PERFORM P9000-SEND-MAP-AND-RETURN
013660           ELSE
013670              MOVE P08S-TSQ3-PSTCOMM-AREA TO PSTCOMM-AREA
013680              PERFORM P9050-RESET-ATTRIBUTES
013690              MOVE SPACES           TO SCR08S-ERRORMSG
013700              MOVE -1               TO SCR08S-FUNC-CURSOR(1)
013710              PERFORM P9000-SEND-MAP-AND-RETURN
013720           END-IF
013730        END-IF
013740        IF EIBTRNID = P08D-TRAN
013750           MOVE LOW-VALUES     TO PSTS08S
013760           PERFORM P7010-READ-TSQUEUE
013770           MOVE P08S-TSQ-SAVE-VARIABLE-AREA TO PSTCA-VARIABLE-AREA
013780           PERFORM P9000-SEND-MAP-AND-RETURN
013790        END-IF
013800
013810        PERFORM P9710-READ-TSQUEUE-2
013820        IF SUCCESS
013830           PERFORM P9700-READ-TSQUEUE
013840           IF SUCCESS
013850              PERFORM P9770-READ-TSQUEUE4
013860           END-IF
013870        END-IF
013880        IF SUCCESS
013890           PERFORM P9400-DELETE-TSQUEUE
013900           PERFORM P9795-DELETE-TSQUEUE4
013910           MOVE P08S-TSQ-SCREEN-AREA
013920                               TO PSTS08S
013930           MOVE P08S-TSQ-COMMAREA
013940                               TO PSTCOMM-AREA
013950           PERFORM VARYING SUB1 FROM 1 BY 1
013960              UNTIL SUB1 > ARRAY-MAX
013970              MOVE P08SCA-SUB2 TO SUB2
013980              ADD SUB1         TO SUB2
013990              IF P08S-TSQ-JSKEY2(SUB2) > SPACES
014000                 PERFORM P4080-RESET-EMP-COLORS
014010                 IF P08S-TSQ-CALLED-CODE(SUB2) > SPACES
014020                    PERFORM P4070-LOCK-CALLED-ASGN
014030                 END-IF
014040              END-IF
014050           END-PERFORM
014060           PERFORM P5000-PROCESS-FUNCTIONS
014070           PERFORM P9000-SEND-MAP-AND-RETURN
014080        ELSE
014090           PERFORM P9400-DELETE-TSQUEUE
014100           PERFORM P9795-DELETE-TSQUEUE4
014110           MOVE LOW-VALUES     TO PSTS08S
014120           MOVE PSTCA-DIST     TO SCR08S-DIST
014130           MOVE PSTCA-SUB-DIST TO SCR08S-SUB-DIST
014140           MOVE -1             TO SCR08S-CODE-CURSOR
014150           MOVE SPACES         TO P08SCOMM-AREA
014160                                  P08S-TSQ-AREA
014170                                  P08S-TSQ4-AREA
014180           MOVE ZEROS          TO P08S-TSQ-CNT
014190                                  P08S-TSQ-ACT-CNT
014200           PERFORM P6110-IVR-PFKEY-EDITS
014210           PERFORM P0010-INITIALIZE-FUNCTIONS
014220           PERFORM P9000-SEND-MAP-AND-RETURN
014230        END-IF
014240     END-IF
014250     MOVE EIBAID               TO PF-CHECK
014260     IF EXIT-KEY
014270        MOVE EIBTRMID          TO P08S-TSQ3-TERM
014280*C1174-BEG
014290        PERFORM P6600-REMOVE-TRACKING
014300*C1174-END
014310        EXEC CICS DELETEQ TS
014320                  QUEUE(P08S-TSQ3-ID)
014330                  RESP(WS-RESPONSE)
014340        END-EXEC
014350        PERFORM P9400-DELETE-TSQUEUE
014360        PERFORM P9795-DELETE-TSQUEUE4
014370        PERFORM P9100-SETUP-MAIN-MENU
014380     END-IF
014390     PERFORM P0100-PROCESS-INPUT
014400     PERFORM P9000-SEND-MAP-AND-RETURN.
014410*
014420 P0010-INITIALIZE-FUNCTIONS.
014430*
014440     MOVE SPACES            TO WS-FORMAT-FUNCTIONS
014450     SET P08SCA-REPORT-NOT-READY TO TRUE
014460     MOVE 1                 TO SUB1
014470     MOVE ZEROS             TO SUB2
014480     PERFORM VARYING SUB3 FROM 1 BY 1
014490        UNTIL SUB3 > WS-FUNCTION-TABLE-SIZE
014500        IF WS-FUNC-CODE(SUB3) > SPACES
014510           IF   WS-FUNC-IS-DISPLAYED(SUB3)
014520           AND ((P08SCA-IVR-CALL-ACTIVE
014530           AND  (IVR-YARD-USAGE (SUB3))
014540            OR   IVR-LOCAL-USAGE(SUB3))
014550            OR  (YARD-USAGE     (SUB3)
014560            OR   LOCAL-USAGE    (SUB3)))
014570              IF SUB2 NOT = 0
014580                 AND SUB2 NOT = FUNC-ARRAY-MAX
014590                 MOVE '-'   TO WS-FORMAT-DASH(SUB1, SUB2)
014600              END-IF
014610              ADD 1         TO SUB2
014620              IF SUB2 > FUNC-ARRAY-MAX
014630                 ADD 1      TO SUB1
014640                 MOVE 1     TO SUB2
014650                 IF SUB1 > 3
014660                    MOVE WS-FUNCTION-TABLE-SIZE
014670                            TO SUB3
014680                 ELSE
014690                    MOVE WS-FUNC-CODE(SUB3)
014700                            TO WS-FORMAT-FUNC(SUB1, SUB2)
014710                 END-IF
014720              ELSE
014730                 MOVE WS-FUNC-CODE(SUB3)
014740                            TO WS-FORMAT-FUNC(SUB1, SUB2)
014750              END-IF
014760           END-IF
014770        ELSE
014780           PERFORM VARYING WS-MC-SUB FROM 1 BY 1
014790             UNTIL WS-MC-SUB > WS-MC-MAX
014800              IF WS-MISS-CALL-CODE(WS-MC-SUB) > SPACES
014810                 IF SUB2 NOT = 0
014820                    AND SUB2 NOT = FUNC-ARRAY-MAX
014830                    MOVE '-'
014840                            TO WS-FORMAT-DASH(SUB1, SUB2)
014850                 END-IF
014860                 ADD 1      TO SUB2
014870                 IF SUB2 > FUNC-ARRAY-MAX
014880                    ADD 1   TO SUB1
014890                    MOVE 1  TO SUB2
014900                    IF SUB1 > 3
014910                       MOVE WS-FUNCTION-TABLE-SIZE
014920                            TO SUB3
014930                       MOVE WS-MC-MAX
014940                            TO WS-MC-SUB
014950                    ELSE
014960                       MOVE WS-MISS-CALL-CODE(WS-MC-SUB)
014970                            TO WS-FORMAT-FUNC(SUB1, SUB2)
014980                    END-IF
014990                 ELSE
015000                    MOVE WS-MISS-CALL-CODE(WS-MC-SUB)
015010                            TO WS-FORMAT-FUNC(SUB1, SUB2)
015020                 END-IF
015030              END-IF
015040           END-PERFORM
015050           MOVE WS-FUNCTION-TABLE-SIZE
015060                            TO SUB3
015070        END-IF
015080     END-PERFORM.
015090     MOVE WS-FORMAT-FUNC-DETAIL(1)
015100                            TO SCR08S-FUNC1
015110     MOVE WS-FORMAT-FUNC-DETAIL(2)
015120                            TO SCR08S-FUNC2
015130     MOVE WS-FORMAT-FUNC-DETAIL(3)
015140                            TO SCR08S-FUNC3.
015150*
015160 P0100-PROCESS-INPUT.
015170*
015180     MOVE P08S-MAP-VERSION(PSTCA-SUB) TO P08S-MAP
015190     EXEC CICS RECEIVE MAP(P08S-MAP)
015200                       MAPSET(P08S-SET)
015210                       INTO(PSTS08S)
015220                       RESP(WS-RESPONSE)
015230     END-EXEC
015240     MOVE WS-RESPONSE TO FILE-STATUS
015250     IF NOT SUCCESS
015260        MOVE 'P0100-1'   TO ERR-PARAGRAPH
015270        PERFORM P9999-GOT-PROBLEM
015280     END-IF
015290     IF PFKEY1
015300        PERFORM P7000-WRITE-TSQUEUE
015310        PERFORM P9750-SETUP-SCR998
015320     END-IF
015330
015340     IF PFKEY10
015350        PERFORM P4080-RESET-EMP-COLORS
015360        IF SCR08S-CC(1) NOT > SPACES
015370*               'CANNOT BROWSE UNTIL CREW IS OBTAINED'
015380           MOVE 'C134'               TO MSGLOG-CODE
015390           MOVE -1                   TO SCR08S-CODE-CURSOR
015400           PERFORM VARYING FUNC-SUB FROM 1 BY 1
015410           UNTIL FUNC-SUB > ARRAY-MAX
015420              MOVE SPACES TO SCR08S-FUNC(FUNC-SUB)
015430           END-PERFORM
015440           PERFORM P9000-SEND-MAP-AND-RETURN
015450        END-IF
015460        PERFORM VARYING FUNC-SUB FROM 1 BY 1
015470        UNTIL FUNC-SUB > ARRAY-MAX
015480           IF SCR08S-FUNC(FUNC-SUB) = 'HELP'
015490              PERFORM P9600-READ-TSQUEUE3
015500              MOVE FUNC-SUB       TO P08S-TSQ3-FUNC-SUB
015510              PERFORM P9550-REWRITE-TSQUEUE3
015520              PERFORM P9150-SETUP-SCR997
015530           END-IF
015540        END-PERFORM
015550        MOVE -1                   TO SCR08S-FUNC-CURSOR(1)
015560*            'CAN ONLY BROWSE WHEN HELPING SOMEONE ON'
015570        MOVE 'C135'               TO MSGLOG-CODE
015580        PERFORM P9000-SEND-MAP-AND-RETURN
015590     END-IF
015600
015610     PERFORM P9700-READ-TSQUEUE
015620     PERFORM P9770-READ-TSQUEUE4
015630
015640     IF PFKEY5
015650        IF P08SCA-IVR-CALL-NOT-ACTIVE
015660           PERFORM P6111-COMPANY-EDIT
015670           IF NOT CNTL-OUTBND-IVR-CALL-ACTIVE
015680*             IVR NOT AVAILABLE-VERIFY "OPTION 16"
015690*               IF MANUAL INTERVENTION REQUIRED
015700              MOVE 'I606'             TO MSGLOG-CODE
015710           ELSE
015720*               'IVR CALLS ARE NOT ACTIVE FOR THIS SUB-DISTRICT'
015730              MOVE 'I597'             TO MSGLOG-CODE
015740           END-IF
015750           MOVE -1                 TO SCR08S-CODE-CURSOR
015760           PERFORM P9000-SEND-MAP-AND-RETURN
015770        END-IF
015780        PERFORM P6000-PREFILL-VCLL
015790     END-IF
015800
015810     MOVE DEFAULT-ATTR           TO SCR08S-CODE-HI
015820                                    SCR08S-DATE-HI
015830                                    SCR08S-START-TIME-HI
015840                                    SCR08S-END-TIME-HI
015850                                    SCR08S-EN-TR-CODE-HI
015860
015870     PERFORM VARYING SUB1 FROM 1 BY 1
015880        UNTIL SUB1 > ARRAY-MAX
015890        MOVE P08SCA-SUB2         TO SUB2
015900        ADD SUB1                 TO SUB2
015910        IF P08S-TSQ-CALLED-CODE(SUB2) NOT > SPACES
015920           PERFORM P4090-RESET-HIGHLIGHT
015930        END-IF
015940     END-PERFORM
015950
015960     MOVE SPACES       TO SCR08S-ERRORMSG
015970     IF NOT (ENTER-KEY OR PFKEY7 OR PFKEY8 OR PFKEY11)
015980*           INVALID-FUNC-MSG
015990       MOVE 'I006' TO MSGLOG-CODE
016000       MOVE -1 TO SCR08S-CODE-CURSOR
016010       PERFORM P9000-SEND-MAP-AND-RETURN
016020     END-IF
016030
016040     PERFORM P1000-EDIT-INPUT
016050
016060     IF SCR08S-CODE = 'T' OR 'B'
016070        IF SCR08S-CODE = 'T'
016080           IF (PFKEY7 OR PFKEY8 OR PFKEY11)
016090*                  INVALID-FUNC-MSG
016100              MOVE 'I006'             TO MSGLOG-CODE
016110              MOVE -1                 TO SCR08S-CODE-CURSOR
016120              PERFORM P9000-SEND-MAP-AND-RETURN
016130           END-IF
016140        END-IF
016150        PERFORM P2000-TIEUP-SHIFT
016160        IF SCR08S-CODE = 'B'
016170           MOVE 'C'                   TO SCR08S-CODE
016180                                         P08SCA-CODE
016190        END-IF
016200     END-IF
016210
016220     MOVE SPACES                      TO MSGLOG-CODE
016230                                         SCR08S-ERRORMSG
016240     IF (PFKEY7 OR PFKEY8)
016250        PERFORM P4000-DISPLAY-VACANCIES
016260        IF SCR08S-CODE = 'V'
016270           MOVE -1                    TO SCR08S-CODE-CURSOR
016280        ELSE
016290           MOVE -1                    TO SCR08S-FUNC-CURSOR(1)
016300        END-IF
016310     ELSE
016320        IF SCR08S-CODE = 'V' OR 'C' OR 'B'
016330           IF SCR08S-CODE = 'V'
016340              PERFORM P3000-CALL-SHIFT
016350              PERFORM P4000-DISPLAY-VACANCIES
016360              MOVE -1                 TO SCR08S-CODE-CURSOR
016370           ELSE
016380              IF P08S-TSQ-ACT-CNT > ZERO
016390                 PERFORM P5000-PROCESS-FUNCTIONS
016400              ELSE
016410                 PERFORM P3000-CALL-SHIFT
016420                 IF P08S-TSQ-ACT-CNT > ZERO
016430*                        SHIFT-ERRORMSG
016440                    MOVE 'A001'       TO MSGLOG-CODE
016450                    EXEC CICS SYNCPOINT
016460                              ROLLBACK
016470                              RESP(WS-RESPONSE)
016480                    END-EXEC
016490                    MOVE WS-RESPONSE  TO FILE-STATUS
016500                    IF NOT SUCCESS
016510                       MOVE 'P0100-2' TO ERR-PARAGRAPH
016520                       PERFORM P9999-GOT-PROBLEM
016530                    END-IF
016540                 ELSE
016550                    MOVE SPACES       TO P08S-TSQ-AREA
016560                                         P08S-TSQ4-AREA
016570                    MOVE ZEROS        TO P08S-TSQ-CNT
016580                                         P08S-TSQ-ACT-CNT
016590                                         P08SCA-SUB2
016600                    IF WS-TOT-JOBS > ZERO
016610                    OR P08SCA-REPORT-READY
016620                       PERFORM P8000-START-REPORT
016630                    ELSE
016640*                           'NO JOBS FOUND WITHIN THIS TIME FRAME'
016650                       MOVE 'N049'    TO MSGLOG-CODE
016660                    END-IF
016670                 END-IF
016680                 PERFORM P4000-DISPLAY-VACANCIES
016690                 IF P08S-TSQ-ACT-CNT > ZERO
016700                    MOVE -1           TO SCR08S-FUNC-CURSOR(1)
016710                 ELSE
016720                    MOVE -1           TO SCR08S-CODE-CURSOR
016730                 END-IF
016740              END-IF
016750           END-IF
016760        ELSE
016770           MOVE -1                    TO SCR08S-CODE-CURSOR
016780        END-IF
016790     END-IF
016800     IF SCR08S-ERRORMSG            NOT > SPACES
016810        IF MSGLOG-CODE NOT > SPACES
016820           IF P08S-TSQ-ACT-CNT > ZEROS
016830              IF SCR08S-CODE = 'V' OR 'T'
016840*                VACANCIES DISPLAYED
016850                 MOVE 'V016'             TO MSGLOG-CODE
016860              ELSE
016870*                PROCESSING COMPLETE
016880                 MOVE 'P081'             TO MSGLOG-CODE
016890                 SET P08SCA-REPORT-READY TO TRUE
016900              END-IF
016910           ELSE
016920              IF SCR08S-CODE = 'V' OR 'T'
016930*                PROCESSING COMPLETE
016940                 MOVE 'P081'             TO MSGLOG-CODE
016950                 SET P08SCA-REPORT-READY TO TRUE
016960              ELSE
016970*       PROCESSING COMPLETE - PRESS <ENTER> TO COMPLETE SHIFT CALL
016980                 MOVE 'P082'             TO MSGLOG-CODE
016990                 SET P08SCA-REPORT-READY TO TRUE
017000              END-IF
017010           END-IF
017020        END-IF
017030     END-IF.
017040*
017050 P1000-EDIT-INPUT.
017060*
017070*    EDITS FOR YARD CALL TYPE CODE
017080*
017090     IF SCR08S-CODE NOT = 'B' AND 'C' AND 'T' AND 'V'
017100        MOVE -1                TO SCR08S-CODE-CURSOR
017110        MOVE REV-VIDEO         TO SCR08S-CODE-HI
017120*            INVALID-CODE-MSG
017130        MOVE 'I041' TO MSGLOG-CODE
017140        PERFORM P9000-SEND-MAP-AND-RETURN
017150     END-IF
017160*
017170*    EDITS FOR STARTING DATE
017180*
017190     IF SCR08S-DATE NOT > SPACES
017200        MOVE -1                TO SCR08S-DATE-CURSOR
017210        MOVE REV-VIDEO         TO SCR08S-DATE-HI
017220*            'DATE IS REQUIRED'
017230        MOVE 'M008' TO MSGLOG-CODE
017240        PERFORM P9000-SEND-MAP-AND-RETURN
017250     END-IF
017260     SET DE-YYMMDD-FORMAT      TO TRUE
017270     MOVE SCR08S-DATE          TO DE-YYMMDD
017280     PERFORM P8998-DATEEDIT
017290     IF DE-INVALID-DATE
017300        MOVE -1                TO SCR08S-DATE-CURSOR
017310        MOVE REV-VIDEO         TO SCR08S-DATE-HI
017320*            INVALID-DATE-MSG
017330        MOVE 'I034' TO MSGLOG-CODE
017340        PERFORM P9000-SEND-MAP-AND-RETURN
017350     END-IF
017360*
017370*    EDITS FOR STARTING TIME
017380*
017390     IF SCR08S-START-TIME NOT > SPACES
017400        MOVE -1                TO SCR08S-START-TIME-CURSOR
017410        MOVE REV-VIDEO         TO SCR08S-START-TIME-HI
017420*            'START TIME IS REQUIRED'
017430        MOVE 'S024' TO MSGLOG-CODE
017440        PERFORM P9000-SEND-MAP-AND-RETURN
017450     END-IF
017460     SET TE-MILITARY-FORMAT    TO TRUE
017470     MOVE SCR08S-START-TIME    TO TE-MILITARY-TIME
017480     PERFORM P8997-TIMEEDIT
017490     IF TE-INVALID-TIME
017500        MOVE -1                TO SCR08S-START-TIME-CURSOR
017510        MOVE REV-VIDEO         TO SCR08S-START-TIME-HI
017520*            INVALID-TIME-MSG
017530        MOVE 'I022' TO MSGLOG-CODE
017540        PERFORM P9000-SEND-MAP-AND-RETURN
017550     END-IF
017560*
017570*    EDITS FOR ENDING TIME
017580*
017590     IF SCR08S-END-TIME NOT > SPACES
017600        MOVE -1                TO SCR08S-END-TIME-CURSOR
017610        MOVE REV-VIDEO         TO SCR08S-END-TIME-HI
017620*            'END TIME IS REQUIRED'
017630        MOVE 'E080' TO MSGLOG-CODE
017640        PERFORM P9000-SEND-MAP-AND-RETURN
017650     END-IF
017660     SET TE-MILITARY-FORMAT    TO TRUE
017670     MOVE SCR08S-END-TIME      TO TE-MILITARY-TIME
017680     PERFORM P8997-TIMEEDIT
017690     IF TE-INVALID-TIME
017700        MOVE -1                TO SCR08S-END-TIME-CURSOR
017710        MOVE REV-VIDEO         TO SCR08S-END-TIME-HI
017720*            INVALID-TIME-MSG
017730        MOVE 'I022' TO MSGLOG-CODE
017740        PERFORM P9000-SEND-MAP-AND-RETURN
017750     END-IF
017760*
017770*    EDITS FOR EN/TR CODE
017780*
017790     IF SCR08S-EN-TR-CODE > SPACE
017800        IF SCR08S-EN-TR-CODE NOT = 'EN' AND 'TR'
017810           MOVE -1             TO SCR08S-EN-TR-CODE-CURSOR
017820           MOVE REV-VIDEO      TO SCR08S-EN-TR-CODE-HI
017830*               INVALID-CODE-MSG
017840           MOVE 'I041'         TO MSGLOG-CODE
017850           PERFORM P9000-SEND-MAP-AND-RETURN
017860        END-IF
017870     ELSE
017880        MOVE SPACES            TO SCR08S-EN-TR-CODE
017890     END-IF
017900*
017910*    CONVERT SCR08S-DATE INTO A DAY OF THE WEEK
017920*
017930     MOVE ZEROS                TO DATE-CONVERSION-PARMS
017940     SET PARM-CONV             TO TRUE
017950     MOVE SCR08S-DATE          TO PARM-PRI-DATE-GREG
017960     EXEC CICS LINK
017970               PROGRAM(P903-PGM)
017980               COMMAREA(DATE-CONVERSION-PARMS)
017990               LENGTH(P903-LGTH)
018000               RESP(WS-RESPONSE)
018010     END-EXEC
018020     MOVE WS-RESPONSE          TO FILE-STATUS
018030     IF NOT SUCCESS
018040        MOVE 'P1000-1'         TO ERR-PARAGRAPH
018050        MOVE 'P903LINK'        TO ERR-KEY
018060        PERFORM P9999-GOT-PROBLEM
018070     END-IF
018080*
018090*    INITIAL THE CALL/TIE UP WINDOW
018100*
018110     MOVE SPACES               TO WS-CALL-TIME-AREA
018120     MOVE PARM-PRI-DAY-OF-WEEK TO CALL-DAY1
018130     MOVE SCR08S-DATE          TO START-DATE-1
018140                                  END-DATE-1
018150     MOVE SCR08S-START-TIME    TO START-TIME-1
018160     IF SCR08S-END-TIME < SCR08S-START-TIME
018170        MOVE '2400'            TO END-TIME-1
018180        MOVE ZEROS             TO DATE-CONVERSION-PARMS
018190        SET PARM-ADD           TO TRUE
018200        MOVE SCR08S-DATE       TO PARM-PRI-DATE-GREG
018210        MOVE 1                 TO PARM-SEC-DATE-GREG
018220        EXEC CICS LINK
018230                  PROGRAM(P903-PGM)
018240                  COMMAREA(DATE-CONVERSION-PARMS)
018250                  LENGTH(P903-LGTH)
018260                  RESP(WS-RESPONSE)
018270        END-EXEC
018280        MOVE WS-RESPONSE       TO FILE-STATUS
018290        IF NOT SUCCESS
018300           MOVE 'P1000-2'      TO ERR-PARAGRAPH
018310           MOVE 'P903LINK'     TO ERR-KEY
018320           PERFORM P9999-GOT-PROBLEM
018330        END-IF
018340        MOVE PARM-RES-DAY-OF-WEEK
018350                               TO CALL-DAY2
018360        MOVE PARM-RES-DATE-GREG
018370                               TO START-DATE-2
018380                                  END-DATE-2
018390        MOVE '0000'            TO START-TIME-2
018400        MOVE SCR08S-END-TIME   TO END-TIME-2
018410     ELSE
018420        MOVE SCR08S-END-TIME   TO END-TIME-1
018430        MOVE WS-CALL-AREA-1    TO WS-CALL-AREA-2
018440     END-IF
018450*
018460*    COMPUTE THE DIFFERENCE BETWEEN THE START & END TIMES
018470*
018480     MOVE ZEROS                TO DATE-CONVERSION-PARMS
018490     SET PARM-DIFF             TO TRUE
018500     MOVE END-DATE-2           TO PARM-PRI-DATE-GREG
018510     MOVE END-TIME-2           TO PARM-PRI-HRMN
018520     MOVE START-DATE-1         TO PARM-SEC-DATE-GREG
018530     MOVE START-TIME-1         TO PARM-SEC-HRMN
018540     EXEC CICS LINK
018550               PROGRAM(P903-PGM)
018560               COMMAREA(DATE-CONVERSION-PARMS)
018570               LENGTH(P903-LGTH)
018580               RESP(WS-RESPONSE)
018590     END-EXEC
018600     MOVE WS-RESPONSE          TO FILE-STATUS
018610     IF NOT SUCCESS
018620        MOVE 'P1000-3'         TO ERR-PARAGRAPH
018630        MOVE 'P903LINK'        TO ERR-KEY
018640        PERFORM P9999-GOT-PROBLEM
018650     END-IF
018660     IF PARM-RES-TOT-DAYS > +0
018670        OR (SCR08S-CODE = 'V' AND PARM-RES-HRMN > 1600)
018680        OR (SCR08S-CODE NOT = 'V' AND PARM-RES-HRMN > 0600)
018690        MOVE -1                TO SCR08S-START-TIME-CURSOR
018700        MOVE REV-VIDEO         TO SCR08S-START-TIME-HI
018710        IF SCR08S-CODE = 'V'
018720*           'MAXIMUM 16 HOURS BETWEEN START & END TIME IS ALLOWED'
018730           MOVE 'M046'         TO MSGLOG-CODE
018740        ELSE
018750*           'MAXIMUM 6 HOURS BETWEEN START & END TIME IS ALLOWED'
018760           MOVE 'M043'         TO MSGLOG-CODE
018770        END-IF
018780        PERFORM P9000-SEND-MAP-AND-RETURN
018790     END-IF
018800
018810     EXEC CICS ASKTIME
018820               ABSTIME(WS-ABSTIME)
018830     END-EXEC
018840     ADD WS-ABSTIME-OFFSET  TO WS-ABSTIME
018850     EXEC CICS FORMATTIME
018860               ABSTIME(WS-ABSTIME)
018870               YYYYMMDD(WS-SYSTEM-DATE-CENT)
018880               TIME(WS-SYSTEM-TIME-AREA)
018890     END-EXEC
018900*
018910*    INSTALL APPLICATION DATE/TIME
018920*
018930     IF PSTCA-DATE-TIME-OFFSET > SPACES
018940        MOVE ZEROS              TO DATE-CONVERSION-PARMS
018950        MOVE WS-SYSTEM-DATE     TO PARM-PRI-DATE-GREG
018960        MOVE WS-SYSTEM-TIME     TO PARM-PRI-HRMN
018970        PERFORM P9810-PROCESS-OFFSET
018980        MOVE PARM-RES-GREG-CENT TO WS-SYSTEM-CENT
018990        MOVE PARM-RES-DATE-GREG TO WS-SYSTEM-DATE
019000        MOVE PARM-RES-HRMN      TO WS-SYSTEM-TIME
019010     END-IF
019020     MOVE WS-SYSTEM-DATE-TIME   TO WORK-HIST-TIME
019030     MOVE ZEROS                 TO WORK-HIST-TIME-TIE
019040*
019050*    CONVERT SYSTEM DATE/TIME TO LOCAL DATE/TIME
019060*
019070     MOVE ZEROS                 TO TZ-PARAMETERS
019080     MOVE SPACES                TO TZ-STATUS-FLAG
019090     MOVE TZ-SYSTEM-TIME-ZONE   TO TZ-IN-ZONE
019100     MOVE PSTCA-TIME-ZONE       TO TZ-OUT-ZONE
019110     MOVE WS-SYSTEM-DATE-TIME   TO TZ-IN-DATE-TIME
019120     PERFORM P8996-TIMEZONE
019130     MOVE TZ-OUT-DATE-TIME-CENT TO WS-LOCAL-DATE-TIME-CENT
019140*
019150*    COMPUTE 24 HOURS AGO FROM LOCAL TIME
019160*
019170     MOVE ZEROS                TO DATE-CONVERSION-PARMS
019180     MOVE WS-LOCAL-DATE        TO PARM-PRI-DATE-GREG
019190     MOVE WS-LOCAL-TIME        TO PARM-PRI-HRMN
019200     SET PARM-SUBTRACT         TO TRUE
019210     MOVE 2400                 TO PARM-SEC-HRMN
019220     EXEC CICS LINK
019230               PROGRAM(P903-PGM)
019240               COMMAREA(DATE-CONVERSION-PARMS)
019250               LENGTH(P903-LGTH)
019260               RESP(WS-RESPONSE)
019270     END-EXEC
019280     MOVE WS-RESPONSE          TO FILE-STATUS
019290     IF NOT SUCCESS
019300        MOVE 'P1000-4'         TO ERR-PARAGRAPH
019310        MOVE 'P903LINK'        TO ERR-KEY
019320        PERFORM P9999-GOT-PROBLEM
019330     END-IF
019340     MOVE PARM-RES-GREG-CENT   TO 24HRS-AGO-CENT
019350     MOVE PARM-RES-DATE-GREG   TO 24HRS-AGO-DATE
019360     MOVE PARM-RES-HRMN        TO 24HRS-AGO-TIME
019370
019380     SET DE-YYMMDD-FORMAT      TO TRUE
019390     MOVE START-DATE-1         TO DE-YYMMDD
019400     PERFORM P8998-DATEEDIT
019410     MOVE DE-CCYYMMDD          TO DE-COMPARE1-DATE
019420     MOVE START-TIME-1         TO DE-COMPARE1-TIME
019430
019440     IF DE-COMPARE1-DATE-TIME < 24HRS-AGO-DT-TM-CE
019450*    IF START-DATE-TIME-1     < 24HRS-AGO-DATE-TIME
019460        MOVE -1                TO SCR08S-DATE-CURSOR
019470        MOVE REV-VIDEO         TO SCR08S-DATE-HI
019480                                  SCR08S-START-TIME-HI
019490*            'STARTING TIME MORE THAN 24 HOURS IN THE PAST'
019500        MOVE 'S025' TO MSGLOG-CODE
019510        PERFORM P9000-SEND-MAP-AND-RETURN
019520     END-IF
019530*
019540*    COMPUTE 6 HOURS IN THE FUTURE FROM LOCAL TIME
019550*
019560     MOVE ZEROS                TO DATE-CONVERSION-PARMS
019570     MOVE WS-LOCAL-DATE        TO PARM-PRI-DATE-GREG
019580     MOVE WS-LOCAL-TIME        TO PARM-PRI-HRMN
019590     SET PARM-ADD              TO TRUE
019600     MOVE 0600                 TO PARM-SEC-HRMN
019610     EXEC CICS LINK
019620               PROGRAM(P903-PGM)
019630               COMMAREA(DATE-CONVERSION-PARMS)
019640               LENGTH(P903-LGTH)
019650               RESP(WS-RESPONSE)
019660     END-EXEC
019670     MOVE WS-RESPONSE          TO FILE-STATUS
019680     IF NOT SUCCESS
019690        MOVE 'P1000-5'         TO ERR-PARAGRAPH
019700        MOVE 'P903LINK'        TO ERR-KEY
019710        PERFORM P9999-GOT-PROBLEM
019720     END-IF
019730     MOVE PARM-RES-GREG-CENT   TO 6HRS-AHEAD-CENT
019740     MOVE PARM-RES-DATE-GREG   TO 6HRS-AHEAD-DATE
019750     MOVE PARM-RES-HRMN        TO 6HRS-AHEAD-TIME
019760     IF DE-COMPARE1-DATE-TIME > 6HRS-AHEAD-DT-TM-CE
019770*    IF START-DATE-TIME-1     > 6HRS-AHEAD-DATE-TIME
019780        MOVE -1                TO SCR08S-DATE-CURSOR
019790        MOVE REV-VIDEO         TO SCR08S-DATE-HI
019800                                  SCR08S-START-TIME-HI
019810*            'STARTING TIME MORE THAN 6 HOURS IN THE FUTURE'
019820        MOVE 'S026' TO MSGLOG-CODE
019830        PERFORM P9000-SEND-MAP-AND-RETURN
019840     END-IF
019850*C1174-BEG
019860     IF PFKEY11
019870        MOVE EIBTRMID          TO P08S-TSQ3-TERM
019880        PERFORM P6600-REMOVE-TRACKING
019890*C1184 - BEG
019900*** SYNC THE TRACKING REMOVAL CHANGES SO THEY ARE NOT PUT BACK
019910*** DURING THE ROLLBOCK IF VACANCIES ARE STILL FOUND
019920        EXEC CICS SYNCPOINT
019930                  RESP(WS-RESPONSE)
019940        END-EXEC
019950        MOVE WS-RESPONSE              TO FILE-STATUS
019960        IF NOT SUCCESS
019970           MOVE 'P2100-6'             TO ERR-PARAGRAPH
019980           PERFORM P9999-GOT-PROBLEM
019990        END-IF
020000*C1184 - END
020010     END-IF
020020     IF PFKEY11
020030        OR SCR08S-CODE NOT = P08SCA-CODE
020040        OR SCR08S-DATE NOT = P08SCA-DATE
020050        OR SCR08S-START-TIME NOT = P08SCA-START-TIME
020060        OR SCR08S-END-TIME NOT = P08SCA-END-TIME
020070        OR SCR08S-EN-TR-CODE NOT = P08SCA-EN-TR-CODE
020080        IF (PFKEY7 OR PFKEY8 OR PFKEY11)
020090           SET ENTER-KEY       TO TRUE
020100        END-IF
020110        MOVE SCR08S-CODE       TO P08SCA-CODE
020120        MOVE SCR08S-DATE       TO P08SCA-DATE
020130        MOVE SCR08S-START-TIME TO P08SCA-START-TIME
020140        MOVE SCR08S-END-TIME   TO P08SCA-END-TIME
020150        MOVE SCR08S-EN-TR-CODE TO P08SCA-EN-TR-CODE
020160        MOVE SPACES            TO P08S-TSQ-AREA
020170                                  P08S-TSQ4-AREA
020180        MOVE ZEROS             TO P08S-TSQ-CNT
020190                                  P08S-TSQ-ACT-CNT
020200                                  P08SCA-SUB2
020210     END-IF.
020220*
020230 P2000-TIEUP-SHIFT.
020240*
020250     MOVE '0'                         TO DONE-CODE
020260     MOVE SPACES                      TO AJTRAINKEY-AREA
020270     MOVE SCR08S-DIST                 TO AJ-TRAIN-DIST
020280     MOVE SCR08S-SUB-DIST             TO AJ-TRAIN-SUB-DIST
020290     MOVE AJTRAINKEY-AREA             TO AJTRAINKEY
020300     EXEC CICS STARTBR
020310               DATASET(AJ-VIA-TRAIN)
020320               RIDFLD(AJTRAINKEY)
020330               GTEQ
020340               RESP(WS-RESPONSE)
020350     END-EXEC
020360     MOVE WS-RESPONSE                 TO FILE-STATUS
020370     IF NOT SUCCESS
020380        IF NOT (NO-RECORD-FND OR END-OF-FILE)
020390           MOVE 'P2000-1'             TO ERR-PARAGRAPH
020400           MOVE AJJOBKEY              TO ERR-KEY
020410           PERFORM P9999-GOT-PROBLEM
020420        END-IF
020430        SET DONE                      TO TRUE
020440     END-IF
020450     PERFORM UNTIL DONE
020460        EXEC CICS READNEXT
020470                  DATASET(AJ-VIA-TRAIN)
020480                  INTO(WS-ASGNED-JOBS)
020490                  LENGTH(AJTRAIN-RLGTH)
020500                  RIDFLD(AJTRAINKEY)
020510                  KEYLENGTH(AJTRAIN-KLGTH)
020520                  RESP(WS-RESPONSE)
020530        END-EXEC
020540        MOVE WS-RESPONSE TO FILE-STATUS
020550        IF NOT SUCCESS
020560           IF NOT (END-OF-FILE OR NO-RECORD-FND)
020570              MOVE 'P2000-2'          TO ERR-PARAGRAPH
020580              MOVE AJTRAINKEY         TO ERR-KEY
020590              PERFORM P9999-GOT-PROBLEM
020600           END-IF
020610           SET DONE                   TO TRUE
020620        ELSE
020630           IF SCR08S-DIST        NOT = AJ-TRAIN-DIST
020640           OR SCR08S-SUB-DIST    NOT = AJ-TRAIN-SUB-DIST
020650              SET DONE                TO TRUE
020660           ELSE
020670              IF AJ-JOB-ON-DUTY
020680*
020690*                IF WE'RE DEALING WITH A RELIEF SPAREBOARD, CHECK
020700*                THE TRCN RECORD OF THE ASSIGNMENT IT'S RELIEVING
020710*
020720                 MOVE SPACES               TO TRCN-KEY3
020730                 IF AJ-JOB-ASGN-ID(1:2) = 'EX'
020740*                   DEFAULT THE JOB-DEF-CLASS TO YARD.  P2150
020750*                   WILL LOOK FOR A YARD AHIST REC FIRST, THEN
020760*                   FOR A LOCAL IF IT DOESN'T FIND IT.
020770*
020780                    SET JOB-DEF-YARD-ASGN  TO TRUE
020790                    PERFORM P2150-GET-REG-ASGN
020800                 ELSE
020810*CNC0562 - BEG
020820                    IF AJ-JOB-ASGN-ID(1:1) = 'X'
020830*                      WHEN THE ASSIGNMENT IS A RELIEF, CHECK FOR
020840*                      'YD' OR 'LO'
020850                       SET JOB-DEF-YARD-ASGN TO TRUE
020860                       PERFORM P2150-GET-REG-ASGN
020870*CNC0562 - END
020880                    ELSE
020890                       MOVE AJ-JOB-ASGN-ID TO TRCN-ASSIGNMENT
020900                       MOVE AJ-JOB-DIST    TO TRCN-DIST3
020910                       MOVE AJ-JOB-SUB-DIST TO TRCN-SDIST3
020920                       MOVE TRCN-KEY3      TO TRCNKEY3
020930                       PERFORM P8630-READ-TRCNKEY3
020940                    END-IF
020950                 END-IF
020960                 IF SUCCESS
020970                    CONTINUE
020980                 ELSE
020990                    IF NO-RECORD-FND
021000                       MOVE SPACES    TO TRCN-SHIFT-CALL-TIE
021010                    ELSE
021020                       MOVE 'P2000-3'    TO ERR-PARAGRAPH
021030                       MOVE TRCNKEY3     TO ERR-KEY
021040                       PERFORM P9999-GOT-PROBLEM
021050                    END-IF
021060                 END-IF
021070
021080                 IF AJ-JOB-ASGN-ID(1:2) = 'EX'
021090*CNC0562 - BEG
021100                 OR AJ-JOB-ASGN-ID(1:1) = 'X'
021110*CNC0562 - END
021120                    MOVE TRCN-ASSIGNMENT TO JOB-DEF-CHECK
021130                 ELSE
021140                    MOVE AJ-JOB-ASGN-ID  TO JOB-DEF-CHECK
021150                 END-IF
021160                 IF TRCN-SHIFT-CALL-TIE-YES
021170                    OR TRCN-SHIFT-TIE-YES
021180*---------------------------------------------------------------*
021190*                   CONVERT  TO PSTCA-TIME-ZONE                 *
021200*---------------------------------------------------------------*
021210                    MOVE AJ-JOB-ON-DUTY-TIME  TO SAVE-JOB-ON-DUTY
021220                    IF AJ-JOB-DIST     NOT = SCR08S-DIST
021230                    OR AJ-JOB-SUB-DIST NOT = SCR08S-SUB-DIST
021240                       MOVE SPACES            TO CNTLKEY-AREA
021250                       MOVE '02'              TO CNTL-REC-TYPE
021260                       MOVE AJ-JOB-DIST       TO CNTL-DIST
021270                       MOVE AJ-JOB-SUB-DIST   TO CNTL-SUB-DIST
021280                       MOVE CNTLKEY-AREA      TO CNTLKEY
021290                       PERFORM P8610-READ-CNTLFILE
021300                       IF NOT SUCCESS
021310                          MOVE 'P2000-4'      TO ERR-PARAGRAPH
021320                          MOVE CNTLKEY        TO ERR-KEY
021330                          PERFORM P9999-GOT-PROBLEM
021340                       END-IF
021350                       MOVE WS-CNTL-FILE      TO SAVE-02-CNTL-FILE
021360                       MOVE SPACES            TO TZ-PARAMETERS
021370                       MOVE CNTL-TIME-ZONE    TO TZ-IN-ZONE
021380                       MOVE PSTCA-TIME-ZONE   TO TZ-OUT-ZONE
021390                       MOVE AJ-JOB-ON-DUTY-TIME TO TZ-IN-DATE-TIME
021400                       PERFORM P8996-TIMEZONE
021410                       IF TZ-INVALID-PARAMETERS
021420                          MOVE 'P2000-5'  TO ERR-PARAGRAPH
021430                          PERFORM P8996-TZERROR
021440                       END-IF
021450                       MOVE TZ-OUT-DATE-TIME TO
021460                                           AJ-JOB-ON-DUTY-TIME
021470                    END-IF
021480                    MOVE ZEROS        TO DATE-CONVERSION-PARMS
021490
021500                    SET PARM-ADD      TO TRUE
021510                    IF AJ-JOB-ON-DUTY-TIME NOT NUMERIC
021520                      MOVE 'P2000-6'  TO ERR-PARAGRAPH
021530                      MOVE SPACES     TO ERR-KEY
021540                      PERFORM P9999-GOT-PROBLEM
021550                    END-IF
021560                    MOVE AJ-JOB-ON-DUTY-DATE
021570                                      TO PARM-PRI-DATE-GREG
021580                    MOVE AJ-JOB-ON-DUTY-HRMN
021590                                      TO PARM-PRI-HRMN
021600                    IF AJ-SCH-DURATION > '0000'
021610                       MOVE AJ-SCH-DURATION
021620                                      TO PARM-SEC-HRMN
021630                    ELSE
021640                       MOVE 0800      TO PARM-SEC-HRMN
021650                    END-IF
021660                    EXEC CICS LINK
021670                              PROGRAM(P903-PGM)
021680                              COMMAREA(DATE-CONVERSION-PARMS)
021690                              LENGTH(P903-LGTH)
021700                              RESP(WS-RESPONSE)
021710                    END-EXEC
021720                    MOVE WS-RESPONSE  TO FILE-STATUS
021730                    IF NOT SUCCESS
021740                      MOVE 'P2000-7'  TO ERR-PARAGRAPH
021750                      MOVE SPACES     TO ERR-KEY
021760                      PERFORM P9999-GOT-PROBLEM
021770                    END-IF
021780                    MOVE PARM-RES-GREG-CENT
021790                                      TO WS-OFF-DUTY-CENT
021800                    MOVE PARM-RES-DATE-GREG
021810                                      TO WS-OFF-DUTY-DATE
021820                    MOVE PARM-RES-HRMN
021830                                      TO WS-OFF-DUTY-TIME
021840                    SET DE-YYMMDD-FORMAT      TO TRUE
021850                    MOVE START-DATE-1         TO DE-YYMMDD
021860                    PERFORM P8998-DATEEDIT
021870                    MOVE DE-CCYYMMDD          TO DE-COMPARE1-DATE
021880                    MOVE START-TIME-1         TO DE-COMPARE1-TIME
021890
021900                    SET DE-YYMMDD-FORMAT      TO TRUE
021910                    MOVE END-DATE-2           TO DE-YYMMDD
021920                    PERFORM P8998-DATEEDIT
021930                    MOVE DE-CCYYMMDD          TO DE-COMPARE2-DATE
021940                    MOVE END-TIME-2           TO DE-COMPARE2-TIME
021950
021960                    IF WS-OFF-DUTY-DT-TM-CE <
021970                                          DE-COMPARE1-DATE-TIME
021980                       OR WS-OFF-DUTY-DT-TM-CE >
021990                                          DE-COMPARE2-DATE-TIME
022000                       CONTINUE
022010                    ELSE
022020                       MOVE AJ-TRAIN-CC
022030                                    TO WS-CRAFT-CODE-CHECK
022040                       IF (SCR08S-EN-TR-CODE = 'EN'
022050                          AND ENGINE-CRAFT)
022060                          OR (SCR08S-EN-TR-CODE = 'TR'
022070                          AND NOT ENGINE-CRAFT)
022080                          OR SCR08S-EN-TR-CODE NOT > SPACE
022090                          PERFORM P2100-TIE-UP
022100                       END-IF
022110                    END-IF
022120                 END-IF
022130              END-IF
022140           END-IF
022150        END-IF
022160     END-PERFORM
022170     EXEC CICS ENDBR
022180               DATASET(AJ-VIA-TRAIN)
022190               RESP(WS-RESPONSE)
022200     END-EXEC.
022210*
022220 P2100-TIE-UP.
022230*
022240     MOVE AJTRAINKEY               TO SAVE-AJTRAINKEY-AREA
022250     EXEC CICS ENDBR
022260               DATASET(AJ-VIA-TRAIN)
022270               RESP(WS-RESPONSE)
022280     END-EXEC
022290     MOVE SPACES                   TO CNTLKEY-AREA
022300     MOVE '02'                     TO CNTL-REC-TYPE
022310     MOVE AJ-JOB-DIST              TO CNTL-DIST
022320     MOVE AJ-JOB-SUB-DIST          TO CNTL-SUB-DIST
022330     MOVE CNTLKEY-AREA             TO CNTLKEY
022340     PERFORM P8610-READ-CNTLFILE
022350     IF NOT SUCCESS
022360        MOVE 'P2100-9'             TO ERR-PARAGRAPH
022370        MOVE CNTLKEY               TO ERR-KEY
022380        PERFORM P9999-GOT-PROBLEM
022390     END-IF
022400
022410     MOVE ZEROES                   TO WS-HOS-EMP-NBR
022420
022430     IF CNTL-US-DOMICILED
022440        MOVE ZEROS                 TO ON-DUTY-EMP
022450        MOVE SPACES                TO WS-ASGN-FILE
022460        MOVE 'A'                   TO ASGN-JOB-TYPE
022470        MOVE AJ-JOB-DIST           TO ASGN-DIST
022480        MOVE AJ-JOB-SUB-DIST       TO ASGN-SUB-DIST
022490        MOVE AJ-JOB-ASSIGNMENT     TO
022500                                   ASGN-AJ-JOB IN ASGN-ASSIGNMENT
022510        PERFORM PXXXX-ON-DUTY-EMP
022520        MOVE ASGN-EMP-NO           TO ON-DUTY-EMP
022530        IF VALID-ONDUTY-EMP
022540           MOVE ASGN-EMP-NO        TO WS-HOS-EMP-NBR
022550        END-IF
022560     END-IF
022570
022580     MOVE SPACES                   TO P922-COMMAREA-PARMS
022590                                      CNTLKEY-AREA
022600     MOVE '2A'                     TO CNTL-REC-TYPE
022610     MOVE AJ-JOB-DIST              TO CNTL-DIST
022620     MOVE AJ-JOB-SUB-DIST          TO CNTL-SUB-DIST
022630     MOVE CNTLKEY-AREA             TO CNTLKEY
022640     PERFORM P8610-READ-CNTLFILE
022650     IF NOT SUCCESS
022660        MOVE 'P2100-1'             TO ERR-PARAGRAPH
022670        MOVE CNTLKEY               TO ERR-KEY
022680        PERFORM P9999-GOT-PROBLEM
022690     END-IF
022700     MOVE WS-CNTL-FILE             TO SAVE-2A-CNTL-FILE
022710     IF CNTL-RESTRICT-TIE-YARD
022720        SET P922-QUICK-TIE-SHFT-CALL TO TRUE
022730     END-IF
022740     SET P922-TIE-UP-SHIFT-CALL    TO TRUE
022750     MOVE AJ-JOB-DIST              TO P922-AJ-DIST
022760     MOVE AJ-JOB-SUB-DIST          TO P922-AJ-SUB-DIST
022770     MOVE AJ-JOB-ASSIGNMENT        TO P922-AJ-ASSIGN
022780     MOVE AJ-TRAIN-CC              TO P922-AT-CC
022790     MOVE AJ-JOB-ON-DUTY-TIME      TO P922-JOB-OD-DATE-TIME
022800     MOVE WS-OFF-DUTY-DATE-TIME    TO P922-OFFD-DATE-TIME
022810                                      P922-ARR-DATE-TIME
022820
022830     IF AJ-JOB-ASGN-ID(1:2) = 'EX'
022840*CNC0562 - BEG
022850     OR AJ-JOB-ASGN-ID(1:1) = 'X'
022860*CNC0562 - END
022870        MOVE TRCN-ASSIGNMENT       TO JOB-DEF-CHECK
022880     ELSE
022890        MOVE AJ-JOB-ASGN-ID        TO JOB-DEF-CHECK
022900     END-IF
022910     IF JOB-DEF-RELIEF-ASGN
022920        PERFORM P2150-GET-REG-ASGN
022930     END-IF
022940
022950     IF TRCN-ARR-OFFSET-TIME NUMERIC   AND
022960        TRCN-ARR-OFFSET-TIME > ZEROES  AND
022970        TRCN-LOCAL-REC                 AND
022980       (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
022990        MOVE TRCN-ARR-OFFSET-TIME  TO WS-HR-MINS
023000        MOVE ZEROS                 TO DATE-CONVERSION-PARMS
023010        MOVE P922-ARR-DATE         TO PARM-PRI-DATE-GREG
023020        MOVE P922-ARR-TIME         TO PARM-PRI-HRMN
023030        SET PARM-SUBTRACT          TO TRUE
023040        MOVE WS-OFFSET-TIME        TO PARM-SEC-HRMN
023050        EXEC CICS LINK
023060                  PROGRAM(P903-PGM)
023070                  COMMAREA(DATE-CONVERSION-PARMS)
023080                  LENGTH(P903-LGTH)
023090                  RESP(WS-RESPONSE)
023100        END-EXEC
023110        MOVE WS-RESPONSE   TO FILE-STATUS
023120        IF NOT SUCCESS
023130           MOVE 'P2100-2'  TO ERR-PARAGRAPH
023140           MOVE SPACES     TO ERR-KEY
023150           PERFORM P9999-GOT-PROBLEM
023160        END-IF
023170        MOVE PARM-RES-DATE-GREG    TO P922-ARR-DATE
023180        MOVE PARM-RES-HRMN         TO P922-ARR-TIME
023190     END-IF
023200
023210     MOVE PSTCA-TIME-ZONE          TO P922-TIME-ZONE
023220     MOVE ZEROES                   TO SCHED-DONE-CODE
023230     MOVE ZEROES                   TO SCHED-2ND-CODE
023240     MOVE ZEROES                   TO SCHED-LST-CODE
023250     MOVE ZEROES                   TO SCHED-FOUND-CODE
023260     IF JOB-DEF-LOCAL-ASGN
023270        MOVE AJ-JOB-ON-DUTY-TIME   TO P922-DEP-DATE-TIME
023280        MOVE '03000'               TO P922-TRAIN-LENGTH
023290        IF TRCN-DEP-OFFSET-TIME NUMERIC   AND
023300           TRCN-DEP-OFFSET-TIME > ZEROES  AND
023310           TRCN-LOCAL-REC                 AND
023320          (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
023330           MOVE TRCN-DEP-OFFSET-TIME  TO WS-HR-MINS
023340           MOVE ZEROS                 TO DATE-CONVERSION-PARMS
023350           MOVE P922-DEP-DATE         TO PARM-PRI-DATE-GREG
023360           MOVE P922-DEP-TIME         TO PARM-PRI-HRMN
023370           SET PARM-ADD               TO TRUE
023380           MOVE WS-OFFSET-TIME        TO PARM-SEC-HRMN
023390           EXEC CICS LINK
023400                     PROGRAM(P903-PGM)
023410                     COMMAREA(DATE-CONVERSION-PARMS)
023420                     LENGTH(P903-LGTH)
023430                     RESP(WS-RESPONSE)
023440           END-EXEC
023450           MOVE WS-RESPONSE   TO FILE-STATUS
023460           IF NOT SUCCESS
023470              MOVE 'P2100-3'  TO ERR-PARAGRAPH
023480              MOVE SPACES     TO ERR-KEY
023490              PERFORM P9999-GOT-PROBLEM
023500           END-IF
023510           MOVE PARM-RES-DATE-GREG    TO P922-DEP-DATE
023520           MOVE PARM-RES-HRMN         TO P922-DEP-TIME
023530        END-IF
023540        MOVE WS-OFF-DUTY-DATE-TIME TO P922-YARD-DATE-TIME
023550        MOVE TRCN-FNL-STN          TO P922-FINAL-STATION
023560*
023570*       GET JOB TYPE FROM ASSIGNMENT HISTORY
023580*
023590        MOVE AJ-JOB-DIST          TO AHK1-DIST
023600        MOVE AJ-JOB-SUB-DIST      TO AHK1-SUB-DIST
023610        IF JOB-DEF-LOCAL-ASGN
023620           MOVE JOB-DEF-LOCAL-POOL TO AHK1-POOL
023630        ELSE
023640           MOVE JOB-DEF-YARD-POOL TO AHK1-POOL
023650        END-IF
023660        MOVE AJ-JOB-ON-DUTY-TIME  TO AHK1-EFF-DATE-TIME
023670        MOVE AJ-TRAIN-SYMBOL      TO AHK1-TRAIN
023680        MOVE AH-KEY-1             TO AH1KEY
023690        MOVE AH-FILE-VIA-K1       TO AH-DATASET
023700
023710        EXEC CICS READ
023720                  DATASET(AH-DATASET)
023730                  INTO(WS-AHIST)
023740                  LENGTH(AH1-RLGTH)
023750                  RIDFLD(AH1KEY)
023760                  KEYLENGTH(AH1-KLGTH)
023770                  RESP(WS-RESPONSE)
023780        END-EXEC
023790        MOVE WS-RESPONSE TO FILE-STATUS
023800        IF NOT SUCCESS
023810           MOVE 'P2100-4' TO ERR-PARAGRAPH
023820           MOVE AH1KEY    TO ERR-KEY
023830           PERFORM P9999-GOT-PROBLEM
023840        END-IF
023850        IF AH-MISC-JOB-TYPE > SPACE
023860           MOVE AH-MISC-JOB-TYPE   TO P922-JOB-TYPE
023870        ELSE
023880           MOVE TRCN-JOB-TYPE      TO P922-JOB-TYPE
023890        END-IF
023900*CNC0562 - BEG
023910        IF AJ-JOB-ASGN-ID(1:1) = 'X'
023920           MOVE AH-CALL-XREF-ID    TO P922-CALL-XREF-ID
023930        END-IF
023940*CNC0562 - END
023950        MOVE ZEROES                TO SCHED-DONE-CODE
023960        MOVE ZEROES                TO SCHED-2ND-CODE
023970        MOVE ZEROES                TO SCHED-LST-CODE
023980        MOVE ZEROES                TO SCHED-FOUND-CODE
023990        IF TRCN-2ND-ROUTE-DESIGNATION > SPACES AND
024000           TRCN-LOCAL-REC                      AND
024010          (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
024020           PERFORM P2200-CHECK-SCHED
024030        END-IF
024040        IF SCHED-FOUND AND
024050           SCHED-2ND
024060           MOVE TRCN-2ND-ROUTE-DESIGNATION
024070                                   TO WS-ROUTE-DESIGNATION
024080        ELSE
024090           MOVE TRCN-ROUTE-DESIGNATION
024100                                   TO WS-ROUTE-DESIGNATION
024110        END-IF
024120        MOVE SPACES                TO TKCRKEY-AREA
024130        SET  RUN-MILES-REC         TO TRUE
024140        MOVE TRCN-ORIGIN-STN       TO TKCR-FROM-LOCATION
024150        MOVE TRCN-FNL-STN          TO TKCR-TO-LOCATION
024160        MOVE WS-ROUTE-DESIGNATION  TO TKCR-ROUTE-DESIGNATION
024170        MOVE AJ-JOB-ON-DUTY-DATE   TO WORK-DATE
024180                                      TKCR-EXP-DATE
024190        IF WORK-YR                  > '60'
024200           MOVE '19'               TO TKCR-EXP-CE
024210        ELSE
024220           MOVE '20'               TO TKCR-EXP-CE
024230        END-IF
024240        MOVE TKCRKEY-AREA          TO TKCRKEY
024250        EXEC CICS READ
024260                  GTEQ
024270                  DATASET(TKCR-FILE-VIA-TKCRKEY)
024280                  INTO(WS-TKCR-FILE)
024290                  LENGTH(TKCRFILE-RLGTH)
024300                  RIDFLD(TKCRKEY)
024310                  KEYLENGTH(TKCRFILE-KLGTH)
024320                  RESP(WS-RESPONSE)
024330        END-EXEC
024340        MOVE WS-RESPONSE           TO FILE-STATUS
024350        IF  SUCCESS
024360        AND RUN-MILES-REC
024370        AND TRCN-ORIGIN-STN         = TKCR-FROM-LOCATION
024380        AND TRCN-FNL-STN            = TKCR-TO-LOCATION
024390        AND WS-ROUTE-DESIGNATION    = TKCR-ROUTE-DESIGNATION
024400           CONTINUE
024410        ELSE
024420           MOVE ZEROES             TO TKCR-EN-RUN-MILES-NUM
024430                                      TKCR-TR-RUN-MILES-NUM
024440        END-IF
024450
024460        IF ENGINE-CRAFT
024470           MOVE TKCR-EN-RUN-MILES     TO P922-ACTUAL-MILES
024480        ELSE
024490           MOVE TKCR-TR-RUN-MILES     TO P922-ACTUAL-MILES
024500        END-IF
024510
024520        IF NOT SCHED-FOUND
024530           MOVE ZEROES                TO SCHED-DONE-CODE
024540           MOVE ZEROES                TO SCHED-2ND-CODE
024550           MOVE ZEROES                TO SCHED-LST-CODE
024560           MOVE ZEROES                TO SCHED-FOUND-CODE
024570           IF TRCN-LOCAL-REC                 AND
024580             (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
024590              PERFORM P2200-CHECK-SCHED
024600           END-IF
024610        END-IF
024620        IF SCHED-FOUND AND
024630           NOT SCHED-LST
024640           MOVE 'Y'                TO P922-SAVE-ON-DUTY
024650        END-IF
024660        MOVE 'N'                   TO P922-EMP-HOME-ONLY
024670                                      P922-EMP-ST-CLMD
024680     ELSE
024690        MOVE TRCN-JOB-TYPE         TO P922-JOB-TYPE
024700*CNC0562 - BEG
024710        IF AJ-JOB-ASGN-ID(1:1)      = 'X'
024720           MOVE AH-KEY-1           TO AH1KEY
024730           MOVE AH-FILE-VIA-K1     TO AH-DATASET
024740
024750           EXEC CICS READ
024760                     DATASET  (AH-DATASET)
024770                     INTO     (WS-AHIST)
024780                     LENGTH   (AH1-RLGTH)
024790                     RIDFLD   (AH1KEY)
024800                     KEYLENGTH(AH1-KLGTH)
024810                     RESP     (WS-RESPONSE)
024820           END-EXEC
024830           MOVE WS-RESPONSE        TO FILE-STATUS
024840           IF NOT SUCCESS
024850              MOVE 'P2100-A'       TO ERR-PARAGRAPH
024860              MOVE AH1KEY          TO ERR-KEY
024870              PERFORM P9999-GOT-PROBLEM
024880           END-IF
024890           MOVE AH-CALL-XREF-ID    TO P922-CALL-XREF-ID
024900        END-IF
024910*CNC0562 - END
024920     END-IF
024930     MOVE SAVE-02-CNTL-FILE    TO WS-CNTL-FILE
024940     IF AJ-JOB-DIST         NOT = CNTL-DIST OR
024950        AJ-JOB-SUB-DIST     NOT = CNTL-SUB-DIST OR
024960        CNTL-AREA              <= SPACE
024970        MOVE SPACES            TO CNTLKEY-AREA
024980        MOVE '02'              TO CNTL-REC-TYPE
024990        MOVE AJ-JOB-DIST       TO CNTL-DIST
025000        MOVE AJ-JOB-SUB-DIST   TO CNTL-SUB-DIST
025010        MOVE CNTLKEY-AREA      TO CNTLKEY
025020        PERFORM P8610-READ-CNTLFILE
025030        IF NOT SUCCESS
025040           MOVE 'P2100-8'      TO ERR-PARAGRAPH
025050           MOVE CNTLKEY        TO ERR-KEY
025060           PERFORM P9999-GOT-PROBLEM
025070        END-IF
025080        MOVE WS-CNTL-FILE      TO SAVE-02-CNTL-FILE
025090     END-IF
025100
025110     MOVE ZEROS                TO ON-DUTY-EMP
025120     MOVE SPACES               TO WS-ASGN-FILE
025130     MOVE 'A'                  TO ASGN-JOB-TYPE
025140     MOVE AJ-JOB-DIST          TO ASGN-DIST
025150     MOVE AJ-JOB-SUB-DIST      TO ASGN-SUB-DIST
025160     MOVE AJ-JOB-ASSIGNMENT    TO
025170                                ASGN-AJ-JOB IN ASGN-ASSIGNMENT
025180     PERFORM PXXXX-ON-DUTY-EMP
025190     MOVE ASGN-EMP-NO          TO ON-DUTY-EMP
025200     IF VALID-ONDUTY-EMP
025210        MOVE ASGN-EMP-NO       TO EMP-NBR OF WS-MSTR
025220                                  MSTRNBRK
025230        PERFORM P8505-READ-MASTER-ONLY
025240        IF IVR-CALL-IN-PROCESS
025250           STRING
025260           'TIEUP PREVENTED WHEN EMPLOYEE IS UNDER IVR CONTROL '
025270           EMP-NBR OF WS-MSTR
025280           ' MUST RESOLVE EMPLOYEE BEFORE PROCEEDING'
025290           DELIMITED BY SIZE        INTO SCR08S-ERRORMSG
025300           PERFORM P9000-SEND-MAP-AND-RETURN
025310        END-IF
025320     END-IF
025330
025340*CNLD-302 B COMMENTING
025340*     IF CNTL-TEST-64-ON-ARR
025350**CNC0600-B
025360*     AND NOT CNTL-CAN-WRR-NEW
025370**CNC0600-E
025380*        PERFORM P2300-CHECK-64-7-RULE
025390*     END-IF
025340*CNLD-302 E
025330
025400     EXEC CICS LINK
025410               PROGRAM(P922-PGM)
025420               COMMAREA(P922-COMMAREA-PARMS)
025430               LENGTH(P922-LGTH)
025440               RESP(WS-RESPONSE)
025450     END-EXEC
025460     MOVE WS-RESPONSE TO FILE-STATUS
025470     IF NOT SUCCESS
025480        MOVE 'P2100-5'             TO ERR-PARAGRAPH
025490        PERFORM P9999-GOT-PROBLEM
025500     END-IF
025510     EXEC CICS SYNCPOINT
025520               RESP(WS-RESPONSE)
025530     END-EXEC
025540     MOVE WS-RESPONSE              TO FILE-STATUS
025550     IF NOT SUCCESS
025560        MOVE 'P2100-6'             TO ERR-PARAGRAPH
025570        PERFORM P9999-GOT-PROBLEM
025580     END-IF
025590
025600     MOVE SPACES                   TO EMP-IMPOSED-REST-US-FL
025610
025620     IF (ENGINE-CRAFT     AND TRCN-EN-REST-REQ = 'N') OR
025630        (NOT ENGINE-CRAFT AND TRCN-TR-REST-REQ = 'N')
025640        CONTINUE
025650     ELSE
025660        IF WS-HOS-EMP-NBR > ZEROES
025670           PERFORM P2500-APPLY-THRESHOLD
025680        END-IF
025690     END-IF
025700
025710     IF EMP-IMPOSED-REST-US-YES
025720        MOVE -1                    TO SCR08S-CODE-CURSOR
025730        MOVE WS-HOS-EMP-NBR        TO MSTRNBRK
025740        PERFORM P8505-READ-MASTER-ONLY
025750        STRING 'TIE UP COMPLETE. EMP ' EMP-NAME IN WS-MSTR (1:10)
025760           ' LAID OFF TO "E0 95" STATUS. ENTER TO PROCESS'
025770        DELIMITED BY SIZE        INTO SCR08S-ERRORMSG
025780        PERFORM P9000-SEND-MAP-AND-RETURN
025790     END-IF
025800
025810     MOVE SAVE-AJTRAINKEY-AREA     TO AJTRAINKEY
025820     EXEC CICS STARTBR
025830               DATASET(AJ-VIA-TRAIN)
025840               RIDFLD(AJTRAINKEY)
025850               GTEQ
025860               RESP(WS-RESPONSE)
025870     END-EXEC
025880     MOVE WS-RESPONSE              TO FILE-STATUS
025890     IF NOT SUCCESS
025900        IF NOT (NO-RECORD-FND OR END-OF-FILE)
025910           MOVE 'P2100-7'          TO ERR-PARAGRAPH
025920           MOVE AJTRAINKEY         TO ERR-KEY
025930           PERFORM P9999-GOT-PROBLEM
025940        END-IF
025950        SET DONE                   TO TRUE
025960     END-IF.
025970*
025980 P2150-GET-REG-ASGN.
025990*
026000     MOVE AJ-TRAIN-DIST                 TO AHK1-DIST
026010     MOVE AJ-TRAIN-SUB-DIST             TO AHK1-SUB-DIST
026020     IF JOB-DEF-LOCAL-ASGN
026030        MOVE JOB-DEF-LOCAL-POOL         TO AHK1-POOL
026040     ELSE
026050        MOVE JOB-DEF-YARD-POOL          TO AHK1-POOL
026060     END-IF
026070     MOVE AJ-JOB-ON-DUTY-TIME           TO AHK1-EFF-DATE-TIME
026080     MOVE AJ-TRAIN-SYMBOL               TO AHK1-TRAIN
026090     MOVE AH-KEY-1                      TO AH1KEY
026100     MOVE AH-FILE-VIA-K1                TO AH-DATASET
026110
026120     EXEC CICS READ
026130               DATASET(AH-DATASET)
026140               INTO(WS-AHIST)
026150               LENGTH(AH1-RLGTH)
026160               RIDFLD(AH1KEY)
026170               KEYLENGTH(AH1-KLGTH)
026180               RESP(WS-RESPONSE)
026190     END-EXEC
026200     MOVE WS-RESPONSE TO FILE-STATUS
026210     IF NOT SUCCESS
026220        IF NO-RECORD-FND
026230           IF AJ-JOB-ASGN-ID(1:2) = 'EX'
026240              MOVE JOB-DEF-LOCAL-POOL   TO AHK1-POOL
026250              MOVE AH-KEY-1             TO AH1KEY
026260              MOVE AH-FILE-VIA-K1       TO AH-DATASET
026270
026280              EXEC CICS READ
026290                        DATASET(AH-DATASET)
026300                        INTO(WS-AHIST)
026310                        LENGTH(AH1-RLGTH)
026320                        RIDFLD(AH1KEY)
026330                        KEYLENGTH(AH1-KLGTH)
026340                        RESP(WS-RESPONSE)
026350              END-EXEC
026360              MOVE WS-RESPONSE          TO FILE-STATUS
026370              IF NOT SUCCESS
026380                 MOVE 'P2150-4'         TO ERR-PARAGRAPH
026390                 MOVE AH1KEY            TO ERR-KEY
026400                 PERFORM P9999-GOT-PROBLEM
026410              END-IF
026420           ELSE
026430*CNC0562-BEG
026440*             WHEN SEARCHING FOR THE ASSIGNMENT HISTORY OF A
026450*             RELIEF JOB DURING THE SHIFT TIE-UP AND THE NOT
026460*             FOUND CONDITION IS ENCOUNTERED, THE OTHER SERVICE
026470*             TYPE WILL BE USED FOR A SECOND SEARCH.
026480*
026490              IF JOB-DEF-LOCAL-ASGN
026500                 MOVE JOB-DEF-YARD-POOL  TO AHK1-POOL
026510              ELSE
026520                 MOVE JOB-DEF-LOCAL-POOL TO AHK1-POOL
026530              END-IF
026540              MOVE AH-KEY-1             TO AH1KEY
026550              MOVE AH-FILE-VIA-K1       TO AH-DATASET
026560              EXEC CICS READ
026570                        DATASET(AH-DATASET)
026580                        INTO(WS-AHIST)
026590                        LENGTH(AH1-RLGTH)
026600                        RIDFLD(AH1KEY)
026610                        KEYLENGTH(AH1-KLGTH)
026620                        RESP(WS-RESPONSE)
026630              END-EXEC
026640              MOVE WS-RESPONSE          TO FILE-STATUS
026650              IF NOT SUCCESS
026660                 MOVE 'P2150-5'         TO ERR-PARAGRAPH
026670                 MOVE AH1KEY            TO ERR-KEY
026680                 PERFORM P9999-GOT-PROBLEM
026690              END-IF
026700           END-IF
026710        ELSE
026720           MOVE 'P2150-1'            TO ERR-PARAGRAPH
026730           MOVE AH1KEY               TO ERR-KEY
026740           PERFORM P9999-GOT-PROBLEM
026750*CNC0562-END
026760        END-IF
026770     END-IF
026780
026790     MOVE SPACES                        TO WS-TRCN-FILE
026800     IF AH-CALL-XREF-ASGN NOT = SPACES
026810        MOVE AH-CALL-XREF-ID            TO TRCNKEY3
026820        EXEC CICS READ
026830                  DATASET(TRAIN-CN-VIA-DSD-ASGN)
026840                  INTO(WS-TRCN-FILE)
026850                  LENGTH(TRAINCN-DSD-RLGTH)
026860                  RIDFLD(TRCNKEY3)
026870                  KEYLENGTH(TRAINCN-DSD-KLGTH)
026880                  RESP(WS-RESPONSE)
026890        END-EXEC
026900        MOVE WS-RESPONSE                TO FILE-STATUS
026910        IF NOT SUCCESS
026920           MOVE 'P2150-2'               TO ERR-PARAGRAPH
026930           MOVE TRCNKEY3                TO ERR-KEY
026940           PERFORM P9999-GOT-PROBLEM
026950        END-IF
026960     ELSE
026970        MOVE 'P2150-3'                  TO ERR-PARAGRAPH
026980        MOVE AH1KEY                     TO ERR-KEY
026990        PERFORM P9999-GOT-PROBLEM
027000     END-IF
027010     .
027020*
027030 P2200-CHECK-SCHED.
027040*
027050     MOVE SPACES               TO WORK-JS-KEY1
027060     MOVE P922-AJ-DIST         TO WK-JSK1-ASGN-DIST
027070     MOVE P922-AJ-SUB-DIST     TO WK-JSK1-ASGN-SUB-DIST
027080     MOVE TRCN-ASSIGNMENT      TO WK-JSK1-ASGN
027090     MOVE P922-AJ-CC           TO WK-JSK1-ASGN-CC
027100     MOVE SAVE-JOB-OD-DATE     TO WK-JSK1-EXP-DATE
027110
027120     MOVE ZEROS                TO DATE-CONVERSION-PARMS
027130     SET PARM-CONV             TO TRUE
027140     MOVE SAVE-JOB-OD-DATE     TO PARM-PRI-DATE-GREG
027150     EXEC CICS LINK
027160               PROGRAM(P903-PGM)
027170               COMMAREA(DATE-CONVERSION-PARMS)
027180               LENGTH(P903-LGTH)
027190               RESP(WS-RESPONSE)
027200     END-EXEC
027210     MOVE WS-RESPONSE          TO FILE-STATUS
027220     IF NOT SUCCESS
027230        MOVE 'P2200-1'         TO ERR-PARAGRAPH
027240        MOVE 'P903LINK'        TO ERR-KEY
027250        PERFORM P9999-GOT-PROBLEM
027260     END-IF
027270
027280     MOVE PARM-PRI-DAY-OF-WEEK TO WK-JSK1-ASGN-DAY-NUM
027290
027300     MOVE WORK-JS-KEY1         TO JSKEY1
027310
027320     EXEC CICS STARTBR
027330               DATASET(JS-VIA-JSKEY1)
027340               RIDFLD(JSKEY1)
027350               GTEQ
027360               RESP(WS-RESPONSE)
027370     END-EXEC
027380     MOVE WS-RESPONSE          TO FILE-STATUS
027390
027400     IF NOT SUCCESS
027410        SET SCHED-DONE         TO TRUE
027420        IF NOT (NO-RECORD-FND OR END-OF-FILE)
027430           MOVE 'P2200-2'           TO ERR-PARAGRAPH
027440           MOVE JSKEY1              TO ERR-KEY
027450           PERFORM P9999-GOT-PROBLEM
027460        END-IF
027470     END-IF
027480     SET ONE-TOUR                   TO TRUE
027490     PERFORM UNTIL SCHED-DONE
027500        EXEC CICS READNEXT
027510                  DATASET(JS-VIA-JSKEY1)
027520                  INTO(WS-JOB-SCHEDULE)
027530                  LENGTH(JSKEY1-RLGTH)
027540                  RIDFLD(JSKEY1)
027550                  KEYLENGTH(JSKEY1-KLGTH)
027560                  RESP(WS-RESPONSE)
027570        END-EXEC
027580        MOVE WS-RESPONSE               TO FILE-STATUS
027590        IF SUCCESS
027600          SET DE-YYMMDD-FORMAT TO TRUE
027610          MOVE WK-JSK1-EXP-DATE TO DE-YYMMDD
027620          PERFORM P8998-DATEEDIT
027630          MOVE DE-CCYYMMDD TO HOLD-WK-JSK1-EXP-DATE
027640
027650          SET DE-YYMMDD-FORMAT TO TRUE
027660          MOVE JSK1-EXP-DATE TO DE-YYMMDD
027670          PERFORM P8998-DATEEDIT
027680          IF JSK1-ASGN-DIST     = WK-JSK1-ASGN-DIST     AND
027690             JSK1-ASGN-SUB-DIST = WK-JSK1-ASGN-SUB-DIST AND
027700             JSK1-ASSIGNMENT    = WK-JSK1-ASSIGNMENT    AND
027710             DE-CCYYMMDD  NOT < HOLD-WK-JSK1-EXP-DATE
027720             IF JSK1-ASGN-DAY   < WK-JSK1-ASGN-DAY
027730               CONTINUE
027740             ELSE
027750               IF JSK1-ASGN-DAY      = WK-JSK1-ASGN-DAY
027760                 MOVE ZEROES           TO SCHED-LST-CODE
027770                 IF SAVE-JOB-OD-TIME < JSK1-ASGN-START-TIME
027780                    SET SCHED-DONE  TO TRUE
027790                    SET TWO-TOURS   TO TRUE
027800                 ELSE
027810                   IF SAVE-JOB-OD-TIME   > JOB-SCHED-END-TIME AND
027820                      JOB-SCHED-END-TIME > JSK1-ASGN-START-TIME
027830                      SET SCHED-2ND   TO TRUE
027840                   ELSE
027850                      SET SCHED-FOUND TO TRUE
027860                      SET SCHED-LST   TO TRUE
027870                   END-IF
027880                 END-IF
027890               ELSE
027900                  SET SCHED-DONE     TO TRUE
027910               END-IF
027920             END-IF
027930          ELSE
027940             SET SCHED-DONE     TO TRUE
027950          END-IF
027960        ELSE
027970           SET SCHED-DONE         TO TRUE
027980           IF NOT (NO-RECORD-FND OR END-OF-FILE)
027990              MOVE 'P2200-3'           TO ERR-PARAGRAPH
028000              MOVE JSKEY1              TO ERR-KEY
028010              PERFORM P9999-GOT-PROBLEM
028020           END-IF
028030        END-IF
028040     END-PERFORM
028050
028060     EXEC CICS ENDBR
028070               DATASET(JS-VIA-JSKEY1)
028080               RESP(WS-RESPONSE)
028090     END-EXEC.
025340
025340*CNLD-302 B COMMENTING
028100**
028110* P2300-CHECK-64-7-RULE.
028120**
028130*     MOVE SAVE-2A-CNTL-FILE  TO WS-CNTL-FILE
028140*     IF  TRCN-LOCAL-REC
028150*     AND NOT CNTL-SPLIT-SHIFT-HOS
028160*        CONTINUE
028170*     ELSE
028180*        SET SCHED-2ND        TO TRUE
028190*     END-IF
028200*     IF SCHED-2ND
028210*        OR ONE-TOUR
028220*        MOVE ZEROS              TO ON-DUTY-EMP
028230*        MOVE SPACES             TO WS-ASGN-FILE
028240*        MOVE 'A'                TO ASGN-JOB-TYPE
028250*        MOVE AJ-JOB-DIST        TO ASGN-DIST
028260*        MOVE AJ-JOB-SUB-DIST    TO ASGN-SUB-DIST
028270*        MOVE AJ-JOB-ASSIGNMENT TO ASGN-AJ-JOB IN ASGN-ASSIGNMENT
028280*        PERFORM PXXXX-ON-DUTY-EMP
028290*        MOVE ASGN-EMP-NO        TO ON-DUTY-EMP
028300*        IF VALID-ONDUTY-EMP
028310*           PERFORM P2320-TEST-64
028320*        END-IF
028330*     END-IF.
028340**
028350* P2320-TEST-64.
028360**----------------------------------------------------------------
028370**   CHECK TO SEE IF THE EMPLOYEE IS GOING TO VIOLATE THE
028380**   '64 HOURS IN 7 DAYS' RULE WITH THIS TRIP.  PS08-PGM WILL
028390**   RETURN THE EMPLOYEE'S TOTAL ON-DUTY TIME FOR THE LAST 7 DAYS.
028400**----------------------------------------------------------------
028410*     INITIALIZE PS08-COMMAREA-PARMS
028420*     SET PS08-INQUIRY-FUN          TO TRUE
028430*     MOVE ON-DUTY-EMP              TO PS08-EMP-NBR
028440*     MOVE 07                       TO PS08-CALC-NBR-DAYS
028450**----------------------------------------------------------------
028460**  DO WE NEED TO DO ANY TIMEZONE CONVERSIONS?
028470**----------------------------------------------------------------
028480*     SET DE-YYMMDD-FORMAT          TO TRUE
028490*     SET DE-REFORMAT-ONLY          TO TRUE
028500*     MOVE AJ-ON-DUTY-DATE          TO DE-YYMMDD
028510*     PERFORM P8998-DATEEDIT
028520*     MOVE DE-CCYYMMDD              TO PS08-OD-DATE-CENT
028530*     MOVE AJ-ON-DUTY-HRMN          TO PS08-OD-TIME
028540*
028550*     MOVE WS-OFF-DUTY-DT-TM-CE     TO PS08-CALC-DATE-TIME
028560*
028570*     MOVE AJ-TRAIN-SYMBOL          TO PS08-TRAIN-ASGN
028580*     EXEC CICS LINK
028590*               PROGRAM(PS08-PGM)
028600*               COMMAREA(PS08-COMMAREA-PARMS)
028610*               LENGTH(PS08-LGTH)
028620*               RESP(WS-RESPONSE)
028630*     END-EXEC
028640*     MOVE WS-RESPONSE             TO FILE-STATUS
028650*     IF NOT SUCCESS
028660*        MOVE 'P2320-1'            TO ERR-PARAGRAPH
028670*        MOVE 'PS08LINK'           TO ERR-KEY
028680*        PERFORM P9999-GOT-PROBLEM
028690*     END-IF
028700*     IF NOT PS08-NO-ERRORS
028710*        MOVE 'P2320-2'            TO ERR-PARAGRAPH
028720*        MOVE PS08-RETURN-ERRORS   TO ERR-KEY
028730*        MOVE 'CHECK S08 INPUT PARAMETERS'
028740*                                  TO ERR-SENTENCE
028750*        PERFORM P9999-GOT-PROBLEM
028760*     END-IF
028770*
028780*     PERFORM P2321-DETERMINE-YARD-TTOD
028790*
028800*     ADD PS08-TOTAL-HRS-ON-DUTY    TO WS-TOTAL-HOURS
028810*     ADD PS08-TOTAL-MNS-ON-DUTY    TO WS-TOTAL-MINUTES
028820*     PERFORM P2323-CALCULATE-64-TIME
028830*
028840*     IF WS-64-TOT-TIME-ON-DUTY >  6400
028850*        SET P922-IMPOSED-REST TO TRUE
028860*     END-IF.
028870**
028880* P2321-DETERMINE-YARD-TTOD.
028890**
028900*     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
028910*     SET PARM-DIFF                 TO TRUE
028920*     MOVE WS-OFF-DUTY-DATE         TO PARM-PRI-DATE-GREG
028930*     MOVE WS-OFF-DUTY-TIME         TO PARM-PRI-HRMN
028940*     MOVE AJ-ON-DUTY-DATE          TO PARM-SEC-DATE-GREG
028950*     MOVE AJ-ON-DUTY-HRMN          TO PARM-SEC-HRMN
028960*     EXEC CICS LINK
028970*               PROGRAM (P903-PGM)
028980*               COMMAREA(DATE-CONVERSION-PARMS)
028990*               LENGTH  (P903-LGTH)
029000*               RESP    (WS-RESPONSE)
029010*     END-EXEC
029020*     MOVE WS-RESPONSE              TO FILE-STATUS
029030*     IF NOT SUCCESS
029040*        MOVE 'P2321-1'             TO ERR-PARAGRAPH
029050*        PERFORM P9999-GOT-PROBLEM
029060**     END-IF
029070**     MULTIPLY PARM-RES-TOT-DAYS BY 24 GIVING WS-TOTAL-HOURS
029080*     ADD PARM-RES-HR               TO WS-TOTAL-HOURS
029090*     MOVE PARM-RES-MN              TO WS-TOTAL-MINUTES.
029100*****************************************************************
029110* P2323-CALCULATE-64-TIME.
029120*****************************************************************
029130*     SET TIME-NOT-DONE             TO TRUE
029140*     PERFORM UNTIL TIME-DONE
029150*        IF WS-TOTAL-MINUTES > 59
029160*           SUBTRACT 60           FROM WS-TOTAL-MINUTES
029170*           ADD 1                   TO WS-TOTAL-HOURS
029180*        ELSE
029190*          SET TIME-DONE            TO TRUE
029200*        END-IF
029210*     END-PERFORM
029220*     MOVE WS-TOTAL-HOURS           TO WS-64-TOT-HRS-ON-DUTY
029230*     MOVE WS-TOTAL-MINUTES         TO WS-64-TOT-MNS-ON-DUTY.
025340*
025340*CNLD-302 E
025340
029240*
029250 P2500-APPLY-THRESHOLD.
029260*
029270* CALL PS94 TO LAYOFF EMP IF EXCEEDED STARTS. PS94-HOS-ACTION-YES
029280* FLAG DRIVES THE LAYOFF PROCESSING.
029290     MOVE SPACES                    TO PS94-COMMAREA-PARMS
029300                                       WS-HST-STORAGE
029310     SET PS94-HST-FUNC              TO TRUE
029320     SET PS94-HOS-ACTION-YES        TO TRUE
029330     MOVE WS-HOS-EMP-NBR            TO PS94-EMP-NBR
029340     PERFORM P2550-GET-HST-KEY
029350     MOVE WS-OFF-DUTY-DATE-TIME     TO PS94-HSL-EVENT-STRT-TS-DTTM
029360     MOVE SPACES                    TO WS-CNTL-FILE
029370     SET  SUB-DIST-TYPE-REC         TO TRUE
029380     MOVE AJ-JOB-DIST               TO CNTL-DIST
029390     MOVE AJ-JOB-SUB-DIST           TO CNTL-SUB-DIST
029400     MOVE CNTLKEY-AREA              TO CNTLKEY
029410     PERFORM P8610-READ-CNTLFILE
029420     IF SUCCESS
029430        MOVE CNTL-TIME-ZONE         TO PS94-TIME-ZONE
029440     ELSE
029450        MOVE PSTCA-TIME-ZONE        TO PS94-TIME-ZONE
029460     END-IF
029470     PERFORM P9993-CALL-PS94-PROGRAM
029480     IF PS94-HST-LAYOFF-DONE
029490        SET EMP-IMPOSED-REST-US-YES TO TRUE
029500     END-IF
029510     .
029520*
029530 P2550-GET-HST-KEY.
029540*
029550     MOVE SPACES                   TO ASGNKEY2
029560     MOVE ZEROS                    TO HST-SEQ
029570     MOVE PS94-EMP-NBR             TO ASGN-EMP-NO
029580     PERFORM PXXXX-JOB-OWNED
029590     IF ASGN-ASSIGNMENT > SPACE
029600        MOVE ASGN-DIST             TO HST-DIST
029610        MOVE ASGN-SUB-DIST         TO HST-SUB-DIST
029620        EVALUATE TRUE
029630           WHEN ASGN-JOB-TYPE = 'U'
029640              MOVE 'U'             TO HST-ASGN-TYPE
029650           WHEN ASGN-JOB-TYPE = 'X'
029660              MOVE 'X'             TO HST-ASGN-TYPE
029670           WHEN ASGN-JOB-TYPE = 'A'
029680              MOVE AJ-JOB-ASSIGNMENT
029690                                   TO JOB-DEF-CHECK
029700              IF JOB-DEF-YARDMASTER-ASGN
029710                 MOVE 'Y'          TO HST-ASGN-TYPE
029720              ELSE
029730                 MOVE 'O'          TO HST-ASGN-TYPE
029740              END-IF
029750        END-EVALUATE
029760        MOVE ASGN-AJ-JOB-NO        TO HST-ASGN-ID
029770     END-IF
029780     MOVE SPACES                   TO ASGNKEY2
029790     MOVE EMP-NBR OF WS-MSTR       TO ASGN-EMP-NO
029800     PERFORM PXXXX-LATEST-TEMP-JOB
029810     IF ASGN-ASSIGNMENT > SPACE
029820        MOVE ASGN-DIST             TO HST-DIST
029830        MOVE ASGN-SUB-DIST         TO HST-SUB-DIST
029840        EVALUATE TRUE
029850           WHEN ASGN-JOB-TYPE = 'U'
029860              MOVE 'U'             TO HST-ASGN-TYPE
029870           WHEN ASGN-JOB-TYPE = 'X'
029880              MOVE 'X'             TO HST-ASGN-TYPE
029890           WHEN ASGN-JOB-TYPE = 'A'
029900              MOVE AJ-JOB-ASSIGNMENT
029910                                   TO JOB-DEF-CHECK
029920              IF JOB-DEF-YARDMASTER-ASGN
029930                 MOVE 'Y'          TO HST-ASGN-TYPE
029940              ELSE
029950                 MOVE 'O'          TO HST-ASGN-TYPE
029960              END-IF
029970        END-EVALUATE
029980        MOVE ASGN-AJ-JOB-NO        TO HST-ASGN-ID
029990     END-IF
030000     IF HST-KEY NOT > SPACES
030010        MOVE DIST      OF WS-MSTR TO HST-DIST
030020        MOVE SUB-DIST  OF WS-MSTR TO HST-SUB-DIST
030030     END-IF
030040
030050     PERFORM PSQL-HSTOPN02
030060     PERFORM PSQL-HSTFET02
030070     PERFORM PSQL-HSTCLO02
030080     IF HST-SEQ > ZERO
030090        MOVE HST-KEY                TO PS94-HST-KEY
030100     END-IF.
030110*
030120 P3000-CALL-SHIFT.
030130*
030140     MOVE SPACES                    TO P08S-TSQ-AREA
030150                                       P08S-TSQ4-AREA
030160                                       WS-VRU-SCAL-TABLE
030170     MOVE ZEROS                     TO P08S-TSQ-CNT
030180                                       P08S-TSQ-ACT-CNT
030190                                       P08SCA-SUB2
030200                                       WS-TOT-JOBS
030210     MOVE CALL-DAY1                 TO WS-START-DAY
030220     MOVE START-DATE-1              TO WS-START-DATE
030230     MOVE START-TIME-1              TO WS-START-TIME
030240     MOVE END-TIME-1                TO WS-END-TIME
030250     PERFORM P3001-BROWSE-SCHEDULES
030260     IF WS-CALL-AREA-1 NOT = WS-CALL-AREA-2
030270        MOVE CALL-DAY2              TO WS-START-DAY
030280        MOVE START-DATE-2           TO WS-START-DATE
030290        MOVE START-TIME-2           TO WS-START-TIME
030300        MOVE END-TIME-2             TO WS-END-TIME
030310        PERFORM P3001-BROWSE-SCHEDULES
030320     END-IF.
030330*
030340 P3001-BROWSE-SCHEDULES.
030350*
030360     MOVE SPACES                    TO WORK-JS-KEY2
030370     MOVE SCR08S-DIST               TO WK-JSK2-ASGN-DIST
030380     MOVE SCR08S-SUB-DIST           TO WK-JSK2-ASGN-SUB-DIST
030390     MOVE WS-START-DAY              TO WK-JSK2-ASGN-DAY-NUM
030400     MOVE WS-START-TIME             TO WK-JSK2-ASGN-START-TIME
030410     MOVE WORK-JS-KEY2              TO JSKEY2
030420     MOVE '0'                       TO DONE-CODE
030430     PERFORM P3002-START-JSKEY2
030440     IF NOT SUCCESS
030450        SET DONE                    TO TRUE
030460        IF NOT (NO-RECORD-FND OR END-OF-FILE)
030470           MOVE 'P3001-1'           TO ERR-PARAGRAPH
030480           MOVE JSKEY2              TO ERR-KEY
030490           PERFORM P9999-GOT-PROBLEM
030500        END-IF
030510     END-IF
030520     PERFORM UNTIL DONE
030530        MOVE SPACES                 TO TEMPORARY-ASGNMT
030540                                       ON-DUTY-ASGNMT
030550                                       SAVE-ONDUTY-FLAG
030560        PERFORM P3003-READNEXT-JSKEY2
030570        IF SUCCESS
030580           IF  JSK2-ASGN-DIST        = WK-JSK2-ASGN-DIST
030590           AND JSK2-ASGN-SUB-DIST    = WK-JSK2-ASGN-SUB-DIST
030600           AND NOT JSK2-SCH-INACTIVE
030610           AND JSK2-ASGN-DAY-NUM     = WS-START-DAY
030620           AND JSK2-ASGN-START-TIME NOT > WS-END-TIME
030630              IF NOT JSK2-ASGN-SPAREBOARD AND
030640                 NOT JSK2-ASGN-TRAIN
030650                 MOVE JSK2-ASGN        TO JOB-DEF-CHECK
030660                 MOVE SPACES           TO TRCN-KEY3
030670                 MOVE JSK2-ASGN        TO TRCN-ASSIGNMENT
030680                 MOVE JSK2-ASGN-DIST   TO TRCN-DIST3
030690                 MOVE JSK2-ASGN-SUB-DIST
030700                                       TO TRCN-SDIST3
030710                 MOVE TRCN-KEY3        TO TRCNKEY3
030720                 PERFORM P8630-READ-TRCNKEY3
030730                 IF SUCCESS
030740                    CONTINUE
030750                 ELSE
030760                    IF NO-RECORD-FND
030770                       MOVE SPACES        TO TRCN-SHIFT-CALL-TIE
030780                    ELSE
030790                       MOVE 'P3001-2'     TO ERR-PARAGRAPH
030800                       MOVE TRCNKEY3      TO ERR-KEY
030810                       PERFORM P9999-GOT-PROBLEM
030820                    END-IF
030830                 END-IF
030840                 SET DE-YYMMDD-FORMAT     TO TRUE
030850                 MOVE START-DATE-1        TO DE-YYMMDD
030860                 PERFORM P8998-DATEEDIT
030870                 MOVE DE-CCYYMMDD         TO DE-COMPARE1-DATE
030880
030890                 SET DE-YYMMDD-FORMAT     TO TRUE
030900                 MOVE JSK2-EXP-DATE       TO DE-YYMMDD
030910                 PERFORM P8998-DATEEDIT
030920                 MOVE DE-CCYYMMDD         TO DE-COMPARE2-DATE
030930
030940                 IF DE-COMPARE1-DATE       > DE-COMPARE2-DATE
030950*                IF START-DATE-1           > JSK2-EXP-DATE
030960                    MOVE SPACES           TO TRCN-SHIFT-CALL-TIE
030970                 END-IF
030980                 IF TRCN-SHIFT-CALL-TIE-YES
030990                 OR TRCN-SHIFT-CALL-YES
031000                    MOVE SPACES        TO WS-ANNUL-FROM-DATE-CENT
031010                                          WS-ANNUL-TO-DATE-CENT
031020                    MOVE JSK2-ASGN-DIST
031030                                       TO AJ-JOB-DIST
031040                    MOVE JSK2-ASGN-SUB-DIST
031050                                       TO AJ-JOB-SUB-DIST
031060                    MOVE JSK2-ASGN     TO AJ-JOB-ASGN-ID
031070                    MOVE JSK2-ASGN-CC  TO AJ-JOB-ASGN-CC
031080                    MOVE AJJOBKEY-AREA TO AJJOBKEY
031090                    PERFORM P8640-READ-AJJOBKEY
031100                    IF SUCCESS
031110                       IF AJ-ANNUL-FROM-DATE > '000000'
031120                          MOVE AJ-ANNUL-FROM-DATE
031130                                       TO WS-ANNUL-FROM-DATE
031140                          MOVE AJ-ANNUL-TO-DATE
031150                                       TO WS-ANNUL-TO-DATE
031160                       END-IF
031170                       MOVE AJ-JOB-ON-DUTY-CODE
031180                                       TO SAVE-ONDUTY-FLAG
031190                    END-IF
031200                    IF JSK1-ASGN(1:2) NOT = 'EX'
031210                       MOVE JS-KEY1       TO AJJOBKEY
031220                       PERFORM P8640-READ-AJJOBKEY
031230                       IF NOT SUCCESS
031240                          MOVE 'P3001-3'  TO ERR-PARAGRAPH
031250                          MOVE AJJOBKEY   TO ERR-KEY
031260                          PERFORM P9999-GOT-PROBLEM
031270                       END-IF
031280                    END-IF
031290*-----------------------------------------------------------*
031300*                  CONVERT TO CNTL-TIME-ZONE                *
031310*-----------------------------------------------------------*
031320                    IF JSK1-ASGN-DIST        NOT = SCR08S-DIST
031330                    OR JSK1-ASGN-SUB-DIST    NOT = SCR08S-SUB-DIST
031340                       MOVE SPACES            TO CNTLKEY-AREA
031350                       MOVE '02'              TO CNTL-REC-TYPE
031360                       MOVE JSK1-ASGN-DIST    TO CNTL-DIST
031370                       MOVE JSK1-ASGN-SUB-DIST TO CNTL-SUB-DIST
031380                       MOVE CNTLKEY-AREA      TO CNTLKEY
031390                       PERFORM P8610-READ-CNTLFILE
031400                       IF NOT SUCCESS
031410                          MOVE 'P3001-4'      TO ERR-PARAGRAPH
031420                          MOVE CNTLKEY        TO ERR-KEY
031430                          PERFORM P9999-GOT-PROBLEM
031440                       END-IF
031450                       MOVE SPACES            TO TZ-PARAMETERS
031460                       MOVE CNTL-TIME-ZONE    TO TZ-OUT-ZONE
031470                       MOVE PSTCA-TIME-ZONE   TO TZ-IN-ZONE
031480                       MOVE AJ-EFF-DATE       TO TZ-IN-DATE
031490                       MOVE '0001'            TO TZ-IN-TIME
031500                       PERFORM P8996-TIMEZONE
031510                       IF TZ-INVALID-PARAMETERS
031520                          MOVE '3001-TZ'  TO ERR-PARAGRAPH
031530                          PERFORM P8996-TZERROR
031540                       END-IF
031550                    ELSE
031560                       MOVE AJ-EFF-DATE       TO TZ-OUT-DATE
031570                    END-IF
031580                    SET DE-YYMMDD-FORMAT     TO TRUE
031590                    MOVE TZ-OUT-DATE         TO DE-YYMMDD
031600                    PERFORM P8998-DATEEDIT
031610                    MOVE DE-CCYYMMDD         TO DE-COMPARE1-DATE
031620
031630                    SET DE-YYMMDD-FORMAT     TO TRUE
031640                    MOVE WS-START-DATE       TO DE-YYMMDD
031650                    PERFORM P8998-DATEEDIT
031660                    MOVE DE-CCYYMMDD         TO DE-COMPARE2-DATE
031670
031680                    IF NOT AJ-JOB-ON-DUTY
031690                       AND DE-COMPARE1-DATE NOT > DE-COMPARE2-DATE
031700                       MOVE AJ-JOB-ASGN-CC
031710                                       TO WS-CRAFT-CODE-CHECK
031720                       IF (SCR08S-EN-TR-CODE = 'EN'
031730                             AND ENGINE-CRAFT)
031740                          OR (SCR08S-EN-TR-CODE = 'TR'
031750                             AND NOT ENGINE-CRAFT)
031760                          OR SCR08S-EN-TR-CODE NOT > SPACE
031770                          PERFORM P3010-CROSS-CHECK-EMP-SCHED
031780                       END-IF
031790                    END-IF
031800                 END-IF
031810              END-IF
031820           ELSE
031830              SET DONE              TO TRUE
031840           END-IF
031850        ELSE
031860           SET DONE                 TO TRUE
031870           IF NOT (NO-RECORD-FND OR END-OF-FILE)
031880              MOVE 'P3001-5'        TO ERR-PARAGRAPH
031890              MOVE JSKEY2           TO ERR-KEY
031900              PERFORM P9999-GOT-PROBLEM
031910           END-IF
031920        END-IF
031930     END-PERFORM
031940     PERFORM P3004-END-JSKEY2.
031950*
031960 P3002-START-JSKEY2.
031970*
031980     EXEC CICS STARTBR
031990               DATASET(JS-VIA-JSKEY2)
032000               RIDFLD(JSKEY2)
032010               GTEQ
032020               RESP(WS-RESPONSE)
032030     END-EXEC
032040     MOVE WS-RESPONSE               TO FILE-STATUS.
032050*
032060 P3003-READNEXT-JSKEY2.
032070*
032080     EXEC CICS READNEXT
032090               DATASET(JS-VIA-JSKEY2)
032100               INTO(WS-JOB-SCHEDULE)
032110               LENGTH(JSKEY2-RLGTH)
032120               RIDFLD(JSKEY2)
032130               KEYLENGTH(JSKEY2-KLGTH)
032140               RESP(WS-RESPONSE)
032150     END-EXEC
032160     MOVE WS-RESPONSE               TO FILE-STATUS.
032170*
032180 P3004-END-JSKEY2.
032190*
032200     EXEC CICS ENDBR
032210               DATASET(JS-VIA-JSKEY2)
032220               RESP(WS-RESPONSE)
032230     END-EXEC.
032240*
032250 P3010-CROSS-CHECK-EMP-SCHED.
032260*
032270     PERFORM P3004-END-JSKEY2
032280*
032290*    FIRST LOCK IN ON THE CORRECT SCHEDULE "SET" BY DETERMINING
032300*    THE EXPIRATION DATE
032310*
032320     MOVE WS-JOB-SCHEDULE           TO WS-HOLD-JOB-SCHED
032330     MOVE JS-KEY1                   TO WORK-JS-KEY1
032340     MOVE JS-KEY2                   TO WORK-JS-KEY2
032350     IF JSK2-ASGN-DAY-NUM = CALL-DAY2
032360        MOVE START-DATE-2           TO JSK1-EXP-DATE
032370     ELSE
032380        MOVE START-DATE-1           TO JSK1-EXP-DATE
032390     END-IF
032400     SET JSK1-ASGN-STATS            TO TRUE
032410     MOVE SPACES                    TO JSK1-ASGN-START-TIME
032420     MOVE JS-KEY1                   TO JSKEY1
032430     EXEC CICS READ
032440               DATASET(JS-VIA-JSKEY1)
032450               INTO(WS-JOB-SCHEDULE)
032460               LENGTH(JSKEY1-RLGTH)
032470               RIDFLD(JSKEY1)
032480               KEYLENGTH(JSKEY1-KLGTH)
032490               GTEQ
032500               RESP(WS-RESPONSE)
032510     END-EXEC
032520     MOVE WS-RESPONSE               TO FILE-STATUS
032530     IF  SUCCESS
032540     AND JSK1-ASGN-DIST     = WK-JSK1-ASGN-DIST
032550     AND JSK1-ASGN-SUB-DIST = WK-JSK1-ASGN-SUB-DIST
032560     AND JSK1-ASGN          = WK-JSK1-ASGN
032570     AND JSK1-ASGN-CC       = WK-JSK1-ASGN-CC
032580     AND JSK1-EXP-DATE      = WK-JSK1-EXP-DATE
032590     AND JSK1-ASGN-STATS
032600        MOVE WS-HOLD-JOB-SCHED      TO WS-JOB-SCHEDULE
032610        MOVE SPACES                 TO TRCN-KEY3
032620        MOVE JSK2-ASGN-DIST         TO TRCN-DIST3
032630        MOVE JSK2-ASGN-SUB-DIST     TO TRCN-SDIST3
032640        MOVE JSK2-ASGN              TO TRCN-ASSIGNMENT
032650        MOVE TRCN-KEY3              TO TRCNKEY3
032660        PERFORM P8630-READ-TRCNKEY3
032670        IF SUCCESS
032680           PERFORM P3020-EDIT-FOR-SELECTION
032690        ELSE
032700           IF NOT NO-RECORD-FND
032710              MOVE 'P3010-1'        TO ERR-PARAGRAPH
032720              MOVE TRCNKEY3         TO ERR-KEY
032730              PERFORM P9999-GOT-PROBLEM
032740           END-IF
032750        END-IF
032760     ELSE
032770        IF SUCCESS
032780           SET NO-RECORD-FND        TO TRUE
032790        END-IF
032800        IF NOT (NO-RECORD-FND OR END-OF-FILE)
032810           MOVE 'P3010-2'           TO ERR-PARAGRAPH
032820           MOVE JSKEY1              TO ERR-KEY
032830           PERFORM P9999-GOT-PROBLEM
032840        END-IF
032850     END-IF
032860*
032870*    NOW RESET POSITION BACK TO WHERE WE WERE IN THE FILE
032880*
032890     MOVE WORK-JS-KEY2              TO JSKEY2
032900     PERFORM P3002-START-JSKEY2
032910     IF SUCCESS
032920        PERFORM P3003-READNEXT-JSKEY2
032930        IF NOT SUCCESS
032940           MOVE 'P3010-3'           TO ERR-PARAGRAPH
032950           MOVE WORK-JS-KEY2        TO ERR-KEY
032960           PERFORM P9999-GOT-PROBLEM
032970        END-IF
032980     ELSE
032990        MOVE 'P3010-4'              TO ERR-PARAGRAPH
033000        MOVE WORK-JS-KEY2           TO ERR-KEY
033010        PERFORM P9999-GOT-PROBLEM
033020     END-IF.
033030*
033040 P3020-EDIT-FOR-SELECTION.
033050*
033060*---------------------------------------------------------------*
033070*                   CONVERT  TO PSTCA-TIME-ZONE                 *
033080*---------------------------------------------------------------*
033090     IF (AJ-JOB-DIST     NOT = SCR08S-DIST
033100     OR  AJ-JOB-SUB-DIST NOT = SCR08S-SUB-DIST)
033110     AND AJ-JOB-ON-DUTY-TIME > '0000000000'
033120        MOVE SPACES            TO CNTLKEY-AREA
033130        MOVE '02'              TO CNTL-REC-TYPE
033140        MOVE AJ-JOB-DIST       TO CNTL-DIST
033150        MOVE AJ-JOB-SUB-DIST   TO CNTL-SUB-DIST
033160        MOVE CNTLKEY-AREA      TO CNTLKEY
033170        PERFORM P8610-READ-CNTLFILE
033180        IF NOT SUCCESS
033190           MOVE 'P3020-C'      TO ERR-PARAGRAPH
033200           MOVE CNTLKEY        TO ERR-KEY
033210           PERFORM P9999-GOT-PROBLEM
033220        END-IF
033230        MOVE SPACES            TO TZ-PARAMETERS
033240        MOVE CNTL-TIME-ZONE    TO TZ-IN-ZONE
033250        MOVE PSTCA-TIME-ZONE   TO TZ-OUT-ZONE
033260        MOVE AJ-JOB-ON-DUTY-TIME TO TZ-IN-DATE-TIME
033270        PERFORM P8996-TIMEZONE
033280        IF TZ-INVALID-PARAMETERS
033290           MOVE 'P3020TZ'  TO ERR-PARAGRAPH
033300           PERFORM P8996-TZERROR
033310        END-IF
033320        MOVE TZ-OUT-DATE-TIME  TO AJ-JOB-ON-DUTY-TIME
033330     END-IF
033340     MOVE ZEROS        TO DATE-CONVERSION-PARMS
033350
033360     MOVE SPACES                    TO CNTLKEY-AREA
033370                                       DAILY-MARKUP-YARD-FLAG
033380     MOVE '02'                      TO CNTL-REC-TYPE
033390     MOVE AJ-JOB-DIST               TO CNTL-DIST
033400     MOVE AJ-JOB-SUB-DIST           TO CNTL-SUB-DIST
033410     MOVE CNTLKEY-AREA              TO CNTLKEY
033420     PERFORM P8610-READ-CNTLFILE
033430     IF NOT SUCCESS
033440        MOVE 'P3020-1'              TO ERR-PARAGRAPH
033450        MOVE CNTLKEY                TO ERR-KEY
033460        PERFORM P9999-GOT-PROBLEM
033470     END-IF
033480     IF DAILY-MARKUP-ALLOWED
033490        SET THIS-IS-A-DAILY-MARKUP-YARD
033500                                    TO TRUE
033510     END-IF
033520*
033530*    VERIFY LAST DAY / TIME JOB WAS ON-DUTY AGAINST THE
033540*    JOB SCHEDULE AND THE CURRENT TIME FRAME.
033550*
033560     MOVE ZEROS TO DATE-CONVERSION-PARMS
033570     MOVE WS-SYSTEM-DATE TO PARM-PRI-DATE-GREG
033580     MOVE WS-SYSTEM-TIME TO PARM-PRI-HRMN
033590     SET PARM-SUBTRACT TO TRUE
033600     MOVE 2 TO PARM-SEC-DATE-GREG
033610     EXEC CICS LINK
033620               PROGRAM(P903-PGM)
033630               COMMAREA(DATE-CONVERSION-PARMS)
033640               LENGTH(P903-LGTH)
033650               RESP(WS-RESPONSE)
033660     END-EXEC
033670     MOVE WS-RESPONSE TO FILE-STATUS
033680     IF NOT SUCCESS
033690        MOVE 'P3001-6' TO ERR-PARAGRAPH
033700        MOVE 'P903LINK' TO ERR-KEY
033710        PERFORM P9999-GOT-PROBLEM
033720     END-IF
033730     MOVE PARM-RES-GREG-CENT TO WS-2B4-SYSTEM-CENT
033740     MOVE PARM-RES-DATE-GREG TO WS-2B4-SYSTEM-DATE
033750
033760     MOVE SPACES                    TO WK-CALL-DATE-TIME
033770     IF JSK2-ASGN-DAY-NUM = CALL-DAY1
033780        MOVE START-DATE-1           TO WK-CALL-DATE
033790        MOVE JSK2-ASGN-START-TIME   TO WK-CALL-TIME
033800        MOVE CALL-DAY1              TO CALL-DAY
033810     ELSE
033820        MOVE START-DATE-2           TO WK-CALL-DATE
033830        MOVE JSK2-ASGN-START-TIME   TO WK-CALL-TIME
033840        MOVE CALL-DAY2              TO CALL-DAY
033850     END-IF
033860     MOVE SPACES                    TO WS-AHIST-MISC
033870     MOVE JSK2-ASGN-DIST            TO AHM-DIST
033880     MOVE JSK2-ASGN-SUB-DIST        TO AHM-SUB-DIST
033890
033900     MOVE SPACES                    TO WS-POOL
033910     IF JOB-DEF-YARD-ASGN
033920        MOVE JOB-DEF-YARD-POOL      TO AHM-POOL
033930                                       WS-POOL
033940     ELSE
033950        IF JOB-DEF-LOCAL-ASGN
033960           MOVE JOB-DEF-LOCAL-POOL  TO AHM-POOL
033970                                       WS-POOL
033980        END-IF
033990     END-IF
034000     MOVE WK-CALL-DATE-TIME         TO AHM-EFF-DATE-TIME
034010     MOVE TRCN-TRAIN-ASGN           TO AHM-ASSIGNMENT
034020     SET AHM-EMPLOYEE-RECORD        TO TRUE
034030
034040     MOVE SPACES                    TO WS-CRAFT-SEQ
034050     SET CT-IND                     TO 1
034060     SEARCH CRAFT-CODE-TABLE VARYING CT-IND
034070            AT END CONTINUE
034080            WHEN CT-CRAFT-CODE(CT-IND) =
034090                 JSK2-ASGN-CC
034100                 MOVE CT-CRAFT-SEQ(CT-IND)
034110                                    TO AHM-CRAFT-SEQ
034120                                       WS-CRAFT-SEQ
034130     END-SEARCH
034140     MOVE AHM-KEY                   TO AHMKEY
034150     MOVE AHM-FILE                  TO AHM-DATASET
034160
034170     MOVE '0'                           TO WS-AHMISC-FOUND-FLAG
034180     PERFORM P8700-STRTBR-AHMFILE
034190     IF SUCCESS
034200        SET AHMISC-NOT-DONE             TO TRUE
034210        PERFORM UNTIL AHMISC-DONE
034220           PERFORM P8710-READNXT-AHMFILE
034230           IF SUCCESS
034240              IF AHM-DIST                = JSK2-ASGN-DIST
034250                 AND AHM-SUB-DIST        = JSK2-ASGN-SUB-DIST
034260                 AND AHM-POOL            = WS-POOL
034270                 AND AHM-EFF-DATE-TIME   = WK-CALL-DATE-TIME
034280                 AND AHM-ASSIGNMENT      = TRCN-TRAIN-ASGN
034290                 AND AHM-EMPLOYEE-RECORD
034300                 AND AHM-CRAFT-SEQ       = WS-CRAFT-SEQ
034310                 SET AHMISC-FOUND       TO TRUE
034320                 SET AHMISC-DONE        TO TRUE
034330              ELSE
034340                 SET AHMISC-DONE        TO TRUE
034350              END-IF
034360           ELSE
034370              SET AHMISC-DONE TO TRUE
034380              IF NOT (NO-RECORD-FND OR END-OF-FILE)
034390                 MOVE 'P3020-2'          TO ERR-PARAGRAPH
034400                 MOVE AHMKEY             TO ERR-KEY
034410                 PERFORM P9999-GOT-PROBLEM
034420              END-IF
034430           END-IF
034440        END-PERFORM
034450        PERFORM P8720-ENDBR-AHMFILE
034460     END-IF
034470     IF AHMISC-FOUND
034480        AND AJ-JOB-ON-DUTY-TIME > '0000000000'
034490        SET DE-YYMMDD-FORMAT        TO TRUE
034500        MOVE AJ-JOB-ON-DUTY-DATE    TO DE-YYMMDD
034510        PERFORM P8998-DATEEDIT
034520        MOVE DE-CCYYMMDD            TO DE-COMPARE1-DATE
034530     END-IF
034540     IF AHMISC-FOUND
034550        OR AJ-JOB-ON-DUTY
034560        OR (AJ-WAS-ANNULLED
034570           AND WK-CALL-DATE-TIME = AJ-JOB-ON-DUTY-TIME)
034580        OR (JSK2-ASGN-DAY-NUM = AJ-SCH-DAY AND
034590            JSK2-ASGN-START-TIME = AJ-SCH-TIME AND
034600            DE-COMPARE1-DATE    > WS-2B4-SYSTEM-DATE-CENT)
034610        AND AJ-JOB-ON-DUTY-TIME > '0000000000'
034620        CONTINUE
034630     ELSE
034640        MOVE SPACES                 TO WS-TRK-REASON
034650        IF WS-ANNUL-FROM-DATE > SPACES
034660           SET DE-YYMMDD-FORMAT     TO TRUE
034670           MOVE WS-ANNUL-FROM-DATE  TO DE-YYMMDD
034680           PERFORM P8998-DATEEDIT
034690           MOVE DE-CCYYMMDD         TO WS-ANNUL-FROM-DATE-CENT
034700
034710           SET DE-YYMMDD-FORMAT     TO TRUE
034720           MOVE WS-ANNUL-TO-DATE    TO DE-YYMMDD
034730           PERFORM P8998-DATEEDIT
034740           MOVE DE-CCYYMMDD         TO WS-ANNUL-TO-DATE-CENT
034750        END-IF
034760
034770        PERFORM P7300-CONVERT-WK-CALL-DATES
034780        IF WK-CALL-DATE-CENT < WS-ANNUL-FROM-DATE-CENT
034790        OR WK-CALL-DATE-CENT > WS-ANNUL-TO-DATE-CENT
034800        OR WS-ANNUL-FROM-DATE-CENT NOT > SPACES
034810           MOVE SPACES              TO WS-MSTR
034820           SET REST-OK              TO TRUE
034830           MOVE AJ-JOB-ASGN-CC      TO WS-CRAFT-CODE-CHECK
034840           IF THIS-IS-A-DAILY-MARKUP-YARD
034850              AND YARD-CRAFT
034860              PERFORM P3200-GET-MARKUP-EMP
034870           ELSE
034880              PERFORM P3100-GET-AJ-EMPS
034890           END-IF
034900           MOVE SPACES               TO CNTLKEY-AREA
034910           MOVE '02'                 TO CNTL-REC-TYPE
034920           MOVE SCR08S-DIST          TO CNTL-DIST
034930           MOVE SCR08S-SUB-DIST      TO CNTL-SUB-DIST
034940           MOVE CNTLKEY-AREA         TO CNTLKEY
034950           PERFORM P8610-READ-CNTLFILE
034960
034970           IF SUCCESS
034980              MOVE CNTL-US-DOM       TO WS-US-DOM-FLAG
034990              MOVE CNTL-TIME-ZONE    TO SAVE-TIME-ZONE
035000           END-IF
035010
035020           IF WS-US-DOM-YES
035030              MOVE ZEROES            TO WS-HOS-EMP-NBR
035040              MOVE SPACES            TO WS-EMP-HOS-FL
035050              IF ON-DUTY-EMP > ZERO
035060                 MOVE ON-DUTY-EMP    TO WS-HOS-EMP-NBR
035070              ELSE
035080                 IF AJ-TEMP-EMP-NBR > ZERO
035090                    MOVE AJ-TEMP-EMP-NBR
035100                                     TO WS-HOS-EMP-NBR
035110                 ELSE
035120                    IF AJ-EMP-NBR > ZERO
035130                       MOVE AJ-EMP-NBR
035140                                     TO WS-HOS-EMP-NBR
035150                    END-IF
035160                 END-IF
035170              END-IF
035180
035190              IF WS-HOS-EMP-NBR  > ZEROES
035200                 MOVE WS-HOS-EMP-NBR TO MSTRNBRK
035210                 PERFORM P8505-READ-MASTER-ONLY
035220                 IF AVAILABLE
035230                    MOVE SPACES      TO PS94-COMMAREA-PARMS
035240                                        WS-HST-STORAGE
035250                    SET  PS94-HST-FUNC
035260                                     TO TRUE
035270                    MOVE WS-HOS-EMP-NBR
035280                                     TO PS94-EMP-NBR
035290                    PERFORM P2550-GET-HST-KEY
035300                    MOVE WK-CALL-DATE-TIME
035310                                     TO
035320                                     PS94-HSL-EVENT-STRT-TS-DTTM
035330                    MOVE SAVE-TIME-ZONE
035340                                     TO PS94-TIME-ZONE
035350                    PERFORM P9993-CALL-PS94-PROGRAM
035360                    EVALUATE TRUE
035370                       WHEN PS94-HST-LT-ALERT-LIMIT
035380                       WHEN PS94-HST-LT-ALERT-THRESHOLD
035390                       WHEN PS94-HST-TT-ALERT-LIMIT
035400                       WHEN PS94-HST-TT-ALERT-THRESHOLD
035410                       WHEN PS94-HST-ST-ALERT-THRESHOLD
035420                            SET EMP-HOS-EXD TO TRUE
035430                            PERFORM P3400-SETUP-VAC-COMMAREA
035440                    END-EVALUATE
035450                 END-IF
035460              END-IF
035470           END-IF
035480
035490           IF ON-DUTY-EMP > ZERO
035500              MOVE ON-DUTY-EMP      TO MSTRNBRK
035510              PERFORM P3030-CHECK-FOR-VACANCY
035520           ELSE
035530              IF JOB-SCHED-ASGN-XTRA
035540                 SET WS-TRK-DONT-TRACK   TO TRUE
035550                 PERFORM P3400-SETUP-VAC-COMMAREA
035560              ELSE
035570                 IF AJ-TEMP-EMP-NBR > ZERO
035580                    MOVE AJ-TEMP-EMP-NBR
035590                                       TO MSTRNBRK
035600                    PERFORM P3030-CHECK-FOR-VACANCY
035610                 ELSE
035620                    IF AJ-EMP-NBR > ZERO
035630                       MOVE AJ-EMP-NBR TO MSTRNBRK
035640                       PERFORM P3030-CHECK-FOR-VACANCY
035650                    ELSE
035660                       SET WS-TRK-DONT-TRACK   TO TRUE
035670                       PERFORM P3400-SETUP-VAC-COMMAREA
035680                    END-IF
035690                 END-IF
035700              END-IF
035710           END-IF
035720        ELSE
035730           IF SCR08S-CODE NOT = 'V'
035740              PERFORM P3300-CALL
035750           END-IF
035760        END-IF
035770     END-IF.
035780*
035790 P3030-CHECK-FOR-VACANCY.
035800*
035810*    IN ADDITION TO CHECKING FOR A VACANCY, ALSO CHECK TO
035820*    THAT THE EMPLOYEE IS QUALIFIED AND NOT RESTRICTED.
035830*
035840     PERFORM P8500-READ-MASTER
035850     PERFORM P7200-CONVERT-MASTER-DATES
035860     PERFORM P7300-CONVERT-WK-CALL-DATES
035870     IF WS-LAST-TIE-UP-TIME-CENT > WK-CALL-DT-TM-CE
035880        SET WORKING IN WS-MSTR   TO TRUE
035890     END-IF
035900     IF AJ-TEMP-EMP-NBR > ZERO
035910        MOVE AJ-TEMP-EMP-NBR     TO WS-CURR-OWNER
035920     ELSE
035930        IF AJ-EMP-NBR > ZERO
035940           MOVE AJ-EMP-NBR       TO WS-CURR-OWNER
035950        ELSE
035960           MOVE ZEROES           TO WS-CURR-OWNER
035970           SET WS-TRK-DONT-TRACK TO TRUE
035980        END-IF
035990     END-IF
036000
036010* THE FOLLOWING IF STATEMENT WAS CHANGED TO FIX A BUG REPORTED BY
036020* CN DURING THE QUICK TIE ENHANCEMENT. EMPLOYEES IN UNAVAILABLE
036030* STATUS WERE NOT BEING PRESENTED FOR THE SECOND SPLIT OF GO
036040* TRAINS. IF THE IF STATEMENT IS MODIFIED, MAKE SURE THE BUG
036050* DOES NOT RE-OCCUR. - NJB
036060     MOVE OD-AREA                TO JOB-DEF-CHECK
036070     IF  WS-EMP-PERS-REST-CENT    NOT > WK-CALL-DT-TM-CE
036080     AND WS-LAST-TIE-UP-TIME-CENT NOT > WK-CALL-DT-TM-CE
036090     AND ((AVAILABLE
036100     AND   LAYOFF-CODE-2 NOT  = '1')
036110      OR  (ON-DUTY-EMP        > ZERO
036120     AND   JOB-DEF-COMMUTER-ASGN
036130     AND   JSK1-ASGN-DIST     = OD-DIST
036140     AND   JSK1-ASGN-SUB-DIST = OD-SUB-DIST
036150     AND   JSK1-ASSIGNMENT    = OD-AREA))
036160     AND NOT EMP-HOS-EXD
036170        SET  CONTINUE-VACANCY-CHECK TO TRUE
036180        IF ON-DUTY-EMP > ZERO
036190           CONTINUE
036200        ELSE
036210           IF AJ-TEMP-EMP-NBR > ZERO
036220*
036230*             IF THE TV OWNER OF THE JOB HOLDS A MORE RECENT TEMP
036240*             TREAT THE ASSIGNMENT AS A VACANCY.
036250*
036260              IF TA-DIST          NOT = JSK1-ASGN-DIST
036270                 OR TA-SUB-DIST   NOT = JSK1-ASGN-SUB-DIST
036280                 OR TA-AREA       NOT = JSK1-ASSIGNMENT
036290                 SET WS-TRK-DONT-TRACK TO TRUE
036300                 PERFORM P3400-SETUP-VAC-COMMAREA
036310                 MOVE ZEROES  TO CONTINUE-VACANCY-CHECK-FL
036320              END-IF
036330           ELSE
036340              IF AJ-EMP-NBR > ZERO
036350                 IF TEMPORARY-ASGNMT > SPACES
036360                    SET WS-TRK-DONT-TRACK TO TRUE
036370                    PERFORM P3400-SETUP-VAC-COMMAREA
036380                    MOVE ZEROES  TO CONTINUE-VACANCY-CHECK-FL
036390                 END-IF
036400              END-IF
036410           END-IF
036420           IF CONTINUE-VACANCY-CHECK
036430              IF ON-DUTY-ASGNMT > SPACES
036440                 SET WS-TRK-ON-DUTY     TO TRUE
036450                 PERFORM P3400-SETUP-VAC-COMMAREA
036460                 MOVE ZEROES  TO CONTINUE-VACANCY-CHECK-FL
036470              END-IF
036480           END-IF
036490        END-IF
036500
036510* IF THE OWNER OF AN ASSIGNMENT WAS NOT AVAILABLE TO
036520* WORK FOR THE FIRST SHIFT OF A GO TRAIN THEN
036530* THE OWNER IS NOT ENTITLED TO WORK THE SECOND SHIFT
036540* -- PREVIOUSLY THE SHIFT CALL WAS CALLING THE OWNER OF THE ASSIGN
036550        IF CONTINUE-VACANCY-CHECK
036560           MOVE JSK1-ASSIGNMENT TO JOB-DEF-CHECK
036570           IF JOB-DEF-COMMUTER-ASGN
036580              IF AJ-EMP-NBR > ZERO AND
036590                 AJ-EMP-NBR = EMP-NBR IN WS-MSTR
036600                  SET OWNER-ELIGIBLE TO TRUE
036610                  PERFORM P3050-CHECK-EARLIER-SHIFT
036620                  IF NOT-OWNER-ELIGIBLE
036630                     SET WS-TRK-UNKNOWN            TO TRUE
036640                     PERFORM P3400-SETUP-VAC-COMMAREA
036650                     MOVE ZEROES  TO CONTINUE-VACANCY-CHECK-FL
036660                  END-IF
036670              END-IF
036680           END-IF
036690        END-IF
036700
036710        IF CONTINUE-VACANCY-CHECK
036720           MOVE JSK2-ASGN-CC       TO WS-CRAFT-CODE-CHECK
036730
036740           PERFORM P3035-CHECK-REQUIRED-REST
036750           IF REST-OK
036760*               CHECK FOR QUALIFICATIONS AND RESTRICTIONS
036770              PERFORM P3040-CHECK-QUALIFICATIONS
036780              IF EMP-DISQUALIFIED
036790                 IF EMP-NBR IN WS-MSTR = WS-CURR-OWNER
036800                    SET WS-TRK-NOT-QUALIFIED      TO TRUE
036810                 END-IF
036820              ELSE
036830                 PERFORM P3060-CHECK-STARTS
036840                 PERFORM P3080-CHECK-QUICK-TIES
036850                 IF P980-QUICKTIE-RESTRICTED
036860                    SET QTIE-DISQUALIFIED      TO TRUE
036870                    SET EMP-DISQUALIFIED       TO TRUE
036880                    IF EMP-NBR IN WS-MSTR = WS-CURR-OWNER
036890                       SET WS-TRK-QUICK-TIE    TO TRUE
036900                    END-IF
036910                 END-IF
036920              END-IF
036930              IF EMP-DISQUALIFIED
036940                 PERFORM P3400-SETUP-VAC-COMMAREA
036950                 MOVE SPACES TO WS-EMP-DISQUALIFIED
036960                                WS-QTIE-DISQUALIFIED
036970              ELSE
036980                 IF SCR08S-CODE NOT = 'V'
036990                    PERFORM P3300-CALL
037000                 END-IF
037010              END-IF
037020           ELSE
037030              IF EMP-NBR IN WS-MSTR = WS-CURR-OWNER
037040                 SET WS-TRK-REQUIRED-REST TO TRUE
037050              END-IF
037060              PERFORM P3400-SETUP-VAC-COMMAREA
037070           END-IF
037080        END-IF
037090     ELSE
037100*
037110*       DETERMINE THE TRACKING REASON
037120*
037130        IF EMP-NBR IN WS-MSTR NOT = WS-CURR-OWNER AND
037140           WS-CURR-OWNER > ZEROES
037150           SET WS-TRK-UNKNOWN            TO TRUE
037160        ELSE
037170           IF AJ-TEMP-EMP-NBR > ZERO
037180*
037190*             IF THE TV OWNER OF THE JOB HOLDS A MORE RECENT TEMP
037200*             TREAT THE ASSIGNMENT AS A VACANCY.
037210*
037220              IF TA-DIST          NOT = JSK1-ASGN-DIST
037230                 OR TA-SUB-DIST   NOT = JSK1-ASGN-SUB-DIST
037240                 OR TA-AREA       NOT = JSK1-ASSIGNMENT
037250                 SET WS-TRK-DONT-TRACK TO TRUE
037260              ELSE
037270                 IF AJ-EMP-NBR > ZERO
037280                    IF TEMPORARY-ASGNMT > SPACES
037290                       SET WS-TRK-DONT-TRACK TO TRUE
037300                    END-IF
037310                 END-IF
037320              END-IF
037330           END-IF
037340        END-IF
037350        IF WS-TRK-REASON NOT > SPACES
037360           IF ON-DUTY-ASGNMT > SPACES OR WORKING
037370              SET WS-TRK-ON-DUTY               TO TRUE
037380           ELSE
037390              IF NOT-NOTIFIED
037400                 SET WS-TRK-NOT-NOTIFIED       TO TRUE
037410              ELSE
037420                 PERFORM P3035-CHECK-REQUIRED-REST
037430                 IF REST-NOT-OK
037440                    SET WS-TRK-REQUIRED-REST   TO TRUE
037450                 ELSE
037460                    IF NOT AVAILABLE
037470                       SET WS-TRK-UNAVAIL      TO TRUE
037480                    ELSE
037490                       IF WS-EMP-PERS-REST-CENT > WK-CALL-DT-TM-CE
037500                          SET WS-TRK-PERS-REST TO TRUE
037510                       END-IF
037520                    END-IF
037530                 END-IF
037540              END-IF
037550           END-IF
037560        END-IF
037570
037580        IF WS-EMP-PERS-REST-CENT > WK-CALL-DT-TM-CE
037590           SET REST-NOT-OK     TO TRUE
037600        END-IF
037610        IF NOT EMP-HOS-EXD
037620           PERFORM P3400-SETUP-VAC-COMMAREA
037630        END-IF
037640     END-IF
037650     .
037660*
037670 P3031-DETERMINE-IF-HOS-EXEMPT.
037680*
037690     MOVE WS-CNTL-FILE            TO SAVE-CNTL-FILE
037700
037710     MOVE SAVE-2A-CNTL-FILE       TO WS-CNTL-FILE
037720     IF SUB-DIST-TYPE-2A-REC AND
037730        CNTL-DIST                  = AJ-JOB-DIST AND
037740        CNTL-SUB-DIST              = AJ-JOB-SUB-DIST
037750        CONTINUE
037760     ELSE
037770        MOVE SPACES               TO WS-CNTL-FILE
037780        MOVE AJ-JOB-DIST          TO CNTL-DIST
037790        MOVE AJ-JOB-SUB-DIST      TO CNTL-SUB-DIST
037800        SET SUB-DIST-TYPE-2A-REC  TO TRUE
037810        MOVE CNTLKEY-AREA         TO CNTLKEY
037820        PERFORM P8610-READ-CNTLFILE
037830        IF NOT SUCCESS
037840           MOVE 'P3031-1'         TO ERR-PARAGRAPH
037850           MOVE CNTLKEY           TO ERR-KEY
037860           PERFORM P9999-GOT-PROBLEM
037870        ELSE
037880           MOVE WS-CNTL-FILE      TO SAVE-2A-CNTL-FILE
037890        END-IF
037900     END-IF
037910
037920     IF CNTL-YDM-HOS-EXEMPT
037930        MOVE ZERO                 TO EMP-MTOD
037940        SET HOS-EXEMPT            TO TRUE
037950     END-IF
037960
037970     MOVE SAVE-CNTL-FILE          TO WS-CNTL-FILE.
037980*
037990 P3035-CHECK-REQUIRED-REST.
038000*
038010*    CHECK IF U.S. REST IS REQUIRED
038020*
038030     PERFORM P7300-CONVERT-WK-CALL-DATES
038040*
038050*    CHECK IF MTOD IS REQUIRED
038060*
038070     PERFORM P3036-DETERMINE-PREP-TIME
038080     PERFORM P3038-DETERMINE-EARLIEST-CALL
038090     PERFORM P3038A-DETERMINE-EARLI-US-CALL
038100
038110     IF ENGINE-CRAFT
038120        IF TRCN-EN-REST-REQ = ('U' OR 'B')
038130           IF WS-US-DOM-YES AND
038140              WS-HS-US-ACTIVATE
038150              IF WS-EARLI-US-CENT-DATE-TIME >
038160                  WS-EMP-OD-CENT-DATE-TIME
038170                  SET REST-NOT-OK  TO TRUE
038180              END-IF
038190           ELSE
038200              IF WS-EMP-US-RSTD-CENT > WK-CALL-DT-TM-CE
038210                 SET REST-NOT-OK  TO TRUE
038220              END-IF
038230           END-IF
038240        END-IF
038250     ELSE
038260        IF TRCN-TR-REST-REQ = ('U' OR 'B')
038270           IF WS-US-DOM-YES AND
038280              WS-HS-US-ACTIVATE
038290              IF WS-EARLI-US-CENT-DATE-TIME >
038300                  WS-EMP-OD-CENT-DATE-TIME
038310                  SET REST-NOT-OK  TO TRUE
038320              END-IF
038330           ELSE
038340              IF WS-EMP-US-RSTD-CENT > WK-CALL-DT-TM-CE
038350                 SET REST-NOT-OK  TO TRUE
038360              END-IF
038370           END-IF
038380        END-IF
038390     END-IF
038400
038410     IF ENGINE-CRAFT
038420        IF TRCN-EN-REST-REQ = ('C' OR 'B')
038430           OR (TRCN-EN-REST-REQ = 'N' AND
038440              EMP-LAST-TOUR-OUT-OF-BOX)
038450           IF WS-EARLIEST-CENT-DATE-TIME >
038460               WS-EMP-OD-CENT-DATE-TIME
038470              SET REST-NOT-OK  TO TRUE
038480           END-IF
038490        END-IF
038500     ELSE
038510        IF TRCN-TR-REST-REQ = ('C' OR 'B')
038520           OR (TRCN-TR-REST-REQ = 'N' AND
038530              EMP-LAST-TOUR-OUT-OF-BOX)
038540           IF WS-EARLIEST-CENT-DATE-TIME >
038550               WS-EMP-OD-CENT-DATE-TIME
038560              SET REST-NOT-OK  TO TRUE
038570           END-IF
038580        END-IF
038590     END-IF
038600     MOVE ZEROES                TO SCHED-DONE-CODE
038610                                   SCHED-2ND-CODE
038620                                   SCHED-LST-CODE
038630                                   SCHED-FOUND-CODE
038640     IF   TRCN-2ND-ROUTE-DESIGNATION > SPACES
038650     AND  TRCN-LOCAL-REC
038660     AND (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
038670        PERFORM P3039-GET-CNTL-DIST-SUB-DIST
038680        IF NOT CNTL-SPLIT-SHIFT-HOS
038690           PERFORM P3037-CHECK-SCHED
038700        END-IF
038710     END-IF
038720     SET NOT-VACANCY-FILLED       TO TRUE
038730     MOVE SCHED-2ND-CODE          TO HOLD-SCHED-2ND-CODE
038740     PERFORM P5950-GET-HRS-SVC-INFO
038750     MOVE HOLD-SCHED-2ND-CODE     TO SCHED-2ND-CODE
038760     IF  SCHED-2ND
038770     AND ON-SCHED-1ST
038780        SET REST-OK               TO TRUE
038790        SET P925-EMP-STATUS-GREEN TO TRUE
038800     ELSE
038810        IF P925-EMP-STATUS-RED
038820           AND NOT-HOS-EXEMPT
038830           SET REST-NOT-OK        TO TRUE
038840        END-IF
038850     END-IF
038860     .
038870*=================================================================
038880 P3036-DETERMINE-PREP-TIME.
038890*=================================================================
038900*   NOTE:  VALID PREP TIME WILL BE 15 MINUTES OR LESS.
038910*          FOR SHIFT CALL, PREP TIME EQUALS 0000.
038920*
038930*   DETERMINE DIFFERENCE BETWEEN EMPLOYEE'S ON DUTY DT/TM AND THE
038940*   ASSIGNMENT ORDER DT/TM.
038950*-----------------------------------------------------------------
038960     MOVE SPACES                   TO WS-EMP-OD-CENT-DATE-TIME
038970
038980     MOVE WK-CALL-DT-TM-CE         TO WS-EMP-OD-CENT-DATE-TIME
038990     MOVE 0                        TO WS-PREP-TIME
039000     .
039010*=================================================================
039020 P3037-CHECK-SCHED.
039030*=================================================================
039040     MOVE WS-JOB-SCHEDULE               TO WS-HOLD-JOB-SCHED
039050     MOVE JS-KEY1                       TO WORK-JS-KEY1
039060     MOVE WK-JSK1-ASGN-START-TIME       TO SAVE-JOB-OD-TIME
039070     MOVE SPACES                        TO WK-JSK1-ASGN-START-TIME
039080     MOVE WORK-JS-KEY1                  TO JSKEY1
039090
039100     EXEC CICS STARTBR
039110               DATASET(JS-VIA-JSKEY1)
039120               RIDFLD(JSKEY1)
039130               GTEQ
039140               RESP(WS-RESPONSE)
039150     END-EXEC
039160     MOVE WS-RESPONSE                   TO FILE-STATUS
039170
039180     IF NOT SUCCESS
039190        SET SCHED-DONE                  TO TRUE
039200        IF NOT (NO-RECORD-FND OR END-OF-FILE)
039210           MOVE 'P3037-1'               TO ERR-PARAGRAPH
039220           MOVE JSKEY1                  TO ERR-KEY
039230           PERFORM P9999-GOT-PROBLEM
039240        END-IF
039250     END-IF
039260
039270     PERFORM UNTIL SCHED-DONE
039280        EXEC CICS READNEXT
039290                  DATASET(JS-VIA-JSKEY1)
039300                  INTO(WS-JOB-SCHEDULE)
039310                  LENGTH(JSKEY1-RLGTH)
039320                  RIDFLD(JSKEY1)
039330                  KEYLENGTH(JSKEY1-KLGTH)
039340                  RESP(WS-RESPONSE)
039350        END-EXEC
039360        MOVE WS-RESPONSE                TO FILE-STATUS
039370        IF SUCCESS
039380           SET DE-YYMMDD-FORMAT         TO TRUE
039390           MOVE WK-JSK1-EXP-DATE        TO DE-YYMMDD
039400           PERFORM P8998-DATEEDIT
039410           MOVE DE-CCYYMMDD             TO HOLD-WK-JSK1-EXP-DATE
039420
039430           SET DE-YYMMDD-FORMAT         TO TRUE
039440           MOVE JSK1-EXP-DATE           TO DE-YYMMDD
039450           PERFORM P8998-DATEEDIT
039460           IF  JSK1-ASGN-DIST             = WK-JSK1-ASGN-DIST
039470           AND JSK1-ASGN-SUB-DIST         = WK-JSK1-ASGN-SUB-DIST
039480           AND JSK1-ASSIGNMENT            = WK-JSK1-ASSIGNMENT
039490           AND DE-CCYYMMDD            NOT < HOLD-WK-JSK1-EXP-DATE
039500              IF JSK1-ASGN-DAY            < WK-JSK1-ASGN-DAY
039510                 CONTINUE
039520              ELSE
039530                IF JSK1-ASGN-DAY          = WK-JSK1-ASGN-DAY
039540                   MOVE ZEROES           TO SCHED-LST-CODE
039550                   IF SAVE-JOB-OD-TIME    < JSK1-ASGN-START-TIME
039560                      SET SCHED-DONE     TO TRUE
039570                   ELSE
039580                      IF SAVE-JOB-OD-TIME > JOB-SCHED-END-TIME AND
039590                         JOB-SCHED-END-TIME > JSK1-ASGN-START-TIME
039600                         SET SCHED-2ND   TO TRUE
039610                      ELSE
039620                         SET SCHED-FOUND TO TRUE
039630                         SET SCHED-LST   TO TRUE
039640                      END-IF
039650                   END-IF
039660                ELSE
039670                   SET SCHED-DONE       TO TRUE
039680                END-IF
039690             END-IF
039700          ELSE
039710             SET SCHED-DONE             TO TRUE
039720          END-IF
039730        ELSE
039740           SET SCHED-DONE               TO TRUE
039750           IF NOT (NO-RECORD-FND OR END-OF-FILE)
039760              MOVE 'P3037-2'            TO ERR-PARAGRAPH
039770              MOVE JSKEY1               TO ERR-KEY
039780              PERFORM P9999-GOT-PROBLEM
039790           END-IF
039800        END-IF
039810     END-PERFORM
039820
039830     EXEC CICS ENDBR
039840               DATASET(JS-VIA-JSKEY1)
039850               RESP(WS-RESPONSE)
039860     END-EXEC
039870     MOVE WS-HOLD-JOB-SCHED             TO WS-JOB-SCHEDULE
039880
039890     IF SCHED-2ND
039900        SET  ON-SCHED-1ST               TO TRUE
039910        MOVE SPACES                     TO WS-AHIST
039920        MOVE JSK1-ASGN                  TO AHK2-ASSIGNMENT
039930        MOVE WK-CALL-DATE               TO AHK2-EFF-DATE
039940        MOVE AH-KEY-2                   TO AH2KEY
039950        MOVE AH-FILE-VIA-K2             TO AH-DATASET
039960
039970        EXEC CICS READ
039980                  DATASET  (AH-DATASET)
039990                  INTO     (WS-AHIST)
040000                  LENGTH   (AH2-RLGTH)
040010                  RIDFLD   (AH2KEY)
040020                  KEYLENGTH(AH2-KLGTH)
040030                  RESP     (WS-RESPONSE)
040040                  GTEQ
040050        END-EXEC
040060
040070        MOVE WS-RESPONSE                TO FILE-STATUS
040080
040090        IF  SUCCESS
040100        AND AHK2-ASSIGNMENT              = JSK1-ASGN
040110        AND AHK2-EFF-DATE                = WK-CALL-DATE
040120        AND AHK2-EFF-TIME                < WK-CALL-TIME
040130           MOVE SPACES                  TO WS-AHIST-MISC
040140           MOVE AH-KEY-1                TO AHM-AHIST-KEY
040150           SET  AHM-EMPLOYEE-RECORD     TO TRUE
040160           SEARCH CRAFT-CODE-TABLE VARYING CT-IND
040170              AT END CONTINUE
040180                 WHEN CT-CRAFT-CODE(CT-IND) = WK-JSK1-ASGN-CC
040190                    MOVE CT-CRAFT-SEQ(CT-IND)
040200                                        TO AHM-CRAFT-SEQ
040210           END-SEARCH
040220           MOVE AHM-KEY                 TO AHMKEY
040230           MOVE AHM-FILE                TO AHM-DATASET
040240           EXEC CICS READ
040250                     DATASET  (AHM-DATASET)
040260                     INTO     (WS-AHIST-MISC)
040270                     LENGTH   (AHM-RLGTH)
040280                     RIDFLD   (AHMKEY)
040290                     KEYLENGTH(AHM-KLGTH)
040300                     RESP     (WS-RESPONSE)
040310                     GTEQ
040320           END-EXEC
040330           MOVE WS-RESPONSE             TO FILE-STATUS
040340           IF  SUCCESS
040350           AND AHM-EMPLOYEE-RECORD
040360           AND AHM-AHIST-KEY             = AH-KEY-1
040370           AND AHM-EMPLOYEE-NUMBER  NOT  = EMP-NBR OF WS-MSTR
040380              SET  NOT-ON-SCHED-1ST     TO TRUE
040390           END-IF
040400        END-IF
040410     END-IF
040420     .
040430*=================================================================
040440 P3038-DETERMINE-EARLIEST-CALL.
040450*=================================================================
040460*  DETERMINE THE EARLIEST ON DUTY DATE/TIME THE EMPLOYEE IS
040470*  CONSIDERED RESTED USING THE BELOW FORMULA.
040480*
040490*  MTOD + LEAD TIME - PREP TIME  = EARLIEST EMPLOYEE OD DT/TM
040500*-----------------------------------------------------------------
040510     MOVE SPACES                   TO WS-EARLIEST-CENT-DATE-TIME
040520*
040530*    REGULAR OWNERS OF COMMUTER JOBS DO NOT HAVE LEAD TIME
040540*    APPLIED TO THEM.  SEE IF THE EMPLOYEE WE'RE EVALUATING
040550*    IS SUCH AN EMPLOYEE.
040560*
040570     IF TRCN-IGNORE-LEAD-TIME
040580        SET WS-IGNORE-LEAD-TIME    TO TRUE
040590     ELSE
040600        SET WS-INCLUDE-LEAD-TIME   TO TRUE
040610     END-IF
040620     IF TRCN-LOCAL-REC
040630        AND P08S-TSQ-CALL-MINUS-LD(SUB2)
040640        AND WS-INCLUDE-LEAD-TIME
040650        MOVE TRCN-ASSIGNMENT       TO JOB-DEF-CHECK
040660        IF JOB-DEF-COMMUTER-ASGN
040670           MOVE P08S-TSQ-JSKEY2(SUB2)
040680                                   TO JSKEY2
040690           IF TEMPORARY-ASGNMT > SPACES
040700              IF TA-DIST            = JSK2-ASGN-DIST
040710                 AND TA-SUB-DIST    = JSK2-ASGN-SUB-DIST
040720                 AND TA-1           = JSK2-ASGN(1:6)
040730                 SET WS-IGNORE-LEAD-TIME
040740                                   TO TRUE
040750              END-IF
040760           ELSE
040770              IF NORMAL-ASGNMT > SPACES
040780                 IF NA-DIST         = JSK2-ASGN-DIST
040790                    AND NA-SUB-DIST = JSK2-ASGN-SUB-DIST
040800                    AND NA-1        = JSK2-ASGN(1:6)
040810                    SET WS-IGNORE-LEAD-TIME
040820                                   TO TRUE
040830                 END-IF
040840              END-IF
040850           END-IF
040860        END-IF
040870     END-IF
040880
040890     IF WS-IGNORE-LEAD-TIME
040900        MOVE '0000'                TO WK-LEAD-TIME
040910     ELSE
040920        MOVE SPACES                TO WORK-CNTLKEY
040930        MOVE '02'                  TO WK-CNTL-REC-TYPE
040940        MOVE SCR08S-DIST           TO WK-CNTL-DIST
040950        MOVE SCR08S-SUB-DIST       TO WK-CNTL-SUB-DIST
040960        MOVE WORK-CNTLKEY          TO CNTLKEY
040970        PERFORM P8610-READ-CNTLFILE
040980        IF NOT SUCCESS
040990           MOVE 'P3038-1'          TO ERR-PARAGRAPH
041000           MOVE CNTLKEY            TO ERR-KEY
041010           PERFORM P9999-GOT-PROBLEM
041020        END-IF
041030        MOVE CNTL-LEAD-CALL-TIME    TO WK-LEAD-TIME
041040        IF TRCN-ADDL-LEAD-TIME > SPACES
041050           ADD TRCN-ADDL-LEAD-TIME-HR TO WK-LEAD-TIME-HR
041060           MOVE WK-LEAD-TIME-MN     TO WK-LEAD-TIME-MN3
041070           ADD TRCN-ADDL-LEAD-TIME-MN TO WK-LEAD-TIME-MN3
041080           IF WK-LEAD-TIME-MN3 > 60
041090              ADD 1                 TO WK-LEAD-TIME-HR
041100              SUBTRACT 60         FROM WK-LEAD-TIME-MN3
041110           END-IF
041120           MOVE WK-LEAD-TIME-MN3    TO WK-LEAD-TIME-MN
041130        END-IF
041140     END-IF
041150
041160     IF EMP-MTOD > '0000000000'
041170        MOVE ZEROS                 TO DATE-CONVERSION-PARMS
041180        SET PARM-ADD               TO TRUE
041190        MOVE EMP-MTOD-DATE         TO PARM-PRI-DATE-GREG
041200        MOVE EMP-MTOD-TIME         TO PARM-PRI-HRMN
041210        MOVE WK-LEAD-TIME          TO PARM-SEC-HRMN
041220        EXEC CICS LINK
041230                  PROGRAM(P903-PGM)
041240                  COMMAREA(DATE-CONVERSION-PARMS)
041250                  LENGTH(P903-LGTH)
041260                  RESP(WS-RESPONSE)
041270        END-EXEC
041280        MOVE WS-RESPONSE           TO FILE-STATUS
041290        IF NOT SUCCESS
041300           MOVE 'P3038-2'          TO ERR-PARAGRAPH
041310           MOVE 'P903LINK'         TO ERR-KEY
041320           PERFORM P9999-GOT-PROBLEM
041330        END-IF
041340        MOVE PARM-RES-GREG-CENT    TO WS-EARLIEST-CENT
041350        MOVE PARM-RES-DATE-GREG    TO WS-EARLIEST-DATE
041360        MOVE PARM-RES-HRMN         TO WS-EARLIEST-TIME
041370     END-IF
041380
041390     IF WS-PREP-TIME > 0
041400        MOVE ZEROS                 TO DATE-CONVERSION-PARMS
041410        SET PARM-SUBTRACT          TO TRUE
041420        MOVE WS-EARLIEST-DATE      TO PARM-PRI-DATE-GREG
041430        MOVE WS-EARLIEST-TIME      TO PARM-PRI-HRMN
041440        MOVE WS-PREP-TIME          TO PARM-SEC-HRMN
041450        EXEC CICS LINK
041460                  PROGRAM(P903-PGM)
041470                  COMMAREA(DATE-CONVERSION-PARMS)
041480                  LENGTH(P903-LGTH)
041490                  RESP(WS-RESPONSE)
041500        END-EXEC
041510        MOVE WS-RESPONSE           TO FILE-STATUS
041520        IF NOT SUCCESS
041530           MOVE 'P3038-3'          TO ERR-PARAGRAPH
041540           MOVE 'P903LINK'         TO ERR-KEY
041550           PERFORM P9999-GOT-PROBLEM
041560        END-IF
041570        MOVE PARM-RES-GREG-CENT    TO WS-EARLIEST-CENT
041580        MOVE PARM-RES-DATE-GREG    TO WS-EARLIEST-DATE
041590        MOVE PARM-RES-HRMN         TO WS-EARLIEST-TIME
041600     END-IF
041610     IF WS-EARLIEST-CENT-DATE-TIME NOT > SPACES
041620        IF WS-INCLUDE-LEAD-TIME
041630           MOVE WK-CALL-MINUS-LEAD TO WS-EARLIEST-CENT-DATE-TIME
041640        ELSE
041650           MOVE WK-CALL-DT-TM-CE   TO WS-EARLIEST-CENT-DATE-TIME
041660        END-IF
041670     END-IF
041680     .
041690*=================================================================
041700 P3038A-DETERMINE-EARLI-US-CALL.
041710*=================================================================
041720     MOVE SPACES                   TO CNTLKEY-AREA
041730     MOVE '02'                     TO CNTL-REC-TYPE
041740     MOVE SCR08S-DIST              TO CNTL-DIST
041750     MOVE SCR08S-SUB-DIST          TO CNTL-SUB-DIST
041760     MOVE CNTLKEY-AREA             TO CNTLKEY
041770     PERFORM P8610-READ-CNTLFILE
041780     IF NOT SUCCESS
041790        MOVE 'P3038A-1'            TO ERR-PARAGRAPH
041800        MOVE CNTLKEY               TO ERR-KEY
041810        PERFORM P9999-GOT-PROBLEM
041820     END-IF
041830     MOVE CNTL-US-DOM              TO WS-US-DOM-FLAG
041840     MOVE SPACES                   TO CNTLKEY-AREA
041850     MOVE 'HS'                     TO CNTL-REC-TYPE
041860     MOVE CNTLKEY-AREA             TO CNTLKEY
041870     PERFORM P8610-READ-CNTLFILE
041880     IF NOT SUCCESS
041890        MOVE 'P3038A-2'            TO ERR-PARAGRAPH
041900        MOVE CNTLKEY               TO ERR-KEY
041910        PERFORM P9999-GOT-PROBLEM
041920     END-IF
041930     MOVE CNTL-HS-US-ACTIVATE-FL   TO WS-HS-US-ACTIVATE-FL
041940     IF WS-US-DOM-YES AND
041950        WS-HS-US-ACTIVATE AND
041960        EMP-US-RSTD > '0000000000'
041970        MOVE SPACES                TO WS-EARLI-US-CENT-DATE-TIME
041980        IF TRCN-IGNORE-LEAD-TIME
041990           SET WS-IGNORE-LEAD-TIME TO TRUE
042000        ELSE
042010           SET WS-INCLUDE-LEAD-TIME TO TRUE
042020        END-IF
042030        IF TRCN-LOCAL-REC
042040           AND P08S-TSQ-CALL-MINUS-LD(SUB2)
042050           AND WS-INCLUDE-LEAD-TIME
042060           MOVE TRCN-ASSIGNMENT    TO JOB-DEF-CHECK
042070           IF JOB-DEF-COMMUTER-ASGN
042080              MOVE P08S-TSQ-JSKEY2(SUB2)
042090                                      TO JSKEY2
042100              IF TEMPORARY-ASGNMT > SPACES
042110                 IF TA-DIST         = JSK2-ASGN-DIST
042120                    AND TA-SUB-DIST = JSK2-ASGN-SUB-DIST
042130                    AND TA-1        = JSK2-ASGN(1:6)
042140                    SET WS-IGNORE-LEAD-TIME
042150                                      TO TRUE
042160                 END-IF
042170              ELSE
042180                 IF NORMAL-ASGNMT > SPACES
042190                    IF NA-DIST      = JSK2-ASGN-DIST
042200                       AND NA-SUB-DIST = JSK2-ASGN-SUB-DIST
042210                       AND NA-1     = JSK2-ASGN(1:6)
042220                       SET WS-IGNORE-LEAD-TIME
042230                                      TO TRUE
042240                    END-IF
042250                 END-IF
042260              END-IF
042270           END-IF
042280        END-IF
042290
042300        IF WS-IGNORE-LEAD-TIME
042310           MOVE '0000'             TO WK-LEAD-TIME
042320        ELSE
042330           MOVE SPACES             TO WORK-CNTLKEY
042340           MOVE '02'               TO WK-CNTL-REC-TYPE
042350           MOVE SCR08S-DIST        TO WK-CNTL-DIST
042360           MOVE SCR08S-SUB-DIST    TO WK-CNTL-SUB-DIST
042370           MOVE WORK-CNTLKEY       TO CNTLKEY
042380           PERFORM P8610-READ-CNTLFILE
042390           IF NOT SUCCESS
042400              MOVE 'P3038A-3'      TO ERR-PARAGRAPH
042410              MOVE CNTLKEY         TO ERR-KEY
042420              PERFORM P9999-GOT-PROBLEM
042430           END-IF
042440*          INCLUDE SHORT LEAD FUNCTIONALITY
042450           MOVE SPACES              TO PS52-COMMAREA-PARMS
042460           MOVE EMP-NBR IN WS-MSTR  TO PS52-EMP-NBR
042470           SET PS52-CMA-MODE        TO TRUE
042480           MOVE WS-LOCAL-DATE-TIME  TO PS52-INPUT-DATE-TIME
042490           PERFORM P8520-LINK-TO-PS52
042500           IF NOT SUCCESS
042510              MOVE 'P3038A-4'       TO ERR-PARAGRAPH
042520              PERFORM P9999-GOT-PROBLEM
042530           END-IF
042540           IF (PS52-REQ-LEAD-TIME     > SPACES
042550              AND PS52-REQ-LEAD-TIME  > '0000')
042560              MOVE PS52-REQ-LEAD-TIME TO WK-LEAD-TIME
042570           ELSE
042580              MOVE CNTL-LEAD-CALL-TIME TO WK-LEAD-TIME
042590           END-IF
042600           IF TRCN-ADDL-LEAD-TIME > SPACES
042610              ADD TRCN-ADDL-LEAD-TIME-HR TO WK-LEAD-TIME-HR
042620              MOVE WK-LEAD-TIME-MN  TO WK-LEAD-TIME-MN3
042630              ADD TRCN-ADDL-LEAD-TIME-MN TO WK-LEAD-TIME-MN3
042640              IF WK-LEAD-TIME-MN3 > 60
042650                 ADD 1              TO WK-LEAD-TIME-HR
042660                 SUBTRACT 60      FROM WK-LEAD-TIME-MN3
042670              END-IF
042680              MOVE WK-LEAD-TIME-MN3 TO WK-LEAD-TIME-MN
042690           END-IF
042700        END-IF
042710
042720        MOVE ZEROS                 TO DATE-CONVERSION-PARMS
042730        SET PARM-ADD               TO TRUE
042740        MOVE EMP-US-RSTD-DATE      TO PARM-PRI-DATE-GREG
042750        MOVE EMP-US-RSTD-TIME      TO PARM-PRI-HRMN
042760        MOVE WK-LEAD-TIME          TO PARM-SEC-HRMN
042770        EXEC CICS LINK
042780                  PROGRAM(P903-PGM)
042790                  COMMAREA(DATE-CONVERSION-PARMS)
042800                  LENGTH(P903-LGTH)
042810                  RESP(WS-RESPONSE)
042820        END-EXEC
042830        MOVE WS-RESPONSE           TO FILE-STATUS
042840        IF NOT SUCCESS
042850           MOVE 'P3038A-5'         TO ERR-PARAGRAPH
042860           MOVE 'P903LINK'         TO ERR-KEY
042870           PERFORM P9999-GOT-PROBLEM
042880        END-IF
042890        MOVE PARM-RES-GREG-CENT TO WS-EARLI-US-CENT
042900        MOVE PARM-RES-DATE-GREG TO WS-EARLI-US-DATE
042910        MOVE PARM-RES-HRMN         TO WS-EARLI-US-TIME
042920
042930        IF WS-EARLI-US-CENT-DATE-TIME NOT > SPACES
042940           IF WS-INCLUDE-LEAD-TIME
042950              MOVE WK-CALL-MINUS-LEAD
042960                                   TO WS-EARLI-US-CENT-DATE-TIME
042970           ELSE
042980              MOVE WK-CALL-DT-TM-CE TO WS-EARLI-US-CENT-DATE-TIME
042990           END-IF
043000        END-IF
043010     END-IF.
043020*
043030 P3039-GET-CNTL-DIST-SUB-DIST.
043040*
043050     MOVE SPACES                TO WORK-CNTLKEY
043060     MOVE '2A'                  TO WK-CNTL-REC-TYPE
043070     MOVE JSK1-ASGN-DIST        TO WK-CNTL-DIST
043080     MOVE JSK1-ASGN-SUB-DIST    TO WK-CNTL-SUB-DIST
043090     MOVE WORK-CNTLKEY          TO CNTLKEY
043100     PERFORM P8610-READ-CNTLFILE
043110     IF NOT SUCCESS
043120        MOVE 'P3039-1'          TO ERR-PARAGRAPH
043130        MOVE CNTLKEY            TO ERR-KEY
043140        PERFORM P9999-GOT-PROBLEM
043150     END-IF.
043160*
043170 P3040-CHECK-QUALIFICATIONS.
043180*
043190*    SEE IF THE ASSIGNMENT HAS ANY QUALIFICATIONS SPECIFIC TO
043200*    TO THE ASSIGNMENT.
043210*
043220     MOVE SPACES                TO WS-EMP-DISQUALIFIED
043230     MOVE EMP-NBR IN WS-MSTR    TO QUAL-EMP-NO
043240     MOVE SPACES                TO WORK-CNTLKEY
043250     MOVE '13'                  TO WK-CNTL-REC-TYPE
043260     MOVE AJ-JOB-DIST           TO WK-CNTL-DIST
043270     MOVE AJ-JOB-SUB-DIST       TO WK-CNTL-SUB-DIST
043280     MOVE 'A'                   TO WK-CNTL-ASGN-QUAL-TYPE
043290     MOVE AJ-JOB-ASSIGNMENT     TO WK-CNTL-ASGN-QUAL
043300     MOVE WORK-CNTLKEY          TO CNTLKEY
043310     PERFORM P8610-READ-CNTLFILE
043320     IF NOT SUCCESS
043330        MOVE '**'               TO WK-CNTL-ASGN-QUAL-5-6
043340        MOVE WORK-CNTLKEY       TO CNTLKEY
043350        PERFORM P8610-READ-CNTLFILE
043360     END-IF
043370     IF SUCCESS
043380        PERFORM VARYING SUB3 FROM 1 BY 1
043390           UNTIL SUB3 > 5
043400           IF CNTL-QUALIFICATION-TABLE-ENTRY(SUB3) > SPACE
043410              MOVE CNTL-QUALIFICATION-TABLE-ENTRY(SUB3)
043420                                  TO QUALIFICATION
043430              MOVE QUALEMP-KEY    TO QUALEMP
043440              EXEC CICS READ
043450                        DATASET(QUAL-FILE-VIA-QUALEMP)
043460                        INTO(WS-QUAL-FILE)
043470                        LENGTH(QUALEMP-RLGTH)
043480                        RIDFLD(QUALEMP)
043490                        KEYLENGTH(QUALEMP-KLGTH)
043500                        RESP(WS-RESPONSE)
043510              END-EXEC
043520              MOVE WS-RESPONSE    TO FILE-STATUS
043530              IF SUCCESS
043540                 IF QUAL-DATE NOT > '000000'
043550                    MOVE '999999' TO QUAL-DATE
043560                 END-IF
043570                 IF QUAL-RESTRICTION
043580*
043590*                   IF THE EMPLOYEE HAS THE QUAL CODE AND THAT
043600*                   QUALIFICATION IS A "RESTRICTION", AND THE
043610*                   QUALIFICATION HAS NOT YET "EXPIRED", THE
043620*                   EMPLOYEE CANNOT BE CALLED FOR THE ASSIGNMENT
043630*
043640                    PERFORM P9811-FORMAT-CENTURY
043650                    IF WS-CENT-QUAL-DATE > WS-CENT-CALL-DATE
043660                       MOVE 7     TO SUB3
043670                       SET EMP-DISQUALIFIED TO TRUE
043680                    END-IF
043690                 ELSE
043700*
043710*                IF THE EMPLOYEE HAS THE QUALIFICATION CODE,
043720*                AND THE QUALIFICATION HAS "EXPIRED", THE
043730*                EMPLOYEE CANNOT BE CALLED FOR THE ASSIGNMENT
043740*
043750                    PERFORM P9811-FORMAT-CENTURY
043760                    IF WS-CENT-QUAL-DATE < WS-CENT-CALL-DATE
043770                       MOVE 7    TO SUB3
043780                       SET EMP-DISQUALIFIED TO TRUE
043790                    END-IF
043800                 END-IF
043810              ELSE
043820                 IF (NO-RECORD-FND OR END-OF-FILE)
043830*
043840*                IF THE EMPLOYEE DOES NOT HAVE THE
043850*                QUALIFICATION, IF IT IS A "RESTRICTION" THEN
043860*                ALL IS OK, IF IT IS NOT A "RESTRICTION", THE
043870*                EMPLOYEE CANNOT BE CALLED FOR THE ASSIGNMENT
043880*
043890                    IF NOT CNTL-QUAL-RESTRICTION(SUB3)
043900                       MOVE 7    TO SUB3
043910                       SET EMP-DISQUALIFIED TO TRUE
043920                    END-IF
043930                 ELSE
043940                    MOVE 'P3040-1'
043950                                    TO ERR-PARAGRAPH
043960                    MOVE QUALEMP TO ERR-KEY
043970                    PERFORM P9999-GOT-PROBLEM
043980                 END-IF
043990              END-IF
044000           END-IF
044010        END-PERFORM
044020     ELSE
044030        IF NOT (NO-RECORD-FND OR END-OF-FILE)
044040           MOVE 'P3040-2'       TO ERR-PARAGRAPH
044050           MOVE WORK-CNTLKEY    TO ERR-KEY
044060           PERFORM P9999-GOT-PROBLEM
044070        END-IF
044080     END-IF
044090*
044100*    HERE WE GET THE "SIXTH" QUALIFICATION FROM THE ASSIGNMENT
044110*    PROFILE RECORD
044120*CNC0572 - WITH THIS ENHANCEMENT THERE CAN BE UP TO 3 SPCL QUALS
044130*          ASSIGNED TO A CRAFT.
044140     MOVE SPACES                TO QUALIFICATION
044150     MOVE WK-CNTL-ASGN-QUAL-7-8 TO WS-HOLD-CC
044160     IF WS-HOLD-CC = 'FI' OR 'SE'
044170        MOVE 'SE'               TO WS-HOLD-CC
044180     END-IF
044190     IF WS-HOLD-CC = 'S1' OR 'S2'
044200        MOVE 'SW'               TO WS-HOLD-CC
044210     END-IF
044220     IF WK-CNTL-ASGN-QUAL-7-8 = ('B1' OR 'B2')
044230        MOVE 'BK'               TO WS-HOLD-CC
044240     END-IF
044250*CNC0572 - BEG
044260***  PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
044270***     IF WS-HOLD-CC = TRCN-SPCL-QUAL-CC(SUBA)
044280***        MOVE TRCN-SPCL-QUAL(SUBA) TO QUALIFICATION
044290***     END-IF
044300***  END-PERFORM
044310******************************************************************
044320* THERE CAN NOW BE UP TO 3 SPECIAL QUALIFICATIONS PER CRAFT      *
044330* (PREVIOUSLY 1). FOR EACH MATCH PERFORM THE QUALIFICATION LOGIC.*
044340******************************************************************
044350     PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
044360        IF WS-HOLD-CC = TRCN-SPCL-QUAL-CC(SUBA)
044370           MOVE TRCN-SPCL-QUAL(SUBA) TO QUALIFICATION
044380           PERFORM P3045-CHECK-QUALIFICATION
044390        END-IF
044400     END-PERFORM
044410     PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
044420        IF WS-HOLD-CC = TRCN-SPCL-QUAL2-CC(SUBA)
044430           MOVE TRCN-SPCL-QUAL2(SUBA) TO QUALIFICATION
044440           PERFORM P3045-CHECK-QUALIFICATION
044450        END-IF
044460     END-PERFORM
044470*CNC0572 - END
044480
044490     IF NOT EMP-DISQUALIFIED
044500        MOVE SPACES                   TO P947-COMMAREA-PARMS
044510        SET P947-EMP-RESTR-FUNCTION   TO TRUE
044520        MOVE EMP-NBR OF WS-MSTR       TO P947-EMP-NO
044530        SET P947-ASGN-AJ              TO TRUE
044540        MOVE AJ-JOB-DIST              TO P947-ASGN-DIST
044550        MOVE AJ-JOB-SUB-DIST          TO P947-ASGN-SUB-DIST
044560        MOVE AJ-JOB-ASSIGNMENT        TO P947-ASGN-ASSIGNMENT
044570        SET P947-FROM-WORK            TO TRUE
044580        EXEC CICS LINK
044590                  PROGRAM(P947-PGM)
044600                  COMMAREA(P947-COMMAREA-PARMS)
044610                  LENGTH(P947-LGTH)
044620                  RESP(WS-RESPONSE)
044630        END-EXEC
044640        MOVE WS-RESPONSE              TO FILE-STATUS
044650        IF NOT SUCCESS
044660           MOVE 'P3040-4'             TO ERR-PARAGRAPH
044670           MOVE 'LINK947'             TO ERR-KEY
044680           PERFORM P9999-GOT-PROBLEM
044690        END-IF
044700        IF P947-RESTRICTED
044710           SET EMP-DISQUALIFIED TO TRUE
044720        END-IF
044730     END-IF
044740     IF NOT EMP-DISQUALIFIED
044750     AND TRCN-LOCAL-REC
044760        MOVE SPACES                   TO WS-QUAL-FLAG
044770        MOVE EMP-NBR IN WS-MSTR       TO WS-TERR-EMP-NO
044780        MOVE CRAFT IN WS-MSTR         TO WS-TERR-EMP-CC
044790        PERFORM P6301-GET-QUAL-LSP-LSD
044800        IF QUAL-MSG-YES
044810           SET EMP-DISQUALIFIED       TO TRUE
044820        END-IF
044830     END-IF
044840     .
044850*
044860 P3045-CHECK-QUALIFICATION.
044870*
044880     IF QUALIFICATION > SPACES
044890        MOVE EMP-NBR IN WS-MSTR  TO QUAL-EMP-NO
044900        MOVE QUALEMP-KEY         TO QUALEMP
044910        EXEC CICS READ
044920                  DATASET(QUAL-FILE-VIA-QUALEMP)
044930                  INTO(WS-QUAL-FILE)
044940                  LENGTH(QUALEMP-RLGTH)
044950                  RIDFLD(QUALEMP)
044960                  KEYLENGTH(QUALEMP-KLGTH)
044970                  RESP(WS-RESPONSE)
044980        END-EXEC
044990        MOVE WS-RESPONSE TO FILE-STATUS
045000        IF SUCCESS
045010           IF QUAL-DATE NOT > '000000'
045020              MOVE '999999'       TO QUAL-DATE
045030           END-IF
045040           IF QUAL-RESTRICTION
045050*
045060*           IF THE EMPLOYEE HAS THE QUALIFICATION CODE, AND THAT
045070*           QUALIFICATION IS A "RESTRICTION", AND THE QUAL CODE
045080*           HAS NOT YET "EXPIRED", THE EMPLOYEE CANNOT BE CALLED
045090*           FOR THE ASSIGNMENT.
045100*
045110              PERFORM P9811-FORMAT-CENTURY
045120              IF WS-CENT-QUAL-DATE > WS-CENT-CALL-DATE
045130                 MOVE 7    TO SUB3
045140                 SET EMP-DISQUALIFIED TO TRUE
045150              END-IF
045160           ELSE
045170*
045180*          IF THE EMPLOYEE HAS THE QUALIFICATION CODE, AND THE
045190*          QUALIFICATION HAS "EXPIRED", THE EMPLOYEE CANNOT BE
045200*          CALLED FOR THE ASSIGNMENT
045210*
045220              PERFORM P9811-FORMAT-CENTURY
045230              IF WS-CENT-QUAL-DATE < WS-CENT-CALL-DATE
045240                 MOVE 7    TO SUB3
045250                 SET EMP-DISQUALIFIED TO TRUE
045260              END-IF
045270           END-IF
045280        ELSE
045290           IF (NO-RECORD-FND OR END-OF-FILE)
045300*
045310*          IF THE EMPLOYEE DOES NOT HAVE THE QUALIFICATION AND IT
045320*          IS A "RESTRICTION" THEN ALL IS OK.  IF IT IS NOT A
045330*          RESTRICTION, THE EMPLOYEE CANNOT BE CALLED FOR THE
045340*          ASSIGNMENT
045350*
045360              MOVE QUALEMP TO QUALEMP-KEY
045370              IF NOT QUAL-RESTRICTION
045380                 SET EMP-DISQUALIFIED TO TRUE
045390              END-IF
045400           ELSE
045410              MOVE 'P3040-3' TO ERR-PARAGRAPH
045420              MOVE QUALEMP TO ERR-KEY
045430              PERFORM P9999-GOT-PROBLEM
045440           END-IF
045450        END-IF
045460     END-IF
045470     .
045480*
045490 P3050-CHECK-EARLIER-SHIFT.
045500*
045510     MOVE WS-JOB-SCHEDULE               TO WS-HOLD-JOB-SCHED
045520
045530     MOVE JS-KEY1                       TO WORK-JS-KEY1
045540     MOVE WK-JSK1-ASGN-START-TIME       TO SAVE-JOB-OD-TIME
045550     MOVE SPACES                        TO WK-JSK1-ASGN-START-TIME
045560     MOVE SPACES                        TO WS-FIRST-SHIFT-TIME
045570     MOVE WORK-JS-KEY1                  TO JSKEY1
045580
045590     MOVE ZEROES                        TO SCHED-DONE-2-CODE
045600     EXEC CICS STARTBR
045610               DATASET(JS-VIA-JSKEY1)
045620               RIDFLD(JSKEY1)
045630               GTEQ
045640               RESP(WS-RESPONSE)
045650     END-EXEC
045660     MOVE WS-RESPONSE                   TO FILE-STATUS
045670
045680     IF NOT SUCCESS
045690        SET SCHED-DONE-2                TO TRUE
045700        IF NOT (NO-RECORD-FND OR END-OF-FILE)
045710           MOVE 'P3050-1'               TO ERR-PARAGRAPH
045720           MOVE JSKEY1                  TO ERR-KEY
045730           PERFORM P9999-GOT-PROBLEM
045740        END-IF
045750     END-IF
045760
045770* TECHNICALLY ONLY ONE READ IS NECESSARY TO SEE IF THERE IS
045780* A START TIME SCHEDULE WHICH EXISTS PRIOR TO THE START
045790* TIME OF THE CURRENT SHIFT.  IF MORE THAN 2 SHIFTS ARE
045800* POSSIBLE THEN IT MIGHT BE NECESSARY TO CHANGE THIS LOGIC
045810
045820     PERFORM UNTIL SCHED-DONE-2
045830        EXEC CICS READNEXT
045840                  DATASET(JS-VIA-JSKEY1)
045850                  INTO(WS-JOB-SCHEDULE)
045860                  LENGTH(JSKEY1-RLGTH)
045870                  RIDFLD(JSKEY1)
045880                  KEYLENGTH(JSKEY1-KLGTH)
045890                  RESP(WS-RESPONSE)
045900        END-EXEC
045910        MOVE WS-RESPONSE                TO FILE-STATUS
045920        IF SUCCESS
045930           SET DE-YYMMDD-FORMAT         TO TRUE
045940           MOVE WK-JSK1-EXP-DATE        TO DE-YYMMDD
045950           PERFORM P8998-DATEEDIT
045960           MOVE DE-CCYYMMDD             TO HOLD-WK-JSK1-EXP-DATE
045970
045980           SET DE-YYMMDD-FORMAT         TO TRUE
045990           MOVE JSK1-EXP-DATE           TO DE-YYMMDD
046000           PERFORM P8998-DATEEDIT
046010           IF  JSK1-ASGN-DIST         = WK-JSK1-ASGN-DIST
046020           AND JSK1-ASGN-SUB-DIST     = WK-JSK1-ASGN-SUB-DIST
046030           AND JSK1-ASSIGNMENT        = WK-JSK1-ASSIGNMENT
046040           AND DE-CCYYMMDD        NOT < HOLD-WK-JSK1-EXP-DATE
046050           AND JSK1-ASGN-DAY          = WK-JSK1-ASGN-DAY
046060              IF JSK1-ASGN-START-TIME < SAVE-JOB-OD-TIME
046070* A START TIME ON THE SAME DAY LESS THAN THE CURRENT
046080*   START TIME IMPLIES THAT CURRENT JOB IS THE SECOND SHIFT
046090                 MOVE JSK1-ASGN-START-TIME
046100                                        TO WS-FIRST-SHIFT-TIME
046110                 SET SCHED-DONE-2       TO TRUE
046120              ELSE
046130                 SET SCHED-DONE-2       TO TRUE
046140              END-IF
046150           ELSE
046160              SET SCHED-DONE-2          TO TRUE
046170           END-IF
046180        ELSE
046190           SET SCHED-DONE-2             TO TRUE
046200           IF NOT (NO-RECORD-FND OR END-OF-FILE)
046210              MOVE 'P3050-2'            TO ERR-PARAGRAPH
046220              MOVE JSKEY1               TO ERR-KEY
046230              PERFORM P9999-GOT-PROBLEM
046240           END-IF
046250        END-IF
046260     END-PERFORM
046270
046280     EXEC CICS ENDBR
046290               DATASET(JS-VIA-JSKEY1)
046300               RESP(WS-RESPONSE)
046310     END-EXEC
046320
046330     MOVE WS-HOLD-JOB-SCHED             TO WS-JOB-SCHEDULE
046340
046350     IF WS-FIRST-SHIFT-TIME > SPACES
046360        PERFORM P3055-CHECK-REPLACED
046370     END-IF.
046380*
046390 P3055-CHECK-REPLACED.
046400*
046410     MOVE SPACES                   TO AHM-KEY
046420     MOVE AHM-FILE                 TO AHM-DATASET
046430     MOVE JSK1-ASGN-DIST           TO AHM-DIST
046440     MOVE JSK1-ASGN-SUB-DIST       TO AHM-SUB-DIST
046450     MOVE JSK1-ASGN                TO AHM-AJOB-NO
046460
046470     MOVE SPACES                   TO WS-POOL
046480     IF JOB-DEF-YARD-ASGN
046490        MOVE JOB-DEF-YARD-POOL     TO AHM-POOL
046500                                      WS-POOL
046510     ELSE
046520        IF JOB-DEF-LOCAL-ASGN
046530           MOVE JOB-DEF-LOCAL-POOL TO AHM-POOL
046540                                      WS-POOL
046550        END-IF
046560     END-IF
046570     MOVE WK-CALL-DATE             TO AHM-EFF-DATE
046580     MOVE WS-FIRST-SHIFT-TIME      TO AHMK1-EFF-TIME
046590     SET AHM-EMPLOYEE-RECORD       TO TRUE
046600
046610     MOVE AHM-KEY                  TO AHMKEY
046620
046630     EXEC CICS STARTBR
046640               DATASET(AHM-DATASET)
046650               RIDFLD(AHMKEY)
046660               GTEQ
046670               RESP(WS-RESPONSE)
046680     END-EXEC
046690     MOVE WS-RESPONSE TO FILE-STATUS
046700     IF SUCCESS
046710        MOVE '0' TO WS-AHMISC-CODE
046720        PERFORM UNTIL AHMISC-DONE
046730           EXEC CICS READNEXT
046740                     DATASET(AHM-DATASET)
046750                     INTO(WS-AHIST-MISC)
046760                     LENGTH(AHM-RLGTH)
046770                     RIDFLD(AHMKEY)
046780                     KEYLENGTH(AHM-KLGTH)
046790                     RESP(WS-RESPONSE)
046800           END-EXEC
046810           MOVE WS-RESPONSE TO FILE-STATUS
046820           IF SUCCESS
046830              IF  AHM-DIST       = JSK1-ASGN-DIST
046840              AND AHM-SUB-DIST   = JSK1-ASGN-SUB-DIST
046850              AND AHM-POOL       = WS-POOL
046860              AND AHM-EFF-DATE   = WK-CALL-DATE
046870              AND AHMK1-EFF-TIME = WS-FIRST-SHIFT-TIME
046880              AND AHM-AJOB-NO    = JSK1-ASGN
046890                 IF AHM-ODA-CC       = JSK1-ASGN-CC AND
046900                    AHM-EMP-REPLACED = AJ-EMP-NBR
046910                    SET NOT-OWNER-ELIGIBLE TO TRUE
046920                    SET AHMISC-DONE TO TRUE
046930                 END-IF
046940              ELSE
046950                 SET AHMISC-DONE TO TRUE
046960              END-IF
046970           ELSE
046980              SET AHMISC-DONE TO TRUE
046990              IF NOT (NO-RECORD-FND OR END-OF-FILE)
047000                 MOVE 'P3055-1'          TO ERR-PARAGRAPH
047010                 MOVE AHMKEY             TO ERR-KEY
047020                 PERFORM P9999-GOT-PROBLEM
047030              END-IF
047040           END-IF
047050        END-PERFORM
047060     END-IF
047070
047080     MOVE WS-RESPONSE              TO FILE-STATUS
047090     EXEC CICS ENDBR
047100               DATASET(AHM-DATASET)
047110               RESP(WS-RESPONSE)
047120     END-EXEC.
047130*
047140 P3060-CHECK-STARTS.
047150*
047160*    SEE IF EMPLOYEE IS NOT QUALIFIED BECAUSE THE EMPLOYEE HAS
047170*    MORE THAN 5 STARTS ACCUMULATED IN THE JOB CRAFT TYPE.
047180*    ACCUMULATIONS ARE DONE BASED UPON THE COMBINE STARTS FLAG IN
047190*    THE SUB-DISTRICT CNTL RECORD.  NOTE:  FOR UNASSIGNED
047200*    YARDMASTERS, THE CNTL-OTHER-WORK-OK FLAG IS CHECKED FOR TRUE.
047210*    WHEN IT IS TRUE AND THE EMPLOYEE IS NOT WORKING A YARDMASTER
047220*    POSITION - STARTS WILL NOT BE EVALUATED.
047230*
047240     MOVE SPACES                    TO CNTLKEY-AREA
047250     MOVE '02'                      TO CNTL-REC-TYPE
047260     MOVE AJ-JOB-DIST               TO CNTL-DIST
047270     MOVE AJ-JOB-SUB-DIST           TO CNTL-SUB-DIST
047280     MOVE CNTLKEY-AREA              TO CNTLKEY
047290     PERFORM P8610-READ-CNTLFILE
047300     IF NOT SUCCESS
047310        MOVE 'P3060-1'              TO ERR-PARAGRAPH
047320        MOVE CNTLKEY                TO ERR-KEY
047330        PERFORM P9999-GOT-PROBLEM
047340     END-IF
047350     MOVE SPACES                    TO P931-COMMAREA-PARMS
047360                                       WS-STARTS-DISQUALIFIED
047370     SET P931-INQUIRY-FUN           TO TRUE
047380     MOVE EMP-NBR OF WS-MSTR        TO P931-INQ-EMP-NO
047390     SET P931-INQ-STARTS            TO TRUE
047400     SET P931-CURRENT-PERIOD        TO TRUE
047410     MOVE CRAFT OF WS-MSTR          TO P931-INQ-CRAFT
047420     EXEC CICS LINK
047430               PROGRAM(P931-PGM)
047440               COMMAREA(P931-COMMAREA-PARMS)
047450               LENGTH(P931-LGTH)
047460               RESP(WS-RESPONSE)
047470     END-EXEC
047480     MOVE WS-RESPONSE               TO FILE-STATUS
047490     IF NOT SUCCESS
047500        MOVE 'P3060-2'              TO ERR-PARAGRAPH
047510        MOVE 'P931LINK'             TO ERR-KEY
047520        PERFORM P9999-GOT-PROBLEM
047530     END-IF
047540*    ADD P931-RET-CRAFT-STT, P931-RET-CRAFT-OVT GIVING
047550*                                               WS-STARTS-TOTAL
047560     MOVE P931-RET-CRAFT-STT        TO WS-STARTS-TOTAL
047570     IF WS-STARTS-TOTAL > WS-STARTS-DISQUALIFY
047580        AND CNTL-VERIFY-STARTS-YES
047590        SET STARTS-DISQUALIFIED     TO TRUE
047600        SET EMP-DISQUALIFIED        TO TRUE
047610        IF EMP-NBR IN WS-MSTR = WS-CURR-OWNER
047620           SET WS-TRK-STARTS        TO TRUE
047630        END-IF
047640     END-IF.
047650*
047660 P3080-CHECK-QUICK-TIES.
047670*
047680     MOVE SPACES                     TO WS-QTIE-DISQUALIFIED
047690                                        P980-COMMAREA-PARMS
047700     SET P980-QUICKTIE-RESTRICT-FUNC TO TRUE
047710     MOVE EMP-NBR OF WS-MSTR         TO P980-EMP-NO
047720     EXEC CICS LINK
047730               PROGRAM(P980-PGM)
047740               COMMAREA(P980-COMMAREA-PARMS)
047750               LENGTH(P980-LGTH)
047760               RESP(WS-RESPONSE)
047770     END-EXEC
047780     MOVE WS-RESPONSE                TO FILE-STATUS
047790     IF NOT SUCCESS
047800        MOVE 'P3080-1'               TO ERR-PARAGRAPH
047810        MOVE 'LINK980'               TO ERR-KEY
047820        PERFORM P9999-GOT-PROBLEM
047830     END-IF.
047840*
047850 P3100-GET-AJ-EMPS.
047860*
047870     SET NOT-OUT-OF-TOWN TO TRUE
047880     MOVE ZEROS  TO AJ-EMP-NBR AJ-TEMP-EMP-NBR ON-DUTY-EMP
047890     MOVE SPACES TO WS-ASGN-FILE
047900     IF JSK1-ASGN(1:2) = 'EX'
047910        MOVE 'X'    TO ASGN-JOB-TYPE
047920        MOVE JSK1-ASGN-DIST     TO ASGN-DIST
047930        MOVE JSK1-ASGN-SUB-DIST TO ASGN-SUB-DIST
047940        MOVE JSK1-ASSIGNMENT    TO ASGN-XB-JOB IN ASGN-ASSIGNMENT
047950     ELSE
047960        MOVE 'A'    TO ASGN-JOB-TYPE
047970        MOVE AJ-JOB-DIST        TO ASGN-DIST
047980        MOVE AJ-JOB-SUB-DIST    TO ASGN-SUB-DIST
047990        MOVE AJ-JOB-ASSIGNMENT  TO ASGN-AJ-JOB IN ASGN-ASSIGNMENT
048000     END-IF
048010     MOVE '1'      TO ASGN-REC-TYPE
048020     MOVE ZEROS    TO ASGN-DATE-TIME
048030     PERFORM PXXXX-JOB-OWNER
048040     MOVE ASGN-EMP-NO TO AJ-EMP-NBR
048050     MOVE SPACES      TO WS-ASGN-FILE
048060     IF JSK1-ASGN(1:2) = 'EX'
048070        MOVE 'X'    TO ASGN-JOB-TYPE
048080        MOVE JSK1-ASGN-DIST     TO ASGN-DIST
048090        MOVE JSK1-ASGN-SUB-DIST TO ASGN-SUB-DIST
048100        MOVE JSK1-ASSIGNMENT    TO ASGN-XB-JOB IN ASGN-ASSIGNMENT
048110     ELSE
048120        MOVE 'A'    TO ASGN-JOB-TYPE
048130        MOVE AJ-JOB-DIST        TO ASGN-DIST
048140        MOVE AJ-JOB-SUB-DIST    TO ASGN-SUB-DIST
048150        MOVE AJ-JOB-ASSIGNMENT  TO ASGN-AJ-JOB IN ASGN-ASSIGNMENT
048160     END-IF
048170     MOVE '2'      TO ASGN-REC-TYPE
048180     MOVE ZEROS    TO ASGN-DATE-TIME
048190     PERFORM PXXXX-LATEST-TEMP
048200     MOVE ASGN-EMP-NO TO AJ-TEMP-EMP-NBR
048210     MOVE SPACES      TO WS-ASGN-FILE
048220     IF JSK1-ASGN(1:2) = 'EX'
048230        MOVE 'X'    TO ASGN-JOB-TYPE
048240        MOVE JSK1-ASGN-DIST     TO ASGN-DIST
048250        MOVE JSK1-ASGN-SUB-DIST TO ASGN-SUB-DIST
048260        MOVE JSK1-ASSIGNMENT    TO ASGN-XB-JOB IN ASGN-ASSIGNMENT
048270     ELSE
048280        MOVE 'A'    TO ASGN-JOB-TYPE
048290        MOVE AJ-JOB-DIST        TO ASGN-DIST
048300        MOVE AJ-JOB-SUB-DIST    TO ASGN-SUB-DIST
048310        MOVE AJ-JOB-ASSIGNMENT  TO ASGN-AJ-JOB IN ASGN-ASSIGNMENT
048320     END-IF
048330     MOVE '3'            TO ASGN-REC-TYPE
048340     MOVE ZEROS          TO ASGN-DATE-TIME
048350     PERFORM PXXXX-ON-DUTY-EMP
048360     MOVE ASGN-EMP-NO    TO ON-DUTY-EMP
048370     MOVE ASGN-ON-DUTY-DATE-TIME TO ON-DUTY-OUT-TOWN-CODE.
048380*
048390 P3200-GET-MARKUP-EMP.
048400*---------------------------------------------------------------*
048410*                   CONVERT  TO CNTL-TIME-ZONE                  *
048420*---------------------------------------------------------------*
048430     IF AJ-JOB-DIST    NOT = SCR08S-DIST
048440     OR AJ-JOB-SUB-DIST NOT = SCR08S-SUB-DIST
048450        MOVE SPACES            TO CNTLKEY-AREA
048460        MOVE '02'              TO CNTL-REC-TYPE
048470        MOVE AJ-JOB-DIST       TO CNTL-DIST
048480        MOVE AJ-JOB-SUB-DIST   TO CNTL-SUB-DIST
048490        MOVE CNTLKEY-AREA      TO CNTLKEY
048500        PERFORM P8610-READ-CNTLFILE
048510        IF NOT SUCCESS
048520           MOVE 'P3200-C'      TO ERR-PARAGRAPH
048530           MOVE CNTLKEY        TO ERR-KEY
048540           PERFORM P9999-GOT-PROBLEM
048550        END-IF
048560        MOVE SPACES            TO TZ-PARAMETERS
048570        MOVE CNTL-TIME-ZONE    TO TZ-OUT-ZONE
048580        MOVE PSTCA-TIME-ZONE   TO TZ-IN-ZONE
048590        MOVE WK-CALL-DATE-TIME TO TZ-IN-DATE-TIME
048600        PERFORM P8996-TIMEZONE
048610        IF TZ-INVALID-PARAMETERS
048620           MOVE 'P3200TZ'  TO ERR-PARAGRAPH
048630           PERFORM P8996-TZERROR
048640        END-IF
048650        MOVE ZEROES            TO DATE-CONVERSION-PARMS
048660        SET PARM-CONV          TO TRUE
048670        MOVE TZ-OUT-DATE       TO PARM-PRI-DATE-GREG
048680        EXEC CICS LINK
048690                  PROGRAM(P903-PGM)
048700                  COMMAREA(DATE-CONVERSION-PARMS)
048710                  LENGTH(P903-LGTH)
048720                  RESP(WS-RESPONSE)
048730        END-EXEC
048740        MOVE WS-RESPONSE          TO FILE-STATUS
048750        IF NOT SUCCESS
048760           MOVE 'P1000-1'         TO ERR-PARAGRAPH
048770           MOVE 'P903LINK'        TO ERR-KEY
048780           PERFORM P9999-GOT-PROBLEM
048790        END-IF
048800     ELSE
048810        MOVE CALL-DAY             TO PARM-PRI-DAY-OF-WEEK
048820     END-IF
048830
048840     MOVE SPACES   TO WS-SWASSGN-FILE
048850     MOVE ZEROS    TO AJ-EMP-NBR AJ-TEMP-EMP-NBR ON-DUTY-EMP
048860     MOVE AJ-JOB-DIST                TO SWASSGN-K-DISTRICT
048870     MOVE AJ-JOB-SUB-DIST            TO SWASSGN-K-SUB-DIST
048880     MOVE AJ-JOB-ASSIGNMENT          TO SWASSGN-K-ASSIGN
048890     MOVE PARM-PRI-DAY-OF-WEEK       TO SWASSGN-K-DAY
048900     MOVE SWASSGN-KEY                TO SWJOBKEY
048910     EXEC CICS READ
048920               DATASET(SWASSGN-VIA-ASSIGNMENT)
048930               INTO(WS-SWASSGN-FILE)
048940               LENGTH(SWASSIGN-RLGTH)
048950               RIDFLD(SWJOBKEY)
048960               KEYLENGTH(SWASSIGN-KLGTH)
048970               RESP(WS-RESPONSE)
048980     END-EXEC
048990     MOVE WS-RESPONSE TO FILE-STATUS
049000     IF NOT SUCCESS
049010       IF NOT NO-RECORD-FND
049020         MOVE 'P3200'   TO ERR-PARAGRAPH
049030         MOVE SWJOBKEY  TO ERR-KEY
049040         PERFORM P9999-GOT-PROBLEM
049050       END-IF
049060     ELSE
049070       IF SWASSGN-DATE = WK-CALL-DATE
049080          AND CALLER-CONFIRMED
049090         MOVE SWASSGN-EMP-NO TO AJ-EMP-NBR
049100       END-IF
049110     END-IF.
049120*
049130 P3300-CALL.
049140*
049150*    ESTABLISH THE LEAD TIME FOR THIS JOB
049160*
049170     MOVE SPACES                 TO WORK-CNTLKEY
049180     MOVE '02'                   TO WK-CNTL-REC-TYPE
049190     MOVE SCR08S-DIST            TO WK-CNTL-DIST
049200     MOVE SCR08S-SUB-DIST        TO WK-CNTL-SUB-DIST
049210     MOVE WORK-CNTLKEY           TO CNTLKEY
049220     PERFORM P8610-READ-CNTLFILE
049230     IF NOT SUCCESS
049240        MOVE 'P3420-2'           TO ERR-PARAGRAPH
049250        MOVE CNTLKEY             TO ERR-KEY
049260        PERFORM P9999-GOT-PROBLEM
049270     END-IF
049280     MOVE CNTL-LEAD-CALL-TIME       TO WK-LEAD-TIME
049290     IF TRCN-ADDL-LEAD-TIME > SPACES
049300        ADD TRCN-ADDL-LEAD-TIME-HR  TO WK-LEAD-TIME-HR
049310        MOVE WK-LEAD-TIME-MN        TO WK-LEAD-TIME-MN3
049320        ADD TRCN-ADDL-LEAD-TIME-MN  TO WK-LEAD-TIME-MN3
049330        IF WK-LEAD-TIME-MN3 > 60
049340           ADD 1                    TO WK-LEAD-TIME-HR
049350           SUBTRACT 60            FROM WK-LEAD-TIME-MN3
049360        END-IF
049370        MOVE WK-LEAD-TIME-MN3       TO WK-LEAD-TIME-MN
049380     END-IF
049390
049400     MOVE SPACES                    TO P921-COMMAREA-PARMS
049410     SET P921-CALL-FUNCTION         TO TRUE
049420     SET P08SCA-REPORT-READY        TO TRUE
049430*
049440*    STAMP SCHEDULED EXTRABOARD'S DATA IN AHIST/AHMISC RECORDS,
049450*    NOT THE REGULAR ASSIGNMENT BEING RELIEVED, WHEN APPLICABLE.
049460*
049470     IF JSK1-ASGN(1:2) = 'EX'
049480        MOVE JSK1-ASGN-DIST         TO P921-AJ-DIST
049490        MOVE JSK1-ASGN-SUB-DIST     TO P921-AJ-SUB-DIST
049500        MOVE JSK1-ASSIGNMENT        TO P921-AJ-ASSIGN
049510     ELSE
049520        MOVE AJ-JOB-DIST            TO P921-AJ-DIST
049530        MOVE AJ-JOB-SUB-DIST        TO P921-AJ-SUB-DIST
049540        MOVE AJ-JOB-ASSIGNMENT      TO P921-AJ-ASSIGN
049550     END-IF
049560     MOVE TRCN-KEY1                 TO P921-ASGN-PROFILE-KEY
049570     MOVE TRCN-TRAIN-ASGN           TO P921-TRAIN
049580     SET AJ-ASGN                    TO TRUE
049590     PERFORM P7000-FIND-ASGN-CLASS
049600     MOVE WS-ASGN-MERG-CLASS        TO P921-ASGN-MERG-CLASS
049610     MOVE PSTCA-TIME-ZONE           TO P921-TIME-ZONE
049620     MOVE WK-CALL-DATE-TIME         TO P921-EFF-DATE-TIME
049630                                       P921-JOB-DATE-TIME
049640     MOVE WK-LEAD-TIME              TO P921-CALL-LEAD-TIME
049650
049660     IF WS-ANNUL-FROM-DATE > SPACES
049670        SET DE-YYMMDD-FORMAT        TO TRUE
049680        MOVE WS-ANNUL-FROM-DATE     TO DE-YYMMDD
049690        PERFORM P8998-DATEEDIT
049700        MOVE DE-CCYYMMDD            TO WS-ANNUL-FROM-DATE-CENT
049710
049720        SET DE-YYMMDD-FORMAT        TO TRUE
049730        MOVE WS-ANNUL-TO-DATE       TO DE-YYMMDD
049740        PERFORM P8998-DATEEDIT
049750        MOVE DE-CCYYMMDD            TO WS-ANNUL-TO-DATE-CENT
049760     END-IF
049770
049780     PERFORM P7300-CONVERT-WK-CALL-DATES
049790
049800     IF WK-CALL-DATE-CENT < WS-ANNUL-FROM-DATE-CENT
049810     OR WK-CALL-DATE-CENT > WS-ANNUL-TO-DATE-CENT
049820     OR WS-ANNUL-FROM-DATE-CENT NOT > SPACES
049830        ADD 1                       TO WS-TOT-JOBS
049840        MOVE 'SCAL'                 TO P921-CALL-FUNC-CODE
049850        MOVE EMP-NBR IN WS-MSTR     TO P921-EMP-NO
049860        MOVE ZEROES                 TO SCHED-DONE-CODE
049870                                       SCHED-2ND-CODE
049880                                       SCHED-LST-CODE
049890                                       SCHED-FOUND-CODE
049900        IF   TRCN-2ND-ROUTE-DESIGNATION > SPACES
049910        AND  TRCN-LOCAL-REC
049920        AND (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
049930           PERFORM P3039-GET-CNTL-DIST-SUB-DIST
049940           IF NOT CNTL-SPLIT-SHIFT-HOS
049950              PERFORM P3037-CHECK-SCHED
049960           END-IF
049970        END-IF
049980        SET NOT-VACANCY-FILLED      TO TRUE
049990        MOVE SCHED-2ND-CODE         TO HOLD-SCHED-2ND-CODE
050000        PERFORM P5950-GET-HRS-SVC-INFO
050010        MOVE HOLD-SCHED-2ND-CODE    TO SCHED-2ND-CODE
050020        IF  SCHED-2ND
050030        AND ON-SCHED-1ST
050040           SET  P925-EMP-STATUS-GREEN  TO TRUE
050050        END-IF
050060        MOVE P925-REST-STATUS-FLAG     TO P921-EMP-REST-STATUS
050070*
050080*       SAVE ASSIGNMENT AND EMPLOYEE IN VRU SCAL TABLE
050090*
050100        ADD 1                       TO SUBV
050110        MOVE P921-AJ-PARM           TO WS-VRU-SCAL-PARM(SUBV)
050120        MOVE EMP-NBR IN WS-MSTR     TO WS-VRU-SCAL-EMP (SUBV)
050130     ELSE
050140        MOVE 'SCAN'                 TO P921-CALL-FUNC-CODE
050150     END-IF
050160     MOVE JSK2-ASGN-CC              TO P921-CALL-CC
050170*CNC0562 - BEG
050180*        THE KEY1 FROM THE JS RECORD FOR THE JOB (NOT THE RELIEF)
050190*        NEEDS TO BE PASSED TO P921. THIS IS THE KEY THAT IS
050200*        PASSED TO P921 ON A REGULAR CALL (CNP08).
050210     MOVE JS-KEY1                   TO P921-CALL-JSKEY1
050220     MOVE JSK1-ASGN                 TO JOB-DEF-CHECK
050230     IF JOB-DEF-RELIEF-ASGN
050240        MOVE JSK2-ASGN-DIST       TO P921-CALL-JSK1-ASGN-DIST
050250        MOVE JSK2-ASGN-SUB-DIST   TO P921-CALL-JSK1-ASGN-SUB-DIST
050260        MOVE JSK2-ASGN            TO P921-CALL-JSK1-ASGN
050270        MOVE JSK2-ASGN-CC         TO P921-CALL-JSK1-ASGN-CC
050280        MOVE JSK2-EXP-DATE        TO P921-CALL-JSK1-EXP-DATE
050290        MOVE JSK2-ASGN-DAY        TO P921-CALL-JSK1-ASGN-DAY
050300        MOVE JSK2-ASGN-START-TIME TO P921-CALL-JSK1-ASGN-STRT-TM
050310
050320        MOVE P921-CALL-JSKEY1     TO JSKEY1
050330        EXEC CICS READ
050340                  DATASET  (JS-VIA-JSKEY1)
050350                  INTO     (SAVE-JOB-SCHEDULE)
050360                  LENGTH   (JSKEY1-RLGTH)
050370                  RIDFLD   (JSKEY1)
050380                  KEYLENGTH(JSKEY1-KLGTH)
050390                  RESP     (WS-RESPONSE)
050400        END-EXEC
050410        MOVE WS-RESPONSE            TO FILE-STATUS
050420        IF NOT SUCCESS
050430           MOVE JS-KEY1             TO P921-CALL-JSKEY1
050440        END-IF
050450     END-IF
050460*CNC0562 - END
050470     MOVE TRCN-ROUTE-DESIGNATION    TO P921-ROUTE-DESIGNATION
050480     SET P921-CALLED-FROM-HOME      TO TRUE
050490*CNLD-207-DXC-B
050500     MOVE P925-CAN-WRR-LEAD-TIME    TO P921-CAN-WRR-LEAD-TIME
050510*CNLD-207-DXC-E
050520     EXEC CICS LINK
050530               PROGRAM(P921-PGM)
050540               COMMAREA(P921-COMMAREA-PARMS)
050550               LENGTH(P921-LGTH)
050560               RESP(WS-RESPONSE)
050570     END-EXEC
050580     MOVE WS-RESPONSE                  TO FILE-STATUS
050590     IF NOT SUCCESS
050600        MOVE 'P3300-1'                  TO ERR-PARAGRAPH
050610        PERFORM P9999-GOT-PROBLEM
050620     END-IF
050630*
050640*    UPDATE TRACKED HOS REST STATUS FOR EMPLOYEES
050650*    TRACKED VIA A DEADHEAD CALL.
050660*
050670     IF P921-CALL-FUNC-CODE = 'SCAL'
050680        PERFORM P3350-CALC-TRKEMP-HOS
050690     END-IF
050700*
050710*    CHECK IF VRUINFO RECORD EXISTS - SET CALL STATUS
050720*
050730     MOVE +0                   TO CNT-UNDER-VRU-CONTROL
050740                                  CNT-CMA-NOT-CALLED
050750                                  CNT-VRU-UNSUCCESSFUL
050760     MOVE WK-CALL-DATE-TIME TO WS-9300-CALL-DATE-TIME
050770     PERFORM P9300-READ-TASK-BY-STATION
050780     IF WS-TASK-FOUND
050790        MOVE SPACES            TO VRUINFO-FILE
050800        MOVE TASK-LOCATION-KEY TO VRUINFO-TASK-KEY
050810
050820        PERFORM P9310-READ-UPDATE-VRUINFO
050830        EVALUATE TRUE
050840           WHEN SUCCESS
050850              PERFORM VARYING VRUINFO-CC-SUB FROM 1 BY 1
050860                 UNTIL VRUINFO-CC-SUB       > VRUINFO-CC-MAX
050870                 IF EMP-NBR IN WS-MSTR =
050880                             VRUINFO-EMP-NBR(VRUINFO-CC-SUB)
050890                 OR JSK2-ASGN-CC       =
050900                             VRUINFO-CRAFT (VRUINFO-CC-SUB)
050910                    SET CMA-CALL-SUCCESSFUL (VRUINFO-CC-SUB)
050920                          TO TRUE
050930                    MOVE VRUINFO-CC-MAX TO VRUINFO-CC-SUB
050940                 END-IF
050950              END-PERFORM
050960              PERFORM P9320-REWRITE-VRUINFO
050970
050980*            SPIN THRU VRUINFO SEE IF TASK-STATUS CHANGE NEEDED
050990              PERFORM VARYING VRUINFO-CC-SUB FROM 1 BY 1
051000                        UNTIL VRUINFO-CC-SUB > VRUINFO-CC-MAX
051010                 IF EMP-UNDER-VRU-CONTROL (VRUINFO-CC-SUB)
051020                    ADD +1 TO CNT-UNDER-VRU-CONTROL
051030                 END-IF
051040                 IF VRUINFO-CRAFT(VRUINFO-CC-SUB) > SPACES
051050                    IF VRU-CALL-UNSUCCESSFUL(VRUINFO-CC-SUB)
051060                       ADD +1 TO CNT-VRU-UNSUCCESSFUL
051070                    END-IF
051080                    IF VRU-REQUIRED-CRAFT(VRUINFO-CC-SUB)
051090                      IF NOT EMP-UNDER-VRU-CONTROL(VRUINFO-CC-SUB)
051100                      AND NO-CALL-ATTMPT-MADE(VRUINFO-CC-SUB)
051110                         ADD +1 TO CNT-CMA-NOT-CALLED
051120                      END-IF
051130                    END-IF
051140                 END-IF
051150              END-PERFORM
051160           WHEN OTHER
051170              CONTINUE
051180        END-EVALUATE
051190        PERFORM P6500-CHECK-IVR-CALL-COMP
051200     END-IF
051210     .
051220*
051230 P3350-CALC-TRKEMP-HOS.
051240*
051250*    BROWSE THROUGH ALL EMPLOYEES BEING TRACKED ON THE ASSIGNMENT
051260*
051270     SET CALC-TRK-HOS                   TO TRUE
051280
051290     MOVE SPACES                        TO WS-EMP-TRACK
051300     SET EMPTRK-ASG-AJ                  TO TRUE
051310     MOVE P921-AJ-PARM                  TO EMPTRK-ASSIGNMENT
051320     MOVE ZEROES                        TO EMPTRK-ASGN-SEQ-NUM
051330     SET ETRK-NOT-DONE                  TO TRUE
051340     PERFORM UNTIL ETRK-DONE
051350        ADD  1                          TO EMPTRK-ASGN-SEQ-NUM
051360        MOVE EMPTRKASGNKEY-AREA         TO ETRACK-ASGNKEY
051370        EXEC CICS READ
051380                  DATASET(ETRACK-VIA-ASGN)
051390                  INTO(WS-EMP-TRACK)
051400                  RIDFLD(ETRACK-ASGNKEY)
051410                  LENGTH(ETRACK-ASGN-RLGTH)
051420                  KEYLENGTH(ETRACK-ASGN-KLGTH)
051430                  GTEQ
051440                  RESP(WS-RESPONSE)
051450        END-EXEC
051460        MOVE WS-RESPONSE                TO FILE-STATUS
051470        IF SUCCESS
051480           IF EMPTRK-ASG-AJ
051490              AND EMPTRK-ASSIGNMENT = P921-AJ-PARM
051500*
051510*             IF WE'RE DEALING WITH AN EMPLOYEE WHOSE TRACKING
051520*             INITIATED VIA A DEADHEAD, GET THEIR REST STATUS.
051530*
051540              IF EMPTRK-DHEMP
051550                 MOVE EMPTRK-EMP-NO     TO MSTRNBRK
051560                 PERFORM P8505-READ-MASTER-ONLY
051570                 PERFORM P5950-GET-HRS-SVC-INFO
051580                 IF P925-REST-STATUS-FLAG
051590                                     NOT = EMPTRK-REST-STATUS-FLAG
051600                    AND NOT (P925-EMP-STATUS-UNKNOWN
051610                    OR P925-EMP-STATUS-NA)
051620                    PERFORM P3355-UPDATE-HOS-STATUS
051630                 END-IF
051640              END-IF
051650           ELSE
051660              SET ETRK-DONE             TO TRUE
051670           END-IF
051680        ELSE
051690           SET ETRK-DONE                TO TRUE
051700           IF NOT (NO-RECORD-FND OR END-OF-FILE)
051710              MOVE 'P3350-1'            TO ERR-PARAGRAPH
051720              MOVE ETRACK-ASGNKEY       TO ERR-KEY
051730              PERFORM P9999-GOT-PROBLEM
051740           END-IF
051750        END-IF
051760     END-PERFORM
051770*
051780*    SET CALC-TRK-NOS-NO TO TRUE SO THAT THE NEXT TIME WE LINK TO
051790*    CNP925, WE DON'T INADVERTENTLY INCLUDE VIRTUAL HOS RECORDS
051800*    IN THE WORKING EMPLOYEE'S HOS CALCULATIONS.
051810*
051820     SET CALC-TRK-HOS-NO             TO TRUE
051830     .
051840*
051850 P3355-UPDATE-HOS-STATUS.
051860*
051870     MOVE EMPTRKKEY-AREA                TO ETRACK-EMPKEY
051880     EXEC CICS READ
051890               UPDATE
051900               DATASET(ETRACK-VIA-EMP)
051910               INTO(WS-EMP-TRACK)
051920               LENGTH(ETRACK-EMP-RLGTH)
051930               RIDFLD(ETRACK-EMPKEY)
051940               RESP(WS-RESPONSE)
051950     END-EXEC
051960     MOVE WS-RESPONSE                   TO FILE-STATUS
051970     IF SUCCESS
051980        MOVE P925-REST-STATUS-FLAG      TO EMPTRK-REST-STATUS-FLAG
051990        IF NOT EMPTRK-VALID-REST-STATUS
052000           SET EMPTRK-EMP-STATUS-GREEN  TO TRUE
052010        END-IF
052020        EXEC CICS REWRITE
052030                  DATASET(ETRACK-VIA-EMP)
052040                  FROM(WS-EMP-TRACK)
052050                  LENGTH(ETRACK-EMP-RLGTH)
052060                  RESP(WS-RESPONSE)
052070        END-EXEC
052080        MOVE WS-RESPONSE                TO FILE-STATUS
052090        IF NOT SUCCESS
052100           MOVE 'P3355-1'               TO ERR-PARAGRAPH
052110           MOVE ETRACK-EMPKEY           TO ERR-KEY
052120           PERFORM P9999-GOT-PROBLEM
052130        END-IF
052140     ELSE
052150        IF NOT NO-RECORD-FND
052160           MOVE 'P3355-2'               TO ERR-PARAGRAPH
052170           MOVE ETRACK-EMPKEY           TO ERR-KEY
052180           PERFORM P9999-GOT-PROBLEM
052190        END-IF
052200     END-IF
052210     .
052220*
052230 P3400-SETUP-VAC-COMMAREA.
052240*
052250     MOVE SPACES              TO RELIEF-ONDUTY-FLAG
052260     MOVE AJ-JOB-ASGN-ID      TO JOB-DEF-CHECK
052270     IF JOB-DEF-RELIEF-ASGN
052280        MOVE SAVE-ONDUTY-FLAG TO RELIEF-ONDUTY-FLAG
052290     END-IF
052300     IF NOT (AJ-BLANK-JOB OR AJ-CLOSED-JOB OR RELIEF-ONDUTY)
052310        ADD 1                  TO P08S-TSQ-CNT
052320                                  P08S-TSQ-ACT-CNT
052330                                  WS-TOT-JOBS
052340        IF P08S-TSQ-CNT NOT > P08S-TSQ-MAX
052350           MOVE P08S-TSQ-CNT   TO SUB2
052360           MOVE SPACES         TO P08S-TSQ-ARRAY(SUB2)
052370           MOVE JS-KEY2        TO P08S-TSQ-JSKEY2(SUB2)
052380           IF JOB-SCHED-ASGN-XTRA
052390              IF PSTCA-SUB = 1
052400                 MOVE '** FILL FROM SPAREBOARD **'
052410                               TO P08S-TSQ-OWNER-NAME(SUB2)
052420              ELSE
052430                 MOVE '** REMPLIR A PARTIR DE SB **'
052440                               TO P08S-TSQ-OWNER-NAME(SUB2)
052450              END-IF
052460           ELSE
052470              IF AJ-TEMP-EMP-NBR > ZEROS
052480                 MOVE AJ-TEMP-EMP-NBR
052490                               TO P08S-TSQ-OWNER(SUB2)
052500              ELSE
052510                 MOVE AJ-EMP-NBR
052520                               TO P08S-TSQ-OWNER(SUB2)
052530              END-IF
052540           END-IF
052550*
052560*          IF AN EMPLOYEE IS DISQUALIFIED, WE WILL DISPLAY THE
052570*          EMPLOYEE, WITH A MESSAGE.
052580*
052590           IF EMP-DISQUALIFIED
052600              MOVE EMP-NBR IN WS-MSTR TO P08S-TSQ-EMP-SEL(SUB2)
052610           END-IF
052620           IF STARTS-DISQUALIFIED
052630              MOVE WS-STARTS-TOTAL TO P08S-TSQ-STARTS-TOTAL(SUB2)
052640              SET P08S-TSQ-STARTS-DISQUALIFIED(SUB2) TO TRUE
052650           END-IF
052660           IF QTIE-DISQUALIFIED
052670              SET P08S-TSQ-QTIE-DISQUALIFIED(SUB2)  TO TRUE
052680           END-IF
052690           IF EMP-HOS-EXD
052700              MOVE EMP-NBR IN WS-MSTR TO P08S-TSQ-EMP-SEL(SUB2)
052710           END-IF
052720           MOVE JSK2-ASGN      TO JOB-DEF-CHECK
052730           SET JOB-IND TO 1
052740           SEARCH JOB-ENTRIES VARYING JOB-IND
052750              AT END
052760                 MOVE 1         TO SUB1
052770              WHEN JOB-DEF-TBL-CLASS(JOB-IND) = JOB-DEF-CLASS
052780                 SET SUB1       TO JOB-IND
052790           SEARCH CRAFT-CODE-TABLE VARYING CT-IND
052800              AT END CONTINUE
052810                 WHEN CT-CRAFT-CODE(CT-IND) = JSK2-ASGN-CC
052820                    MOVE CT-CRAFT-COLOR(CT-IND)
052830                                TO P08S-TSQ-COLOR(SUB2)
052840           END-SEARCH
052850           IF WS-TRK-REASON NOT > SPACES
052860              SET P08S-TSQ-TRK-UNKNOWN(SUB2) TO TRUE
052870           ELSE
052880              MOVE WS-TRK-REASON     TO P08S-TSQ-TRK-REASON(SUB2)
052890           END-IF
052900           PERFORM P3410-GET-JOB-QUALIFICATIONS
052910           PERFORM P3420-SETUP-CALL-TIME
052920        END-IF
052930     END-IF.
052940*
052950 P3410-GET-JOB-QUALIFICATIONS.
052960*
052970*    SEE IF THE ASSIGNMENT HAS ANY QUALIFICATIONS SPECIFIC TO
052980*    TO THE ASSIGNMENT (THIS WILL GET THE FIRST FIVE)
052990*
053000     MOVE SPACES                TO WORK-CNTLKEY
053010     MOVE '13'                  TO WK-CNTL-REC-TYPE
053020     MOVE AJ-JOB-DIST           TO WK-CNTL-DIST
053030     MOVE AJ-JOB-SUB-DIST       TO WK-CNTL-SUB-DIST
053040     MOVE 'A'                   TO WK-CNTL-ASGN-QUAL-TYPE
053050     MOVE AJ-JOB-ASSIGNMENT     TO WK-CNTL-ASGN-QUAL
053060     MOVE WORK-CNTLKEY          TO CNTLKEY
053070     PERFORM P8610-READ-CNTLFILE
053080     IF NOT SUCCESS
053090        MOVE '**'               TO WK-CNTL-ASGN-QUAL-5-6
053100        MOVE WORK-CNTLKEY       TO CNTLKEY
053110        PERFORM P8610-READ-CNTLFILE
053120     END-IF
053130     MOVE SPACES                TO P08S-TSQ-QUAL-AREA(SUB2)
053140     IF SUCCESS
053150        PERFORM VARYING SUB3 FROM 1 BY 1
053160           UNTIL SUB3 > 5
053170           IF CNTL-QUALIFICATION-TABLE-ENTRY(SUB3) > SPACE
053180              MOVE CNTL-QUALIFICATION-TABLE-ENTRY(SUB3)
053190                                TO P08S-TSQ-QUAL(SUB2, SUB3)
053200           END-IF
053210        END-PERFORM
053220     ELSE
053230        IF NOT (NO-RECORD-FND OR END-OF-FILE)
053240           MOVE 'P3410-1'       TO ERR-PARAGRAPH
053250           MOVE WORK-CNTLKEY    TO ERR-KEY
053260           PERFORM P9999-GOT-PROBLEM
053270        END-IF
053280     END-IF
053290*
053300*    HERE WE GET THE "SIXTH" QUALIFICATION FROM THE ASSIGNMENT
053310*    PROFILE RECORD
053320*CNC0572 - WITH THIS ENHANCEMENT THERE CAN BE UP TO 3 SPCL QUALS
053330*
053340     MOVE WK-CNTL-ASGN-QUAL-7-8 TO WS-HOLD-CC
053350     IF WS-HOLD-CC = 'FI' OR 'SE'
053360        MOVE 'SE'               TO WS-HOLD-CC
053370     END-IF
053380     IF WS-HOLD-CC = 'S1' OR 'S2'
053390        MOVE 'SW'               TO WS-HOLD-CC
053400     END-IF
053410     IF WK-CNTL-ASGN-QUAL-7-8 = ('B1' OR 'B2')
053420        MOVE 'BK'               TO WS-HOLD-CC
053430     END-IF
053440*CNC0572 - BEG
053450***  PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
053460***     IF WS-HOLD-CC = TRCN-SPCL-QUAL-CC(SUBA)
053470***        MOVE TRCN-SPCL-QUAL(SUBA) TO P08S-TSQ-QUAL(SUB2, 6)
053480***     END-IF
053490***  END-PERFORM.
053500     MOVE 5                           TO SUBB
053510     PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
053520        IF WS-HOLD-CC = TRCN-SPCL-QUAL-CC(SUBA)
053530           ADD 1 TO SUBB
053540           IF SUBB < 9
053550              MOVE TRCN-SPCL-QUAL(SUBA)
053560                                      TO P08S-TSQ-QUAL(SUB2, SUBB)
053570           END-IF
053580        END-IF
053590     END-PERFORM
053600     PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
053610        IF WS-HOLD-CC = TRCN-SPCL-QUAL2-CC(SUBA)
053620           ADD 1 TO SUBB
053630           IF SUBB < 9
053640              MOVE TRCN-SPCL-QUAL2(SUBA)
053650                                      TO P08S-TSQ-QUAL(SUB2, SUBB)
053660           END-IF
053670        END-IF
053680     END-PERFORM
053690     .
053700*CNC0572 - END
053710*
053720 P3420-SETUP-CALL-TIME.
053730*
053740*    ESTABLISH THE CALL DATE/TIME FOR THIS JOB
053750*
053760     MOVE PSTCA-TIME-ZONE        TO WS-TIME-ZONE
053770     MOVE SPACES                 TO WORK-CNTLKEY
053780     MOVE '02'                   TO WK-CNTL-REC-TYPE
053790     MOVE SCR08S-DIST            TO WK-CNTL-DIST
053800     MOVE SCR08S-SUB-DIST        TO WK-CNTL-SUB-DIST
053810     MOVE WORK-CNTLKEY           TO CNTLKEY
053820     PERFORM P8610-READ-CNTLFILE
053830     IF NOT SUCCESS
053840        MOVE 'P3420-2'           TO ERR-PARAGRAPH
053850        MOVE CNTLKEY             TO ERR-KEY
053860        PERFORM P9999-GOT-PROBLEM
053870     END-IF
053880     IF JSK2-ASGN-DAY-NUM = CALL-DAY1
053890        MOVE START-DATE-1           TO P08S-TSQ-CALL-DATE(SUB2)
053900        MOVE JSK2-ASGN-START-TIME   TO P08S-TSQ-CALL-TIME(SUB2)
053910        MOVE CALL-DAY1              TO P08S-TSQ-CALL-DAY(SUB2)
053920     ELSE
053930        MOVE START-DATE-2           TO P08S-TSQ-CALL-DATE(SUB2)
053940        MOVE JSK2-ASGN-START-TIME   TO P08S-TSQ-CALL-TIME(SUB2)
053950        MOVE CALL-DAY2              TO P08S-TSQ-CALL-DAY(SUB2)
053960     END-IF
053970     MOVE P08S-TSQ-CALL-DATE-TIME(SUB2)
053980                               TO P08S-TSQ-JOB-DATE-TIME(SUB2)
053990*
054000*    ESTABLISH THE LEAD TIME FOR THIS JOB
054010*
054020     MOVE CNTL-LEAD-CALL-TIME       TO WK-LEAD-TIME
054030     IF TRCN-ADDL-LEAD-TIME > SPACES
054040        ADD TRCN-ADDL-LEAD-TIME-HR  TO WK-LEAD-TIME-HR
054050        MOVE WK-LEAD-TIME-MN        TO WK-LEAD-TIME-MN3
054060        ADD TRCN-ADDL-LEAD-TIME-MN  TO WK-LEAD-TIME-MN3
054070        IF WK-LEAD-TIME-MN3 > 60
054080           ADD 1                    TO WK-LEAD-TIME-HR
054090           SUBTRACT 60            FROM WK-LEAD-TIME-MN3
054100        END-IF
054110        MOVE WK-LEAD-TIME-MN3       TO WK-LEAD-TIME-MN
054120     END-IF
054130     MOVE WK-LEAD-TIME              TO P08S-TSQ-LEAD-TIME(SUB2)
054140     SET P08S-TSQ-CALL-MINUS-LD(SUB2)
054150                                    TO TRUE.
054160*
054170 P4000-DISPLAY-VACANCIES.
054180*
054190     MOVE P08SCA-SUB2               TO SUB2
054200     IF PFKEY7
054210        IF SUB2 NOT LESS ARRAY-MAX
054220           SUBTRACT ARRAY-MAX     FROM SUB2
054230        ELSE
054240*              NO-FURTHER-SCROLL-MSG
054250           MOVE 'N012'              TO MSGLOG-CODE
054260           MOVE -1                  TO SCR08S-CODE-CURSOR
054270           PERFORM P9000-SEND-MAP-AND-RETURN
054280        END-IF
054290     ELSE
054300        IF PFKEY8
054310           ADD ARRAY-MAX            TO SUB2
054320           ADD 1                    TO SUB2
054330           IF SUB2 > P08S-TSQ-MAX
054340              OR P08S-TSQ-ARRAY(SUB2) NOT > SPACE
054350              MOVE 'N012'           TO MSGLOG-CODE
054360              MOVE -1               TO SCR08S-CODE-CURSOR
054370              PERFORM P9000-SEND-MAP-AND-RETURN
054380           END-IF
054390           SUBTRACT 1             FROM SUB2
054400        ELSE
054410           MOVE ZEROS               TO SUB2
054420        END-IF
054430     END-IF
054440     MOVE SUB2                      TO P08SCA-SUB2
054450     PERFORM VARYING SUB1 FROM 1 BY 1
054460        UNTIL SUB1 > ARRAY-MAX
054470        MOVE LOW-VALUES             TO SCR08S-DETAIL-ARRAY(SUB1)
054480        MOVE SPACES                 TO SCR08S-FUNC(SUB1)
054490                                       SCR08S-OD-TIME(SUB1)
054500                                       SCR08S-ASGN(SUB1)
054510                                       SCR08S-CC(SUB1)
054520        PERFORM P4050-CLEAR-EMP-DATA
054530        PERFORM P4090-RESET-HIGHLIGHT
054540        PERFORM P4060-LOCK-FIELDS
054550     END-PERFORM
054560     MOVE P08SCA-SUB2               TO SUB2
054570     MOVE ZEROS                     TO SUB1
054580                                       DONE-CODE
054590     PERFORM UNTIL DONE
054600        ADD 1                       TO SUB1
054610        IF SUB1 NOT > ARRAY-MAX
054620           ADD 1                    TO SUB2
054630           IF SUB2 NOT > P08S-TSQ-CNT
054640              PERFORM P4010-DISPLAY-VACANCY
054650           ELSE
054660              SET DONE              TO TRUE
054670           END-IF
054680        ELSE
054690           SET DONE                 TO TRUE
054700        END-IF
054710     END-PERFORM
054720     MOVE SPACES                    TO SCR08S-MORE
054730     IF P08SCA-SUB2 > ZERO
054740        COMPUTE SUB3 = P08SCA-SUB2 + ARRAY-MAX + 1
054750        IF SUB3 NOT > P08S-TSQ-CNT
054760           IF PSTCA-SUB = 1
054770              MOVE '<< MORE >>'     TO SCR08S-MORE
054780           ELSE
054790              MOVE '<< MORE >>'     TO SCR08S-MORE
054800           END-IF
054810        ELSE
054820           IF PSTCA-SUB = 1
054830              MOVE '<<<< MORE '     TO SCR08S-MORE
054840           ELSE
054850              MOVE '<<<< MORE '     TO SCR08S-MORE
054860           END-IF
054870        END-IF
054880     ELSE
054890        COMPUTE SUB3 = P08SCA-SUB2 + ARRAY-MAX + 1
054900        IF SUB3 NOT > P08S-TSQ-CNT
054910           IF PSTCA-SUB = 1
054920              MOVE 'MORE >>>> '     TO SCR08S-MORE
054930           ELSE
054940              MOVE 'MORE >>>> '     TO SCR08S-MORE
054950           END-IF
054960        ELSE
054970           MOVE SPACES              TO SCR08S-MORE
054980        END-IF
054990     END-IF.
055000*
055010 P4010-DISPLAY-VACANCY.
055020*
055030     IF SCR08S-CODE NOT = 'V'
055040        MOVE MDT                    TO SCR08S-FUNC-ATTR(SUB1)
055050                                       SCR08S-OD-TIME-ATTR(SUB1)
055060                                       SCR08S-NAME-ATTR(SUB1)
055070                                       SCR08S-ST-ATTR(SUB1)
055080                                       SCR08S-ECC-ATTR(SUB1)
055090     END-IF
055100     MOVE P08S-TSQ-JSKEY2(SUB2)     TO JSKEY2
055110     PERFORM P8600-READ-JSKEY2
055120     IF NOT SUCCESS
055130        MOVE 'P4010-1'              TO ERR-PARAGRAPH
055140        MOVE JSKEY2                 TO ERR-KEY
055150        PERFORM P9999-GOT-PROBLEM
055160     END-IF
055170     MOVE JSK2-ASGN-DIST            TO TRCN-DIST3
055180     MOVE JSK2-ASGN-SUB-DIST        TO TRCN-SDIST3
055190     MOVE JSK2-ASGN                 TO TRCN-ASSIGNMENT
055200     MOVE TRCN-KEY3                 TO TRCNKEY3
055210     PERFORM P8630-READ-TRCNKEY3
055220     IF NOT SUCCESS
055230        MOVE 'P4010-2'              TO ERR-PARAGRAPH
055240        MOVE TRCNKEY3               TO ERR-KEY
055250        PERFORM P9999-GOT-PROBLEM
055260     END-IF
055270
055280     MOVE JSK1-ASGN-DIST            TO AJ-JOB-DIST
055290     MOVE JSK1-ASGN-SUB-DIST        TO AJ-JOB-SUB-DIST
055300     MOVE JSK1-ASGN                 TO AJ-JOB-ASGN-ID
055310     MOVE JSK1-ASGN-CC              TO AJ-JOB-ASGN-CC
055320     MOVE AJJOBKEY-AREA             TO AJJOBKEY
055330     PERFORM P8640-READ-AJJOBKEY
055340     IF NOT SUCCESS
055350        MOVE 'P4010-3'              TO ERR-PARAGRAPH
055360        MOVE AJJOBKEY               TO ERR-KEY
055370        PERFORM P9999-GOT-PROBLEM
055380     END-IF
055390     MOVE TRCN-TRAIN-ASGN           TO SCR08S-ASGN(SUB1)
055400     MOVE JSK2-ASGN-CC              TO SCR08S-CC(SUB1)
055410     SET  P08S-TSQ-NO-CALL(SUB2)    TO TRUE
055420     MOVE P08S-TSQ-CALL-TIME(SUB2)  TO SCR08S-OD-TIME(SUB1)
055430     PERFORM P4080-RESET-EMP-COLORS
055440     PERFORM P4020-DISPLAY-EMP-INFO
055450     IF P08S-TSQ-EMP-SEL(SUB2) <= '0000000000'
055460        PERFORM P4030-DISPLAY-OWNR-INFO
055470     END-IF.
055480*
055490 P4020-DISPLAY-EMP-INFO.
055500*
055510     MOVE ' '                      TO P08S-TSQ-NO-CALL-CODE(SUB2)
055520                                      WS-CBCK-FL
055530     PERFORM P4050-CLEAR-EMP-DATA
055540     PERFORM P4080-RESET-EMP-COLORS
055550*CNLD-220-B
055560     MOVE WHITE               TO SCR08S-NAME-COL(SUB1)
055570*CNLD-220-E
055580     IF P08S-TSQ-EMP-SEL(SUB2)         > '000000000'
055590        MOVE P08S-TSQ-EMP-SEL(SUB2)   TO MSTRNBRK
055600        PERFORM P8500-READ-MASTER
055610        MOVE SPACES                   TO PS52-COMMAREA-PARMS
055620        MOVE P08S-TSQ-EMP-SEL(SUB2)   TO PS52-EMP-NBR
055630        SET PS52-CMA-MODE             TO TRUE
055640        MOVE WS-LOCAL-DATE-TIME       TO PS52-INPUT-DATE-TIME
055650        PERFORM P8520-LINK-TO-PS52
055660
055670        MOVE EMP-NAME IN WS-MSTR      TO SCR08S-NAME(SUB1)
055680                                         P08S-TSQ-EMP-NAME(SUB2)
055690        IF LAYOFF-CODE IN WS-MSTR  NOT = 'A0'
055700           MOVE LAYOFF-CODE           TO SCR08S-ST(SUB1)
055710           IF LAYOFF-EM-CODE > SPACES
055720              MOVE LAYOFF-EM-CODE     TO SCR08S-ECC(SUB1)
055730           END-IF
055740        END-IF
055750        MOVE ZEROS                    TO DATE-CONVERSION-PARMS
055760        SET  PARM-SUBTRACT            TO TRUE
055770        MOVE P08S-TSQ-CALL-DATE(SUB2) TO PARM-PRI-DATE-GREG
055780                                         WK-CALL-DATE
055790        MOVE P08S-TSQ-CALL-TIME(SUB2) TO PARM-PRI-HRMN
055800                                         WK-CALL-TIME
055810        MOVE P08S-TSQ-LEAD-TIME(SUB2) TO PARM-SEC-HRMN
055820        EXEC CICS LINK
055830                  PROGRAM(P903-PGM)
055840                  COMMAREA(DATE-CONVERSION-PARMS)
055850                  LENGTH(P903-LGTH)
055860                  RESP(WS-RESPONSE)
055870        END-EXEC
055880        MOVE WS-RESPONSE            TO FILE-STATUS
055890        IF NOT SUCCESS
055900           MOVE 'P4020-1'           TO ERR-PARAGRAPH
055910           PERFORM P9999-GOT-PROBLEM
055920        END-IF
055930        MOVE PARM-RES-GREG-CENT     TO WK-CALL-ML-CENT
055940        MOVE PARM-RES-DATE-GREG     TO WK-CALL-ML-DATE
055950        MOVE PARM-RES-HRMN          TO WK-CALL-ML-TIME
055960        MOVE SCR08S-CC(SUB1)        TO WS-CRAFT-CODE-CHECK
055970        PERFORM P7200-CONVERT-MASTER-DATES
055980        PERFORM P7300-CONVERT-WK-CALL-DATES
055990
056000        PERFORM P4025-DISPLAY-PHONE-INFO
056010
056020        PERFORM P3036-DETERMINE-PREP-TIME
056030        PERFORM P3038-DETERMINE-EARLIEST-CALL
056040        PERFORM P3038A-DETERMINE-EARLI-US-CALL
056050*
056060*       IF EMPLOYEE HAS REQUESTED A SHORT CALL TIME, AND THE
056070*       ORDER TIME MINUS EMP LEAD TIME (ADJUSTED FOR PREP) IS
056080*       GREATER THAN THE CURRENT TIME, POPULATE THE FUNC FIELD
056090*       WITH A 'CBCK' (CALLBACK)
056100*
056110        MOVE SPACE                      TO WS-SHORT-FLAG
056120        IF PS52-REQ-LEAD-TIME > SPACES
056130           AND PS52-REQ-LEAD-TIME > '0000'
056140*
056150*          ADJUST FOR PREP TIME
056160*
056170           MOVE ZEROS                   TO DATE-CONVERSION-PARMS
056180           SET PARM-SUBTRACT            TO TRUE
056190           MOVE WK-CALL-DATE            TO PARM-PRI-DATE-GREG
056200           MOVE WK-CALL-TIME            TO PARM-PRI-HRMN
056210           MOVE WS-PREP-TIME            TO PARM-SEC-HRMN
056220           EXEC CICS LINK
056230                     PROGRAM(P903-PGM)
056240                     COMMAREA(DATE-CONVERSION-PARMS)
056250                     LENGTH(P903-LGTH)
056260                     RESP(WS-RESPONSE)
056270           END-EXEC
056280           MOVE WS-RESPONSE             TO FILE-STATUS
056290           IF NOT SUCCESS
056300              MOVE 'P4020-2'            TO ERR-PARAGRAPH
056310              PERFORM P9999-GOT-PROBLEM
056320           END-IF
056330           MOVE PARM-RES-DATE-GREG      TO WS-ADJUSTED-DATE
056340           MOVE PARM-RES-TIME           TO WS-ADJUSTED-TIME
056350*
056360*          SUBTRACT THE EMPLOYEE'S REQUESTED LEAD TIME
056370*
056380           MOVE ZEROS                   TO DATE-CONVERSION-PARMS
056390           SET PARM-SUBTRACT            TO TRUE
056400           MOVE WS-ADJUSTED-DATE        TO PARM-PRI-DATE-GREG
056410           MOVE WS-ADJUSTED-TIME        TO PARM-PRI-HRMN
056420           MOVE PS52-REQ-LEAD-TIME      TO PARM-SEC-HRMN
056430           EXEC CICS LINK
056440                     PROGRAM(P903-PGM)
056450                     COMMAREA(DATE-CONVERSION-PARMS)
056460                     LENGTH(P903-LGTH)
056470                     RESP(WS-RESPONSE)
056480           END-EXEC
056490           MOVE WS-RESPONSE             TO FILE-STATUS
056500           IF NOT SUCCESS
056510              MOVE 'P4020-3'            TO ERR-PARAGRAPH
056520              PERFORM P9999-GOT-PROBLEM
056530           END-IF
056540           MOVE PARM-RES-GREG-CENT      TO WS-ADJUSTED-CC
056550           MOVE PARM-RES-DATE-GREG      TO WS-ADJUSTED-DATE
056560           MOVE PARM-RES-TIME           TO WS-ADJUSTED-TIME
056570           IF WS-ADJUSTED-DATE-TIME      > WS-LOCAL-DATE-TIME-CENT
056580              MOVE SPACES               TO SCR08S-PHONE(SUB1)
056590              IF SCR08S-FUNC(SUB1)   NOT = 'QTIE'
056600              AND NOT WS-OD-TIME-CHANGED
056610                 MOVE 'CBCK'            TO SCR08S-FUNC(SUB1)
056620                 STRING 'SHORT LEAD REQ: ENTER KEY TO CBCK '
056630                        'OR ENTER V??? FOR IVR SHORT CALL'
056640                        DELIMITED BY SIZE
056650                        INTO SCR08S-PHONE(SUB1)
056660                 MOVE 'Y'         TO P08S-TSQ-SAVE-CBCK-FLAG(SUB2)
056670              ELSE
056680                 STRING 'SHORT LEAD REQ: TYPE "CBCK" '
056690                        'IN FUNC AND PRESS ENTER FOR CALLBACK'
056700                         DELIMITED BY SIZE
056710                         INTO SCR08S-PHONE(SUB1)
056720              END-IF
056730              SET  WS-CBCK              TO TRUE
056740              SET  WS-SHORT-LEAD        TO TRUE
056750              MOVE REV-VIDEO            TO SCR08S-FUNC-HI(SUB1)
056760           END-IF
056770        END-IF
056780
056790        IF PS52-REQ-CALL-TIME > SPACES
056800           AND PS52-REQ-CALL-TIME > '0000'
056810           MOVE ZEROS                   TO DATE-CONVERSION-PARMS
056820           SET PARM-SUBTRACT            TO TRUE
056830           MOVE WK-CALL-DATE            TO PARM-PRI-DATE-GREG
056840           MOVE WK-CALL-TIME            TO PARM-PRI-HRMN
056850           MOVE PS52-REQ-CALL-TIME      TO PARM-SEC-HRMN
056860           EXEC CICS LINK
056870                     PROGRAM(P903-PGM)
056880                     COMMAREA(DATE-CONVERSION-PARMS)
056890                     LENGTH(P903-LGTH)
056900                     RESP(WS-RESPONSE)
056910           END-EXEC
056920           MOVE WS-RESPONSE             TO FILE-STATUS
056930           IF NOT SUCCESS
056940              MOVE 'P4020-4'            TO ERR-PARAGRAPH
056950              PERFORM P9999-GOT-PROBLEM
056960           END-IF
056970           MOVE PARM-RES-GREG-CENT      TO WS-SHORT-CALL-CC
056980           MOVE PARM-RES-DATE-GREG      TO WS-SHORT-CALL-DATE
056990           MOVE PARM-RES-TIME           TO WS-SHORT-CALL-TIME
057000           IF WS-SHORT-CALL-DATE-TIME > WS-LOCAL-DATE-TIME-CENT
057010              IF PS52-REQ-LEAD-TIME NOT >  SPACES
057020              OR PS52-REQ-LEAD-TIME < '0001'
057030                 MOVE WS-SHORT-CALL-DATE-TIME
057040                                        TO WS-ADJUSTED-DATE-TIME
057050                 MOVE SPACES            TO SCR08S-PHONE(SUB1)
057060                 IF SCR08S-FUNC(SUB1)   NOT = 'QTIE'
057070                 AND NOT WS-OD-TIME-CHANGED
057080                    MOVE 'CBCK'         TO SCR08S-FUNC(SUB1)
057090                    STRING 'SHORT CALL REQ: ENTER KEY TO CBCK '
057100                           'OR ENTER V??? FOR IVR SHORT CALL'
057110                           DELIMITED BY SIZE
057120                           INTO SCR08S-PHONE(SUB1)
057130                    MOVE 'Y'   TO P08S-TSQ-SAVE-CBCK-FLAG(SUB2)
057140                 ELSE
057150                    STRING 'SHORT CALL REQ: TYPE "CBCK" '
057160                           'IN FUNC AND PRESS ENTER FOR CALLBACK'
057170                            DELIMITED BY SIZE
057180                            INTO SCR08S-PHONE(SUB1)
057190                 END-IF
057200                 SET  WS-CBCK           TO TRUE
057210                 SET  WS-SHORT-CALL     TO TRUE
057220                 MOVE REV-VIDEO         TO SCR08S-FUNC-HI(SUB1)
057230              ELSE
057240                 IF WS-SHORT-CALL-DATE-TIME
057250                                        >= WS-ADJUSTED-DATE-TIME
057260                    MOVE WS-SHORT-CALL-DATE-TIME
057270                                        TO WS-ADJUSTED-DATE-TIME
057280                    MOVE SPACES         TO SCR08S-PHONE(SUB1)
057290                    IF SCR08S-FUNC(SUB1)   NOT = 'QTIE'
057300                    AND NOT WS-OD-TIME-CHANGED
057310                       MOVE 'CBCK'            TO SCR08S-FUNC(SUB1)
057320                       STRING 'SHORT LEAD REQ: ENTER KEY TO CBCK '
057330                              'OR ENTER V??? FOR IVR SHORT CALL'
057340                              DELIMITED BY SIZE
057350                              INTO SCR08S-PHONE(SUB1)
057360                       MOVE 'Y'   TO P08S-TSQ-SAVE-CBCK-FLAG(SUB2)
057370                    ELSE
057380                       STRING 'SHORT LEAD REQ: TYPE "CBCK" '
057390                            'IN FUNC AND PRESS ENTER FOR CALLBACK'
057400                               DELIMITED BY SIZE
057410                               INTO SCR08S-PHONE(SUB1)
057420                    END-IF
057430                    SET  WS-CBCK        TO TRUE
057440                    SET  WS-SHORT-CALL  TO TRUE
057450                    MOVE REV-VIDEO      TO SCR08S-FUNC-HI(SUB1)
057460                 END-IF
057470              END-IF
057480           END-IF
057490        END-IF
057500
057510        MOVE ZEROES                TO SCHED-DONE-CODE
057520                                      SCHED-2ND-CODE
057530                                      SCHED-LST-CODE
057540                                      SCHED-FOUND-CODE
057550        IF TRCN-2ND-ROUTE-DESIGNATION > SPACES
057560        AND TRCN-LOCAL-REC
057570        AND (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
057580           PERFORM P3039-GET-CNTL-DIST-SUB-DIST
057590           IF NOT CNTL-SPLIT-SHIFT-HOS
057600              PERFORM P3037-CHECK-SCHED
057610           END-IF
057620        END-IF
057630        IF ENGINE-CRAFT
057640           IF TRCN-EN-REST-REQ       = ('C' OR 'B')
057650           OR (     TRCN-EN-REST-REQ = 'N'
057660                AND EMP-LAST-TOUR-OUT-OF-BOX)
057670              IF (     P08S-TSQ-CALL-NRML(SUB2)
057680                   AND WS-EMP-MTOD-CENT > WK-CALL-DT-TM-CE)
057690              OR (     P08S-TSQ-CALL-MINUS-LD(SUB2)
057700                   AND WS-EARLIEST-CENT-DATE-TIME >
057710                       WS-EMP-OD-CENT-DATE-TIME   )
057720                 MOVE EMP-MTOD-TIME TO SCR08S-MTOD(SUB1)
057730                 SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
057740              ELSE
057750                 SET P08SCA-EMP-RESTED-YES(SUB1)TO TRUE
057760              END-IF
057770              IF EMP-PREV-DUTY-MTOD NUMERIC
057780                 MOVE EMP-PREV-DUTY-MTOD
057790                                    TO SCR08S-MTOD-PD(SUB1)
057800              END-IF
057810              IF P08S-TSQ-DECKEY(SUB2) > SPACES
057820                 OR P08S-USER-OVERRIDE(SUB2)
057830                 SET VACANCY-FILLED        TO TRUE
057840              ELSE
057850                 SET NOT-VACANCY-FILLED    TO TRUE
057860              END-IF
057860*07/26/23 RJA B CNC0600 CNLD-269
057850              SET NOT-PROMOTING-EMP        TO TRUE
057810              IF P08S-TSQ-DECKEY(SUB2) > SPACES
057860                 MOVE P08S-TSQ-DECKEY(SUB2) TO DECKEY
057860                 MOVE SPACES TO DEC-REQ-1
057870                                DEC-REQ-2
057880                 PERFORM P8670-READ-DECISION
057890                 IF ON-TURN OF WORK-DEC-TABLE
057900                 OR ON-JOB  OF WORK-DEC-TABLE
057860                    SET PROMOTING-EMP      TO TRUE
057860                 END-IF
057870              END-IF
057880*07/26/23 RJA E
057890
057900              MOVE SCHED-2ND-CODE          TO HOLD-SCHED-2ND-CODE
057910              PERFORM P5950-GET-HRS-SVC-INFO
057900              MOVE HOLD-SCHED-2ND-CODE     TO SCHED-2ND-CODE
057910              IF  SCHED-2ND
057920              AND ON-SCHED-1ST
057930                 SET P925-EMP-STATUS-GREEN TO TRUE
057940              END-IF
057950              IF  SCR08S-FUNC(SUB1) = 'CBCK'
057960              OR (SCR08S-FUNC(SUB1) = 'QTIE' AND WS-CBCK)
057970                 MOVE P925-REST-STATUS-FLAG
057980                                     TO P08S-TSQ-REST-STATUS(SUB2)
057990              END-IF
058000              IF P925-EMP-STATUS-RED
058010                 AND NOT-HOS-EXEMPT
058020*CNLD-230-B
058030                 EVALUATE TRUE
058040                    WHEN WS-RFR-7DAY-RND-ERT
058050                         CONTINUE
      *CNLD-257-DXC-B
058060*                   WHEN WS-RFR-60-192-ERT
                          WHEN WS-RFR-60-ERT
                          WHEN WS-RFR-192-ERT
      *CNLD-257-DXC-E
058070                         MOVE YELLOW
058080                                   TO P08S-TSQ-COLOR-OVRD(SUB2)
058090                    WHEN OTHER
058100*CNLD-230-E
058110                         MOVE RED
058120                                   TO P08S-TSQ-COLOR-OVRD(SUB2)
058130*CNLD-230-B
058140                 END-EVALUATE
058150*CNLD-230-E
058160                 PERFORM P4080-RESET-EMP-COLORS
058170                 MOVE 'HOUR'   TO SCR08S-FUNC(SUB1)
058180              ELSE
058190                 IF P925-EMP-STATUS-YELLOW
058200                    AND NOT-HOS-EXEMPT
058210                    AND (SCR08S-FUNC(SUB1) NOT = 'CBCK'
058220                    AND  SCR08S-FUNC(SUB1) NOT = 'QTIE')
058230                    MOVE YELLOW TO P08S-TSQ-COLOR-OVRD(SUB2)
058240                    PERFORM P4080-RESET-EMP-COLORS
058250                    MOVE 'HOUR'   TO SCR08S-FUNC(SUB1)
058260                    IF P925-FROZEN-DATE-TIME > SPACES
058270                       MOVE SPACES  TO SCR08S-PHONE(SUB1)
058280                       STRING 'FROZEN UNTIL - '
058290                              P925-FROZEN-DATE '-'
058300                              P925-FROZEN-TIME '-'
058310                              DELIMITED BY SIZE
058320                              INTO SCR08S-PHONE(SUB1)
058330                       MOVE YELLOW  TO SCR08S-PHONE-COL(SUB1)
058340                    END-IF
058350                 END-IF
058360              END-IF
058370           END-IF
058380           IF TRCN-EN-REST-REQ       = ('U' OR 'B')
058390              IF WS-US-DOM-YES AND
058400                 WS-HS-US-ACTIVATE
058410                 IF WS-EARLI-US-CENT-DATE-TIME >
058420                    WS-EMP-OD-CENT-DATE-TIME
058430                    MOVE EMP-US-RSTD-TIME
058440                                    TO SCR08S-USHR(SUB1)
058450                    MOVE RED        TO SCR08S-NAME-COL(SUB1)
058460                    IF EMP-PREV-DUTY   IS NUMERIC
058470                       MOVE EMP-PREV-DUTY
058480                                    TO SCR08S-USPD(SUB1)
058490                    END-IF
058500                    SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
058510                 END-IF
058520              ELSE
058530                 IF WS-EMP-US-RSTD-CENT > WK-CALL-DT-TM-CE
058540                    MOVE EMP-US-RSTD-TIME
058550                                    TO SCR08S-USHR(SUB1)
058560                    IF EMP-PREV-DUTY   IS NUMERIC
058570                       MOVE EMP-PREV-DUTY
058580                                    TO SCR08S-USPD(SUB1)
058590                    END-IF
058600                    SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
058610                 END-IF
058620              END-IF
058630              IF WS-EMP-US-RSTD-CENT >
058640                 WS-EMP-OD-CENT-DATE-TIME
058650                 SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
058660              ELSE
058670                 SET P08SCA-EMP-RESTED-YES(SUB1)TO TRUE
058680              END-IF
058690           END-IF
058700           MOVE SPACE           TO P08SCA-SHORT-RESTED-FLAG(SUB1)
058710           IF  P08SCA-EMP-RESTED-NO(SUB1)
058720           AND P08S-TSQ-SAVE-CBCK-FLAG(SUB2) = 'Y'
058730              IF TRCN-EN-REST-REQ       = ('C' OR 'B')
058740                 IF (P08S-TSQ-CALL-NRML(SUB2)
058750                      AND WS-EMP-MTOD-CENT >
058760                          WS-ADJUSTED-DATE-TIME)
058770                 OR (P08S-TSQ-CALL-MINUS-LD(SUB2)
058780                      AND WS-EARLIEST-CENT-DATE-TIME >
058790                          WS-ADJUSTED-DATE-TIME      )
058800                    SET P08SCA-SHORT-RESTED-NO(SUB1) TO TRUE
058810                 ELSE
058820                    SET P08SCA-SHORT-RESTED-YES(SUB1)TO TRUE
058830                 END-IF
058840              END-IF
058850              IF TRCN-EN-REST-REQ       = ('U' OR 'B')
058860                 IF WS-US-DOM-YES AND
058870                    WS-HS-US-ACTIVATE
058880                    IF WS-EARLI-US-CENT-DATE-TIME >
058890                       WS-ADJUSTED-DATE-TIME
058900                       SET P08SCA-SHORT-RESTED-NO(SUB1) TO TRUE
058910                    ELSE
058920                       SET P08SCA-SHORT-RESTED-YES(SUB1)TO TRUE
058930                    END-IF
058940                 ELSE
058950                    IF WS-EMP-US-RSTD-CENT
058960                                        > WS-ADJUSTED-DATE-TIME
058970                       SET P08SCA-SHORT-RESTED-NO(SUB1) TO TRUE
058980                    ELSE
058990                       SET P08SCA-SHORT-RESTED-YES(SUB1)TO TRUE
059000                    END-IF
059010                 END-IF
059020              END-IF
059030           END-IF
059040        ELSE
059050           IF TRCN-TR-REST-REQ       = ('C' OR 'B')
059060           OR (     TRCN-TR-REST-REQ = 'N'
059070                AND EMP-LAST-TOUR-OUT-OF-BOX)
059080              IF (     P08S-TSQ-CALL-NRML(SUB2)
059090                   AND WS-EMP-MTOD-CENT > WK-CALL-DT-TM-CE)
059100              OR (     P08S-TSQ-CALL-MINUS-LD(SUB2)
059110                   AND WS-EARLIEST-CENT-DATE-TIME >
059120                       WS-EMP-OD-CENT-DATE-TIME   )
059130                 MOVE EMP-MTOD-TIME TO SCR08S-MTOD(SUB1)
059140                 SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
059150              ELSE
059160                 SET P08SCA-EMP-RESTED-YES(SUB1)TO TRUE
059170              END-IF
059180
059190              IF EMP-PREV-DUTY-MTOD NUMERIC
059200                 MOVE EMP-PREV-DUTY-MTOD
059210                                    TO SCR08S-MTOD-PD(SUB1)
059220              END-IF
059230              IF P08S-TSQ-DECKEY(SUB2) > SPACES
059240                 OR P08S-USER-OVERRIDE(SUB2)
059250                 SET VACANCY-FILLED        TO TRUE
059260              ELSE
059270                 SET NOT-VACANCY-FILLED    TO TRUE
059280              END-IF
057860*07/26/23 RJA B CNC0600 CNLD-269
057850              SET NOT-PROMOTING-EMP        TO TRUE
057810              IF P08S-TSQ-DECKEY(SUB2) > SPACES
057860                 MOVE P08S-TSQ-DECKEY(SUB2) TO DECKEY
057860                 MOVE SPACES TO DEC-REQ-1
057870                                DEC-REQ-2
057880                 PERFORM P8670-READ-DECISION
057890                 IF ON-TURN OF WORK-DEC-TABLE
057900                 OR ON-JOB  OF WORK-DEC-TABLE
057860                    SET PROMOTING-EMP      TO TRUE
057860                 END-IF
057870              END-IF
057880*07/26/23 RJA E
059290              MOVE SCHED-2ND-CODE          TO HOLD-SCHED-2ND-CODE
059300              PERFORM P5950-GET-HRS-SVC-INFO
059310              MOVE HOLD-SCHED-2ND-CODE     TO SCHED-2ND-CODE
059320              IF  SCHED-2ND
059330              AND ON-SCHED-1ST
059340                 SET P925-EMP-STATUS-GREEN TO TRUE
059350              END-IF
059360              IF  SCR08S-FUNC(SUB1) = 'CBCK'
059370              OR (SCR08S-FUNC(SUB1) = 'QTIE' AND WS-CBCK)
059380                 MOVE P925-REST-STATUS-FLAG
059390                                     TO P08S-TSQ-REST-STATUS(SUB2)
059400              END-IF
059410              IF P925-EMP-STATUS-RED
059420                 AND NOT-HOS-EXEMPT
059430*CNLD-230-B
059440                 EVALUATE TRUE
059450                    WHEN WS-RFR-7DAY-RND-ERT
059460                         CONTINUE
      *CNLD-257-DXC-B
059470*                   WHEN WS-RFR-60-192-ERT
                          WHEN WS-RFR-60-ERT
                          WHEN WS-RFR-192-ERT
      *CNLD-257-DXC-E
059480                         MOVE YELLOW
059490                                   TO P08S-TSQ-COLOR-OVRD(SUB2)
059500                    WHEN OTHER
059510*CNLD-230-E
059520                         MOVE RED
059530                                   TO P08S-TSQ-COLOR-OVRD(SUB2)
059540*CNLD-230-B
059550                 END-EVALUATE
059560*CNLD-230-E
059570                 PERFORM P4080-RESET-EMP-COLORS
059580                 MOVE 'HOUR'   TO SCR08S-FUNC(SUB1)
059590              ELSE
059600                 IF P925-EMP-STATUS-YELLOW
059610                    AND NOT-HOS-EXEMPT
059620                    AND (SCR08S-FUNC(SUB1) NOT = 'CBCK'
059630                    AND  SCR08S-FUNC(SUB1) NOT = 'QTIE')
059640                    MOVE YELLOW TO P08S-TSQ-COLOR-OVRD(SUB2)
059650                    PERFORM P4080-RESET-EMP-COLORS
059660                    MOVE 'HOUR'  TO SCR08S-FUNC(SUB1)
059670                    IF P925-FROZEN-DATE-TIME > SPACES
059680                       MOVE SPACES  TO SCR08S-PHONE(SUB1)
059690                       STRING 'FROZEN UNTIL - '
059700                              P925-FROZEN-DATE '-'
059710                              P925-FROZEN-TIME '-'
059720                              DELIMITED BY SIZE
059730                              INTO SCR08S-PHONE(SUB1)
059740                       MOVE YELLOW TO SCR08S-PHONE-COL(SUB1)
059750                    END-IF
059760                 END-IF
059770              END-IF
059780           END-IF
059790           IF TRCN-TR-REST-REQ       = ('U' OR 'B')
059800              IF WS-US-DOM-YES AND
059810                 WS-HS-US-ACTIVATE
059820                 IF WS-EARLI-US-CENT-DATE-TIME >
059830                    WS-EMP-OD-CENT-DATE-TIME
059840                    MOVE RED        TO SCR08S-NAME-COL(SUB1)
059850                    MOVE EMP-US-RSTD-TIME
059860                                    TO SCR08S-USHR(SUB1)
059870                    IF EMP-PREV-DUTY   IS NUMERIC
059880                       MOVE EMP-PREV-DUTY
059890                                    TO SCR08S-USPD(SUB1)
059900                    END-IF
059910                 END-IF
059920              ELSE
059930                 IF EMP-US-RSTD         = WK-CALL-DATE-TIME
059940                    MOVE EMP-US-RSTD-TIME
059950                                    TO SCR08S-USHR(SUB1)
059960                    IF EMP-PREV-DUTY   IS NUMERIC
059970                       MOVE EMP-PREV-DUTY
059980                                    TO SCR08S-USPD(SUB1)
059990                   END-IF
060000                   SET P08SCA-EMP-RESTED-NO(SUB1)  TO TRUE
060010                 END-IF
060020              END-IF
060030              IF WS-EMP-US-RSTD-CENT >
060040                 WS-EMP-OD-CENT-DATE-TIME
060050                 SET P08SCA-EMP-RESTED-NO(SUB1)  TO TRUE
060060              ELSE
060070                 SET P08SCA-EMP-RESTED-YES(SUB1) TO TRUE
060080              END-IF
060090           END-IF
060100           MOVE SPACE           TO P08SCA-SHORT-RESTED-FLAG(SUB1)
060110           IF  P08SCA-EMP-RESTED-NO(SUB1)
060120           AND P08S-TSQ-SAVE-CBCK-FLAG(SUB2) = 'Y'
060130              IF TRCN-TR-REST-REQ       = ('C' OR 'B')
060140                 IF (P08S-TSQ-CALL-NRML(SUB2)
060150                      AND WS-EMP-MTOD-CENT >
060160                          WS-ADJUSTED-DATE-TIME)
060170                 OR (P08S-TSQ-CALL-MINUS-LD(SUB2)
060180                      AND WS-EARLIEST-CENT-DATE-TIME >
060190                          WS-ADJUSTED-DATE-TIME      )
060200                    SET P08SCA-SHORT-RESTED-NO(SUB1) TO TRUE
060210                 ELSE
060220                    SET P08SCA-SHORT-RESTED-YES(SUB1)TO TRUE
060230                 END-IF
060240              END-IF
060250              IF TRCN-TR-REST-REQ       = ('U' OR 'B')
060260                 IF WS-US-DOM-YES AND
060270                    WS-HS-US-ACTIVATE
060280                    IF WS-EARLI-US-CENT-DATE-TIME >
060290                       WS-ADJUSTED-DATE-TIME
060300                       SET P08SCA-SHORT-RESTED-NO(SUB1) TO TRUE
060310                    ELSE
060320                       SET P08SCA-SHORT-RESTED-YES(SUB1)TO TRUE
060330                    END-IF
060340                 ELSE
060350                    IF WS-EMP-US-RSTD-CENT > WS-ADJUSTED-DATE-TIME
060360                       SET P08SCA-SHORT-RESTED-NO(SUB1) TO TRUE
060370                    ELSE
060380                       SET P08SCA-SHORT-RESTED-YES(SUB1)TO TRUE
060390                    END-IF
060400                 END-IF
060410              END-IF
060420           END-IF
060430        END-IF
060440        MOVE SPACES               TO CNTLKEY-AREA
060450        MOVE '02'                 TO CNTL-REC-TYPE
060460        MOVE SCR08S-DIST          TO CNTL-DIST
060470        MOVE SCR08S-SUB-DIST      TO CNTL-SUB-DIST
060480        MOVE CNTLKEY-AREA         TO CNTLKEY
060490        PERFORM P8610-READ-CNTLFILE
060500
060510        IF SUCCESS
060520           MOVE CNTL-US-DOM        TO WS-US-DOM-FLAG
060530           MOVE CNTL-TIME-ZONE     TO SAVE-TIME-ZONE
060540        END-IF
060550        IF WS-US-DOM-YES
060560           AND (P08S-TSQ-EMP-SEL(SUB2) > SPACES)
060570           MOVE SPACES             TO PS94-COMMAREA-PARMS
060580                                      WS-HST-STORAGE
060590           MOVE EMP-NBR IN WS-MSTR TO PS94-EMP-NBR
060600           SET  PS94-HST-FUNC      TO TRUE
060610           PERFORM P2550-GET-HST-KEY
060620           MOVE SCR08S-DATE        TO PS94-HSL-EVENT-STRT-TS-DT
060630           MOVE SCR08S-OD-TIME(SUB1)
060640                                   TO PS94-HSL-EVENT-STRT-TS-TM
060650           MOVE SAVE-TIME-ZONE     TO PS94-TIME-ZONE
060660           PERFORM P9993-CALL-PS94-PROGRAM
060670           EVALUATE TRUE
060680              WHEN PS94-HST-LT-ALERT-LIMIT
060690              WHEN PS94-HST-LT-ALERT-THRESHOLD
060700              WHEN PS94-HST-TT-ALERT-LIMIT
060710              WHEN PS94-HST-TT-ALERT-THRESHOLD
060720              WHEN PS94-HST-ST-ALERT-THRESHOLD
060730                   MOVE YELLOW     TO SCR08S-NAME-COL(SUB1)
060740                   SET P08S-TSQ-RESTRICTED(SUB2)
060750                                   TO TRUE
060760                   MOVE 'HOUR'     TO SCR08S-FUNC(SUB1)
060770                   MOVE REV-VIDEO  TO SCR08S-FUNC-HI(SUB1)
060780           END-EVALUATE
060790        END-IF
060800
060810        IF (P08S-TSQ-CALL-NRML(SUB2)
060820           AND WS-EMP-PERS-REST-CENT > WK-CALL-DT-TM-CE)
060830           OR (P08S-TSQ-CALL-MINUS-LD(SUB2)
060840           AND WS-EMP-PERS-REST-CENT > WK-CALL-MINUS-LEAD)
060850           MOVE EMP-PERS-REST-TIME  TO SCR08S-PERS(SUB1)
060860           SET P08SCA-EMP-PERS-REST-NO(SUB1) TO TRUE
060870           IF  P08S-TSQ-SAVE-CBCK-FLAG(SUB2) = 'Y'
060880              IF WS-EMP-PERS-REST-CENT > WS-ADJUSTED-DATE-TIME
060890                 SET P08SCA-SHORT-PERS-REST-NO(SUB1) TO TRUE
060900              ELSE
060910                 SET P08SCA-SHORT-PERS-REST-YES(SUB1)TO TRUE
060920              END-IF
060930           END-IF
060940        ELSE
060950           SET P08SCA-EMP-PERS-REST-YES(SUB1) TO TRUE
060960        END-IF
060970        MOVE SPACES                 TO WS-EMP-DISQUALIFIED
060980        IF P08S-TSQ-QUAL-AREA(SUB2)  > SPACES
060990           MOVE EMP-NBR IN WS-MSTR  TO QUAL-EMP-NO
061000           PERFORM VARYING SUB3 FROM 1 BY 1
061010*CNC0572 - BEG
061020***           UNTIL SUB3 > 6
061030              UNTIL SUB3 > 8
061040*CNC0572 - END
061050              IF P08S-TSQ-QUAL(SUB2, SUB3) > SPACES
061060                 MOVE P08S-TSQ-QUAL(SUB2, SUB3)
061070                                    TO QUALIFICATION
061080                 MOVE QUALEMP-KEY   TO QUALEMP
061090                 EXEC CICS READ
061100                           DATASET(QUAL-FILE-VIA-QUALEMP)
061110                           INTO(WS-QUAL-FILE)
061120                           LENGTH(QUALEMP-RLGTH)
061130                           RIDFLD(QUALEMP)
061140                           KEYLENGTH(QUALEMP-KLGTH)
061150                           RESP(WS-RESPONSE)
061160                 END-EXEC
061170                 MOVE WS-RESPONSE TO FILE-STATUS
061180                 IF SUCCESS
061190                    IF QUAL-DATE NOT > '000000'
061200                       MOVE '999999'
061210                                    TO QUAL-DATE
061220                    END-IF
061230                    IF P08S-TSQ-QUAL-RSTR(SUB2, SUB3)
061240*
061250*                     IF THE EMPLOYEE HAS THE QUALIFICATION CODE,
061260*                     AND THAT QUALIFICATION IS A "RESTRICTION",
061270*                     AND THE QUALIFICATION HAS NOT YET "EXPIRED",
061280*                     THE EMPLOYEE CANNOT BE CALLED FOR THE
061290*                     ASSIGNMENT
061300*
061310                       PERFORM P9811-FORMAT-CENTURY
061320                       IF WS-CENT-QUAL-DATE  > WS-CENT-CALL-DATE
061330                          SET P08S-TSQ-NOT-QUAL(SUB2)
061340                                    TO TRUE
061350                          MOVE SPACES
061360                                    TO SCR08S-PHONE(SUB1)
061370                          IF PSTCA-SUB = 2
061380                             IF TRCN-LOCAL-REC
061390                                STRING 'EMPLOYEE NOT QUALIFIED'
061400                                ' USE FUNCTION "QUAL" FOR DETAILS'
061410                                  DELIMITED BY SIZE INTO
061420                                            SCR08S-PHONE(SUB1)
061430                             ELSE
061440                                MOVE 'EMPLOYEE NOT QUALIFIED'
061450                                         TO SCR08S-PHONE(SUB1)
061460                             END-IF
061470                          ELSE
061480                             IF TRCN-LOCAL-REC
061490                                STRING 'EMPLOYEE NOT QUALIFIED'
061500                                ' USE FUNCTION "QUAL" FOR DETAILS'
061510                                  DELIMITED BY SIZE INTO
061520                                            SCR08S-PHONE(SUB1)
061530                             ELSE
061540                                STRING 'EMPLOYEE NOT QUALIFIED - '
061550                                'HAS RESTRICTION - ' QUALIFICATION
061560                                    ' - EXPIRES ' QUAL-DATE
061570                                    DELIMITED BY SIZE
061580                                    INTO SCR08S-PHONE(SUB1)
061590                             END-IF
061600                          END-IF
061610                          MOVE YELLOW
061620                                    TO SCR08S-PHONE-COL(SUB1)
061630*CNC0572 - BEG
061640***                       MOVE 7    TO SUB3
061650                          MOVE 9    TO SUB3
061660*CNC0572 - BEG
061670                          SET EMP-DISQUALIFIED TO TRUE
061680                       END-IF
061690                    ELSE
061700*
061710*                     IF THE EMPLOYEE HAS THE QUALIFICATION CODE,
061720*                     AND THE QUALIFICATION HAS "EXPIRED", THE
061730*                     EMPLOYEE CANNOT BE CALLED FOR THE ASSIGNMENT
061740*
061750                       PERFORM P9811-FORMAT-CENTURY
061760                       IF WS-CENT-QUAL-DATE < WS-CENT-CALL-DATE
061770                          SET P08S-TSQ-NOT-QUAL(SUB2)
061780                                    TO TRUE
061790                          MOVE SPACES
061800                                    TO SCR08S-PHONE(SUB1)
061810                          IF PSTCA-SUB = 2
061820                             IF TRCN-LOCAL-REC
061830                                STRING 'EMPLOYEE NOT QUALIFIED'
061840                                ' USE FUNCTION "QUAL" FOR DETAILS'
061850                                  DELIMITED BY SIZE INTO
061860                                            SCR08S-PHONE(SUB1)
061870                             ELSE
061880                                MOVE 'EMPLOYEE NOT QUALIFIED'
061890                                         TO SCR08S-PHONE(SUB1)
061900                             END-IF
061910                          ELSE
061920                             IF TRCN-LOCAL-REC
061930                                STRING 'EMPLOYEE NOT QUALIFIED'
061940                                ' USE FUNCTION "QUAL" FOR DETAILS'
061950                                  DELIMITED BY SIZE INTO
061960                                            SCR08S-PHONE(SUB1)
061970                             ELSE
061980                                STRING 'EMPLOYEE NOT QUALIFIED - '
061990                                    QUALIFICATION ' EXPIRED '
062000                                    QUAL-DATE
062010                                    DELIMITED BY SIZE
062020                                    INTO SCR08S-PHONE(SUB1)
062030                             END-IF
062040                          END-IF
062050*CNC0572 - BEG
062060***                       MOVE 7    TO SUB3
062070                          MOVE 9    TO SUB3
062080*CNC0572 - BEG
062090                          MOVE YELLOW
062100                                    TO SCR08S-PHONE-COL(SUB1)
062110                          SET EMP-DISQUALIFIED TO TRUE
062120                       END-IF
062130                    END-IF
062140                 ELSE
062150                    IF (NO-RECORD-FND  OR END-OF-FILE)
062160*
062170*                     IF THE EMPLOYEE DOES NOT HAVE THE
062180*                     QUALIFICATION, IF IT IS A "RESTRICTION" THEN
062190*                     ALL IS OK, IF IT IS NOT A "RESTRICTION", THE
062200*                     EMPLOYEE CANNOT BE CALLED FOR THE ASSIGNMENT
062210*
062220                       IF NOT P08S-TSQ-QUAL-RSTR(SUB2, SUB3)
062230                          SET P08S-TSQ-NOT-QUAL(SUB2)
062240                                    TO TRUE
062250                          MOVE SPACES
062260                                    TO SCR08S-PHONE(SUB1)
062270                          IF PSTCA-SUB = 2
062280                             IF TRCN-LOCAL-REC
062290                                STRING 'EMPLOYEE NOT QUALIFIED'
062300                                ' USE FUNCTION "QUAL" FOR DETAILS'
062310                                  DELIMITED BY SIZE INTO
062320                                            SCR08S-PHONE(SUB1)
062330                             ELSE
062340                                MOVE 'EMPLOYEE NOT QUALIFIED'
062350                                    TO SCR08S-PHONE(SUB1)
062360                             END-IF
062370                          ELSE
062380                             IF TRCN-LOCAL-REC
062390                                STRING 'EMPLOYEE NOT QUALIFIED'
062400                                ' USE FUNCTION "QUAL" FOR DETAILS'
062410                                  DELIMITED BY SIZE INTO
062420                                            SCR08S-PHONE(SUB1)
062430                             ELSE
062440                                STRING 'EMPLOYEE NOT QUALIFIED - '
062450                                    QUALIFICATION
062460                                    DELIMITED BY SIZE
062470                                    INTO SCR08S-PHONE(SUB1)
062480                             END-IF
062490                          END-IF
062500                          MOVE YELLOW
062510                                    TO SCR08S-PHONE-COL(SUB1)
062520*CNC0572 - BEG
062530***                       MOVE 7    TO SUB3
062540                          MOVE 9    TO SUB3
062550*CNC0572 - BEG
062560                          SET EMP-DISQUALIFIED TO TRUE
062570                       END-IF
062580                    ELSE
062590                       MOVE 'P4020-5'
062600                                    TO ERR-PARAGRAPH
062610                       MOVE QUALEMP TO ERR-KEY
062620                       PERFORM P9999-GOT-PROBLEM
062630                    END-IF
062640                 END-IF
062650              END-IF
062660           END-PERFORM
062670        END-IF
062680        IF NOT EMP-DISQUALIFIED
062690        AND TRCN-LOCAL-REC
062700           MOVE SPACES                      TO WS-QUAL-FLAG
062710           PERFORM P6300-CHECK-TERR-QUAL
062720        END-IF
062730        IF NOT EMP-DISQUALIFIED
062740           MOVE SPACES                   TO P947-COMMAREA-PARMS
062750           SET P947-EMP-RESTR-FUNCTION   TO TRUE
062760           MOVE EMP-NBR OF WS-MSTR       TO P947-EMP-NO
062770           SET  P947-ASGN-AJ             TO TRUE
062780           IF  AJ-JOB-DIST            NOT > ZEROS
062790           OR  AJ-JOB-SUB-DIST        NOT > ZEROS
062800           OR  AJ-JOB-ASSIGNMENT      NOT > SPACES
062810               MOVE DIST     OF WS-MSTR  TO P947-ASGN-DIST
062820               MOVE SUB-DIST OF WS-MSTR  TO P947-ASGN-SUB-DIST
062830               MOVE SCR08S-ASGN(SUB1)    TO P947-ASGN-ASSIGNMENT
062840           ELSE
062850               MOVE AJ-JOB-DIST          TO P947-ASGN-DIST
062860               MOVE AJ-JOB-SUB-DIST      TO P947-ASGN-SUB-DIST
062870               MOVE AJ-JOB-ASSIGNMENT    TO P947-ASGN-ASSIGNMENT
062880           END-IF
062890           EXEC CICS ASKTIME END-EXEC
062900           ADD WS-ABSTIME-OFFSET         TO WS-ABSTIME
062910           MOVE SCR08S-CC(SUB1)          TO P947-ASGN-CRAFT
062920           SET P947-FROM-WORK            TO TRUE
062930           EXEC CICS LINK
062940                     PROGRAM(P947-PGM)
062950                     COMMAREA(P947-COMMAREA-PARMS)
062960                     LENGTH(P947-LGTH)
062970                     RESP(WS-RESPONSE)
062980           END-EXEC
062990           MOVE WS-RESPONSE              TO FILE-STATUS
063000           IF NOT SUCCESS
063010              MOVE 'P4020-6'             TO ERR-PARAGRAPH
063020              MOVE 'LINK947'             TO ERR-KEY
063030              PERFORM P9999-GOT-PROBLEM
063040           END-IF
063050           IF P947-RESTRICTED
063060              SET P08S-TSQ-RESTRICTED(SUB2)   TO TRUE
063070              IF PSTCA-SUB = 1
063080                 MOVE 'EMPLOYEE NOT QUALIFIED - HAS RESTRICTION'
063090                                         TO SCR08S-PHONE(SUB1)
063100              ELSE
063110                 MOVE 'EMPLOYEE NOT QUALIFIED - HAS RESTRICTION'
063120                                         TO SCR08S-PHONE(SUB1)
063130              END-IF
063140              MOVE YELLOW                TO SCR08S-PHONE-COL(SUB1)
063150           END-IF
063160           IF P08S-TSQ-STARTS-DISQUALIFIED(SUB2)
063170              SET P08S-TSQ-RESTRICTED(SUB2)   TO TRUE
063180              IF PSTCA-SUB = 2
063190                 MOVE '5 START WARNING' TO SCR08S-PHONE(SUB1)
063200              ELSE
063210                 MOVE '5 START WARNING' TO SCR08S-PHONE(SUB1)
063220              END-IF
063230              MOVE YELLOW TO SCR08S-PHONE-COL(SUB1)
063240           END-IF
063250           IF P08S-TSQ-QTIE-DISQUALIFIED(SUB2)
063260              IF SCR08S-FUNC(SUB1) = 'CBCK'
063270                 MOVE SPACES             TO SCR08S-PHONE(SUB1)
063280                 STRING 'SHORT CALL OR LEAD REQ: TYPE "CBCK" '
063290                        'IN FUNC AND PRESS ENTER FOR CALLBACK'
063300                         DELIMITED BY SIZE
063310                         INTO SCR08S-PHONE(SUB1)
063320              END-IF
063330              SET P08S-TSQ-RESTRICTED(SUB2)   TO TRUE
063340              MOVE 'QTIE'                TO SCR08S-FUNC(SUB1)
063350              MOVE REV-VIDEO             TO SCR08S-FUNC-HI(SUB1)
063360           END-IF
063370        END-IF
063380     ELSE
063390        IF P08S-TSQ-CALLED-CODE(SUB2) > SPACES
063400           IF PSTCA-SUB = 1
063410              MOVE '** VACANT ASSIGNMENT **'
063420                                    TO SCR08S-NAME(SUB1)
063430           ELSE
063440              MOVE '** VACANT ASSIGNMENT **'
063450                                    TO SCR08S-NAME(SUB1)
063460           END-IF
063470        ELSE
063480           IF PSTCA-SUB = 1
063490              MOVE '** VACANCY **'  TO SCR08S-NAME(SUB1)
063500           ELSE
063510              MOVE '** VACANCY **'  TO SCR08S-NAME(SUB1)
063520           END-IF
063530        END-IF
063540     END-IF
063550     IF P08S-TSQ-CALLED-CODE(SUB2) > SPACES
063560        PERFORM P4070-LOCK-CALLED-ASGN
063570     END-IF.
063580*
063590 P4025-DISPLAY-PHONE-INFO.
063600*
063610*CNLD-226-B
063620     MOVE BLUE               TO SCR08S-PHONE-COL(SUB1)
063630*CNLD-226-E
063640     MOVE SPACES             TO PS52-COMMAREA-PARMS
063650                                WS-TELEPHONE-NBRS
063660     MOVE EMP-NBR IN WS-MSTR TO PS52-EMP-NBR
063670     SET PS52-CMA-MODE       TO TRUE
063680     MOVE WS-LOCAL-DATE-TIME TO PS52-INPUT-DATE-TIME
063690     PERFORM P8520-LINK-TO-PS52
063700     IF NOT SUCCESS
063710        MOVE 'P1045-1'       TO ERR-PARAGRAPH
063720        PERFORM P9999-GOT-PROBLEM
063730     END-IF
063740
063750     IF PS52-PAGER-NO > SPACES
063760        IF PSTCA-SUB = 2
063770           STRING '<< PIECE '
063780                  PS52-PAGER-NO
063790                  ' >>'
063800                  DELIMITED BY SIZE
063810                  INTO SCR08S-PHONE(SUB1)
063820        ELSE
063830           STRING '<< ROOM '
063840                  PS52-PAGER-NO
063850                  ' >>'
063860                  DELIMITED BY SIZE
063870                  INTO SCR08S-PHONE(SUB1)
063880        END-IF
063890     ELSE
063900        PERFORM VARYING T FROM 1 BY 1 UNTIL T > 3
063910          IF PS52-PH-NUMBER(T) > SPACES
063920            MOVE PS52-PH-NUMBER(T)  TO WS-TELEPHONE-NBR(T)
063930            MOVE PS52-PH-COMMENT(T) TO WS-TELEPHONE-COMMENTS(T)
063940            MOVE PS52-HOME-AWAY-CD(T) TO WS-TELEPHONE-CODE(T)
063950          END-IF
063960        END-PERFORM
063970        IF WS-TELEPHONE-NBRS > SPACES
063980           MOVE WS-TELEPHONE-NBRS TO SCR08S-PHONE(SUB1)
063990        ELSE
064000           MOVE 'EMPLOYEE HAS NO PHONE NUMBERS ON FILE'
064010                                  TO SCR08S-PHONE(SUB1)
064020        END-IF
064030     END-IF
064040     .
064050*
064060 P4030-DISPLAY-OWNR-INFO.
064070*
064080     MOVE SPACES                    TO SCR08S-PHONE(SUB1)
064090     IF P08S-TSQ-OWNER(SUB2) > '0000000000'
064100        MOVE P08S-TSQ-OWNER(SUB2)   TO MSTRNBRK
064110        PERFORM P8500-READ-MASTER
064120        SET NOT-HOS-EXEMPT          TO TRUE
064130        MOVE AJ-JOB-ASSIGNMENT      TO JOB-DEF-CHECK
064140        MOVE CRAFT OF WS-MSTR       TO WS-CRAFT-CODE-CHECK
064150        IF JOB-DEF-YARDMASTER-ASGN
064160*CNLD-246 B
064160*          AND YARDMASTER-CRAFT
064160           AND (YARDMASTER-CRAFT OR YARDMASTER-TRAINEE)
064160*CNLD-246 E
064170           PERFORM P3031-DETERMINE-IF-HOS-EXEMPT
064180        END-IF
064190        MOVE EMP-NAME IN WS-MSTR    TO SCR08S-OWNR-NAME(SUB1)
064200        IF TEMPORARY-ASGNMT > SPACE
064210           AND TEMPORARY-ASGNMT NOT = AJJOBKEY-AREA
064220           MOVE 'TV'                TO LAYOFF-CODE IN WS-MSTR
064230           MOVE SPACES              TO LAYOFF-EM-CODE
064240        ELSE
064250           IF ON-DUTY-ASGNMT > SPACE
064260              MOVE 'OT'             TO LAYOFF-CODE IN WS-MSTR
064270              MOVE SPACES           TO LAYOFF-EM-CODE
064280           END-IF
064290        END-IF
064300        IF LAYOFF-CODE IN WS-MSTR NOT = 'A0'
064310           MOVE LAYOFF-CODE         TO SCR08S-OWNR-ST(SUB1)
064320           IF LAYOFF-EM-CODE > SPACES
064330              MOVE LAYOFF-EM-CODE   TO SCR08S-OWNR-ECC(SUB1)
064340           END-IF
064350        END-IF
064360        IF P08S-TSQ-STARTS-DISQUALIFIED(SUB2)
064370           MOVE '#5'                TO SCR08S-OWNR-ST(SUB1)
064380        END-IF
064390        MOVE P08S-TSQ-CALL-DATE(SUB2)
064400                                    TO WK-CALL-DATE
064410        MOVE P08S-TSQ-CALL-TIME(SUB2)
064420                                    TO WK-CALL-TIME
064430        MOVE SCR08S-CC(SUB1)        TO WS-CRAFT-CODE-CHECK
064440        PERFORM P7200-CONVERT-MASTER-DATES
064450        PERFORM P7300-CONVERT-WK-CALL-DATES
064460        MOVE ZEROES                TO SCHED-DONE-CODE
064470                                      SCHED-2ND-CODE
064480                                      SCHED-LST-CODE
064490                                      SCHED-FOUND-CODE
064500        IF TRCN-2ND-ROUTE-DESIGNATION > SPACES
064510        AND TRCN-LOCAL-REC
064520        AND (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
064530           PERFORM P3039-GET-CNTL-DIST-SUB-DIST
064540           IF NOT CNTL-SPLIT-SHIFT-HOS
064550              PERFORM P3037-CHECK-SCHED
064560           END-IF
064570        END-IF
064580        IF ENGINE-CRAFT
064590           IF TRCN-EN-REST-REQ = ('C' OR 'B')
064600              OR (TRCN-EN-REST-REQ = 'N'
064610              AND EMP-LAST-TOUR-OUT-OF-BOX)
064620              IF WS-EMP-MTOD-CENT > WK-CALL-DT-TM-CE
064630                 MOVE EMP-MTOD-TIME TO SCR08S-OWNR-MTOD(SUB1)
064640              END-IF
064650              SET NOT-VACANCY-FILLED       TO TRUE
064660              MOVE SCHED-2ND-CODE          TO HOLD-SCHED-2ND-CODE
064670              PERFORM P5950-GET-HRS-SVC-INFO
064680              MOVE HOLD-SCHED-2ND-CODE     TO SCHED-2ND-CODE
064690              IF  SCHED-2ND
064700              AND ON-SCHED-1ST
064710                 SET P925-EMP-STATUS-GREEN TO TRUE
064720              END-IF
064730              IF P925-EMP-STATUS-RED
064740*CNC0600-CNLD-226-B
064750                 IF  WS-CAN-WRR-NEW
064760                    MOVE EMP-MTOD-TIME TO SCR08S-OWNR-MTOD(SUB1)
      *CNLD-265-B
065390                    MOVE EMP-PERS-REST-TIME
065390                                       TO SCR08S-OWNR-PERS(SUB1)
      *CNLD-265-E
064770                    MOVE RED       TO SCR08S-PHONE-COL(SUB1)
064780                 ELSE
064790                    MOVE 'RED'     TO SCR08S-OWNR-MTOD(SUB1)
064800                 END-IF
064810*CNC0600-CNLD-226-E
064820              END-IF
064830           END-IF
064840           IF TRCN-EN-REST-REQ = ('U' OR 'B')
064850              IF WS-EMP-US-RSTD-CENT > WK-CALL-DT-TM-CE
064860                 MOVE EMP-US-RSTD-TIME
064870                                    TO SCR08S-OWNR-USHR(SUB1)
064880                 IF EMP-PREV-DUTY IS NUMERIC
064890                    MOVE EMP-PREV-DUTY
064900                                    TO SCR08S-OWNR-USPD(SUB1)
064910                 END-IF
064920                 SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
064930              ELSE
064940                 SET P08SCA-EMP-RESTED-YES(SUB1) TO TRUE
064950              END-IF
064960           END-IF
064970        ELSE
064980           IF TRCN-TR-REST-REQ = ('C' OR 'B')
064990              OR (TRCN-TR-REST-REQ = 'N'
065000              AND EMP-LAST-TOUR-OUT-OF-BOX)
065010              IF WS-EMP-MTOD-CENT > WK-CALL-DT-TM-CE
065020                 MOVE EMP-MTOD-TIME TO SCR08S-OWNR-MTOD(SUB1)
065030              END-IF
065040              SET NOT-VACANCY-FILLED       TO TRUE
065050              MOVE SCHED-2ND-CODE          TO HOLD-SCHED-2ND-CODE
065060              PERFORM P5950-GET-HRS-SVC-INFO
065070              MOVE HOLD-SCHED-2ND-CODE     TO SCHED-2ND-CODE
065080              IF  SCHED-2ND
065090              AND ON-SCHED-1ST
065100                 SET P925-EMP-STATUS-GREEN TO TRUE
065110              END-IF
065120              IF P925-EMP-STATUS-RED
065130*CNC0600-CNLD-226-B
065140                 IF  WS-CAN-WRR-NEW
065150                    MOVE EMP-MTOD-TIME TO SCR08S-OWNR-MTOD(SUB1)
      *CNLD-265-B
065390                    MOVE EMP-PERS-REST-TIME
065390                                       TO SCR08S-OWNR-PERS(SUB1)
      *CNLD-265-E
065160                    MOVE RED           TO SCR08S-PHONE-COL(SUB1)
065170                 ELSE
065180                    MOVE 'RED'         TO SCR08S-OWNR-MTOD(SUB1)
065190                 END-IF
065200*                MOVE 'RED'     TO SCR08S-OWNR-MTOD(SUB2)
065210*CNC0600-CNLD-226-E
065220              END-IF
065230           END-IF
065240           IF TRCN-TR-REST-REQ = ('U' OR 'B')
065250              IF WS-EMP-US-RSTD-CENT > WK-CALL-DT-TM-CE
065260                 MOVE EMP-US-RSTD-TIME
065270                                    TO SCR08S-OWNR-USHR(SUB1)
065280                 IF EMP-PREV-DUTY IS NUMERIC
065290                    MOVE EMP-PREV-DUTY
065300                                    TO SCR08S-OWNR-USPD(SUB1)
065310                 END-IF
065320                 SET P08SCA-EMP-RESTED-NO(SUB1) TO TRUE
065330              ELSE
065340                 SET P08SCA-EMP-RESTED-YES(SUB1) TO TRUE
065350              END-IF
065360           END-IF
065370        END-IF
065380        IF WS-EMP-PERS-REST-CENT > WK-CALL-DT-TM-CE
065390           MOVE EMP-PERS-REST-TIME  TO SCR08S-OWNR-PERS(SUB1)
065400        END-IF
065410     ELSE
065420        IF P08S-TSQ-OWNER-NAME(SUB2) > SPACES
065430           MOVE P08S-TSQ-OWNER-NAME(SUB2)
065440                                    TO SCR08S-OWNR-NAME(SUB1)
065450        ELSE
065460           MOVE AJ-OPEN-ASGN-MSG(PSTCA-SUB)
065470                                    TO SCR08S-OWNR-NAME(SUB1)
065480        END-IF
065490     END-IF.
065500*
065510 P4050-CLEAR-EMP-DATA.
065520*
065530     MOVE SPACES                    TO SCR08S-NAME(SUB1)
065540                                       SCR08S-ST(SUB1)
065550                                       SCR08S-ECC(SUB1)
065560                                       SCR08S-MTOD(SUB1)
065570                                       SCR08S-MTOD-PD(SUB1)
065580                                       SCR08S-USHR(SUB1)
065590                                       SCR08S-USPD(SUB1)
065600                                       SCR08S-PERS(SUB1)
065610                                       SCR08S-PHONE(SUB1).
065620*
065630 P4060-LOCK-FIELDS.
065640*
065650     MOVE AUTOSKIP-MDT              TO SCR08S-FUNC-ATTR(SUB1)
065660                                       SCR08S-OD-TIME-ATTR(SUB1)
065670                                       SCR08S-NAME-ATTR(SUB1)
065680                                       SCR08S-ST-ATTR(SUB1)
065690                                       SCR08S-ECC-ATTR(SUB1).
065700*
065710 P4070-LOCK-CALLED-ASGN.
065720*
065730     PERFORM P4060-LOCK-FIELDS
065740     MOVE REV-VIDEO                 TO SCR08S-FUNC-HI(SUB1)
065750                                       SCR08S-OD-TIME-HI(SUB1)
065760                                       SCR08S-ASGN-HI(SUB1)
065770                                       SCR08S-CC-HI(SUB1)
065780                                       SCR08S-NAME-HI(SUB1)
065790     MOVE P08S-TSQ-CALLED-CODE(SUB2)
065800                                    TO SCR08S-FUNC(SUB1).
065810*
065820 P4080-RESET-EMP-COLORS.
065830*
065840     IF  SUB1                        > ZEROES
065850     AND SUB2                        > ZEROES
065860        MOVE P08S-TSQ-COLOR(SUB2)   TO SCR08S-OD-TIME-COL(SUB1)
065870                                       SCR08S-ASGN-COL(SUB1)
065880                                       SCR08S-CC-COL(SUB1)
065890        IF P08S-TSQ-COLOR-OVRD(SUB2) NOT = SPACE
065900           MOVE P08S-TSQ-COLOR-OVRD(SUB2)
065910                                    TO SCR08S-NAME-COL(SUB1)
065920        ELSE
065930           MOVE WHITE               TO SCR08S-NAME-COL(SUB1)
065940        END-IF
065950     END-IF
065960     .
065970*
065980 P4090-RESET-HIGHLIGHT.
065990*
066000     MOVE DEFAULT-ATTR              TO SCR08S-FUNC-HI(SUB1)
066010                                       SCR08S-OD-TIME-HI(SUB1)
066020                                       SCR08S-ASGN-HI(SUB1)
066030                                       SCR08S-CC-HI(SUB1)
066040                                       SCR08S-NAME-HI(SUB1)
066050                                       SCR08S-ST-HI(SUB1)
066060                                       SCR08S-ECC-HI(SUB1).
066070*
066080 P5000-PROCESS-FUNCTIONS.
066090*
066100     MOVE SPACE                     TO ERRORS-FOUND-CODE
066110                                       WS-FUNCTION-CODE
066120     PERFORM VARYING SUB1 FROM 1 BY 1
066130        UNTIL SUB1 > ARRAY-MAX
066140        MOVE SPACE                  TO WS-OD-TIME-CHANGED-FL
066150        MOVE P08SCA-SUB2            TO SUB2
066160        ADD SUB1                    TO SUB2
066170        IF P08S-TSQ-CALLED-CODE(SUB2) NOT > SPACES
066180           IF SCR08S-FUNC(SUB1)           > SPACES
066190              IF P08S-TSQ-JSKEY2(SUB2)    > SPACES
066200                 MOVE P08S-TSQ-JSKEY2(SUB2)
066210                                    TO JSKEY2
066220                 PERFORM P8600-READ-JSKEY2
066230                 IF NOT SUCCESS
066240                    MOVE 'P5000-1'  TO ERR-PARAGRAPH
066250                    MOVE JSKEY2     TO ERR-KEY
066260                    PERFORM P9999-GOT-PROBLEM
066270                 END-IF
066280                 MOVE JSK2-ASGN-DIST
066290                                    TO TRCN-DIST3
066300                 MOVE JSK2-ASGN-SUB-DIST
066310                                    TO TRCN-SDIST3
066320                 MOVE JSK2-ASGN     TO TRCN-ASSIGNMENT
066330                                       JOB-DEF-CHECK
066340                 MOVE TRCN-KEY3     TO TRCNKEY3
066350                 PERFORM P8630-READ-TRCNKEY3
066360                 IF NOT SUCCESS
066370                    MOVE 'P5000-2'  TO ERR-PARAGRAPH
066380                    MOVE TRCNKEY3   TO ERR-KEY
066390                    PERFORM P9999-GOT-PROBLEM
066400                 END-IF
066410                 MOVE DEFAULT-ATTR  TO SCR08S-FUNC-HI(SUB1)
066420                                       SCR08S-NAME-HI(SUB1)
066430                 MOVE P08S-TSQ-CALL-DATE-TIME(SUB2)
066440                                    TO WK-CALL-DATE-TIME
066450                 PERFORM P5010-VALIDATE-FUNCTION
066460                 IF SCR08S-OD-TIME(SUB1) NOT = WK-CALL-TIME
066470                    MOVE SCR08S-OD-TIME(SUB1)
066480                                    TO TE-MILITARY-TIME
066490                    SET TE-MILITARY-FORMAT
066500                                    TO TRUE
066510                    PERFORM P8997-TIMEEDIT
066520                    IF TE-INVALID-TIME
066530                       MOVE -1      TO SCR08S-OD-TIME-CURSOR(SUB1)
066540                       MOVE REV-VIDEO TO SCR08S-OD-TIME-HI(SUB1)
066550*                        INVALID-TIME-MSG
066560                       MOVE 'I022'  TO MSGLOG-CODE
066570                       PERFORM P9000-SEND-MAP-AND-RETURN
066580                    END-IF
066590                    IF SCR08S-OD-TIME(SUB1)
066600                       > P08S-TSQ-CALL-TIME(SUB2)
066610                       MOVE SCR08S-OD-TIME(SUB1)
066620                                    TO WK-CALL-TIME
066630                    ELSE
066640                       MOVE SCR08S-OD-TIME(SUB1)
066650                                    TO WK-CALL-TIME
066660                       MOVE P08S-TSQ-CALL-TIME(SUB2)
066670                                    TO JOB-SHIFT-TIME
066680                       IF 2ND-SHIFT-JOB
066690                          MOVE SCR08S-OD-TIME(SUB1)
066700                                    TO JOB-SHIFT-TIME
066710                          IF 3RD-SHIFT-JOB
066720                             MOVE ZEROS   TO DATE-CONVERSION-PARMS
066730                             SET PARM-ADD TO TRUE
066740                             MOVE WK-CALL-DATE
066750                                          TO PARM-PRI-DATE-GREG
066760                             MOVE 000001  TO PARM-SEC-DATE-GREG
066770                             EXEC CICS LINK
066780                                  PROGRAM(P903-PGM)
066790                                  COMMAREA(DATE-CONVERSION-PARMS)
066800                                  LENGTH(P903-LGTH)
066810                                  RESP(WS-RESPONSE)
066820                             END-EXEC
066830                             MOVE WS-RESPONSE
066840                                          TO FILE-STATUS
066850                             IF NOT SUCCESS
066860                                MOVE 'P5000-3'
066870                                          TO ERR-PARAGRAPH
066880                                MOVE 'P903LINK'
066890                                          TO ERR-KEY
066900                                PERFORM P9999-GOT-PROBLEM
066910                             END-IF
066920                             MOVE PARM-RES-DATE-GREG
066930                                          TO WK-CALL-DATE
066940                          END-IF
066950                       END-IF
066960                    END-IF
066970                    PERFORM P5020-EDIT-CALL-TIME
066980                    IF ERRORS-FOUND
066990                       MOVE -1      TO SCR08S-OD-TIME-CURSOR(SUB1)
067000                       MOVE REV-VIDEO
067010                                    TO SCR08S-OD-TIME-HI(SUB1)
067020                       MOVE ARRAY-MAX
067030                                    TO SUB1
067040                    ELSE
067050                       MOVE WK-CALL-DATE-TIME
067060                                 TO P08S-TSQ-CALL-DATE-TIME(SUB2)
067070                    END-IF
067080                    IF IVR-CALL-FUNCTION
067090                       SET WS-OD-TIME-CHANGED   TO TRUE
067100                       PERFORM P4020-DISPLAY-EMP-INFO
067110                    END-IF
067120                 END-IF
067130                 IF NOT ERRORS-FOUND
067140                    EVALUATE TRUE
067150                       WHEN CALL-FUNCTION
067160                          PERFORM P5100-PROCESS-CALL-FUNCTION
067170                       WHEN IVR-CALL-FUNCTION
067180                          PERFORM P6100-PROCESS-IVR-CALL
067190                       WHEN VACATE-FUNCTION
067200                          PERFORM P5100-PROCESS-CALL-FUNCTION
067210                       WHEN FILL-VACANCY-FUNCTION
067220                          PERFORM P5200-PROCESS-FILL-FUNCTION
067230                       WHEN LAYOFF-ON-CALL
067240                          PERFORM P5300-PROCESS-LAYOFF
067250                       WHEN USER-OVERRIDE
067260                          PERFORM P5500-PROCESS-OVERRIDE
067270                       WHEN SENIOR-MAN-TARGET
067280                          PERFORM P5600-GET-SENIOR-MAN
067290                       WHEN DISPLAY-CREW-FUNCTION
067300                          PERFORM P5700-DISPLAY-CREW
067310                       WHEN SENIOR-MAN-FUNCTION
067320                          CONTINUE
067330                       WHEN PHONE-FUNCTION
067340                          PERFORM P5800-DISPLAY-PHONE-NUMBERS
067350                       WHEN QTIE-NON-FUNCTION
067360                          CONTINUE
067370                       WHEN ATTEMPTED-NOTIFY
067380                          PERFORM P5900-ATTEMPTED-NOTIFY
067390                       WHEN DISPLAY-HRS-SVC-INFO
067400                          PERFORM P5970-DISPLAY-HRS-SVC-INFO
067410                       WHEN CALL-BACK
067420                          PERFORM P5400-CREATE-CALLBACK-TASK
067430                       WHEN DISPLAY-HOS-INFO
067440                          PERFORM P5980-DISPLAY-HOS-INFO
067450                       WHEN QUAL-FUNC
067460                          IF TRCN-LOCAL-REC
067470                             PERFORM P6200-DISPLAY-QUAL-INFO
067480                          ELSE
067490*                            INVALID-FUNC-CODE
067500                             MOVE 'I012' TO MSGLOG-CODE
067510
067520                             MOVE -1     TO
067530                                         SCR08S-FUNC-CURSOR(SUB1)
067540                             MOVE REV-VIDEO
067550                                         TO SCR08S-FUNC-HI(SUB1)
067560                             PERFORM P9000-SEND-MAP-AND-RETURN
067570                          END-IF
067580                       WHEN OTHER
067590                          SET ERRORS-FOUND
067600                                    TO TRUE
067610                          MOVE -1   TO SCR08S-FUNC-CURSOR(SUB1)
067620                          MOVE REV-VIDEO
067630                                    TO SCR08S-FUNC-HI(SUB1)
067640*                             'FUNCTION NOT YET AVAILABLE'
067650                          MOVE 'F003'
067660                                    TO MSGLOG-CODE
067670                          MOVE ARRAY-MAX
067680                                    TO SUB1
067690                    END-EVALUATE
067700                 END-IF
067710              ELSE
067720                 MOVE -1            TO SCR08S-FUNC-CURSOR(SUB1)
067730                 MOVE REV-VIDEO     TO SCR08S-FUNC-HI(SUB1)
067740*                  'IMPROPER FUNCTION - NO ASSIGNMENT PRESENT'
067750                 MOVE 'I049'        TO MSGLOG-CODE
067760                 PERFORM P9000-SEND-MAP-AND-RETURN
067770              END-IF
067780           END-IF
067790        END-IF
067800     END-PERFORM
067810     IF NOT ERRORS-FOUND
067820        IF WS-FUNCTION-CODE NOT > SPACES
067830*               'NO FUNCTION(S) SELECTED'
067840           IF EIBTRNID NOT = P08S-TRAN
067850              MOVE 'P005'          TO MSGLOG-CODE
067860           ELSE
067870              MOVE 'N004'          TO MSGLOG-CODE
067880           END-IF
067890        END-IF
067900        IF CALL-FUNCTION
067910        OR IVR-CALL-FUNCTION
067920        OR VACATE-FUNCTION
067930        OR WS-FUNCTION-CODE NOT > SPACES
067940           PERFORM VARYING SUB1 FROM 1 BY 1
067950              UNTIL SUB1 > ARRAY-MAX
067960              IF SCR08S-FUNC(SUB1) NOT > SPACES
067970                 COMPUTE SUB2 = P08SCA-SUB2 + SUB1
067980                 IF P08S-TSQ-JSKEY2(SUB2) > SPACES
067990                    MOVE -1 TO SCR08S-FUNC-CURSOR(SUB1)
068000                    MOVE ARRAY-MAX TO SUB1
068010                 ELSE
068020                    IF SUB1 = ARRAY-MAX
068030                       MOVE -1 TO SCR08S-CODE-CURSOR
068040                    END-IF
068050                 END-IF
068060              ELSE
068070                 IF SUB1 = ARRAY-MAX
068080                    MOVE -1 TO SCR08S-CODE-CURSOR
068090                 END-IF
068100              END-IF
068110           END-PERFORM
068120        END-IF
068130     END-IF.
068140*
068150 P5010-VALIDATE-FUNCTION.
068160*
068170     PERFORM VARYING SUB3 FROM 1 BY 1
068180        UNTIL SUB3 > WS-FUNCTION-TABLE-SIZE
068190        IF WS-FUNC-CODE(SUB3) = SCR08S-FUNC(SUB1)
068200           IF  ((P08SCA-IVR-CALL-ACTIVE
068210           AND  (IVR-YARD-USAGE (SUB3) AND JOB-DEF-YARD-ASGN)
068220            OR   IVR-LOCAL-USAGE(SUB3) AND JOB-DEF-LOCAL-ASGN))
068230            OR  (YARD-USAGE (SUB3)     AND JOB-DEF-YARD-ASGN)
068240            OR  (LOCAL-USAGE(SUB3)     AND JOB-DEF-LOCAL-ASGN)
068250              MOVE WS-FUNC-CODE(SUB3) TO WS-FUNCTION-CODE
068260              MOVE WS-FUNCTION-TABLE-SIZE
068270                                   TO SUB3
068280           ELSE
068290              SET ERRORS-FOUND     TO TRUE
068300           END-IF
068310        ELSE
068320           IF SUB3 = WS-FUNCTION-TABLE-SIZE
068330              SET ERRORS-FOUND     TO TRUE
068340           END-IF
068350        END-IF
068360     END-PERFORM
068370     IF ERRORS-FOUND
068380     OR REJECT-EMPLOYEE-FUNCTION
068390        SET FIND-MC-CODE       TO TRUE
068400        IF REJECT-EMPLOYEE-FUNCTION
068410           PERFORM P5015-CHECK-MISSED-CALL-TABLE
068420        END-IF
068430        IF FIND-MC-CODE
068440           PERFORM VARYING WS-MC-SUB FROM 1 BY 1
068450              UNTIL WS-MC-SUB > WS-MC-MAX
068460              IF WS-MISS-CALL-CODE(WS-MC-SUB) = SCR08S-FUNC(SUB1)
068470                 AND SCR08S-FUNC(SUB1) > SPACE
068480                 MOVE '0'          TO ERRORS-FOUND-CODE
068490                 SET MISSING-CALL  TO TRUE
068500                 MOVE WS-MC-MAX    TO WS-MC-SUB
068510              END-IF
068520           END-PERFORM
068530        END-IF
068540     END-IF
068550*CNC0600-B
068560     MOVE SPACES                   TO CNTLKEY-AREA
068570     MOVE '02'                     TO CNTL-REC-TYPE
068580     MOVE SCR08S-DIST              TO CNTL-DIST
068590     MOVE SCR08S-SUB-DIST          TO CNTL-SUB-DIST
068600     MOVE CNTLKEY-AREA             TO CNTLKEY
068610     PERFORM P8610-READ-CNTLFILE
068620     IF NOT SUCCESS
068630        MOVE 'P5010-1'             TO ERR-PARAGRAPH
068640        MOVE CNTLKEY               TO ERR-KEY
068650        PERFORM P9999-GOT-PROBLEM
068660     END-IF
068670     MOVE CNTL-CAN-WRR-FLAG        TO WS-CAN-WRR-FLAG
068680
068690     IF WS-CAN-WRR-NEW AND WS-FUNCTION-CODE = 'RJHS'
068700        MOVE -1                    TO SCR08S-FUNC-CURSOR(SUB1)
068710        MOVE REV-VIDEO             TO SCR08S-FUNC-HI(SUB1)
068720*             'RJHS NOT ALLOWED WITH NEW CANADIAN WORK REST RULES'
068730        MOVE 'R135'                TO MSGLOG-CODE
068740        PERFORM P9000-SEND-MAP-AND-RETURN
068750     END-IF
068760
068770*CNC0600-E
068780     IF ERRORS-FOUND
068790        MOVE SCR08S-FUNC(SUB1)     TO WS-FUNCTION-CODE
068800        IF P08SCA-IVR-CALL-NOT-ACTIVE
068810        AND IVR-CALL-FUNCTION
068820           PERFORM P6111-COMPANY-EDIT
068830           IF NOT CNTL-OUTBND-IVR-CALL-ACTIVE
068840*             IVR NOT AVAILABLE-VERIFY "OPTION 16"
068850*               IF MANUAL INTERVENTION REQUIRED
068860              MOVE 'I606'             TO MSGLOG-CODE
068870           ELSE
068880*               EMPLOYEE NOT ELIGIBLE FOR IVR PROCESSING
068890              MOVE 'E338'             TO MSGLOG-CODE
068900           END-IF
068910        ELSE
068920*               INVALID-CODE-MSG
068930           MOVE 'I041'             TO MSGLOG-CODE
068940        END-IF
068950        MOVE -1                    TO SCR08S-FUNC-CURSOR(SUB1)
068960        MOVE REV-VIDEO             TO SCR08S-FUNC-HI(SUB1)
068970        PERFORM P9000-SEND-MAP-AND-RETURN
068980     ELSE
068990        IF CREW-CONSIST-VACT
069000           SET VACATE-JOB          TO TRUE
069010        END-IF
069020     END-IF.
069030*
069040 P5015-CHECK-MISSED-CALL-TABLE.
069050*
069060     MOVE P08S-TSQ-EMP-SEL(SUB2)       TO MSTRNBRK
069070     PERFORM P8500-READ-MASTER
069080     IF (TEMP-ASGN-XB
069090        OR (NORM-ASGN-XB AND TEMPORARY-ASGNMT NOT > SPACE))
069100        AND ON-DUTY-ASGNMT NOT > SPACE
069110        MOVE SPACES                    TO WORK-CNTLKEY
069120        MOVE '69'                      TO WK-CNTL-MC-REC-TYPE
069130        MOVE WS-FUNCTION-CODE          TO WK-CNTL-MC-CODE
069140        IF JOB-DEF-YARD-ASGN
069150           MOVE 'YD'                   TO WK-CNTL-MC-TYPE-SVC
069160        ELSE
069170           MOVE 'RD'                   TO WK-CNTL-MC-TYPE-SVC
069180        END-IF
069190        IF TEMPORARY-ASGNMT > SPACES
069200           MOVE TA-DIST                TO WK-CNTL-MC-DIST
069210           MOVE TA-SUB-DIST            TO WK-CNTL-MC-SDIST
069220           MOVE TA-CC                  TO WK-CNTL-MC-XB
069230        ELSE
069240           MOVE NA-DIST                TO WK-CNTL-MC-DIST
069250           MOVE NA-SUB-DIST            TO WK-CNTL-MC-SDIST
069260           MOVE NA-CC                  TO WK-CNTL-MC-XB
069270        END-IF
069280        MOVE MSTR-CONS-MISSED-CALLS    TO WK-CNTL-MC-CONS
069290        PERFORM P5030-READ-MC-CONTROL
069300        IF NOT SUCCESS
069310           MOVE '**'                   TO WK-CNTL-MC-XB
069320           PERFORM P5030-READ-MC-CONTROL
069330           IF NOT SUCCESS
069340              MOVE '**'                TO WK-CNTL-MC-SDIST
069350              PERFORM P5030-READ-MC-CONTROL
069360              IF NOT SUCCESS
069370                 MOVE '**'             TO WK-CNTL-MC-DIST
069380                 PERFORM P5030-READ-MC-CONTROL
069390              END-IF
069400           END-IF
069410        END-IF
069420        IF NOT SUCCESS
069430           SET DONT-FIND-MC-CODE       TO TRUE
069440        END-IF
069450     ELSE
069460        SET DONT-FIND-MC-CODE          TO TRUE
069470     END-IF.
069480*
069490 P5020-EDIT-CALL-TIME.
069500*
069510*    SEE IF ORDER DATE/TIME MORE THAN 04 HOURS FROM ORIGINALLY
069520*    SCHEDULED ON-DUTY TIME
069530*
069540     MOVE ZEROS                     TO DATE-CONVERSION-PARMS
069550     SET PARM-SUBTRACT              TO TRUE
069560     MOVE P08S-TSQ-JOB-DATE(SUB2)   TO PARM-PRI-DATE-GREG
069570     MOVE P08S-TSQ-JOB-TIME(SUB2)   TO PARM-PRI-HRMN
069580     MOVE '0600'                    TO PARM-SEC-HRMN
069590     EXEC CICS LINK
069600               PROGRAM(P903-PGM)
069610               COMMAREA(DATE-CONVERSION-PARMS)
069620               LENGTH(P903-LGTH)
069630               RESP(WS-RESPONSE)
069640     END-EXEC
069650     MOVE WS-RESPONSE TO FILE-STATUS
069660     IF NOT SUCCESS
069670        MOVE 'P5020-1'              TO ERR-PARAGRAPH
069680        PERFORM P9999-GOT-PROBLEM
069690     END-IF
069700     MOVE PARM-RES-GREG-CENT        TO WS-COMPARE-CENT
069710     MOVE PARM-RES-DATE-GREG        TO WS-COMPARE-DATE
069720     MOVE PARM-RES-HRMN             TO WS-COMPARE-TIME
069730     PERFORM P7300-CONVERT-WK-CALL-DATES
069740     IF WK-CALL-DT-TM-CE   < WS-COMPARE-DT-TM-CE
069750        SET ERRORS-FOUND            TO TRUE
069760*          'ORDER DATE/TIME OVER 4 HOURS FROM SCHEDULED TIME'
069770        MOVE 'O018'                 TO MSGLOG-CODE
069780     END-IF
069790*
069800*    SEE IF ORDER DATE/TIME MORE THAN 4 HOURS IN FUTURE
069810*
069820     IF NOT ERRORS-FOUND
069830        MOVE ZEROS                  TO DATE-CONVERSION-PARMS
069840        SET PARM-ADD                TO TRUE
069850        MOVE P08S-TSQ-JOB-DATE(SUB2)
069860                                    TO PARM-PRI-DATE-GREG
069870        MOVE P08S-TSQ-JOB-TIME(SUB2)
069880                                    TO PARM-PRI-HRMN
069890        MOVE '0600'                 TO PARM-SEC-HRMN
069900        EXEC CICS LINK
069910                  PROGRAM(P903-PGM)
069920                  COMMAREA(DATE-CONVERSION-PARMS)
069930                  LENGTH(P903-LGTH)
069940                  RESP(WS-RESPONSE)
069950        END-EXEC
069960        MOVE WS-RESPONSE            TO FILE-STATUS
069970        IF NOT SUCCESS
069980           MOVE 'P5020-2'           TO ERR-PARAGRAPH
069990           PERFORM P9999-GOT-PROBLEM
070000        END-IF
070010        MOVE PARM-RES-GREG-CENT     TO WS-COMPARE-CENT
070020        MOVE PARM-RES-DATE-GREG     TO WS-COMPARE-DATE
070030        MOVE PARM-RES-HRMN          TO WS-COMPARE-TIME
070040        PERFORM P7300-CONVERT-WK-CALL-DATES
070050        IF WK-CALL-DT-TM-CE  > WS-COMPARE-DT-TM-CE
070060           SET ERRORS-FOUND         TO TRUE
070070*             'ORDER DATE/TIME OVER 4 HOURS FROM SCHEDULED TIME'
070080           MOVE 'O018'              TO MSGLOG-CODE
070090        END-IF
070100     END-IF.
070110*
070120 P5030-READ-MC-CONTROL.
070130*
070140     MOVE WORK-CNTLKEY              TO CNTLKEY
070150     EXEC CICS READ
070160               DATASET(CNTL-FILE-VIA-CNTLKEY)
070170               INTO(WS-CNTL-FILE)
070180               LENGTH(CNTLFILE-RLGTH)
070190               RIDFLD(CNTLKEY)
070200               KEYLENGTH(CNTLFILE-KLGTH)
070210               GTEQ
070220               RESP(WS-RESPONSE)
070230     END-EXEC
070240     MOVE WS-RESPONSE               TO FILE-STATUS
070250     IF SUCCESS
070260        IF CNTL-REC-TYPE NOT = '69'
070270           OR CNTL-MC-CODE NOT = WK-CNTL-MC-CODE
070280           OR CNTL-MC-TYPE-SVC NOT = WK-CNTL-MC-TYPE-SVC
070290           OR CNTL-MC-DIST NOT = WK-CNTL-MC-DIST
070300           OR CNTL-MC-SDIST NOT = WK-CNTL-MC-SDIST
070310           OR CNTL-MC-XB NOT = WK-CNTL-MC-XB
070320           SET NO-RECORD-FND        TO TRUE
070330        END-IF
070340     ELSE
070350        IF NOT (NO-RECORD-FND OR END-OF-FILE)
070360           MOVE 'P5030-1'           TO ERR-PARAGRAPH
070370           MOVE CNTLKEY             TO ERR-KEY
070380           PERFORM P9999-GOT-PROBLEM
070390        END-IF
070400     END-IF.
070410*
070420 P5100-PROCESS-CALL-FUNCTION.
070430*
070440     IF VACATE-FUNCTION
070450        MOVE SPACES                TO P08S-TSQ-EMP-SEL(SUB2)
070460                                      P08S-TSQ-EMP-NAME(SUB2)
070470     ELSE
070480        IF P08S-TSQ-NO-CALL(SUB2)
070490           OR P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
070500           MOVE -1                 TO SCR08S-FUNC-CURSOR(SUB1)
070510           MOVE REV-VIDEO          TO SCR08S-FUNC-HI(SUB1)
070520           IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
070530*            'EMPLOYEES STATUS PROHIBITS CALL'
070540              MOVE 'E030'          TO MSGLOG-CODE
070550           ELSE
070560*               'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
070570              MOVE 'I050'          TO MSGLOG-CODE
070580           END-IF
070590           PERFORM P9000-SEND-MAP-AND-RETURN
070600        END-IF
070610        MOVE P08S-TSQ-EMP-SEL(SUB2)
070620                                   TO EMP-NBR IN WS-MSTR
070630        MOVE ZEROES                TO SCHED-2ND-CODE
070640        IF   TRCN-2ND-ROUTE-DESIGNATION > SPACES
070650        AND  TRCN-LOCAL-REC
070660        AND (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
070670           PERFORM P3039-GET-CNTL-DIST-SUB-DIST
070680           IF NOT CNTL-SPLIT-SHIFT-HOS
070690              PERFORM P3037-CHECK-SCHED
070700           END-IF
070710        END-IF
070720        IF P08S-TSQ-DECKEY(SUB2) > SPACES
070730           OR P08S-USER-OVERRIDE(SUB2)
070740           SET VACANCY-FILLED          TO TRUE
070750        ELSE
070760           SET NOT-VACANCY-FILLED      TO TRUE
070770        END-IF
057860*07/26/23 RJA B CNC0600 CNLD-269
057850        SET NOT-PROMOTING-EMP        TO TRUE
057810        IF P08S-TSQ-DECKEY(SUB2) > SPACES
057860           MOVE P08S-TSQ-DECKEY(SUB2) TO DECKEY
057860           MOVE SPACES TO DEC-REQ-1
057870                          DEC-REQ-2
057880           PERFORM P8670-READ-DECISION
057890           IF ON-TURN OF WORK-DEC-TABLE
057900           OR ON-JOB  OF WORK-DEC-TABLE
057860              SET PROMOTING-EMP      TO TRUE
057860           END-IF
057870        END-IF
057880*07/26/23 RJA E
070780        MOVE SCHED-2ND-CODE          TO HOLD-SCHED-2ND-CODE
070790        PERFORM P5950-GET-HRS-SVC-INFO
070800*   IF YARDMASTER AND HOS EXEMPT, EMP-MTOD IS IRRELEVENT
070810        SET NOT-HOS-EXEMPT           TO TRUE
070820        MOVE JSK2-ASGN               TO JOB-DEF-CHECK
070830        MOVE CRAFT OF WS-MSTR        TO WS-CRAFT-CODE-CHECK
070840        IF JOB-DEF-YARDMASTER-ASGN
064160*CNLD-246 B
070850*          AND YARDMASTER-CRAFT
064160           AND (YARDMASTER-CRAFT OR YARDMASTER-TRAINEE)
064160*CNLD-246 E
070860           MOVE JSK2-ASGN-DIST       TO AJ-JOB-DIST
070870           MOVE JSK2-ASGN-SUB-DIST   TO AJ-JOB-SUB-DIST
070880           PERFORM P3031-DETERMINE-IF-HOS-EXEMPT
070890        END-IF
070900        MOVE HOLD-SCHED-2ND-CODE     TO SCHED-2ND-CODE
070910        IF  SCHED-2ND
070920        AND ON-SCHED-1ST
070930           SET P925-EMP-STATUS-GREEN TO TRUE
070940        ELSE
070950           IF P925-EMP-STATUS-RED
070960          AND NOT-HOS-EXEMPT
070970* 5/21/23- RJA - B CNC0600
070980*             MOVE -1              TO SCR08S-FUNC-CURSOR(SUB2)
070990*             MOVE REV-VIDEO       TO SCR08S-FUNC-HI(SUB2)
071000              MOVE -1              TO SCR08S-FUNC-CURSOR(SUB1)
071010              MOVE REV-VIDEO       TO SCR08S-FUNC-HI(SUB1)
071020* 5/21/23- RJA - E CNC0600
071030
071040*CNC0600-NXA-B
071050              IF WS-CAN-WRR-NEW
071060*CNLD-230-B
071070                 EVALUATE TRUE
      *CNLD-259 B CNC0600
                          WHEN WS-RFR-LEAD-IN-SYS-UD

      *             'LEAD TIME IS OVERLAPPING WITH SYS RESET UDR.'
                             MOVE 'L050'     TO MSGLOG-CODE
                          WHEN WS-RFR-OD-IN-SYS-RB

      *             'ON-DUTY TIME IS DURING SYS RESET BRK.'
                             MOVE 'O098'     TO MSGLOG-CODE
      *CNLD-259 E
071080                    WHEN WS-RFR-7DAY-RND-ERT
071090                       MOVE P925-RFR-MSG
071100                                    TO WS-RFR-MSG
071110*             'ERT=XXXX RNDTRIP:NOT ENOUGH TIME FOR 7DAY
071120*                             RESET BRK.'
071130                       MOVE 'E378'  TO MSGLOG-CODE
071140                    WHEN WS-RFR-7DAY-ERT
071150                       MOVE P925-RFR-MSG
071160                                    TO WS-RFR-MSG
071170*             'ERT=XXXX :NOT ENOUGH TIME FOR 7DAY
071180*                             RESET BRK.'
071190                       MOVE 'E379'  TO MSGLOG-CODE
071200                    WHEN WS-RFR-7DAY-HRS
071210
071220*             'NOT ENOUGH HOURS LEFT FOR 7DAY RESET BRK.'
071230                       MOVE 'N268'  TO MSGLOG-CODE
      *CNLD-257-DXC-B
071240*                   WHEN WS-RFR-60-192-ERT
071240                    WHEN WS-RFR-60-ERT
071250                       MOVE P925-RFR-MSG
071260                                    TO WS-RFR-MSG
071270*             'ERT XXXX :NOT ENOUGH HOURS ON 60/7 DAY CLOCK'
071290                       MOVE 'E380'  TO MSGLOG-CODE
                          WHEN WS-RFR-192-ERT
                             MOVE P925-RFR-MSG
                                          TO WS-RFR-MSG
      *           'ERT XXXX :NOT ENOUGH HOURS ON 192/28 DAY CLOCK.
                             MOVE 'E383'     TO MSGLOG-CODE
071280*
071300*                   WHEN WS-RFR-60-192-HRS
071300                    WHEN WS-RFR-60-HRS
071310
071320*             'NOT ENOUGH HOURS LEFT ON 60/7 DAY CLOCK'
071330*
071340                       MOVE 'N270'  TO MSGLOG-CODE
                          WHEN WS-RFR-192-HRS

      *                'NOT ENOUGH HOURS LEFT ON 192/28 DAY CLOCK.'
                             MOVE 'N272'     TO MSGLOG-CODE
      *CNLD-257-DXC-E
071350                    WHEN WS-RFR-MTOD
071360*             'EMPLOYEE NOT RESTED'
071370                       MOVE 'E377'  TO MSGLOG-CODE
071380                 END-EVALUATE
071390* 5/21/23- RJA - B CNC0600
071400*                MOVE RED           TO SCR08S-NAME-COL(SUB2)
071410                 MOVE RED           TO SCR08S-NAME-COL(SUB1)
071420* 5/21/23- RJA - E CNC0600
071430
071440*CNLD-230-E
071450              ELSE
071460*CNC0600-NXA-E
071470*                  'EMPLOYEES STATUS PROHIBITS CALL'
071480                 MOVE 'E030'          TO MSGLOG-CODE
071490*CNC0600-NXA-B
071500              END-IF
071510*CNC0600-NXA-E
071520              PERFORM P9000-SEND-MAP-AND-RETURN
071530           END-IF
071540*CNLD-236 - B CNC0600
071550           IF VACANCY-FILLED AND
071560              WS-CAN-WRR-NEW
071570              IF P925-MTOD-GT-LOCAL
071620*                11/1/'23 NOTE: P925-MTOD-GT-LOCAL WOULD
071620*                NOT BE TRUE IF PROMOTING-EMP IS TRUE
071580                 PERFORM P3090-CHECK-EMP-UNDST-REST
071590                 IF WS-EMP-UNDST-REST
071600                    MOVE WS-UD-MESG TO SCR08S-PHONE(SUB1)
071610                    MOVE YELLOW     TO SCR08S-PHONE-COL(SUB1)
      *CNLD-245-B
071620*                   PERFORM P9000-SEND-MAP-AND-RETURN
      *CNLD-245-E
071630                 END-IF
071640              END-IF
057860*CNLD-259 B CNC0600
071650*             IF P925-LOCAL-LT-RESET
071650              IF P925-LOCAL-LE-UDR2
057860*CNLD-259 E CNC0600
071660                 MOVE P925-RB-MSG-1-HRMN(1:2)
071670                                    TO WS-RB-1-HH
071680                 MOVE P925-RB-MSG-1-HRMN(3:2)
071690                                    TO WS-RB-1-MM
071700                 MOVE WS-RB-1-MESG TO SCR08S-PHONE(SUB1)
071710                 MOVE YELLOW     TO SCR08S-PHONE-COL(SUB1)
071720                 PERFORM P9000-SEND-MAP-AND-RETURN
071730              END-IF
071740           END-IF
071750*CNLD-236 - E CNC0600
071750
      *CNLD-245-B
      *CNLD-285-B 11/01/23
                 IF ((ENGINE-CRAFT AND TRCN-EN-REST-REQ = 'U')
                     OR ((NOT ENGINE-CRAFT) AND
                         TRCN-TR-REST-REQ = 'U'))
      *             WS-EMP-UNDST-REST-FLAG IS INITIALIZED ELSEWHERE
      *             WHEN TRCN-EN/TR-REST-REQ = 'B', CHECK CNLD-285 TAGS
                    MOVE SPACES     TO WS-EMP-UNDST-REST-FLAG
      *CNLD-330-B
                    MOVE ZEROS      TO WS-SAVE-UD-DTTM
      *CNLD-330-E
                 END-IF
      *CNLD-330-B COMMENTING
      *          MOVE SPACES     TO WS-REST-US-STATUS-FLAG
      *CNLD-330-E
      *CNLD-285-E
057860*07/26/23 RJA B CNC0600 CNLD-269
      *          IF VACANCY-FILLED AND
                 IF (VACANCY-FILLED AND NOT-PROMOTING-EMP) AND
057860*07/26/23 RJA E
                    ((ENGINE-CRAFT AND TRCN-EN-REST-REQ = ('U' OR 'B'))
                     OR (NOT ENGINE-CRAFT AND
                        TRCN-TR-REST-REQ = ('U' OR 'B')))
                    PERFORM P3095-CHECK-US-UNDIST-REST
                 END-IF
071590           IF WS-EMP-UNDST-REST
071620              PERFORM P9000-SEND-MAP-AND-RETURN
071630           END-IF
      *CNLD-245-E
071760        END-IF
071770     END-IF
071780*
071790*    SEE IF ASSIGNMENT HAS ALREADY BEEN CALLED FOR THIS DATE/TIME
071800*
071810     MOVE SPACES                   TO AHM-KEY
071820     MOVE AHM-FILE                 TO AHM-DATASET
071830     MOVE JSK2-ASGN-DIST           TO AHM-DIST
071840     MOVE JSK2-ASGN-SUB-DIST       TO AHM-SUB-DIST
071850     IF JOB-DEF-YARD-ASGN
071860        MOVE JOB-DEF-YARD-POOL     TO AHM-POOL
071870     ELSE
071880        IF JOB-DEF-LOCAL-ASGN
071890           MOVE JOB-DEF-LOCAL-POOL TO AHM-POOL
071900        END-IF
071910     END-IF
071920     MOVE WK-CALL-DATE             TO AHM-EFF-DATE
071930     MOVE WK-CALL-TIME             TO AHMK1-EFF-TIME
071940     MOVE SCR08S-ASGN(SUB1)        TO AHM-TRAIN
071950     SET AHM-EMPLOYEE-RECORD       TO TRUE
071960     SET CT-IND                    TO 1
071970     SEARCH CRAFT-CODE-TABLE VARYING CT-IND
071980        AT END CONTINUE
071990           MOVE '99'               TO AHM-CRAFT-SEQ
072000        WHEN CT-CRAFT-CODE(CT-IND) = SCR08S-CC(SUB1)
072010           MOVE CT-CRAFT-SEQ(CT-IND)
072020                                   TO AHM-CRAFT-SEQ
072030     MOVE WK-CALL-DATE-TIME        TO AHM-EMP-OD-DATE-TIME
072040     MOVE AHM-KEY                  TO AHMKEY
072050
072060     EXEC CICS READ
072070               DATASET(AHM-DATASET)
072080               INTO(WS-AHIST-MISC)
072090               LENGTH(AHM-RLGTH)
072100               RIDFLD(AHMKEY)
072110               KEYLENGTH(AHM-KLGTH)
072120               RESP(WS-RESPONSE)
072130     END-EXEC
072140     MOVE WS-RESPONSE              TO FILE-STATUS
072150     IF SUCCESS
072160        MOVE -1                    TO SCR08S-OD-TIME-CURSOR(SUB1)
072170        MOVE REV-VIDEO             TO SCR08S-OD-TIME-HI(SUB1)
072180*            'EMPLOYEE ALREADY CALLED FOR THIS ON DUTY TIME'
072190        MOVE 'E026'                TO MSGLOG-CODE
072200        PERFORM P9000-SEND-MAP-AND-RETURN
072210     ELSE
072220        IF NOT NO-RECORD-FND
072230           MOVE 'P5100-1'          TO ERR-PARAGRAPH
072240           MOVE AHMKEY             TO ERR-KEY
072250           PERFORM P9999-GOT-PROBLEM
072260        END-IF
072270     END-IF
072280     MOVE AHM-KEY                  TO WS-AHM-KEY
072290*
072300*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
072310* ALLOW CALL OF AN EMPLOYEE WHO IS STILL WORKING THE PREV SHIFT
072320* AS LONG AS QUICK TIEUP IS ALLOWED IN THE SUB-DIST OF THE PREV
072330* ASSIGNMENT AND THE PERSON IS WORKING AN ASSIGNED JOB.
072340*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
072350*
072360     IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
072370        MOVE P08S-TSQ-EMP-SEL(SUB2)  TO MSTRNBRK
072380        PERFORM P8500-READ-MASTER
072390        IF WORKING OF WS-MSTR
072400           MOVE SPACES               TO WS-CNTL-FILE
072410           SET SUB-DIST-TYPE-2A-REC  TO TRUE
072420           MOVE OD-DIST              TO CNTL-DIST
072430           MOVE OD-SUB-DIST          TO CNTL-SUB-DIST
072440           MOVE CNTLKEY-AREA         TO CNTLKEY
072450           PERFORM P8610-READ-CNTLFILE
072460           IF NOT SUCCESS
072470              MOVE 'P5100-2'         TO ERR-PARAGRAPH
072480              MOVE CNTLKEY           TO ERR-KEY
072490              PERFORM P9999-GOT-PROBLEM
072500           END-IF
072510           MOVE CNTL-RESTRICT-TIE-FLAG  TO WS-ALLOW-QUICK-TIE-FLAG
072520
072530           IF ALLOW-QUICK-TIEUP AND
072540              ON-DUTY-ASGNMT > SPACES AND
072550              ON-DUTY-AJ AND
072560              EMP-TRHIST-KEY > SPACE
072570              CONTINUE
072580           ELSE
072590              MOVE -1                TO SCR08S-FUNC-CURSOR(SUB1)
072600              MOVE REV-VIDEO         TO SCR08S-FUNC-HI(SUB1)
072610*             'EMPLOYEES STATUS PROHIBITS CALL'
072620              MOVE 'E030'            TO MSGLOG-CODE
072630              PERFORM P9000-SEND-MAP-AND-RETURN
072640           END-IF
072650        END-IF
072660     END-IF
072670*
072680*    IF THE JOB HAS AN OWNER AND THE OWNER IS NOT BEING CALLED,
072690*    INITIATE TRACKING.
072700*
072710     IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
072720        AND NOT VACATE-FUNCTION
072730        IF P08S-TSQ-OWNER(SUB2) > '000000000' AND
072740           P08S-TSQ-OWNER(SUB2) NOT = P08S-TSQ-EMP-SEL(SUB2)
072750           IF NOT P08S-TSQ-TRK-DONT-TRACK(SUB2)
072760              AND NOT P08S-TSQ-TRK-RJH(SUB2)
072770              IF P08S-TSQ-TRK-REASON(SUB2) NOT > SPACES
072780                 SET P08S-TSQ-TRK-UNKNOWN(SUB2)  TO TRUE
072790              END-IF
072800              MOVE P08S-TSQ-TRK-REASON(SUB2) TO WS-TRK-REASON
072810              PERFORM P8080-INITIATE-TRACKING
072820           END-IF
072830        END-IF
072840     END-IF
072850
072860     MOVE SPACES                   TO P921-COMMAREA-PARMS
072870     SET P921-CALL-FUNCTION        TO TRUE
072880*
072890*    IF THIS IS A GO-TRAIN (COMMUTER) BEING WORKED BY A RELIEF
072900*    JOB, SEND THE RELEIF JOB'S DATA TO 921 SO THAT THE EMPLOYEE
072910*    STAYS ON THE JOB FOR THE SECOND TOUR.
072920*
072930*    ALWAYS SEND THE ACTUAL ASSIGNMENT WORKED
072940*    (RELIEF, SCHED XB...) TO P921, INSTEAD OF THE 'WORKING' ASGN.
072950*    THE AHMISC EMPLOYEE RECORD NEEDS TO REFLECT THE ASSIGNMENT
072960*    THE EMPLOYEE ACTUALLY MANNED WHILE CALLED OUT ON THE
072970*    'WORKING' ASSIGNMENT.
072980*
072990     MOVE JSK1-ASGN-DIST           TO P921-AJ-DIST
073000     MOVE JSK1-ASGN-SUB-DIST       TO P921-AJ-SUB-DIST
073010     MOVE JSK1-ASGN                TO P921-AJ-ASGN
073020     MOVE JSK1-ASGN-CC             TO P921-AJ-CC
073030     MOVE P08S-TSQ-CALL-DATE-TIME(SUB2)
073040                                   TO P921-EFF-DATE-TIME
073050     MOVE P08S-TSQ-JOB-DATE-TIME(SUB2)
073060                                   TO P921-JOB-DATE-TIME
073070     MOVE P08S-TSQ-LEAD-TIME(SUB2) TO P921-CALL-LEAD-TIME
073080     MOVE PSTCA-TIME-ZONE          TO P921-TIME-ZONE
073090     MOVE SCR08S-ASGN(SUB1)        TO P921-TRAIN
073100     MOVE TRCN-KEY1                TO P921-ASGN-PROFILE-KEY
073110     MOVE SCR08S-FUNC(SUB1)        TO P921-CALL-FUNC-CODE
073120     MOVE SCR08S-CC(SUB1)          TO P921-CALL-CC
073130     IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
073140        AND NOT VACATE-FUNCTION
073150        MOVE P08S-TSQ-EMP-SEL(SUB2)
073160                                   TO P921-EMP-NO
073170     END-IF
073180     IF P08S-TSQ-DECKEY(SUB2) > SPACES
073190        MOVE P08S-TSQ-DECKEY(SUB2) TO P921-DECKEY
073200     END-IF
073210*CNC0562 - BEG
073220*        THE KEY1 FROM THE JS RECORD FOR THE JOB (NOT THE RELIEF)
073230*        NEEDS TO BE PASSED TO P921. THIS IS THE KEY THAT IS
073240*        PASSED TO P921 ON A REGULAR CALL (CNP08).
073250     MOVE JS-KEY1                   TO P921-CALL-JSKEY1
073260     MOVE JSK1-ASGN                 TO JOB-DEF-CHECK
073270     IF JOB-DEF-RELIEF-ASGN
073280        MOVE JSK2-ASGN-DIST       TO P921-CALL-JSK1-ASGN-DIST
073290        MOVE JSK2-ASGN-SUB-DIST   TO P921-CALL-JSK1-ASGN-SUB-DIST
073300        MOVE JSK2-ASGN            TO P921-CALL-JSK1-ASGN
073310        MOVE JSK2-ASGN-CC         TO P921-CALL-JSK1-ASGN-CC
073320        MOVE JSK2-EXP-DATE        TO P921-CALL-JSK1-EXP-DATE
073330        MOVE JSK2-ASGN-DAY        TO P921-CALL-JSK1-ASGN-DAY
073340        MOVE JSK2-ASGN-START-TIME TO P921-CALL-JSK1-ASGN-STRT-TM
073350
073360        MOVE P921-CALL-JSKEY1     TO JSKEY1
073370        EXEC CICS READ
073380                  DATASET  (JS-VIA-JSKEY1)
073390                  INTO     (SAVE-JOB-SCHEDULE)
073400                  LENGTH   (JSKEY1-RLGTH)
073410                  RIDFLD   (JSKEY1)
073420                  KEYLENGTH(JSKEY1-KLGTH)
073430                  RESP     (WS-RESPONSE)
073440        END-EXEC
073450        MOVE WS-RESPONSE            TO FILE-STATUS
073460        IF NOT SUCCESS
073470           MOVE JS-KEY1             TO P921-CALL-JSKEY1
073480        END-IF
073490     END-IF
073500*CNC0562 - END
073510     IF P08S-TSQ-STARTS-DISQUALIFIED(SUB2)
073520        MOVE P08S-TSQ-STARTS-TOTAL(SUB2)
073530                                   TO P921-DISQUALIFIED-STARTS
073540     ELSE
073550        MOVE ZEROS                 TO P921-DISQUALIFIED-STARTS
073560     END-IF
073570     MOVE P08S-TSQ-RJHS-EMPNO(SUB2)  TO P921-RJHS-EMPNO
073580     MOVE P925-REST-STATUS-FLAG      TO P921-EMP-REST-STATUS
073590     IF SCR08S-USHR(SUB1)             > SPACES
073600        AND P08SCA-EMP-RESTED-NO(SUB1)
073610        SET P921-US-REST-RED         TO TRUE
073620     ELSE
073630        SET P921-US-REST-GREEN       TO TRUE
073640     END-IF
073650     IF IVR-CALL-FUNCTION
073660        SET  P921-FROM-IVR-CALL      TO TRUE
073670     END-IF
073680     MOVE TRCN-ROUTE-DESIGNATION    TO P921-ROUTE-DESIGNATION
073690     SET P921-CALLED-FROM-HOME      TO TRUE
073700*CNLD-207-DXC-B
073710     MOVE P925-CAN-WRR-LEAD-TIME    TO P921-CAN-WRR-LEAD-TIME
073720*CNLD-207-DXC-E
073730     EXEC CICS LINK
073740               PROGRAM(P921-PGM)
073750               COMMAREA(P921-COMMAREA-PARMS)
073760               LENGTH(P921-LGTH)
073770               RESP(WS-RESPONSE)
073780     END-EXEC
073790     MOVE WS-RESPONSE              TO FILE-STATUS
073800     IF NOT SUCCESS
073810        MOVE 'P5100-3'             TO ERR-PARAGRAPH
073820        PERFORM P9999-GOT-PROBLEM
073830     END-IF
073840     MOVE WS-FUNCTION-CODE         TO P08S-TSQ-CALLED-CODE(SUB2)
073850     MOVE SPACE                    TO P08S-TSQ-COLOR-OVRD(SUB2)
073860     SUBTRACT 1                  FROM P08S-TSQ-ACT-CNT
073870     IF P08S-TSQ-STARTS-DISQUALIFIED(SUB2) AND
073880        NOT P08S-TSQ-STARTS-VACANCY(SUB2)
073890        PERFORM P4010-DISPLAY-VACANCY
073900     END-IF
073910     PERFORM P4020-DISPLAY-EMP-INFO
073920     IF NOT VACATE-FUNCTION
073930        MOVE SUB1                  TO HOLD-SUB1
073940        MOVE SUB2                  TO HOLD-SUB2
073950        PERFORM VARYING SUB1 FROM 1 BY 1
073960           UNTIL SUB1 > ARRAY-MAX
073970           MOVE P08SCA-SUB2        TO SUB2
073980           ADD SUB1                TO SUB2
073990           IF SUB1 NOT = HOLD-SUB1
074000              IF P08S-TSQ-EMP-SEL(SUB2)
074010                 = P08S-TSQ-EMP-SEL(HOLD-SUB2)
074020                 MOVE SPACES       TO P08S-TSQ-EMP-SEL(SUB2)
074030                 PERFORM P4020-DISPLAY-EMP-INFO
074040              END-IF
074050           END-IF
074060        END-PERFORM
074070        PERFORM VARYING SUB2 FROM 1 BY 1
074080           UNTIL SUB2 > (P08S-TSQ-CNT OR P08S-TSQ-MAX)
074090           IF SUB2 NOT = HOLD-SUB2
074100              IF P08S-TSQ-EMP-SEL(SUB2)
074110                 = P08S-TSQ-EMP-SEL(HOLD-SUB2)
074120                 MOVE SPACES       TO P08S-TSQ-EMP-SEL(SUB2)
074130              END-IF
074140           END-IF
074150        END-PERFORM
074160        MOVE HOLD-SUB1             TO SUB1
074170        MOVE HOLD-SUB2             TO SUB2
074180     ELSE
074190        IF ANNULL-JOB
074200           PERFORM P5110-RELEASE-TRACKED-EMPS
074210        END-IF
074220     END-IF
074230
074240     IF NOT IVR-CALL-FUNCTION
074250        MOVE P08S-TSQ-JSKEY2        (SUB2) TO WORK-JS-KEY2
074260        MOVE P08S-TSQ-CALL-DATE     (SUB2) TO WS-9300-CALL-DATE
074270        MOVE WK-JSK2-ASGN-START-TIME       TO WS-9300-CALL-TIME
074280        PERFORM P9300-READ-TASK-BY-STATION
074290        IF WS-TASK-FOUND
074300           MOVE SPACES                TO VRUINFO-FILE
074310           MOVE TASK-LOCATION-KEY     TO VRUINFO-TASK-KEY
074320           PERFORM P9310-READ-UPDATE-VRUINFO
074330           EVALUATE TRUE
074340              WHEN SUCCESS
074350                   PERFORM P6165-UPDATE-VRU-INFO
074360              WHEN OTHER
074370                   CONTINUE
074380           END-EVALUATE
074390        END-IF
074400     END-IF
074410     .
074420*CNLD-236 - B CNC0600
074430*
074440 P3090-CHECK-EMP-UNDST-REST.
074450*
074460     MOVE P925-EMP-NO              TO MSTR3NBRK
074470     PERFORM P8570-READ-MASTER3
074480     MOVE P925-EMP-NO              TO MSTRNBRK
074490     PERFORM P8505-READ-MASTER-ONLY
074500
074510     IF WS-CAN-WRR-NEW
074520        IF EMP-MTOD-NUM > WS-LOCAL-DATE-TIME
074530           MOVE EMP-MTOD-TIME(1:2) TO WS-UD-HH
074550           MOVE EMP-MTOD-TIME(3:2) TO WS-UD-MM
074570           SET WS-EMP-UNDST-REST         TO TRUE
      *CNLD-245-B
                 MOVE EMP-MTOD-NUM       TO WS-SAVE-UD-DTTM
      *CNLD-245-E
074580        END-IF
074590        IF WS-MSTR3-REC-FOUND
074600           IF WS-LOCAL-DATE-TIME >= MSTR3-SYSTEM-UD1-FROM-NUM
074610           AND WS-LOCAL-DATE-TIME <= MSTR3-SYSTEM-UD1-TO-NUM
074620              IF EMP-MTOD-NUM > MSTR3-SYSTEM-UD1-TO-NUM
074630                 CONTINUE
074640              ELSE
074650                 MOVE MSTR3-SYSTEM-UD1-TO-TIME(1:2) TO WS-UD-HH
074670                 MOVE MSTR3-SYSTEM-UD1-TO-TIME(3:2) TO WS-UD-MM
074690                 SET WS-EMP-UNDST-REST         TO TRUE
      *CNLD-245-B
                       MOVE MSTR3-SYSTEM-UD1-TO-NUM
                                         TO WS-SAVE-UD-DTTM
      *CNLD-245-E
074700              END-IF
074710           ELSE
074720              IF  WS-LOCAL-DATE-TIME >= MSTR3-SYSTEM-UD2-FROM-NUM
074730               AND WS-LOCAL-DATE-TIME <= MSTR3-SYSTEM-UD2-TO-NUM
074740                  MOVE MSTR3-SYSTEM-UD2-TO-TIME(1:2) TO WS-UD-HH
074760                  MOVE MSTR3-SYSTEM-UD2-TO-TIME(3:2) TO WS-UD-MM
074780                  SET WS-EMP-UNDST-REST         TO TRUE
      *CNLD-245-B
                        MOVE MSTR3-SYSTEM-UD2-TO-NUM
                                         TO WS-SAVE-UD-DTTM
      *CNLD-245-E
074790              END-IF
074800           END-IF
074810        END-IF
074820     END-IF
074830     .
      *CNLD-245-B
       P3095-CHECK-US-UNDIST-REST.
      *

           IF (EMP-US-RSTD-NUM > WS-LOCAL-DATE-TIME
              AND WS-SAVE-UD-DTTM <= EMP-US-RSTD-NUM)
              MOVE EMP-US-RSTD-TIME(1:2) TO WS-UD-HH
              MOVE EMP-US-RSTD-TIME(3:2) TO WS-UD-MM
              SET WS-EMP-UNDST-REST         TO TRUE
              MOVE WS-UD-MESG            TO SCR08S-PHONE(SUB1)
              MOVE YELLOW                TO SCR08S-PHONE-COL(SUB1)
      *CNLD-330-B COMMENTING
      *       SET WS-EMP-US-STATUS-RED   TO TRUE
      *CNLD-330-E
           END-IF

           .
      *CNLD-245-E
074840*
074850 P8570-READ-MASTER3.
074860*
074870     EXEC CICS READ
074880               DATASET(MSTR3-VIA-EMP-NBR)
074890               INTO(WS-MSTR3)
074900               LENGTH(MSTR3ENBR-RLGTH)
074910               RIDFLD(MSTR3NBRK)
074920               KEYLENGTH(MSTR3ENBR-KLGTH)
074930               RESP(WS-RESPONSE)
074940     END-EXEC
074950     MOVE WS-RESPONSE           TO FILE-STATUS
074960     IF NOT SUCCESS
074970        IF NOT (NO-RECORD-FND OR END-OF-FILE)
074980           MOVE 'P8570'            TO ERR-PARAGRAPH
074990           MOVE MSTR3NBRK          TO ERR-KEY
075000           PERFORM P9999-GOT-PROBLEM
075010        END-IF
075020     ELSE
075030        SET WS-MSTR3-REC-FOUND  TO TRUE
075040     END-IF.
075050
075060*CNLD-236 - E CNC0600
075070*
075080 P5110-RELEASE-TRACKED-EMPS.
075090*
075100     MOVE SPACES                      TO P938-COMMAREA-PARMS
075110     SET P938-CANCEL-FUNCTION         TO TRUE
075120     SET P938-ASGN-AJ                 TO TRUE
075130     MOVE JSK1-ASGN-DIST              TO P938-ASGN-DIST
075140     MOVE JSK1-ASGN-SUB-DIST          TO P938-ASGN-SUB-DIST
075150     MOVE JSK1-ASGN                   TO P938-ASGN-ASGN
075160     MOVE JSK1-ASGN-CC                TO P938-ASGN-CC
075170     MOVE WK-CALL-DATE-TIME           TO P938-EFF-DATE-TIME
075180     MOVE PSTCA-TIME-ZONE             TO P938-TIME-ZONE
075190     SET P938-ASGN-IS-HOME            TO TRUE
075200     SET P938-CAN-ANNULL-JOB          TO TRUE
075210     SET P938-CANCEL-IN-TOWN          TO TRUE
075220     SET AJ-ASGN                      TO TRUE
075230     PERFORM P7000-FIND-ASGN-CLASS
075240     MOVE WS-ASGN-MERG-CLASS          TO P938-ASGN-MERG-CLASS
075250     EXEC CICS LINK
075260               PROGRAM(P938-PGM)
075270               COMMAREA(P938-COMMAREA-PARMS)
075280               LENGTH(P938-LGTH)
075290               RESP(WS-RESPONSE)
075300     END-EXEC
075310     MOVE WS-RESPONSE                 TO FILE-STATUS
075320     IF NOT SUCCESS
075330        MOVE 'P5110-1'                TO ERR-PARAGRAPH
075340        MOVE 'P938LINK'               TO ERR-KEY
075350        PERFORM P9999-GOT-PROBLEM
075360     END-IF
075370     .
075380*
075390 P5200-PROCESS-FILL-FUNCTION.
075400*
075410     IF (REJECT-EMPLOYEE-FUNCTION AND NOT REJECT-NOT-RESTED) OR
075420        (REJECT-NOT-RESTED AND SCR08S-ST(SUB1) NOT = 'NR' AND
075430         SCR08S-MTOD(SUB1) NOT > SPACES AND
075440         SCR08S-USHR(SUB1) NOT > SPACES AND
075450         SCR08S-PERS(SUB1) NOT > SPACES)
075460        IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
075470           MOVE -1                  TO SCR08S-FUNC-CURSOR(SUB1)
075480*               '"FILL" IS REQUIRED BEFORE "REJECT" '
075490*               'IS POSSIBLE'
075500           MOVE 'F010'              TO MSGLOG-CODE
075510           PERFORM P9000-SEND-MAP-AND-RETURN
075520        END-IF
075530        IF P08S-TSQ-DECKEY(SUB2) NOT > SPACES
075540           OR P08S-TSQ-SENR-RESTRICTED(SUB2)
075550           IF REJECT-HRS-SERVICE
075560              CONTINUE
075570           ELSE
075580              MOVE -1               TO SCR08S-FUNC-CURSOR(SUB1)
075590*               '"FILL" IS REQUIRED BEFORE "REJECT" '
075600*               'IS POSSIBLE'
075610              MOVE 'F010'           TO MSGLOG-CODE
075620              PERFORM P9000-SEND-MAP-AND-RETURN
075630           END-IF
075640        END-IF
075650     END-IF
075660     IF REJECT-AND-HOLD
075670        SET WS-TRK-RJH              TO TRUE
075680        PERFORM P8080-INITIATE-TRACKING
075690     END-IF
075700*CNC0578-BEG
075710     IF CREW-CONSIST-RJSU
075720        PERFORM P5220-WRITE-NO-FUNC-HISTORY
075730     END-IF
075740*CNC0578-END
075750     MOVE SPACES                    TO P923-COMMAREA-PARMS
075760     IF REJECT-HRS-SERVICE
075770        PERFORM P5960-REJECT-HRS-SVC
075780     END-IF
075790     MOVE SCR08S-FUNC(SUB1)         TO P923-FILL-FUNC-CODE
075800     SET AJ-VACANCY                 TO TRUE
075810     MOVE JSK2-ASGN-DIST            TO P923-ASGN-DIST
075820                                       P923-DIST
075830     MOVE JSK2-ASGN-SUB-DIST        TO P923-ASGN-SUB-DIST
075840                                       P923-SUB-DIST
075850     MOVE JSK2-ASGN                 TO P923-ASGN-AJ
075860     MOVE JSK2-ASGN-CC              TO P923-ASGN-CC
075870     IF JOB-DEF-YARD-ASGN
075880        MOVE JOB-DEF-YARD-POOL      TO P923-POOL-SERV
075890     ELSE
075900        IF JOB-DEF-LOCAL-ASGN
075910           MOVE JOB-DEF-LOCAL-POOL  TO P923-POOL-SERV
075920        END-IF
075930     END-IF
075940     MOVE '0'                       TO P923-TERMINAL
075950     MOVE JS-KEY1                   TO P923-CALLED-ASGN
075960     IF JOB-DEF-YARD-ASGN
075970        PERFORM P5951-CALC-ERT
075980        MOVE WS-HR-MN               TO P923-AJ-ERT
075990     END-IF
076000
076010     MOVE SCR08S-CC(SUB1)           TO WS-CRAFT-CODE-CHECK
076020     IF FILL-VACANCY
076030        MOVE SPACES                 TO P08S-TSQ-DECKEY(SUB2)
076040                                       P08S-TSQ-LAST-SEN(SUB2)
076050                                       P08S-TSQ-LAST-POS(SUB2)
076060                                       P08S-TSQ-LAST-STARTS(SUB2)
076070                                       P08S-TSQ-REST-CODE(SUB2)
076080                                       P08S-TSQ-EMP-SEL(SUB2)
076090*CNC0579B - BEG
076100                                   P08S-TSQ-DEC-RPT-LOCATION(SUB2)
076110*CNC0579B - END
076120     END-IF
076130     IF P08S-TSQ-DECKEY(SUB2) > SPACES
076140        MOVE P08S-TSQ-DECKEY(SUB2)  TO P923-DEC-KEY
076150        IF P08S-TSQ-LAST-SEN(SUB2) > SPACE
076160           MOVE P08S-TSQ-LAST-SEN(SUB2)
076170                                    TO P923-LAST-SEN-DATE
076180        END-IF
076190        IF P08S-TSQ-LAST-POS(SUB2) > SPACE
076200           MOVE P08S-TSQ-LAST-POS(SUB2)
076210                                    TO P923-LAST-POSITION
076220        END-IF
076230        IF P08S-TSQ-LAST-STARTS(SUB2) > SPACE
076240           MOVE P08S-TSQ-LAST-STARTS(SUB2)
076250                                    TO P923-LAST-STARTS
076260        END-IF
076270     ELSE
076280        SET VACANCY-IS-MUST-FILL    TO TRUE
076290*
076300*       THERE MAY BE TRASH IN THE SVC OVERRIDE FIELDS (LEFT OVER
076310*       FROM SOME PREVIOUS USE OF THIS DATA ERROR), SO MAKE SURE
076320*       THAT THE OVERRIDE CRAFT CODE IS VALID
076330*
076340        IF TRCN-DEC-CC-OVERRIDE > SPACES
076350           MOVE TRCN-DEC-CC-OVERRIDE  TO WS-CRAFT-CODE-CHECK
076360           IF NOT VALID-CRAFT
076370              MOVE SPACES             TO TRCN-DEC-CC-OVERRIDE
076380                                         TRCN-DEC-SVC-OVERRIDE
076390           END-IF
076400           MOVE SCR08S-CC(SUB1)       TO WS-CRAFT-CODE-CHECK
076410        END-IF
076420        IF TRCN-DEC-CC-OVERRIDE > SPACES
076430           PERFORM P5230-CHECK-SVC-OVERRIDE
076440        ELSE
076450           SET DONT-USE-SVC-OVERRIDE  TO TRUE
076460        END-IF
076470        IF USE-SVC-OVERRIDE
076480           MOVE TRCN-DEC-SVC-OVERRIDE TO P923-DEC-SVC-CODE
076490        ELSE
076500           PERFORM VARYING SUBA FROM 1 BY 1
076510              UNTIL SUBA > 8
076520              IF TRCN-DEC-SVC-CC(SUBA) = SCR08S-CC(SUB1)
076530                 MOVE TRCN-DEC-SVC(SUBA) TO P923-DEC-SVC-CODE
076540              END-IF
076550           END-PERFORM
076560           IF P923-DEC-SVC-CODE NOT > SPACES
076570              MOVE SPACES                  TO WS-HOLD-CC
076580              EVALUATE TRUE
076590              WHEN ENGINEER-CRAFT
076600                 MOVE ENGINEER-MSTR-CC     TO WS-HOLD-CC
076610              WHEN FIREMAN-CRAFT OR 2ND-ENGINEER-CRAFT
076620                 MOVE 2ND-ENGINEER-MSTR-CC TO WS-HOLD-CC
076630              WHEN CONDUCTOR-CRAFT
076640                 MOVE CONDUCTOR-MSTR-CC    TO WS-HOLD-CC
076650              WHEN FOREMAN-CRAFT
076660                 MOVE FOREMAN-MSTR-CC      TO WS-HOLD-CC
076670              WHEN SWITCHMAN-CRAFT
076680                 MOVE SWITCHMAN-MSTR-CC    TO WS-HOLD-CC
076690              WHEN BRAKEMAN-CRAFT
076700                 MOVE BRAKEMAN-MSTR-CC     TO WS-HOLD-CC
076710              WHEN YARDMASTER-CRAFT
076720                 MOVE YARDMASTER-MSTR-CC   TO WS-HOLD-CC
076730              END-EVALUATE
076740              IF WS-HOLD-CC > SPACES
076750                 PERFORM VARYING SUBA FROM 1 BY 1
076760                    UNTIL SUBA > 8
076770                    IF WS-HOLD-CC = TRCN-DEC-SVC-CC(SUBA)
076780                       MOVE TRCN-DEC-SVC(SUBA)
076790                                         TO P923-DEC-SVC-CODE
076800                    END-IF
076810                 END-PERFORM
076820              END-IF
076830           END-IF
076840        END-IF
076850     END-IF
076860     MOVE WK-CALL-DATE-TIME         TO P923-CALL-DATE-TIME
076870                                       P923-ORDER-DATE-TIME
076880     MOVE P08S-TSQ-LEAD-TIME(SUB2)  TO P923-LEAD-TIME
076890     MOVE PSTCA-TIME-ZONE           TO P923-TIME-ZONE
076900     MOVE SCR08S-ASGN(SUB1)         TO P923-TRAIN
076910     IF REJECT-EMPLOYEE-FUNCTION
076920       OR LAYOFF-ON-CALL
076930       OR CREW-CONSIST-RJSU
076940        MOVE P08S-TSQ-EMP-SEL(SUB2) TO P923-EMP-NO
076950        SET AJ-ASGN                 TO TRUE
076960        PERFORM P7000-FIND-ASGN-CLASS
076970        MOVE WS-ASGN-MERG-CLASS     TO P923-ASGN-MERG-CLASS
076980        MOVE P08S-TSQ-JOB-DATE-TIME(SUB2) TO P923-CALL-DATE-TIME
076990                                             P923-ORDER-DATE-TIME
077000     END-IF
077010     MOVE TRCN-KEY1                 TO P923-ASGN-PROFILE-KEY
077020     MOVE P08S-TSQ-REST-CODE(SUB2)  TO P923-REST-CODE
077030     MOVE P08S-TSQ-JOB-TIME(SUB2)   TO P923-ASGN-CALL-TIME
077040     EXEC CICS LINK
077050               PROGRAM(P923-PGM)
077060               COMMAREA(P923-COMMAREA-PARMS)
077070               LENGTH(P923-LGTH)
077080               RESP(WS-RESPONSE)
077090     END-EXEC
077100     MOVE WS-RESPONSE               TO FILE-STATUS
077110     IF NOT SUCCESS
077120        MOVE 'P5200-1'              TO ERR-PARAGRAPH
077130        PERFORM P9999-GOT-PROBLEM
077140     END-IF
077150     MOVE P923-ERRORMSG             TO SCR08S-ERRORMSG
077160     MOVE SPACES                    TO SCR08S-FUNC(SUB1)
077170                                       P08S-TSQ-COLOR-OVRD(SUB2)
077180     IF GOT-MAN
077190        MOVE P923-DEC-KEY           TO P08S-TSQ-DECKEY(SUB2)
077200*CNC0579B - BEG
077210                                       DECKEY
077220        PERFORM P8670-READ-DECISION
077230*CNC0579B - END
077240        MOVE P923-LAST-SEN-DATE     TO P08S-TSQ-LAST-SEN(SUB2)
077250        MOVE P923-LAST-POSITION     TO P08S-TSQ-LAST-POS(SUB2)
077260        MOVE P923-LAST-STARTS       TO P08S-TSQ-LAST-STARTS(SUB2)
077270        MOVE P923-REST-CODE         TO P08S-TSQ-REST-CODE(SUB2)
077280        MOVE P923-EMP-NO            TO MSTRNBRK
077290                                       P08S-TSQ-EMP-SEL(SUB2)
077300        PERFORM P8500-READ-MASTER
077310*CNC0579B - BEG
077320        IF DEC-IVR-RPT-AT-HOME
077330           IF DEC-VCNY-RPT-LOCATION > SPACES
077340              MOVE DEC-VCNY-RPT-LOCATION
077350                               TO P08S-TSQ-DEC-RPT-LOCATION(SUB2)
077360           ELSE
077370              IF EMP-HOME-TERM-CODE > SPACES
077380                 MOVE EMP-HOME-TERM-CODE
077390                               TO P08S-TSQ-DEC-RPT-LOCATION(SUB2)
077400              END-IF
077410           END-IF
077420        END-IF
077430*CNC0579B - END
077440        MOVE SPACES     TO P08S-TSQ-STARTS-DISQUAL-FLAG(SUB2)
077450        MOVE DIST OF WS-MSTR     TO AJ-JOB-DIST
077460        MOVE SUB-DIST OF WS-MSTR TO AJ-JOB-SUB-DIST
077470        PERFORM P3060-CHECK-STARTS
077480        IF STARTS-DISQUALIFIED
077490           SET P08S-TSQ-STARTS-DISQUALIFIED(SUB2) TO TRUE
077500        END-IF
077510        MOVE SPACES           TO P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
077520        PERFORM P3080-CHECK-QUICK-TIES
077530        IF P980-QUICKTIE-RESTRICTED
077540           SET P08S-TSQ-QTIE-DISQUALIFIED(SUB2) TO TRUE
077550        END-IF
077560        MOVE P923-FASTSLOW-BOARD TO P08S-TSQ-FASTSLOW-BOARD(SUB2)
077570* ADDED FOR CNC0283 DECISION TABLE DH LEAD TIME 4/28/2000
077580        MOVE P923-CALL-DATE           TO P08S-TSQ-CALL-DATE(SUB2)
077590        MOVE P923-CALL-TIME           TO P08S-TSQ-CALL-TIME(SUB2)
077600                                         SCR08S-OD-TIME(SUB1)
077610
077620        PERFORM P4020-DISPLAY-EMP-INFO
077630        IF REJECT-EMPLOYEE-FUNCTION  OR  LAYOFF-ON-CALL
077640           SET P08SCA-RESUB(SUB1)     TO TRUE
077650        END-IF
077660     ELSE
077670        PERFORM P4050-CLEAR-EMP-DATA
077680        SET P08S-TSQ-CALL-NRML(SUB2)
077690                                    TO TRUE
077700        IF END-OF-DECISIONS
077710           MOVE SPACES              TO P08S-TSQ-DECKEY(SUB2)
077720                               P08S-TSQ-SENR-RESTRICT-FLAG(SUB2)
077730                                       P08S-TSQ-LAST-SEN(SUB2)
077740                                       P08S-TSQ-LAST-POS(SUB2)
077750                                       P08S-TSQ-LAST-STARTS(SUB2)
077760                                       P08S-TSQ-EMP-SEL(SUB2)
077770                                 P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
077780        ELSE
077790           MOVE P923-DEC-KEY        TO P08S-TSQ-DECKEY(SUB2)
077800           MOVE P923-LAST-SEN-DATE  TO P08S-TSQ-LAST-SEN(SUB2)
077810           MOVE P923-LAST-POSITION  TO P08S-TSQ-LAST-POS(SUB2)
077820           MOVE P923-LAST-STARTS    TO P08S-TSQ-LAST-STARTS(SUB2)
077830           MOVE SPACES              TO P08S-TSQ-EMP-SEL(SUB2)
077840                                 P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
077850        END-IF
077860        MOVE P923-NAME-AREA         TO SCR08S-NAME(SUB1)
077870                                       P08S-TSQ-EMP-NAME(SUB2)
077880        MOVE YELLOW                 TO P08S-TSQ-COLOR-OVRD(SUB2)
077890        PERFORM P4080-RESET-EMP-COLORS
077900     END-IF
077910     MOVE -1                        TO SCR08S-FUNC-CURSOR(SUB1).
077920*CNC0578-BEG
077930*
077940 P5220-WRITE-NO-FUNC-HISTORY.
077950*
077960     MOVE SPACES                    TO P943-COMMAREA-PARMS
077970*C1143 - BEG
077980     IF P08S-TSQ-EMP-SEL(SUB2)         > '000000000'
077990*C1143 - END
078000        MOVE P08S-TSQ-EMP-SEL(SUB2) TO MSTRNBRK
078010        PERFORM P8500-READ-MASTER
078020        IF TEMPORARY-ASGNMT          > SPACE
078030           MOVE TEMPORARY-ASGNMT    TO P943-NORM-ASGN
078040        ELSE
078050           IF NORMAL-ASGNMT          > SPACE
078060              MOVE NORMAL-ASGNMT    TO P943-NORM-ASGN
078070           END-IF
078080        END-IF
078090        SET P943-REJECT-CODE-NO     TO TRUE
078100        MOVE P943-NORM-ASGN         TO P943-FUN26-ASGN
078110        MOVE EMP-NBR IN WS-MSTR     TO P943-EMP-NBR
078120        MOVE JSK2-ASGN-DIST         TO P943-DIST
078130        MOVE JSK2-ASGN-SUB-DIST     TO P943-SDIST
078140        MOVE CRAFT IN WS-MSTR       TO P943-CRAFT
078150        MOVE LAYOFF-CODE            TO P943-LO
078160*
078170        IF JOB-DEF-YARD-ASGN
078180           MOVE JOB-DEF-YARD-POOL   TO P943-POOL-ASG
078190        ELSE
078200           IF JOB-DEF-LOCAL-ASGN
078210              MOVE JOB-DEF-LOCAL-POOL TO P943-POOL-ASG
078220           END-IF
078230        END-IF
078240*
078250        MOVE '0'                    TO P943-IN-OUT
078260        MOVE SCR08S-ASGN(SUB1)      TO P943-TEMP-ASGN-TRAIN
078270        MOVE JSK2-ASGN-CC           TO P943-TEMP-ASGN-CC
078280        MOVE SCR08S-FUNC(SUB1)      TO P943-FUN26-TYPE
078290        MOVE WS-LOCAL-DATE-TIME     TO P943-EFF-DATE-TIME
078300        IF P943-DIST             NOT = PSTCA-DIST
078310        OR P943-SDIST            NOT = PSTCA-SUB-DIST
078320           PERFORM P8994-CONVERT-HIST-TIMEZONE
078330        END-IF
078340        IF P08S-TSQ-DECKEY(SUB2) > SPACES
078350           MOVE P08S-TSQ-DECKEY(SUB2)(17:2)
078360                                       TO P943-FUN26-RULE-NO
078370        END-IF
078380        PERFORM P8900-WRITE-HISTORY
078390*C1143 - BEG
078400     END-IF
078410*C1143 - END
078420     .
078430*CNC0578-END
078440*
078450 P5230-CHECK-SVC-OVERRIDE.
078460*
078470*    USE THE SVC-OVERRIDE UNLESS THERE IS AN EFFECTIVE JOB
078480*    SCHEDULE RECORD FOR THE SPECIFIED CRAFT CODE.
078490*
078500     SET USE-SVC-OVERRIDE       TO TRUE
078510     MOVE TRCN-DEC-CC-OVERRIDE  TO WS-JS-CRAFT
078520     IF WS-JS-CRAFT = 'SW'
078530        MOVE 'S1'               TO WS-JS-CRAFT
078540     ELSE
078550        IF WS-JS-CRAFT = 'BK'
078560           MOVE 'B1'            TO WS-JS-CRAFT
078570        END-IF
078580     END-IF
078590     PERFORM P5240-FIND-SCHEDULE
078600     IF JSK2-ASSIGNMENT NOT > SPACES
078610        IF WS-JS-CRAFT = 'S1' OR
078620           WS-JS-CRAFT = 'B1'
078630           IF WS-JS-CRAFT = 'S1'
078640              MOVE 'S2'         TO WS-JS-CRAFT
078650           ELSE
078660              MOVE 'B2'         TO WS-JS-CRAFT
078670           END-IF
078680           PERFORM P5240-FIND-SCHEDULE
078690        END-IF
078700     END-IF
078710
078720     IF JSK2-ASSIGNMENT > SPACES
078730        MOVE JSK2-ASGN-DIST     TO AJ-JOB-DIST
078740        MOVE JSK2-ASGN-SUB-DIST TO AJ-JOB-SUB-DIST
078750        MOVE JSK2-ASGN          TO AJ-JOB-ASGN-ID
078760        MOVE JSK2-ASGN-CC       TO AJ-JOB-ASGN-CC
078770        MOVE AJJOBKEY-AREA      TO AJJOBKEY
078780        PERFORM P8640-READ-AJJOBKEY
078790        IF SUCCESS
078800*----------------------------------------------------------------*
078810*                CONVERT TO CNTL-TIME-ZONE                       *
078820*----------------------------------------------------------------*
078830           IF JSK2-ASGN-DIST       NOT = SCR08S-DIST
078840           OR JSK2-ASGN-SUB-DIST   NOT = SCR08S-SUB-DIST
078850              MOVE SPACES          TO WORK-CNTLKEY
078860              MOVE '02'            TO WK-CNTL-REC-TYPE
078870              MOVE JSK2-ASGN-DIST  TO WK-CNTL-DIST
078880              MOVE JSK2-ASGN-SUB-DIST TO WK-CNTL-SUB-DIST
078890              MOVE WORK-CNTLKEY    TO CNTLKEY
078900              PERFORM P8610-READ-CNTLFILE
078910              IF NOT SUCCESS
078920                 MOVE 'P5230-1'    TO ERR-PARAGRAPH
078930                 MOVE CNTLKEY      TO ERR-KEY
078940                 PERFORM P9999-GOT-PROBLEM
078950              END-IF
078960              MOVE SPACES          TO TZ-PARAMETERS
078970              MOVE PSTCA-TIME-ZONE TO TZ-IN-ZONE
078980              MOVE WS-LOCAL-DATE-TIME TO TZ-IN-DATE-TIME
078990              MOVE CNTL-TIME-ZONE  TO TZ-OUT-ZONE
079000              PERFORM P8996-TIMEZONE
079010              IF TZ-INVALID-PARAMETERS
079020                 MOVE '131000'     TO ERR-PARAGRAPH
079030                 PERFORM P8996-TZERROR
079040              END-IF
079050           ELSE
079060              MOVE WS-LOCAL-DATE    TO TZ-OUT-DATE
079070           END-IF
079080           SET DE-YYMMDD-FORMAT     TO TRUE
079090           MOVE AJ-EFF-DATE         TO DE-YYMMDD
079100           PERFORM P8998-DATEEDIT
079110           MOVE DE-CCYYMMDD         TO DE-COMPARE1-DATE
079120
079130           SET DE-YYMMDD-FORMAT     TO TRUE
079140           MOVE TZ-OUT-DATE         TO DE-YYMMDD
079150           PERFORM P8998-DATEEDIT
079160           MOVE DE-CCYYMMDD         TO DE-COMPARE2-DATE
079170
079180           IF DE-COMPARE1-DATE NOT > DE-COMPARE2-DATE
079190*          IF AJ-EFF-DATE      NOT > TZ-OUT-DATE
079200              SET DONT-USE-SVC-OVERRIDE TO TRUE
079210           END-IF
079220        END-IF
079230     END-IF.
079240*
079250 P5240-FIND-SCHEDULE.
079260*
079270*    ESTABLISH THE SCHEDULE "WINDOW"
079280*
079290     MOVE WK-CALL-DATE-TIME         TO WS-SCH-DATE-TIME
079300     MOVE WS-SCH-DATE               TO WS-SCH-EXP-DATE
079310
079320     MOVE ZEROS                     TO DATE-CONVERSION-PARMS
079330     SET PARM-SUBTRACT              TO TRUE
079340     MOVE WS-SCH-DATE               TO PARM-PRI-DATE-GREG
079350     MOVE WS-SCH-TIME               TO PARM-PRI-HRMN
079360     MOVE 0600                      TO PARM-SEC-HRMN
079370     EXEC CICS LINK
079380               PROGRAM(P903-PGM)
079390               COMMAREA(DATE-CONVERSION-PARMS)
079400               LENGTH(P903-LGTH)
079410               RESP(WS-RESPONSE)
079420     END-EXEC
079430     MOVE WS-RESPONSE               TO FILE-STATUS
079440     IF NOT SUCCESS
079450        MOVE 'P5240-1'              TO ERR-PARAGRAPH
079460        MOVE 'P903LINK'             TO ERR-KEY
079470        PERFORM P9999-GOT-PROBLEM
079480     END-IF
079490     MOVE PARM-RES-DAY-OF-WEEK      TO WS-SCH-DAY-FROM
079500     MOVE PARM-RES-HRMN             TO WS-SCH-TIME-FROM
079510     MOVE PARM-RES-DATE-GREG        TO WS-SCH-DATE-FROM
079520
079530     MOVE ZEROS                     TO DATE-CONVERSION-PARMS
079540     SET PARM-ADD                   TO TRUE
079550     MOVE WS-SCH-DATE               TO PARM-PRI-DATE-GREG
079560     MOVE WS-SCH-TIME               TO PARM-PRI-HRMN
079570     MOVE 0600                      TO PARM-SEC-HRMN
079580     EXEC CICS LINK
079590               PROGRAM(P903-PGM)
079600               COMMAREA(DATE-CONVERSION-PARMS)
079610               LENGTH(P903-LGTH)
079620               RESP(WS-RESPONSE)
079630     END-EXEC
079640     MOVE WS-RESPONSE               TO FILE-STATUS
079650     IF NOT SUCCESS
079660        MOVE 'P5240-2'              TO ERR-PARAGRAPH
079670        MOVE 'P903LINK'             TO ERR-KEY
079680        PERFORM P9999-GOT-PROBLEM
079690     END-IF
079700     MOVE PARM-RES-DAY-OF-WEEK      TO WS-SCH-DAY-TO
079710     MOVE PARM-RES-HRMN             TO WS-SCH-TIME-TO
079720     MOVE PARM-RES-DATE-GREG        TO WS-SCH-DATE-TO
079730
079740     IF WS-SCH-TIME-FROM = 0000
079750        MOVE 2400                   TO WS-SCH-TIME-FROM
079760        IF WS-SCH-DAY-FROM > 1
079770           SUBTRACT 1             FROM WS-SCH-DAY-FROM
079780        ELSE
079790           MOVE 7                   TO WS-SCH-DAY-FROM
079800        END-IF
079810     ELSE
079820        IF WS-SCH-DAY-FROM  = WS-SCH-DAY-TO  AND
079830           WS-SCH-TIME-FROM > WS-SCH-TIME-TO
079840           IF WS-SCH-DAY-FROM > 1
079850              SUBTRACT 1          FROM WS-SCH-DAY-FROM
079860           ELSE
079870              MOVE 7                TO WS-SCH-DAY-FROM
079880           END-IF
079890        END-IF
079900     END-IF
079910*
079920*    FIRST LOCK IN ON THE CORRECT SCHEDULE "SET" BY DETERMINING
079930*    THE EXPIRATION DATE
079940*
079950     MOVE TRCN-KEY3                 TO JS-KEY1
079960     MOVE WS-JS-CRAFT               TO JSK1-ASGN-CC
079970     MOVE WS-SCH-EXP-DATE           TO JSK1-EXP-DATE
079980     SET JSK1-ASGN-STATS            TO TRUE
079990     MOVE JS-KEY1                   TO JSKEY1
080000     EXEC CICS READ
080010               DATASET(JS-VIA-JSKEY1)
080020               INTO(WS-JOB-SCHEDULE)
080030               LENGTH(JSKEY1-RLGTH)
080040               RIDFLD(JSKEY1)
080050               KEYLENGTH(JSKEY1-KLGTH)
080060               GTEQ
080070               RESP(WS-RESPONSE)
080080     END-EXEC
080090     MOVE WS-RESPONSE               TO FILE-STATUS
080100     IF SUCCESS
080110        AND JSK1-ASGN-DIST = TRCN-DIST3
080120        AND JSK1-ASGN-SUB-DIST = TRCN-SDIST3
080130        AND JSK1-ASGN = TRCN-ASSIGNMENT
080140        AND JSK1-ASGN-CC = WS-JS-CRAFT
080150        AND JSK1-ASGN-STATS
080160        CONTINUE
080170     ELSE
080180        IF SUCCESS
080190           SET NO-RECORD-FND        TO TRUE
080200        END-IF
080210     END-IF
080220     IF SUCCESS
080230        MOVE JS-KEY1                TO WORK-JS-KEY1
080240        MOVE WS-SCH-DAY-FROM        TO WS-SCH-DAY
080250        MOVE WS-SCH-TIME-FROM       TO WS-SCH-START-TIME
080260        IF WS-SCH-DAY-TO NOT = WS-SCH-DAY-FROM
080270           MOVE 2400                TO WS-SCH-END-TIME
080280           PERFORM P5250-SEARCH-SCHEDULE
080290           IF NOT SUCCESS
080300              MOVE WS-SCH-DAY-TO    TO WS-SCH-DAY
080310              MOVE ZEROS            TO WS-SCH-START-TIME
080320              MOVE WS-SCH-TIME-TO   TO WS-SCH-END-TIME
080330              PERFORM P5250-SEARCH-SCHEDULE
080340           END-IF
080350        ELSE
080360           MOVE WS-SCH-TIME-TO      TO WS-SCH-END-TIME
080370           PERFORM P5250-SEARCH-SCHEDULE
080380        END-IF
080390     ELSE
080400        IF NOT (NO-RECORD-FND OR END-OF-FILE)
080410           MOVE 'P5240-3'            TO ERR-PARAGRAPH
080420           MOVE JSKEY2               TO ERR-KEY
080430           PERFORM P9999-GOT-PROBLEM
080440        END-IF
080450        MOVE SPACES                  TO WS-JOB-SCHEDULE
080460     END-IF.
080470*
080480 P5250-SEARCH-SCHEDULE.
080490*
080500*    THEN USING THAT KEY AS A BASE, LOOK UP THE CORRECT
080510*    SCHEDULE.
080520*
080530     MOVE WS-SCH-DAY                TO WK-JSK1-ASGN-DAY-NUM
080540     MOVE WS-SCH-START-TIME         TO WK-JSK1-ASGN-START-TIME
080550     MOVE WORK-JS-KEY1              TO JSKEY1
080560     EXEC CICS READ
080570               DATASET(JS-VIA-JSKEY1)
080580               INTO(WS-JOB-SCHEDULE)
080590               LENGTH(JSKEY1-RLGTH)
080600               RIDFLD(JSKEY1)
080610               KEYLENGTH(JSKEY1-KLGTH)
080620               GTEQ
080630               RESP(WS-RESPONSE)
080640     END-EXEC
080650     MOVE WS-RESPONSE              TO FILE-STATUS
080660     IF SUCCESS
080670        AND JSK1-ASGN-DIST = WK-JSK1-ASGN-DIST
080680        AND JSK1-ASGN-SUB-DIST = WK-JSK1-ASGN-SUB-DIST
080690        AND JSK1-ASSIGNMENT = WK-JSK1-ASSIGNMENT
080700        AND JSK1-EXP-DATE = WK-JSK1-EXP-DATE
080710        AND JSK1-ASGN-DAY-NUM = WK-JSK1-ASGN-DAY-NUM
080720        AND JSK1-ASGN-START-TIME NOT > WS-SCH-END-TIME
080730        CONTINUE
080740     ELSE
080750        IF SUCCESS
080760           SET NO-RECORD-FND       TO TRUE
080770        END-IF
080780     END-IF
080790     IF NOT SUCCESS
080800        IF NOT (NO-RECORD-FND OR END-OF-FILE)
080810           MOVE 'P5250-1'           TO ERR-PARAGRAPH
080820           MOVE JSKEY2              TO ERR-KEY
080830           PERFORM P9999-GOT-PROBLEM
080840        END-IF
080850     END-IF.
080860*
080870 P5300-PROCESS-LAYOFF.
080880*
080890     IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
080900        MOVE -1                     TO SCR08S-FUNC-CURSOR(SUB1)
080910*            'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
080920        MOVE 'I050'                 TO MSGLOG-CODE
080930        PERFORM P9000-SEND-MAP-AND-RETURN
080940     END-IF
080950     MOVE SPACES                    TO P917-COMMAREA-PARMS
080960     MOVE P08S-TSQ-EMP-SEL(SUB2)    TO P917-EMP-NO
080970     IF NOT MISSING-CALL
080980        MOVE SCR08S-ST(SUB1)        TO P917-STATUS-CODE1
080990        IF P917-STATUS-CODE1 NOT > SPACES
081000           MOVE -1                  TO SCR08S-ST-CURSOR(SUB1)
081010           MOVE REV-VIDEO           TO SCR08S-ST-HI(SUB1)
081020*               'STATUS CODE IS REQUIRED'
081030           MOVE 'S004'              TO MSGLOG-CODE
081040           PERFORM P9000-SEND-MAP-AND-RETURN
081050        END-IF
081060        MOVE P917-STATUS-CODE       TO SCR08S-ST(SUB1)
081070        MOVE SCR08S-ECC(SUB1)       TO P917-EDB-CODE
081080     END-IF
081090     SET P917-ON-CALL               TO TRUE
081100     MOVE PSTCA-TIME-ZONE           TO P917-TIME-ZONE
081110     MOVE SCR08S-ASGN(SUB1)         TO P917-TRAIN
081120     MOVE SCR08S-CC(SUB1)           TO P917-TRAIN-CC
081130*
081140*    CALL DATE/TIME IS THE ORDERED TIME OF THE TRAIN
081150*
081160     MOVE WK-CALL-DATE-TIME         TO P917-CALL-DATE-TIME
081170     MOVE P08S-TSQ-LEAD-TIME(SUB2)  TO P917-CALL-LEAD-TIME
081180*
081190*    COMPUTE THE ACTION TIME = ORDER - LEAD
081200*
081210     MOVE ZEROS                     TO DATE-CONVERSION-PARMS
081220     SET PARM-SUBTRACT              TO TRUE
081230     MOVE WK-CALL-DATE              TO PARM-PRI-DATE-GREG
081240     MOVE WK-CALL-TIME              TO PARM-PRI-HRMN
081250     MOVE P08S-TSQ-LEAD-TIME(SUB2)  TO PARM-SEC-HRMN
081260     IF PARM-SEC-HRMN > ZERO
081270        EXEC CICS LINK
081280                  PROGRAM(P903-PGM)
081290                  COMMAREA(DATE-CONVERSION-PARMS)
081300                  LENGTH(P903-LGTH)
081310                  RESP(WS-RESPONSE)
081320        END-EXEC
081330        MOVE WS-RESPONSE            TO FILE-STATUS
081340        IF NOT SUCCESS
081350           MOVE 'P5300-1'           TO ERR-PARAGRAPH
081360           PERFORM P9999-GOT-PROBLEM
081370        END-IF
081380        MOVE PARM-RES-DATE-GREG     TO P917-ACTION-DATE
081390        MOVE PARM-RES-HRMN          TO P917-ACTION-TIME
081400     ELSE
081410        MOVE WK-CALL-DATE-TIME      TO P917-ACTION-DATE-TIME
081420     END-IF
081430
081440     IF MISSING-CALL
081450        MOVE SCR08S-FUNC(SUB1)      TO P917-MISSED-CALL-FUNC
081460        SET  P08SCA-RESUB(SUB1)     TO TRUE
081470     END-IF
081480*-------------------------------------------------------------
081490*    IF THE DECISION TABLE RETURNS A TURN FROM A FASTSLOW
081500*    SPAREBOARD, THEN USE THE SIDE OF THE BOARD TO DETERMINE
081510*    ROAD VS. YARD.  OTHERWISE, USE THE JOB DEFINITION.
081520*----------------------------------------------------------PLS
081530     IF P08S-TSQ-FASTSLOW-BOARD(SUB2) = 'S'
081540        SET P917-ROAD               TO TRUE
081550     ELSE
081560        IF P08S-TSQ-FASTSLOW-BOARD(SUB2) = 'F'
081570           SET P917-YARD               TO TRUE
081580        ELSE
081590           IF JOB-DEF-YARD-ASGN
081600              SET P917-YARD               TO TRUE
081610           ELSE
081620              SET P917-ROAD               TO TRUE
081630           END-IF
081640        END-IF
081650     END-IF
081660     IF P08S-TSQ-DECKEY(SUB2) > SPACES
081670        MOVE P08S-TSQ-DECKEY(SUB2)  TO P917-DECKEY
081680     END-IF
081690     MOVE 'A'                       TO P917-OC-ASGN-TYPE
081700     MOVE JSK1-ASGN-DIST            TO AJ-JOB-DIST
081710     MOVE JSK1-ASGN-SUB-DIST        TO AJ-JOB-SUB-DIST
081720     MOVE JSK1-ASGN                 TO AJ-JOB-ASGN-ID
081730     MOVE JSK1-ASGN-CC              TO AJ-JOB-ASGN-CC
081740     MOVE AJJOBKEY-AREA             TO P917-OC-ASGN
081750     MOVE PSTCA-SUB                 TO P917-SUB-CODE
081760
081770     EXEC CICS LINK
081780               PROGRAM(P917-PGM)
081790               COMMAREA(P917-COMMAREA-PARMS)
081800               LENGTH(P917-LGTH)
081810               RESP(WS-RESPONSE)
081820     END-EXEC
081830     MOVE WS-RESPONSE               TO FILE-STATUS
081840     IF NOT SUCCESS
081850        MOVE 'P5300-2'              TO ERR-PARAGRAPH
081860        PERFORM P9999-GOT-PROBLEM
081870     END-IF
081880     IF P917-ERROR-FOUND
081890        IF P917-STATUS-CODE-ERROR
081900           MOVE -1                  TO SCR08S-ST-CURSOR(SUB1)
081910           MOVE REV-VIDEO           TO SCR08S-ST-HI(SUB1)
081920        ELSE
081930           IF P917-EDB-CODE-ERROR
081940              MOVE -1               TO SCR08S-ECC-CURSOR(SUB1)
081950              MOVE REV-VIDEO        TO SCR08S-ECC-HI(SUB1)
081960           ELSE
081970              MOVE -1               TO SCR08S-FUNC-CURSOR(SUB1)
081980           END-IF
081990        END-IF
082000        MOVE P917-ERRORMSG          TO SCR08S-ERRORMSG
082010        PERFORM P9000-SEND-MAP-AND-RETURN
082020     ELSE
082030        PERFORM P5200-PROCESS-FILL-FUNCTION
082040     END-IF
082050     MOVE -1                        TO SCR08S-FUNC-CURSOR(SUB1).
082060******************************************************************
082070 P5400-CREATE-CALLBACK-TASK.
082080******************************************************************
082090     IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
082100        MOVE -1                      TO SCR08S-FUNC-CURSOR(SUB1)
082110*            'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
082120        MOVE 'I050'                  TO MSGLOG-CODE
082130        PERFORM P9000-SEND-MAP-AND-RETURN
082140     END-IF
082150*
082160*    MAKE SURE NO CALLBACK ALREADY EXISTS FOR THE EMPLOYEE
082170*
082180     MOVE SPACES                     TO WS-TASK
082190     MOVE P08S-TSQ-EMP-SEL(SUB2)     TO EMP-NBR OF WS-TASK
082200     SET TASK-CALL-BACK              TO TRUE
082210     MOVE TASK-EMPLOYEE-KEY          TO TASKEMPK
082220     EXEC CICS READ
082230               DATASET(TASK-VIA-EMP-NBR)
082240               INTO(WS-TASK)
082250               LENGTH(TASKENBR-RLGTH)
082260               RIDFLD(TASKEMPK)
082270               KEYLENGTH(TASKENBR-KLGTH)
082280               RESP(WS-RESPONSE)
082290     END-EXEC
082300     MOVE WS-RESPONSE                TO FILE-STATUS
082310     IF NOT SUCCESS
082320        IF NOT NO-RECORD-FND
082330           MOVE 'P5400-1'            TO ERR-PARAGRAPH
082340           MOVE TASKEMPK             TO ERR-KEY
082350           PERFORM P9999-GOT-PROBLEM
082360        END-IF
082370     ELSE
082380*            'A CALLBACK TASK ALREADY EXISTS FOR THIS EMPLOYEE'
082390        MOVE 'A149'                  TO MSGLOG-CODE
082400        MOVE -1                      TO SCR08S-FUNC-CURSOR(SUB1)
082410        MOVE REV-VIDEO               TO SCR08S-FUNC-HI(SUB1)
082420        PERFORM P9000-SEND-MAP-AND-RETURN
082430     END-IF
082440*
082450*    GET THE EMPLOYEES REQUESTED LEAD TIME
082460*
082470     MOVE SPACES                     TO WS-MSTR
082480     MOVE P08S-TSQ-EMP-SEL(SUB2)     TO MSTRNBRK
082490     PERFORM P8505-READ-MASTER-ONLY
082500     MOVE SPACES                     TO PS52-COMMAREA-PARMS
082510                                        WS-SHORT-CALL-REQ
082520     MOVE P08S-TSQ-EMP-SEL(SUB2)     TO PS52-EMP-NBR
082530     SET PS52-CMA-MODE               TO TRUE
082540     MOVE WS-LOCAL-DATE-TIME         TO PS52-INPUT-DATE-TIME
082550     PERFORM P8520-LINK-TO-PS52
082560     IF SUCCESS
082570        IF (PS52-REQ-LEAD-TIME  > SPACES
082580           AND PS52-REQ-LEAD-TIME > '0000')
082590           MOVE PS52-REQ-LEAD-TIME      TO WS-SHORT-CALL-REQ
082600        END-IF
082610        IF (PS52-REQ-CALL-TIME  > SPACES
082620           AND PS52-REQ-CALL-TIME > '0000')
082630           IF WS-SHORT-CALL-REQ = SPACES
082640              MOVE PS52-REQ-CALL-TIME   TO WS-SHORT-CALL-REQ
082650           ELSE
082660              IF PS52-REQ-CALL-TIME < WS-SHORT-CALL-REQ
082670                 MOVE PS52-REQ-CALL-TIME TO WS-SHORT-CALL-REQ
082680              END-IF
082690           END-IF
082700        END-IF
082710     END-IF
082720     IF NOT SUCCESS
082730     OR WS-SHORT-CALL-REQ  NOT > '0000'
082740*            'NO SHORT CALL TIME FOUND'
082750        MOVE 'N227'                  TO MSGLOG-CODE
082760        MOVE -1                      TO SCR08S-FUNC-CURSOR(SUB1)
082770        MOVE REV-VIDEO               TO SCR08S-FUNC-HI(SUB1)
082780        PERFORM P9000-SEND-MAP-AND-RETURN
082790     END-IF
082800
082810     MOVE SPACES                        TO P927-COMMAREA-PARMS
082820     SET P927-CALLBACK-FUNCTION         TO TRUE
082830     MOVE ZEROS                         TO DATE-CONVERSION-PARMS
082840     SET PARM-SUBTRACT                  TO TRUE
082850     MOVE P08S-TSQ-JOB-DATE(SUB2)       TO PARM-PRI-DATE-GREG
082860     MOVE P08S-TSQ-JOB-TIME(SUB2)       TO PARM-PRI-HRMN
082870     MOVE WS-SHORT-CALL-REQ             TO PARM-SEC-HRMN
082880     EXEC CICS LINK
082890               PROGRAM(P903-PGM)
082900               COMMAREA(DATE-CONVERSION-PARMS)
082910               LENGTH(P903-LGTH)
082920               RESP(WS-RESPONSE)
082930     END-EXEC
082940     MOVE WS-RESPONSE                   TO FILE-STATUS
082950     IF NOT SUCCESS
082960        MOVE 'P5400-2'                  TO ERR-PARAGRAPH
082970        MOVE DATE-CONVERSION-PARMS      TO ERR-KEY
082980        PERFORM P9999-GOT-PROBLEM
082990     END-IF
083000     MOVE PARM-RES-DATE-GREG            TO P927-EFF-DATE
083010     MOVE PARM-RES-HRMN                 TO P927-EFF-TIME
083020     MOVE PSTCA-TIME-ZONE               TO P927-TIME-ZONE
083030     MOVE TRCN-DIST                     TO P927-DIST
083040     MOVE TRCN-SDIST                    TO P927-SUB-DIST
083050     MOVE P08S-TSQ-EMP-SEL(SUB2)        TO P927-CB-EMP-NBR
083060     MOVE TRCN-ORIGIN-STN               TO P927-CB-ORG-STATION
083070     MOVE SCR08S-ASGN(SUB1)             TO P927-CB-TRAIN
083080     MOVE P08S-TSQ-CALL-DATE-TIME(SUB2) TO P927-CB-ORDER-DATE-TIME
083090     EXEC CICS LINK
083100               PROGRAM(P927-PGM)
083110               COMMAREA(P927-COMMAREA-PARMS)
083120               LENGTH(P927-LGTH)
083130               RESP(WS-RESPONSE)
083140     END-EXEC
083150     MOVE WS-RESPONSE                   TO FILE-STATUS
083160     IF NOT SUCCESS
083170        MOVE 'P5400-3'                  TO ERR-PARAGRAPH
083180        PERFORM P9999-GOT-PROBLEM
083190     END-IF
083200
083210*         'CALLBACK LOGGED - PLEASE PUT EMPLOYEE ON DUTY'
083220     MOVE 'C228'                     TO MSGLOG-CODE
083230     MOVE -1                         TO SCR08S-FUNC-CURSOR(SUB1)
083240     MOVE REV-VIDEO                  TO SCR08S-FUNC-HI(SUB1)
083250*
083260*    IF THE EMPLOYEE IS NOT RESTED, DISPLAY 'HOUR' IN THE FUNC FLD
083270*
083280     IF P08S-TSQ-REST-YELLOW(SUB2)
083290        MOVE YELLOW                     TO SCR08S-NAME-COL(SUB1)
083300        MOVE 'HOUR'                     TO SCR08S-FUNC(SUB1)
083310     END-IF
083320     MOVE SPACES                     TO P08S-TSQ-REST-STATUS(SUB2)
083330*
083340*    WRITE WORK HISTORY FOR CALLBACK
083350*
083360     MOVE SPACES                        TO P943-COMMAREA-PARMS
083370     SET P943-EMPLOYEE-FUNCTION         TO TRUE
083380     SET P943-CALLBACK-FUN              TO TRUE
083390     MOVE P08S-TSQ-EMP-SEL(SUB2)        TO P943-EMP-NBR
083400     MOVE DIST      OF WS-MSTR          TO P943-DIST
083410     MOVE SUB-DIST  OF WS-MSTR          TO P943-SDIST
083420     MOVE CRAFT     OF WS-MSTR          TO P943-CRAFT
083430     MOVE SCR08S-ASGN(SUB1)             TO P943-NORM-ASGN
083440     MOVE SCR08S-CC(SUB1)               TO P943-NORM-ASGN-CC
083450     MOVE P927-EFF-DATE-TIME            TO P943-EFF-DATE-TIME
083460     MOVE PSTCA-TIME-ZONE               TO P943-EMP-TIME-ZONE
083470
083480     EXEC CICS ASSIGN
083490               USERID(P943-USERID)
083500     END-EXEC
083510
083520     EXEC CICS LINK
083530               PROGRAM(P943-PGM)
083540               COMMAREA(P943-COMMAREA-PARMS)
083550               LENGTH(P943-LGTH)
083560               RESP(WS-RESPONSE)
083570     END-EXEC
083580     MOVE WS-RESPONSE                   TO FILE-STATUS
083590     IF NOT SUCCESS
083600        MOVE 'P5400-4'                  TO ERR-PARAGRAPH
083610        MOVE 'P943'                     TO ERR-KEY
083620        PERFORM P9999-GOT-PROBLEM
083630     END-IF
083640     .
083650******************************************************************
083660 P5500-PROCESS-OVERRIDE.
083670******************************************************************
083680*    CHECK IF USER HAS HIGH-LEVEL AUTHORITY TO REQUEST OVERRIDE
083690*-----------------------------------------------------------------
083700     MOVE SPACES                        TO WS-AUTHORITY
083710     MOVE SCR08S-DIST                   TO USERID-DIST-1
083720     MOVE SCR08S-SUB-DIST               TO USERID-SDIST-1
083730     EXEC CICS LINK
083740               PROGRAM(P902-PGM)
083750               COMMAREA(WS-AUTHORITY)
083760               LENGTH(P902-LGTH)
083770               RESP(WS-RESPONSE)
083780     END-EXEC
083790     MOVE WS-RESPONSE                   TO FILE-STATUS
083800     IF NOT SUCCESS
083810        MOVE 'P5500-S'                  TO ERR-PARAGRAPH
083820        MOVE 'SECURITY'                 TO ERR-KEY
083830        PERFORM P9999-GOT-PROBLEM
083840     END-IF
083850     IF NOT HIGH-LEVEL-AUTHORIZED
083860        MOVE -1                    TO SCR08S-FUNC-CURSOR(SUB1)
083870        MOVE REV-VIDEO             TO SCR08S-FUNC-HI    (SUB1)
083880        MOVE 'U003'                TO MSGLOG-CODE
083890        PERFORM P9000-SEND-MAP-AND-RETURN
083900     END-IF
083910
083920     IF SCR08S-NAME(SUB1) NOT > SPACES
083930        MOVE -1                     TO SCR08S-NAME-CURSOR(SUB1)
083940*            'ENTER FULL OR PARTIAL NAME TO BEGIN '
083950        MOVE 'E033'                 TO MSGLOG-CODE
083960        PERFORM P9000-SEND-MAP-AND-RETURN
083970     END-IF
083980     MOVE SPACES                    TO EMP-NAME-NBR-KEY
083990                                       P08S-TSQ-COLOR-OVRD(SUB2)
084000     SET P08S-TSQ-CALL-MINUS-LD(SUB2)
084010                                    TO TRUE
084020     MOVE SCR08S-NAME(SUB1)         TO EMP-NAME IN WS-MSTR
084030     IF SCR08S-NAME(SUB1) NOT = P08S-TSQ-EMP-NAME(SUB2)
084040        MOVE SPACES                 TO P08S-TSQ-EMP-SEL(SUB2)
084050                                 P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
084060     END-IF
084070     IF P08S-TSQ-DECKEY(SUB2) > SPACES
084080        MOVE SPACES                 TO P08S-TSQ-EMP-SEL(SUB2)
084090                                       P08S-TSQ-EMP-NAME(SUB2)
084100                                       P08S-TSQ-DECKEY(SUB2)
084110                              P08S-TSQ-SENR-RESTRICT-FLAG(SUB2)
084120                                       P08S-TSQ-LAST-SEN(SUB2)
084130                                       P08S-TSQ-LAST-POS(SUB2)
084140                                       P08S-TSQ-LAST-STARTS(SUB2)
084150                                       P08S-TSQ-EMP-NAME(SUB2)
084160                                 P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
084170     END-IF
084180     IF P08S-TSQ-EMP-SEL(SUB2) NUMERIC
084190        MOVE P08S-TSQ-EMP-SEL(SUB2) TO EMP-NBR IN WS-MSTR
084200     END-IF
084210     MOVE EMP-NAME-NBR-KEY          TO MSTREMPK
084220     EXEC CICS STARTBR
084230               DATASET(MSTR-VIA-EMP-NAME)
084240               RIDFLD(MSTREMPK)
084250               GTEQ
084260               RESP(WS-RESPONSE)
084270     END-EXEC
084280     IF SUCCESS
084290        MOVE '0'                    TO DONE-CODE
084300        PERFORM UNTIL DONE
084310           EXEC CICS READNEXT
084320                     DATASET(MSTR-VIA-EMP-NAME)
084330                     INTO(WS-MSTR)
084340                     LENGTH(MSTRENAM-RLGTH)
084350                     RIDFLD(MSTREMPK)
084360                     KEYLENGTH(MSTRENAM-KLGTH)
084370                     RESP(WS-RESPONSE)
084380           END-EXEC
084390           MOVE WS-RESPONSE         TO FILE-STATUS
084400           IF SUCCESS
084410              IF EMP-NBR IN WS-MSTR > P08S-TSQ-EMP-SEL(SUB2)
084420                 SET DONE           TO TRUE
084430                 EXEC CICS ENDBR
084440                           DATASET(MSTR-VIA-EMP-NAME)
084450                           RESP(WS-RESPONSE)
084460                 END-EXEC
084470                 MOVE EMP-NBR IN WS-MSTR
084480                                    TO MSTRNBRK
084490                                       P08S-TSQ-EMP-SEL(SUB2)
084500                 SET P08S-USER-OVERRIDE(SUB2)
084510                                    TO TRUE
084520                 PERFORM P4020-DISPLAY-EMP-INFO
084530                 MOVE SPACES        TO P943-COMMAREA-PARMS
084540                 MOVE EMP-NBR IN WS-MSTR
084550                                    TO P943-EMP-NBR
084560                 MOVE CRAFT IN WS-MSTR
084570                                    TO P943-CRAFT
084580                 IF TEMPORARY-ASGNMT > SPACE
084590                    MOVE TEMPORARY-ASGNMT
084600                                    TO P943-NORM-ASGN
084610                 ELSE
084620                    IF NORMAL-ASGNMT > SPACE
084630                       MOVE NORMAL-ASGNMT
084640                                    TO P943-NORM-ASGN
084650                    END-IF
084660                 END-IF
084670                 MOVE LAYOFF-CODE   TO P943-LO
084680                 SET P943-OVERRIDE-FUN   TO TRUE
084690                 MOVE JSK2-ASGN-DIST
084700                                    TO P943-DIST
084710                 MOVE JSK2-ASGN-SUB-DIST
084720                                    TO P943-SDIST
084730                 MOVE '0'           TO P943-IN-OUT
084740                 MOVE SCR08S-ASGN(SUB1)
084750                                    TO P943-TEMP-ASGN-TRAIN
084760                 MOVE JSK2-ASGN-CC  TO P943-TEMP-ASGN-CC
084770                 MOVE WS-LOCAL-DATE-TIME
084780                                    TO P943-EFF-DATE-TIME
084790                 IF P08S-TSQ-OWNER(SUB2) > '000000000'
084800                    MOVE P08S-TSQ-OWNER(SUB2)
084810                                    TO P943-EMP-NBR-AFFECTED
084820                 ELSE
084830                    MOVE '999999998'
084840                                    TO P943-EMP-NBR-AFFECTED
084850                 END-IF
084860                 IF P943-DIST       NOT = PSTCA-DIST
084870                 OR P943-SDIST      NOT = PSTCA-SUB-DIST
084880                    PERFORM P8994-CONVERT-HIST-TIMEZONE
084890                 END-IF
084900                 PERFORM P8900-WRITE-HISTORY
084910              END-IF
084920           ELSE
084930              SET DONE              TO TRUE
084940              IF NOT (NO-RECORD-FND OR END-OF-FILE)
084950                 MOVE 'P5500-1'     TO ERR-PARAGRAPH
084960                 MOVE MSTREMPK      TO ERR-KEY
084970                 PERFORM P9999-GOT-PROBLEM
084980              END-IF
084990              EXEC CICS ENDBR
085000                        DATASET(MSTR-VIA-EMP-NAME)
085010                        RESP(WS-RESPONSE)
085020              END-EXEC
085030              IF PSTCA-SUB = 1
085040                 MOVE '** END OF FILE **'
085050                                    TO SCR08S-NAME(SUB1)
085060              ELSE
085070                 MOVE '** FIN DU DOSSIER **'
085080                                    TO SCR08S-NAME(SUB1)
085090              END-IF
085100           END-IF
085110        END-PERFORM
085120     ELSE
085130        IF NOT (NO-RECORD-FND OR END-OF-FILE)
085140           MOVE 'P5500-2'           TO ERR-PARAGRAPH
085150           MOVE MSTREMPK            TO ERR-KEY
085160           PERFORM P9999-GOT-PROBLEM
085170        END-IF
085180     END-IF
085190     MOVE SPACES                    TO SCR08S-FUNC(SUB1)
085200     MOVE -1                        TO SCR08S-FUNC-CURSOR(SUB1).
085210*
085220 P5600-GET-SENIOR-MAN.
085230*
085240     IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
085250        MOVE -1                     TO SCR08S-FUNC-CURSOR(SUB1)
085260        MOVE REV-VIDEO              TO SCR08S-FUNC-HI(SUB1)
085270*            'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
085280        MOVE 'I050'                 TO MSGLOG-CODE
085290        PERFORM P9000-SEND-MAP-AND-RETURN
085300     END-IF
085310*
085320*    GET THE SENIORITY OF THE EMPLOYEE CURRENTLY ON THE JOB
085330*
085340     MOVE P08S-TSQ-EMP-SEL(SUB2)    TO WS-SENIOR-EMP-NO
085350     MOVE SPACES                    TO P942-COMMAREA-PARMS
085360     MOVE JSK2-ASGN-DIST            TO P942-ASGN-DIST
085370     MOVE JSK2-ASGN-SUB-DIST        TO P942-ASGN-SUB-DIST
085380     MOVE JSK2-ASGN                 TO P942-ASGN-ASGN
085390     MOVE JSK2-ASGN-CC              TO P942-ASGN-CC
085400     SET P942-EMP-SEN-FUNCTION      TO TRUE
085410     SET P942-ASGN-AJ               TO TRUE
085420     MOVE P08S-TSQ-EMP-SEL(SUB2)    TO P942-EMP-NO
085430     PERFORM P8070-LINK-942-SEN-ANAL
085440     IF P942-EMP-SEN-LEVEL > SPACES
085450        MOVE P942-EMP-SEN-LEVEL        TO WS-SENIOR-LVL
085460        MOVE P942-EMP-SENIORITY        TO WS-SENIOR-SEN-DATE
085470     ELSE
085480        MOVE HIGH-VALUES               TO WS-SENIOR-LVL
085490                                          WS-SENIOR-SEN-DATE
085500     END-IF
085510
085520     MOVE SPACES                    TO SCR08S-FUNC(SUB1)
085530     MOVE SUB1                      TO HOLD-SUB1
085540                                       WS-SENIOR-SUB1
085550     MOVE SUB2                      TO HOLD-SUB2
085560                                       WS-SENIOR-SUB2
085570     MOVE 1                         TO SUB1
085580                                       SUB2
085590     PERFORM UNTIL SUB1 > ARRAY-MAX
085600        MOVE SCR08S-FUNC(SUB1)      TO WS-FUNCTION-CODE
085610        IF SENIOR-MAN-FUNCTION
085620           IF P08S-TSQ-JSKEY2(SUB2) NOT > SPACES
085630              MOVE -1               TO SCR08S-FUNC-CURSOR(SUB1)
085640              MOVE REV-VIDEO        TO SCR08S-FUNC-HI(SUB1)
085650*                  'IMPROPER FUNCTION - NO ASSIGNMENT PRESENT'
085660              MOVE 'I049'           TO MSGLOG-CODE
085670              PERFORM P9000-SEND-MAP-AND-RETURN
085680           END-IF
085690           IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
085700              MOVE -1               TO SCR08S-FUNC-CURSOR(SUB1)
085710              MOVE REV-VIDEO        TO SCR08S-FUNC-HI(SUB1)
085720*                  'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
085730              MOVE 'I050'           TO MSGLOG-CODE
085740              PERFORM P9000-SEND-MAP-AND-RETURN
085750           END-IF
085760           SET NO-RECORD-FND        TO TRUE
085770           MOVE SPACES              TO SCR08S-FUNC(SUB1)
085780           MOVE P08S-TSQ-EMP-SEL(SUB2)    TO P942-EMP-NO
085790           PERFORM P8070-LINK-942-SEN-ANAL
085800           IF P942-EMP-SEN-LEVEL > SPACES
085810              MOVE P942-EMP-SEN-LEVEL     TO WS-COMPARE-LVL
085820              MOVE P942-EMP-SENIORITY     TO WS-COMPARE-SEN-DATE
085830           ELSE
085840              MOVE HIGH-VALUES            TO WS-COMPARE-LVL
085850                                             WS-COMPARE-SEN-DATE
085860           END-IF
085870           IF WS-COMPARE-SEN < WS-SENIOR-SEN
085880              MOVE WS-COMPARE-SEN
085890                                 TO WS-SENIOR-SEN
085900              MOVE SUB1          TO WS-SENIOR-SUB1
085910              MOVE SUB2          TO WS-SENIOR-SUB2
085920              MOVE P08S-TSQ-EMP-SEL(SUB2)
085930                                 TO WS-SENIOR-EMP-NO
085940           END-IF
085950        END-IF
085960        ADD 1                       TO SUB1
085970                                       SUB2
085980     END-PERFORM
085990     IF WS-SENIOR-SUB1 NOT = HOLD-SUB1
086000        MOVE WS-SENIOR-SUB1        TO SUB1
086010        MOVE WS-SENIOR-SUB2        TO SUB2
086020        MOVE P08S-TSQ-EMP-SEL(HOLD-SUB2)
086030                                   TO P08S-TSQ-EMP-SEL(SUB2)
086040                                      MSTRNBRK
086050        MOVE P08S-TSQ-DECKEY(SUB2) TO WS-SENIOR-DEC-KEY
086060        MOVE P08S-TSQ-DECKEY(HOLD-SUB2)
086070                                   TO P08S-TSQ-DECKEY(SUB2)
086080        SET  P08S-TSQ-SENR-RESTRICTED(SUB2) TO TRUE
086090        MOVE P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
086100                                   TO WS-SENIOR-QTIE-DISQUAL-FLAG
086110        MOVE P08S-TSQ-QTIE-DISQUAL-FLAG(HOLD-SUB2)
086120                              TO P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
086130        PERFORM P4020-DISPLAY-EMP-INFO
086140        MOVE HOLD-SUB1             TO SUB1
086150        MOVE HOLD-SUB2             TO SUB2
086160        MOVE WS-SENIOR-EMP-NO      TO P08S-TSQ-EMP-SEL(SUB2)
086170                                      MSTRNBRK
086180        MOVE WS-SENIOR-DEC-KEY     TO P08S-TSQ-DECKEY(SUB2)
086190        SET  P08S-TSQ-SENR-RESTRICTED(SUB2) TO TRUE
086200        MOVE WS-SENIOR-QTIE-DISQUAL-FLAG
086210                              TO P08S-TSQ-QTIE-DISQUAL-FLAG(SUB2)
086220        PERFORM P4020-DISPLAY-EMP-INFO
086230        MOVE SPACES                TO P943-COMMAREA-PARMS
086240        MOVE EMP-NBR IN WS-MSTR    TO P943-EMP-NBR
086250        MOVE CRAFT IN WS-MSTR      TO P943-CRAFT
086260        IF TEMPORARY-ASGNMT > SPACE
086270           MOVE TEMPORARY-ASGNMT   TO P943-NORM-ASGN
086280        ELSE
086290           IF NORMAL-ASGNMT > SPACE
086300              MOVE NORMAL-ASGNMT   TO P943-NORM-ASGN
086310           END-IF
086320        END-IF
086330        MOVE LAYOFF-CODE           TO P943-LO
086340        SET P943-OVERRIDE-SENIOR-FUN  TO TRUE
086350        MOVE JSK2-ASGN-DIST        TO P943-DIST
086360        MOVE JSK2-ASGN-SUB-DIST    TO P943-SDIST
086370        MOVE '0'                   TO P943-IN-OUT
086380        MOVE SCR08S-ASGN(SUB1)     TO P943-TEMP-ASGN-TRAIN
086390        MOVE JSK2-ASGN-CC          TO P943-TEMP-ASGN-CC
086400        MOVE WS-LOCAL-DATE-TIME    TO P943-EFF-DATE-TIME
086410        MOVE WS-SENIOR-SUB1        TO SUB1
086420        MOVE WS-SENIOR-SUB2        TO SUB2
086430        MOVE P08S-TSQ-EMP-SEL(SUB2)
086440                                   TO P943-EMP-NBR-AFFECTED
086450        IF P943-DIST       NOT = PSTCA-DIST
086460        OR P943-SDIST      NOT = PSTCA-SUB-DIST
086470           PERFORM P8994-CONVERT-HIST-TIMEZONE
086480        END-IF
086490        PERFORM P8900-WRITE-HISTORY
086500     END-IF
086510     MOVE HOLD-SUB1                TO SUB1
086520     MOVE HOLD-SUB2                TO SUB2
086530     SET SENIOR-MAN-FUNCTION       TO TRUE
086540     MOVE -1                       TO SCR08S-FUNC-CURSOR(SUB1).
086550*
086560 P5700-DISPLAY-CREW.
086570*
086580     IF EIBTRNID NOT = P08S-TRAN
086590        PERFORM P9500-WRITE-TSQUEUE
086600        PERFORM P9780-WRITE-TSQUEUE4
086610     ELSE
086620        PERFORM P9600-REWRITE-TSQUEUE
086630        PERFORM P9790-REWRITE-TSQUEUE4
086640     END-IF
086650     MOVE SPACES                    TO SCR08S-FUNC(SUB1)
086660     MOVE PSTS08S                   TO P08S-TSQ-SCREEN-AREA
086670     MOVE PSTCOMM-AREA              TO P08S-TSQ-COMMAREA
086680     PERFORM P9510-WRITE-TSQUEUE-2
086690     MOVE SPACES                    TO P12COMM-AREA
086700     MOVE P08S-PGM                  TO PSTCA-FROM-PROGRAM
086710     MOVE JSK2-ASGN                 TO P12CA-ASGN-ID
086720     SET P12CA-INQ-ONLY             TO TRUE
086730     MOVE CALL-DAY1                 TO P12CA-CALL-DAY
086740     MOVE START-TIME-1              TO P12CA-CALL-FROM
086750     MOVE END-TIME-2                TO P12CA-CALL-TO
086760     MOVE START-DATE-1              TO P12CA-CALL-DATE-FROM
086770     EXEC CICS XCTL
086780               PROGRAM(P12-PGM)
086790               COMMAREA(PSTCOMM-AREA)
086800               LENGTH(PSTCOMM-LGTH)
086810               RESP(WS-RESPONSE)
086820     END-EXEC
086830     MOVE WS-RESPONSE               TO FILE-STATUS
086840     IF NOT SUCCESS
086850        MOVE 'P5700-1'              TO ERR-PARAGRAPH
086860        PERFORM P9999-GOT-PROBLEM
086870     END-IF.
086880*
086890 P5800-DISPLAY-PHONE-NUMBERS.
086900*
086910     IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
086920        MOVE -1                      TO SCR08S-FUNC-CURSOR(SUB1)
086930        MOVE REV-VIDEO               TO SCR08S-FUNC-HI(SUB1)
086940*            'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
086950        MOVE 'I050'                  TO MSGLOG-CODE
086960        PERFORM P9000-SEND-MAP-AND-RETURN
086970     END-IF
086980     MOVE P08S-TSQ-EMP-SEL(SUB2)     TO MSTRNBRK
086990     PERFORM P8505-READ-MASTER-ONLY
087000     PERFORM P4025-DISPLAY-PHONE-INFO
087010     MOVE SPACES                     TO SCR08S-FUNC(SUB1)
087020     MOVE -1                         TO SCR08S-FUNC-CURSOR(SUB1)
087030     PERFORM P9000-SEND-MAP-AND-RETURN.
087040*
087050 P5900-ATTEMPTED-NOTIFY.
087060*
087070     MOVE P08S-TSQ-EMP-SEL(SUB2)     TO MSTRNBRK
087080     PERFORM P8505-READ-MASTER-ONLY
087090     IF SCR08S-ASGN(SUB2) NOT > SPACES
087100        MOVE -1 TO SCR08S-FUNC-CURSOR(SUB2)
087110        MOVE REV-VIDEO TO SCR08S-FUNC-HI(SUB2)
087120        MOVE 'IMPROPER FUNCTION - NO ASSIGNMENT PRESENT'
087130           TO SCR08S-ERRORMSG
087140        PERFORM P9000-SEND-MAP-AND-RETURN
087150     END-IF
087160
087170     IF P08S-TSQ-EMP-SEL(SUB2) NOT > SPACES
087180        MOVE -1 TO SCR08S-FUNC-CURSOR(SUB2)
087190        MOVE REV-VIDEO TO SCR08S-FUNC-HI(SUB2)
087200        MOVE 'NO EMPLOYEE TO NOTIFY' TO SCR08S-ERRORMSG
087210        PERFORM P9000-SEND-MAP-AND-RETURN
087220     END-IF
087230
087240     MOVE SPACES                TO P943-COMMAREA-PARMS
087250     MOVE EMP-NBR IN WS-MSTR    TO P943-EMP-NBR
087260     MOVE CRAFT IN WS-MSTR      TO P943-CRAFT
087270     IF TEMPORARY-ASGNMT > SPACE
087280        MOVE TEMPORARY-ASGNMT   TO P943-NORM-ASGN
087290     ELSE
087300        IF NORMAL-ASGNMT > SPACE
087310           MOVE NORMAL-ASGNMT   TO P943-NORM-ASGN
087320        END-IF
087330     END-IF
087340     MOVE LAYOFF-CODE           TO P943-LO
087350     SET P943-ATTEMPT-NTFY-FUN  TO TRUE
087360     MOVE JSK2-ASGN-DIST        TO P943-DIST
087370     MOVE JSK2-ASGN-SUB-DIST    TO P943-SDIST
087380     MOVE SCR08S-ASGN(SUB2)     TO P943-TEMP-ASGN-TRAIN
087390     MOVE JSK2-ASGN-CC          TO P943-TEMP-ASGN-CC
087400     MOVE WS-LOCAL-DATE-TIME    TO P943-EFF-DATE-TIME
087410     MOVE PSTCA-TIME-ZONE       TO P943-EMP-TIME-ZONE
087420     MOVE SCR08S-FUNC(SUB2)     TO P943-ATTEMPT-TYPE
087430     IF P943-DIST       NOT = PSTCA-DIST
087440     OR P943-SDIST      NOT = PSTCA-SUB-DIST
087450        PERFORM P8994-CONVERT-HIST-TIMEZONE
087460     END-IF
087470     PERFORM P8900-WRITE-HISTORY
087480     MOVE 'EMPLOYEE "ATTEMPT" NOTIFICATION COMPLETED'
087490                                    TO SCR08S-ERRORMSG
087500     MOVE '****'    TO SCR08S-FUNC      (SUB2)
087510     MOVE -1        TO SCR08S-FUNC-CURSOR(SUB2).
087520*================================================================*
087530 P5950-GET-HRS-SVC-INFO.
087540*================================================================*
087550*    DETERMINE THE ACTUAL TIME BY CALLING 925
087560*    THE PSTCA-TIME-ZONE SHOULD BE THE SAME AS THE EMP TIME ZONE
087570
087580     MOVE SPACES                        TO P925-COMMAREA-PARMS
087590     SET P925-CALC-ACTUAL-TIME          TO TRUE
087600     MOVE EMP-NBR IN WS-MSTR            TO P925-EMP-NO
087610     MOVE JSK2-ASGN-CC                  TO P925-ASGN-CC
087620     MOVE PSTCA-TIME-ZONE               TO P925-TIME-ZONE
087630     MOVE WK-CALL-DATE-TIME             TO P925-OD-DATE-TIME
087640                                           P925-ORDER-DATE-TIME
087650     MOVE TRCN-JOB-TYPE                 TO P925-JOB-TYPE
087660     MOVE TRCN-KEY1                     TO P925-TRCNKEY1
087670
087680     MOVE 'H'                           TO P925-TERM
087690     IF TRCN-LOCAL-REC
087700        SET P925-TIE-UP-ROAD            TO TRUE
087710        MOVE TRCN-TRAIN-ASGN            TO JOB-DEF-CHECK
087720        IF JOB-DEF-COMMUTER-ASGN
087730           SET P925-LOCAL-COMMUTER      TO TRUE
087740        ELSE
087750           SET P925-LOCAL-JOB           TO TRUE
087760        END-IF
087770     ELSE
087780        SET P925-TIE-UP-YARD            TO TRUE
087790     END-IF
087800
087810     EXEC CICS LINK
087820               PROGRAM(P925-PGM)
087830               COMMAREA(P925-COMMAREA-PARMS)
087840               LENGTH(P925-LGTH)
087850               RESP(WS-RESPONSE)
087860     END-EXEC
087870     MOVE WS-RESPONSE                   TO FILE-STATUS
087880     IF NOT SUCCESS
087890        MOVE 'P5950-2'                  TO ERR-PARAGRAPH
087900        MOVE 'P925LINK'                 TO ERR-KEY
087910        PERFORM P9999-GOT-PROBLEM
087920     END-IF
087930
087940*    CONVERT THE ON DUTY TIME BACK TO THE PSTCA ZONE
087950*    MOVE THE ORDER DATE/TIME BACK TO THE P925 COMM AREA
087960
087970     IF DIST OF WS-MSTR NOT > SPACES
087980        MOVE EMP-NBR IN WS-MSTR         TO MSTRNBRK
087990        PERFORM P8505-READ-MASTER-ONLY
088000     END-IF
088010
088020     MOVE SPACES                        TO CNTLKEY-AREA
088030     MOVE '02'                          TO CNTL-REC-TYPE
088040     MOVE DIST OF WS-MSTR               TO CNTL-DIST
088050     MOVE SUB-DIST OF WS-MSTR           TO CNTL-SUB-DIST
088060     MOVE CNTLKEY-AREA                  TO CNTLKEY
088070     PERFORM P8610-READ-CNTLFILE
088080     IF NOT SUCCESS
088090        MOVE 'P5950-3'                  TO ERR-PARAGRAPH
088100        MOVE WORK-CNTLKEY               TO ERR-KEY
088110        PERFORM P9999-GOT-PROBLEM
088120     END-IF
088130     MOVE CNTL-TIME-ZONE                TO WS-EMPLOYEE-ZONE
088140*CNC0598-B
088150     MOVE CNTL-CAN-WRR-FLAG             TO WS-CAN-WRR-FLAG
088160*CNC0598-E
088170
088180     MOVE SPACES                        TO TZ-PARAMETERS
088190     MOVE WS-EMPLOYEE-ZONE              TO TZ-IN-ZONE
088200     MOVE PSTCA-TIME-ZONE               TO TZ-OUT-ZONE
088210     MOVE P925-OD-DATE-TIME             TO TZ-IN-DATE-TIME
088220     PERFORM P8996-TIMEZONE
088230     IF TZ-INVALID-PARAMETERS
088240        MOVE 'P5950-4'                  TO ERR-PARAGRAPH
088250        PERFORM P8996-TZERROR
088260     END-IF
088270     MOVE TZ-OUT-DATE-TIME              TO P925-OD-DATE-TIME
088280     MOVE WK-CALL-DATE-TIME             TO P925-ORDER-DATE-TIME
088290
088300     MOVE SPACE                         TO P925-FUNCTION-10
088310     SET P925-CALC-REST-STATUS          TO TRUE
088320     IF VACANCY-FILLED
088330        SET P925-VACANCY-FILLED         TO TRUE
057860*07/26/23 RJA B CNC0600 CNLD-269
088320        IF PROMOTING-EMP
088330           SET P925-PROMOTING-EMP       TO TRUE
088340        END-IF
057880*07/26/23 RJA E
088340     END-IF
088350
088360     MOVE JSK2-ASGN-CC                  TO WS-CRAFT-CODE-CHECK
088370     IF ENGINE-CRAFT
088380        IF TRCN-EN-REST-REQ = ('C' OR 'B')
088390           CONTINUE
088400        ELSE
088410           SET P925-EMP-STATUS-NA       TO TRUE
088420        END-IF
088430     ELSE
088440        IF TRCN-TR-REST-REQ = ('C' OR 'B')
088450           CONTINUE
088460        ELSE
088470           SET P925-EMP-STATUS-NA
088480                                        TO TRUE
088490        END-IF
088500     END-IF
088510
088520     IF P925-EMP-STATUS-NA
088530        MOVE 'N/A'                      TO P925-REST-STATUS-TTA
088540                                           P925-REST-STATUS-TTW
088550                                           P925-REST-STATUS-TTR
088560                                           P925-REST-STATUS-ERT
088570                                           P925-REST-STATUS-TWT
088580     ELSE
088590        MOVE TRCN-ORIGIN-STN            TO P925-STATION
088600        MOVE JSK2-ASGN                  TO JOB-DEF-CHECK
088610        IF JOB-DEF-YARD-ASGN
088620           SET P925-TIE-UP-YARD         TO TRUE
088630        ELSE
088640           SET P925-TIE-UP-ROAD         TO TRUE
088650           MOVE 'H'                     TO P925-TERM
088660        END-IF
088670        MOVE TRCN-FNL-STN               TO P925-FINAL-STATION
088680        IF   TRCN-2ND-ROUTE-DESIGNATION > SPACES
088690       AND TRCN-2ND-ROUTE-DESIGNATION NOT = TRCN-ROUTE-DESIGNATION
088700        AND  TRCN-LOCAL-REC
088710        AND (TRCN-SHIFT-TIE-YES OR TRCN-SHIFT-CALL-TIE-YES)
088720           PERFORM P3037-CHECK-SCHED
088730           IF SCHED-2ND
088740              MOVE TRCN-2ND-ROUTE-DESIGNATION TO P925-ROUTE-DESGN
088750           ELSE
088760              MOVE TRCN-ROUTE-DESIGNATION     TO P925-ROUTE-DESGN
088770           END-IF
088780        ELSE
088790           MOVE TRCN-ROUTE-DESIGNATION        TO P925-ROUTE-DESGN
088800        END-IF
088810        IF JOB-DEF-YARD-ASGN
088820           PERFORM P5951-CALC-ERT
088830           MOVE WS-HR-MN                TO P925-AJ-ERT
088840        END-IF
088850*
088860*       IF WE'RE RECALCULATING A TRACKED EMPLOYEE'S REST
088870*       STATUS, WE WANT TO INCLUDE ANY VIRTUAL TOD RECS IN THE
088880*       CALCULATION, AS WELL.
088890*
088900        IF CALC-TRK-HOS
088910           SET P925-INCLUDE-VIRTUALS TO TRUE
088920        END-IF
088930
088940        EXEC CICS LINK
088950                  PROGRAM(P925-PGM)
088960                  COMMAREA(P925-COMMAREA-PARMS)
088970                  LENGTH(P925-LGTH)
088980                  RESP(WS-RESPONSE)
088990        END-EXEC
089000        MOVE WS-RESPONSE                TO FILE-STATUS
089010        IF NOT SUCCESS
089020           MOVE 'P5950-5'               TO ERR-PARAGRAPH
089030           MOVE 'P925LINK'              TO ERR-KEY
089040           PERFORM P9999-GOT-PROBLEM
089050        END-IF
      *CNLD-285-B
      * NOTE: CONTROL FLOWS TO THIS POINT ONLY WHEN
      *       TRCN-EN/TR-REST-REQ = ('C' OR 'B')
              MOVE SPACES   TO WS-EMP-UNDST-REST-FLAG
              MOVE ZEROS    TO WS-SAVE-UD-DTTM
      *CNLD-285-E
089060*CNLD-230-B
089070        IF P925-EMP-STATUS-RED
089080           MOVE P925-REASON-FOR-RED
089090                                     TO WS-REASON-FOR-RED
089100        END-IF
089110*CNLD-230-E
089110
089120*C1193 - BEG
089130*** PER KEVIN,
089140*** "REMOVE THE CODE THAT CAUSES THE SYSTEM TO FLAG SOMEONE WHO IS
089150***  CLOSE TO THE 64/7 CAP AS YELLOW. THIS IS CODE THAT WAS ADDED
089160***  WITH THE INITIAL ENHANCEMENT, BUT IS NOT REQUIRED ANY MORE."
089170***     IF NOT P925-EMP-STATUS-RED
089180***        PERFORM P5952-EXPECTED-ASGN-LENGTH
089190***     END-IF
089200*C1193 - END
089210     END-IF.
089220*================================================================*
089230 P5951-CALC-ERT.
089240*================================================================*
089250     MOVE JSK1-ASGN-START-TIME          TO WS-HR-MN
089260     MULTIPLY WS-HR BY 60           GIVING WS-MIN1
089270     ADD WS-MN                          TO WS-MIN1
089280     MOVE JOB-SCHED-END-TIME            TO WS-HR-MN
089290     MULTIPLY WS-HR BY 60           GIVING WS-MIN2
089300     ADD WS-MN                          TO WS-MIN2
089310     IF WS-MIN2 < WS-MIN1
089320        ADD 1440                        TO WS-MIN2
089330     END-IF
089340     SUBTRACT WS-MIN1                 FROM WS-MIN2
089350     DIVIDE WS-MIN2 BY 60           GIVING WS-HR
089360                                 REMAINDER WS-MN.
089370*================================================================*
089380 P5952-EXPECTED-ASGN-LENGTH.
089390*================================================================*
089400     MOVE P925-REST-STATUS-TWT     TO WORK-TIME
089410     MOVE WORK-HR                  TO WS-TOTAL-HOURS
089420     MOVE WORK-MN                  TO WS-TOTAL-MINUTES
089430
089440     MOVE JSK2-ASGN                TO JOB-DEF-CHECK
089450     IF JOB-DEF-YARD-ASGN
089460        MOVE P925-AJ-ERT           TO WORK-TIME
089470     ELSE
089480        PERFORM P5951-CALC-ERT
089490        MOVE WS-HR-MN              TO WORK-TIME
089500     END-IF
089510
089520     ADD WORK-HR                   TO WS-TOTAL-HOURS
089530     ADD WORK-MN                   TO WS-TOTAL-MINUTES
089540     PERFORM P5954-CALCULATE-64-TIME
089550*    SET TO YELLOW ONLY IF > 6400, NOT >= 6400
089560     IF  WS-64-TOT-TIME-ON-DUTY >  6400
089570*CNC0600-RKW
089580     AND NOT WS-CAN-WRR-NEW
089590        SET P925-EMP-STATUS-YELLOW TO TRUE
089600     END-IF.
089610*================================================================*
089620 P5954-CALCULATE-64-TIME.
089630*================================================================*
089640     SET TIME-NOT-DONE             TO TRUE
089650     PERFORM UNTIL TIME-DONE
089660        IF WS-TOTAL-MINUTES > 59
089670           SUBTRACT 60           FROM WS-TOTAL-MINUTES
089680           ADD 1                   TO WS-TOTAL-HOURS
089690        ELSE
089700          SET TIME-DONE            TO TRUE
089710        END-IF
089720     END-PERFORM
089730     MOVE WS-TOTAL-HOURS           TO WS-64-TOT-HRS-ON-DUTY
089740     MOVE WS-TOTAL-MINUTES         TO WS-64-TOT-MNS-ON-DUTY.
089750*================================================================*
089760 P5960-REJECT-HRS-SVC.
089770*================================================================*
089780     MOVE P08S-TSQ-EMP-SEL(SUB2)   TO MSTRNBRK
089790     PERFORM P8500-READ-MASTER
089800     IF P08S-TSQ-DECKEY(SUB2) > SPACES
089810        OR P08S-USER-OVERRIDE(SUB2)
089820        SET VACANCY-FILLED         TO TRUE
089830     ELSE
089840        SET NOT-VACANCY-FILLED     TO TRUE
089850     END-IF
057860*07/26/23 RJA B CNC0600 CNLD-269
057850     SET NOT-PROMOTING-EMP        TO TRUE
057810     IF P08S-TSQ-DECKEY(SUB2) > SPACES
057860        MOVE P08S-TSQ-DECKEY(SUB2) TO DECKEY
057860        MOVE SPACES TO DEC-REQ-1
057870                       DEC-REQ-2
057880        PERFORM P8670-READ-DECISION
057890        IF ON-TURN OF WORK-DEC-TABLE
057900        OR ON-JOB  OF WORK-DEC-TABLE
057860           SET PROMOTING-EMP      TO TRUE
057860        END-IF
057870     END-IF
057880*07/26/23 RJA E
089860     PERFORM P5950-GET-HRS-SVC-INFO
089870     IF NOT P925-EMP-STATUS-YELLOW
089880*            EMPLOYEE STATUS NOT YELLOW - RJHS NOT ALLOWED
089890        MOVE 'E238'        TO MSGLOG-CODE
089900        MOVE -1            TO SCR08S-FUNC-CURSOR(SUB2)
089910        MOVE REV-VIDEO     TO SCR08S-FUNC-HI(SUB2)
089920        PERFORM P9000-SEND-MAP-AND-RETURN
089930     END-IF
089940     IF P08S-TSQ-RJHS-EMPNO(SUB2) NOT > SPACES
089950        MOVE SPACES        TO CNTLKEY-AREA
089960        MOVE '01'          TO CNTL-REC-TYPE
089970        MOVE DIST IN WS-MSTR  TO CNTL-DIST
089980        MOVE CNTLKEY-AREA     TO CNTLKEY
089990        PERFORM P8610-READ-CNTLFILE
090000        IF NOT SUCCESS
090010           MOVE 'P5960-1'     TO ERR-PARAGRAPH
090020           MOVE CNTLKEY       TO ERR-KEY
090030           PERFORM P9999-GOT-PROBLEM
090040        END-IF
090050        MOVE JSK2-ASGN        TO JOB-DEF-CHECK
090060        IF JOB-DEF-YARD-ASGN
090070           IF RUN-BY-PAY-YD-APPLICABLE
090080              MOVE P08S-TSQ-EMP-SEL(SUB2)
090090                           TO P08S-TSQ-RJHS-EMPNO(SUB2)
090100              MOVE 'Y'     TO P923-RJHS-PAY-FLAG
090110           ELSE
090120              MOVE 'N'     TO P923-RJHS-PAY-FLAG
090130           END-IF
090140        ELSE
090150           IF RUN-BY-PAY-RD-APPLICABLE
090160              MOVE P08S-TSQ-EMP-SEL(SUB2)
090170                           TO P08S-TSQ-RJHS-EMPNO(SUB2)
090180              MOVE 'Y'     TO P923-RJHS-PAY-FLAG
090190           ELSE
090200              MOVE 'N'     TO P923-RJHS-PAY-FLAG
090210           END-IF
090220        END-IF
090230     ELSE
090240        MOVE 'N'           TO P923-RJHS-PAY-FLAG
090250     END-IF.
090260*================================================================*
090270 P5970-DISPLAY-HRS-SVC-INFO.
090280*================================================================*
090290     IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
090300        IF P08S-TSQ-OWNER(SUB2) NOT > '000000000'
090310*            'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
090320           MOVE 'I050'     TO MSGLOG-CODE
090330           MOVE -1         TO SCR08S-FUNC-CURSOR(SUB2)
090340           MOVE REV-VIDEO  TO SCR08S-FUNC-HI(SUB2)
090350           PERFORM P9000-SEND-MAP-AND-RETURN
090360        END-IF
090370     END-IF
090380
090390     IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
090400        MOVE P08S-TSQ-EMP-SEL(SUB2) TO MSTRNBRK
090410     ELSE
090420        MOVE P08S-TSQ-OWNER(SUB2)   TO MSTRNBRK
090430     END-IF
090440     PERFORM P8500-READ-MASTER
090450     IF P08S-TSQ-DECKEY(SUB2) > SPACES
090460        OR P08S-USER-OVERRIDE(SUB2)
090470        SET VACANCY-FILLED          TO TRUE
090480     ELSE
090490        SET NOT-VACANCY-FILLED      TO TRUE
090500     END-IF
057860*07/26/23 RJA B CNC0600 CNLD-269
057850     SET NOT-PROMOTING-EMP        TO TRUE
057810     IF P08S-TSQ-DECKEY(SUB2) > SPACES
057860        MOVE P08S-TSQ-DECKEY(SUB2) TO DECKEY
057860        MOVE SPACES TO DEC-REQ-1
057870                       DEC-REQ-2
057880        PERFORM P8670-READ-DECISION
057890        IF ON-TURN OF WORK-DEC-TABLE
057900        OR ON-JOB  OF WORK-DEC-TABLE
057860           SET PROMOTING-EMP      TO TRUE
057860        END-IF
057870     END-IF
057880*07/26/23 RJA E
090510     PERFORM P5950-GET-HRS-SVC-INFO
090520
090530     IF P925-EMP-STATUS-NA
090540*            'HOURS OF SERVICE NOT APPLICABLE'
090550        MOVE 'H032'                 TO MSGLOG-CODE
090560     ELSE
090570        MOVE SPACES              TO MSGLOG-CODE
090580        IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
090590           IF SCR08S-PERS(SUB2) > SPACES
090600              MOVE EMP-PERS-REST-TIME      TO WS-PERS-REST-TIME
090610              MOVE EMP-PERS-REST-DATE(5:2) TO WS-PERS-REST-DAY
090620           ELSE
090630              MOVE ZEROS                   TO WS-PERS-REST-TIME
090640                                              WS-PERS-REST-DAY
090650           END-IF
090660        ELSE
090670           IF P08S-TSQ-OWNER(SUB2) > '000000000'
090680              IF SCR08S-OWNR-PERS(SUB2) > SPACES
090690                 MOVE EMP-PERS-REST-TIME      TO WS-PERS-REST-TIME
090700                 MOVE EMP-PERS-REST-DATE(5:2) TO WS-PERS-REST-DAY
090710              ELSE
090720                 MOVE ZEROS                   TO WS-PERS-REST-TIME
090730                                                 WS-PERS-REST-DAY
090740              END-IF
090750           ELSE
090760              MOVE ZEROS                   TO WS-PERS-REST-TIME
090770                                              WS-PERS-REST-DAY
090780           END-IF
090790        END-IF
090800*CNC0598-B
090810*CNLD-309 B COMMENTING
090810*       IF WS-CAN-WRR-NEW
090810*CNLD-309 E
090820           STRING 'STATUS=' P925-REST-STATUS-FLAG ' '
090830                  'TTA=' P925-REST-STATUS-TTA ' '
090840                  'ERT=' P925-REST-STATUS-ERT ' '
090850                  'TTW=' P925-REST-STATUS-TTW ' '
090860                  'TWT=' P925-REST-STATUS-TWT ' '
090870                  'PR='  WS-PERS-REST
090880                  DELIMITED BY SIZE
090890                  INTO SCR08S-ERRORMSG
090810*CNLD-309 B COMMENTING
090900*       ELSE
090810*CNLD-309 E
090910*CNC0598-E
090810
090810*CNLD-309 B COMMENTING
090920*          IF P925-TIE-UP-YARD
090930*             STRING 'YD MTOY=(' EMP-MTOY ') '
090940*                    'STATUS=' P925-REST-STATUS-FLAG ' '
090950*                    'TTA=' P925-REST-STATUS-TTA ' '
090960*                    'ERT=' P925-REST-STATUS-ERT ' '
090970*                    'TTW=' P925-REST-STATUS-TTW ' '
090980*                    'TWT=' P925-REST-STATUS-TWT ' '
090990*                    'PR='  WS-PERS-REST
091000*                    DELIMITED BY SIZE
091010*                    INTO SCR08S-ERRORMSG
091020*          ELSE
091030*             STRING 'RD MTOR=(' EMP-MTOR ') '
091040*                    'STATUS=' P925-REST-STATUS-FLAG ' '
091050*                    'TTA=' P925-REST-STATUS-TTA ' '
091060*                    'ERT=' P925-REST-STATUS-ERT ' '
091070*                    'TTW=' P925-REST-STATUS-TTW ' '
091080*                    'TWT=' P925-REST-STATUS-TWT ' '
091090*                    'PR='  WS-PERS-REST
091100*                    DELIMITED BY SIZE
091110*                    INTO SCR08S-ERRORMSG
091120*          END-IF
091130**CNC0598-B
091140*       END-IF
091150**CNC0598-E
090810*CNLD-309 E
091160     END-IF
091170     MOVE -1                     TO SCR08S-FUNC-CURSOR(SUB2)
091180     PERFORM P9000-SEND-MAP-AND-RETURN.
091190*================================================================*
091200 P5980-DISPLAY-HOS-INFO.
091210*================================================================*
091220     IF P08S-TSQ-EMP-SEL(SUB2) NOT > '000000000'
091230        IF P08S-TSQ-OWNER(SUB2) NOT > '000000000'
091240*            'IMPROPER FUNCTION - NO EMPLOYEE PRESENT'
091250           MOVE 'I050'     TO MSGLOG-CODE
091260           MOVE -1         TO SCR08S-FUNC-CURSOR(SUB2)
091270           MOVE REV-VIDEO  TO SCR08S-FUNC-HI(SUB2)
091280           PERFORM P9000-SEND-MAP-AND-RETURN
091290        END-IF
091300     END-IF
091310
091320     INITIALIZE PS94-COMMAREA-PARMS
091330     SET PS94-SUM-FUNC           TO TRUE
091340
091350     IF P08S-TSQ-EMP-SEL(SUB2) > '000000000'
091360        MOVE P08S-TSQ-EMP-SEL(SUB2) TO PS94-EMP-NBR
091370     ELSE
091380        MOVE P08S-TSQ-OWNER(SUB2)   TO PS94-EMP-NBR
091390     END-IF
091400     IF PS94-EMP-NBR > ZEROES
091410        MOVE WS-LOCAL-DATE-TIME     TO PS94-HSL-EVENT-STRT-TS-DTTM
091420                                       PS94-HSL-EVENT-END-TS-DTTM
091430        MOVE PSTCA-TIME-ZONE        TO PS94-TIME-ZONE
091440        PERFORM P9993-CALL-PS94-PROGRAM
091450        MOVE PS94-MTD-TOTAL-TM      TO WS-MISC-TOT-TM
091460        MOVE PS94-MTD-LIMBO-TM      TO WS-MISC-LIMBO-TM
091470        MOVE PS94-CONSECUTIVE-STARTS TO
091480                                       WS-MISC-CONSEC-STARTS
091490     END-IF
091500
091510     IF P925-EMP-STATUS-NA
091520*            'HOURS OF SERVICE NOT APPLICABLE'
091530        MOVE 'H032'                 TO MSGLOG-CODE
091540     ELSE
091550        MOVE SPACES              TO MSGLOG-CODE
091560        MOVE SPACES              TO MSGLOG-CODE
091570        STRING 'HOS INFO : '
091580               'TOTAL(' WS-TOT-TM2(1:3) ':' WS-TOT-TM2(4:2) ') '
091590               'LIMBO(' WS-LIMBO-TM2(1:3) ':' WS-LIMBO-TM2(4:2)')'
091600               ' STARTS(' WS-CONSEC-STARTS ')'
091610                DELIMITED BY SIZE
091620                INTO SCR08S-ERRORMSG
091630     END-IF
091640     MOVE -1                     TO SCR08S-FUNC-CURSOR(SUB2)
091650     PERFORM P9000-SEND-MAP-AND-RETURN.
091660*
091670 P6000-PREFILL-VCLL.
091680*
091690     PERFORM VARYING SUB2 FROM 1 BY 1
091700        UNTIL SUB2 > (P08S-TSQ-CNT OR P08S-TSQ-MAX)
091710        IF P08S-TSQ-EMP-SEL(SUB2)       > SPACES
091720           MOVE P08S-TSQ-JSKEY2(SUB2)  TO JS-KEY2
091730           MOVE SPACES                 TO TRCN-KEY3
091740           MOVE JSK2-ASGN              TO TRCN-ASSIGNMENT
091750           MOVE JSK2-ASGN-DIST         TO TRCN-DIST3
091760           MOVE JSK2-ASGN-SUB-DIST     TO TRCN-SDIST3
091770           MOVE TRCN-KEY3              TO TRCNKEY3
091780           PERFORM P8630-READ-TRCNKEY3
091790           IF SUCCESS
091800              PERFORM P6110-IVR-PFKEY-EDITS
091810              IF  P08S-TSQ-EMP-SEL(SUB2) > SPACES
091820              AND SCR08S-ST(SUB2)    NOT = 'B8'
091830              AND SCR08S-ST(SUB2)    NOT = 'B1'
091840                 IF WS-IVR-CALL-PROF     > SPACES
091850                    MOVE 'VCLL'         TO SCR08S-FUNC(SUB2)
091860                    MOVE DEFAULT-ATTR   TO SCR08S-FUNC-HI(SUB2)
091870                 END-IF
091880              END-IF
091890           END-IF
091900        END-IF
091910     END-PERFORM
091920     MOVE -1                            TO SCR08S-FUNC-CURSOR(1)
091930     IF SCR08S-FUNC(1)                    = 'VCLL'
091940     OR SCR08S-FUNC(2)                    = 'VCLL'
091950     OR SCR08S-FUNC(3)                    = 'VCLL'
091960     OR SCR08S-FUNC(4)                    = 'VCLL'
091970     OR SCR08S-FUNC(5)                    = 'VCLL'
091980     OR SCR08S-FUNC(6)                    = 'VCLL'
091990     OR SCR08S-FUNC(7)                    = 'VCLL'
092000     OR SCR08S-FUNC(8)                    = 'VCLL'
092010     OR SCR08S-FUNC(9)                    = 'VCLL'
092020     OR SCR08S-FUNC(10)                   = 'VCLL'
092030     OR SCR08S-FUNC(11)                   = 'VCLL'
092040*            'IVR CALL FUNCTION FILLED FOR ELIGIBLE EMPLOYEES'
092050        MOVE 'I591'                     TO MSGLOG-CODE
092060     ELSE
092070*            'NO EMPLOYEES ELIGIBLE FOR IVR CALL'
092080        MOVE 'N255'                     TO MSGLOG-CODE
092090     END-IF
092100     PERFORM P9000-SEND-MAP-AND-RETURN
092110     .
092120*
092130 P6100-PROCESS-IVR-CALL.
092140*
092150     PERFORM P6110A-IVR-CALL-EDITS
092160     PERFORM P5100-PROCESS-CALL-FUNCTION
092170     PERFORM P6150-CREATE-UPDATE-TASK
092180     PERFORM P6160-CREATE-UPDATE-VRU-INFO
092190     PERFORM P6170-SETUP-P0ESCOMM
092200     PERFORM P6180-LINK-TO-0ES
092210     .
092220*
092230 P6110-IVR-PFKEY-EDITS.
092240*
092250     MOVE SPACES                        TO WS-IVR-CALL-PROF
092260     SET  P08SCA-IVR-CALL-NOT-ACTIVE    TO TRUE
092270     PERFORM P6111-COMPANY-EDIT
092280     IF CNTL-OUTBND-IVR-CALL-ACTIVE
092290        PERFORM P6112-SUB-DIST-EDIT
092300        IF CNTL-2B-IVR-CALL-FL           = 'Y'
092310           PERFORM P6113-IVR-CALL-PROFILE-EDIT
092320        END-IF
092330     END-IF
092340     .
092350*
092360 P6110A-IVR-CALL-EDITS.
092370*
092380     MOVE SPACES                        TO WS-IVR-CALL-PROF
092390     SET  P08SCA-IVR-CALL-NOT-ACTIVE    TO TRUE
092400     PERFORM P6111-COMPANY-EDIT
092410     IF CNTL-OUTBND-IVR-CALL-ACTIVE
092420        PERFORM P6112-SUB-DIST-EDIT
092430        IF CNTL-2B-IVR-CALL-FL           = 'Y'
092440*          FIND THE IVR CALL PROFILE ON THE CREW PROFILE FIRST
092450           IF TRCN-IVR-CALL-PROF               > SPACES
092460              MOVE TRCN-IVR-CALL-PROF   TO WS-IVR-CALL-PROF
092470           END-IF
092480           PERFORM P6113-IVR-CALL-PROFILE-EDIT
092490           PERFORM P6114-EMP-EDITS
092500        END-IF
092510     END-IF
092520
092530     MOVE SPACE                         TO WS-SHORT-CALL-REQ
092540*    READ TELE VIA PS52
092550     MOVE SPACES                        TO PS52-COMMAREA-PARMS
092560     MOVE EMP-NBR OF WS-MSTR            TO PS52-EMP-NBR
092570     SET PS52-CMA-MODE                  TO TRUE
092580     SET PS52-DISPLAY-ONLY              TO TRUE
092590     MOVE WS-LOCAL-DATE-TIME            TO PS52-INPUT-DATE-TIME
092600     PERFORM P8520-LINK-TO-PS52
092610     IF NOT SUCCESS
092620        MOVE 'P6110A-1'                 TO ERR-PARAGRAPH
092630        PERFORM P9999-GOT-PROBLEM
092640     END-IF
092650
092660     IF (PS52-REQ-LEAD-TIME     > SPACES
092670        AND PS52-REQ-LEAD-TIME  > '0000')
092680        MOVE PS52-REQ-LEAD-TIME         TO WS-SHORT-CALL-REQ
092690     END-IF
092700     IF (PS52-REQ-CALL-TIME     > SPACES
092710        AND PS52-REQ-CALL-TIME  > '0000')
092720        IF WS-SHORT-CALL-REQ   = SPACES
092730           MOVE PS52-REQ-CALL-TIME      TO WS-SHORT-CALL-REQ
092740        ELSE
092750           IF PS52-REQ-CALL-TIME < WS-SHORT-CALL-REQ
092760              MOVE PS52-REQ-CALL-TIME   TO WS-SHORT-CALL-REQ
092770           END-IF
092780        END-IF
092790     END-IF
092800     .
092810*
092820 P6111-COMPANY-EDIT.
092830*
092840     MOVE SPACES                        TO WS-CNTL-FILE
092850     SET  COMPANY-TYPE-REC              TO TRUE
092860     MOVE CNTLKEY-AREA                  TO CNTLKEY
092870     PERFORM P8610-READ-CNTLFILE
092880     IF  SUCCESS
092890     AND COMPANY-TYPE-REC
092900     AND CNTL-OUTBND-IVR-CALL-ACTIVE
092910        CONTINUE
092920     ELSE
092930        MOVE SPACES                     TO WS-CNTL-FILE
092940     END-IF
092950     .
092960*
092970 P6112-SUB-DIST-EDIT.
092980*
092990     MOVE SPACES                        TO WS-CNTL-FILE
093000     SET  SUB-DIST-TYPE-2B-REC          TO TRUE
093010     MOVE SCR08S-DIST                   TO CNTL-DIST
093020     MOVE SCR08S-SUB-DIST               TO CNTL-SUB-DIST
093030     MOVE CNTLKEY-AREA                  TO CNTLKEY
093040     PERFORM P8610-READ-CNTLFILE
093050     IF  SUCCESS
093060     AND CNTL-2B-IVR-CALL-FL             = 'Y'
093070        MOVE CNTL-2B-IVR-CALL-PROF      TO WS-IVR-CALL-PROF
093080     END-IF
093090     .
093100*
093110 P6113-IVR-CALL-PROFILE-EDIT.
093120*
093130     IF WS-IVR-CALL-PROF                 > SPACES
093140*    CHECK IF THE EN IVR CALL PROFILE IS ACTIVE
093150        MOVE SPACES                     TO CNTLKEY-AREA
093160        SET  IVR-CALL-PROFILE-REC       TO TRUE
093170        MOVE WS-IVR-CALL-PROF           TO CNTL-IVR-CALL-PROF
093180        MOVE CNTLKEY-AREA               TO CNTLKEY
093190        PERFORM P8610-READ-CNTLFILE
093200        IF  SUCCESS
093210        AND CNTL-IVR-CALL-ACTIVE
093220           SET  P08SCA-IVR-CALL-ACTIVE  TO TRUE
093230        ELSE
093240           MOVE SPACES                  TO WS-IVR-CALL-PROF
093250        END-IF
093260     END-IF
093270     .
093280*
093290 P6114-EMP-EDITS.
093300*
093310     IF P08SCA-SHORT-RESTED-YES(SUB1)
093320        CONTINUE
093330     ELSE
093340        IF P08SCA-EMP-RESTED-NO(SUB1)
093350           MOVE -1                    TO SCR08S-FUNC-CURSOR(SUB1)
093360           MOVE REV-VIDEO             TO SCR08S-FUNC-HI(SUB1)
093370*          'USE OF IVR CALL NOT ALLOWED - EMPLOYEE UNDER UDR/MAND'
093380           MOVE 'U154'                TO MSGLOG-CODE
093390           PERFORM P9000-SEND-MAP-AND-RETURN
093400        END-IF
093410     END-IF
093420     IF P08SCA-SHORT-PERS-REST-YES(SUB1)
093430        CONTINUE
093440     ELSE
093450        IF P08SCA-EMP-PERS-REST-NO(SUB1)
093460           MOVE -1                    TO SCR08S-FUNC-CURSOR(SUB1)
093470           MOVE REV-VIDEO             TO SCR08S-FUNC-HI(SUB1)
093480*          'USE OF IVR CALL NOT ALLOWED - EMPLOYEE UNDER PERS REST
093490           MOVE 'U156'                TO MSGLOG-CODE
093500           PERFORM P9000-SEND-MAP-AND-RETURN
093510        END-IF
093520     END-IF
093530
093540*    ENSURE EMPLOYEE IS AT HOME AND NOTIFIED
093550
093560     IF P08S-TSQ-EMP-SEL(SUB2)           > ZEROES
093570        MOVE SPACES                     TO WS-MSTR
093580        MOVE P08S-TSQ-EMP-SEL(SUB2)     TO MSTRNBRK
093590        PERFORM P8505-READ-MASTER-ONLY
093600
093610        MOVE P08S-TSQ-JSKEY2(SUB2)     TO WORK-JS-KEY2
093620        MOVE SPACES                    TO TRCN-KEY3
093630        MOVE WK-JSK2-ASGN              TO TRCN-ASSIGNMENT
093640        MOVE WK-JSK2-ASGN-DIST         TO TRCN-DIST3
093650        MOVE WK-JSK2-ASGN-SUB-DIST     TO TRCN-SDIST3
093660        MOVE TRCN-KEY3                 TO TRCNKEY3
093670        PERFORM P8630-READ-TRCNKEY3
093680        IF NOT SUCCESS
093690           IF NO-RECORD-FND
093700              MOVE SPACES              TO TRCN-SHIFT-CALL-TIE
093710           ELSE
093720              MOVE 'P6114-1'           TO ERR-PARAGRAPH
093730              MOVE TRCNKEY3            TO ERR-KEY
093740              PERFORM P9999-GOT-PROBLEM
093750           END-IF
093760        END-IF
093770     END-IF
093780     .
093790*
093800 P6114A-RESET-FUNC-CURSORS.
093810*
093820     MOVE +0                        TO SCR08S-FUNC-CURSOR(1)
093830                                       SCR08S-FUNC-CURSOR(2)
093840                                       SCR08S-FUNC-CURSOR(3)
093850                                       SCR08S-FUNC-CURSOR(4)
093860                                       SCR08S-FUNC-CURSOR(5)
093870                                       SCR08S-FUNC-CURSOR(6)
093880                                       SCR08S-FUNC-CURSOR(7)
093890                                       SCR08S-FUNC-CURSOR(8)
093900                                       SCR08S-FUNC-CURSOR(9)
093910                                       SCR08S-FUNC-CURSOR(10)
093920                                       SCR08S-FUNC-CURSOR(11)
093930     .
093940*
093950 P6150-CREATE-UPDATE-TASK.
093960*
093970     MOVE P08S-TSQ-JSKEY2        (SUB2) TO WORK-JS-KEY2
093980     MOVE P08S-TSQ-CALL-DATE     (SUB2) TO WS-9300-CALL-DATE
093990     MOVE WK-JSK2-ASGN-START-TIME       TO WS-9300-CALL-TIME
094000     PERFORM P9300-READ-TASK-BY-STATION
094010     IF WS-TASK-FOUND
094020        MOVE TASK-LOCATION-KEY          TO TASKTIME
094030        PERFORM P8660-READ-TASK-UPD
094040        SET  TASK-VRU-CALL-INIT         TO TRUE
094050        MOVE SPACES                     TO TASK-VRU-COMMENT
094060        IF TASK-VRU-MESSAGE-CODE         = 'E3'
094070        OR TASK-VRU-MESSAGE-CODE         = 'E4'
094080           MOVE SPACES                  TO TASK-VRU-MESSAGE-CODE
094090        END-IF
094100        PERFORM P8662-REWRITE-TASK
094110        MOVE TASK-LOCATION-KEY          TO HOLD-TASK-KEY
094120     ELSE
094130        PERFORM P6155-SETUP-AND-LINK-TO-927
094140     END-IF
094150     .
094160*
094170 P6155-SETUP-AND-LINK-TO-927.
094180*
094190     MOVE SPACES                        TO P927-COMMAREA-PARMS
094200     SET  P927-IVR-CALL-FUNCTION        TO TRUE
094210     MOVE PSTCA-TIME-ZONE               TO P927-TIME-ZONE
094220     MOVE P08S-TSQ-JSKEY2(SUB2)         TO WORK-JS-KEY2
094230     MOVE P08S-TSQ-CALL-DATE     (SUB2) TO P927-EFF-DATE
094240                                           P927-CALL-DATE
094250     MOVE WK-JSK2-ASGN-START-TIME       TO P927-EFF-TIME
094260                                           P927-CALL-TIME
094270     MOVE SCR08S-DIST                   TO P927-DIST
094280     MOVE SCR08S-SUB-DIST               TO P927-SUB-DIST
094290     SET  P927-CREATE-CALL              TO TRUE
094300     SET  P927-CREATED-BY-IVRCALL       TO TRUE
094310     MOVE TRCN-ASGN-TYPE                TO P927-CALL-ASGN-TYPE
094320     MOVE TRCN-ORIGIN-STN               TO P927-CALL-ORG-STATION
094330     MOVE TRCN-TRAIN-ASGN               TO P927-TRAIN-SYM
094340     MOVE SPACES                        TO P927-TRAIN-CODE
094350     MOVE 'O'                           TO P927-CALL-TYPE
094360     MOVE TRCN-FORMAT-TYPE              TO P927-CALL-TRAIN-TYPE
094370     MOVE 1                             TO J
094380     MOVE SCR08S-CC(SUB1)               TO P927-CALL-INFO
094390     MOVE 'N'                           TO TASK-TRAIN-DELAYED
094400     MOVE TRCN-TRAIN-ASGN               TO P927-TRCN-TRAIN-ASGN
094410     MOVE TRCN-FNL-STN                  TO P927-CALL-FNL-STATION
094420     MOVE TRCN-JOB-TYPE                 TO P927-CALL-JOB-TYPE
094430
094440     EXEC CICS LINK
094450               PROGRAM (P927-PGM)
094460               COMMAREA(P927-COMMAREA-PARMS)
094470               LENGTH  (P927-LGTH)
094480               RESP    (WS-RESPONSE)
094490     END-EXEC
094500     MOVE WS-RESPONSE                   TO FILE-STATUS
094510
094520     IF SUCCESS
094530        MOVE P927-TASK-KEY              TO HOLD-TASK-KEY
094540     ELSE
094550        MOVE 'P6155-1'                  TO ERR-PARAGRAPH
094560        MOVE TASKTIME                   TO ERR-KEY
094570        PERFORM P9999-GOT-PROBLEM
094580     END-IF
094590     .
094600*
094610 P6160-CREATE-UPDATE-VRU-INFO.
094620*
094630     MOVE SPACES                        TO VRUINFO-FILE
094640     MOVE HOLD-TASK-KEY                 TO VRUINFO-TASK-KEY
094650
094660     PERFORM P9310-READ-UPDATE-VRUINFO
094670     EVALUATE TRUE
094680        WHEN SUCCESS
094690             PERFORM P6165-UPDATE-VRU-INFO
094700        WHEN NO-RECORD-FND
094710             PERFORM P6167-WRITE-VRU-INFO
094720        WHEN OTHER
094730             CONTINUE
094740     END-EVALUATE
094750     .
094760*
094770 P6165-UPDATE-VRU-INFO.
094780*    FIND THE MATCHING EMP NBR OR MATCHING CRAFT CODE
094790     PERFORM VARYING VRUINFO-CC-SUB FROM 1 BY 1
094800        UNTIL VRUINFO-CC-SUB             > VRUINFO-CC-MAX
094810        IF (P08S-TSQ-EMP-SEL(SUB2) =
094820                          VRUINFO-EMP-NBR(VRUINFO-CC-SUB))
094830        OR (SCR08S-CC(SUB1) =
094840                          VRUINFO-CRAFT          (VRUINFO-CC-SUB))
094850           MOVE P08S-TSQ-EMP-SEL(SUB2)
094860                       TO VRUINFO-EMP-NBR(VRUINFO-CC-SUB)
094870           MOVE SCR08S-CC(SUB1)
094880                       TO VRUINFO-CRAFT          (VRUINFO-CC-SUB)
094890           MOVE P08S-TSQ-CALLED-CODE(SUB2)
094900                       TO WS-FUNCTION-CODE
094910           IF IVR-CALL-FUNCTION
094920              SET EMP-UNDER-VRU-CONTROL          (VRUINFO-CC-SUB)
094930                       TO TRUE
094940           END-IF
094950           IF CALL-FUNCTION
094960           OR VACATE-FUNCTION
094970              SET CMA-CALL-SUCCESSFUL            (VRUINFO-CC-SUB)
094980                       TO TRUE
094990           END-IF
095000           MOVE P08S-TSQ-DECKEY(SUB2)
095010                  TO VRUINFO-DECISION-KEY(VRUINFO-CC-SUB)
095020           SET          VRU-REQUIRED-CRAFT       (VRUINFO-CC-SUB)
095030                       TO TRUE
095040           IF P08S-TSQ-SAVE-CBCK-FLAG(SUB2) = 'Y'
095050              SET VRU-SHORT-CALLED(VRUINFO-CC-SUB) TO TRUE
095060           END-IF
095070
095080           MOVE SCR08S-DIST
095090                       TO VRUINFO-XFER-DD(VRUINFO-CC-SUB)
095100           MOVE SCR08S-SUB-DIST
095110                       TO VRUINFO-XFER-SD(VRUINFO-CC-SUB)
095120           MOVE VRUINFO-CC-MAX
095130                       TO VRUINFO-CC-SUB
095140           IF WS-AHM-KEY > SPACES
095150              PERFORM P6168-UPDATE-AHM3
095160           END-IF
095170        END-IF
095180     END-PERFORM
095190     PERFORM P9320-REWRITE-VRUINFO
095200     .
095210*
095220 P6167-WRITE-VRU-INFO.
095230*
095240     MOVE SPACES                   TO VRUINFO-FILE
095250*
095260*    LOAD THE VRUINFO CRAFTS IN THEIR PROPER POSITIONS BASED ON
095270*    WSAJDEFN COPYBOOK
095280*
095290     MOVE TRCN-ASSIGNMENT          TO JOB-DEF-CHECK
095300     SET JOB-IND TO 1
095310     SEARCH JOB-ENTRIES VARYING JOB-IND
095320        AT END
095330           MOVE 1 TO SUB8
095340        WHEN JOB-DEF-TBL-CLASS(JOB-IND) = JOB-DEF-CLASS
095350           SET SUB8 TO JOB-IND
095360     PERFORM VARYING VRUINFO-CC-SUB FROM 1 BY 1
095370        UNTIL VRUINFO-CC-SUB > VRUINFO-CC-MAX
095380        MOVE JOB-DEF-TBL-CRAFT-CODE(SUB8, VRUINFO-CC-SUB)
095390                           TO VRUINFO-CRAFT(VRUINFO-CC-SUB)
095400     END-PERFORM
095410
095420     MOVE SPACES                        TO WS-AHIST-MISC
095430                                           WS-AHM-KEY
095440     MOVE HOLD-TASK-KEY                 TO VRUINFO-TASK-KEY
095450                                           TASK-LOCATION-KEY
095460     MOVE HOLD-TASK-KEY(1:14)           TO VRUINFO-EFF-DTTM-CE
095470
095480     MOVE SPACES                        TO WS-AHIST
095490                                           WS-AHIST-MISC
095500     MOVE P08S-TSQ-JSKEY2(SUB2)         TO WORK-JS-KEY2
095510     MOVE WK-JSK2-ASGN                  TO JOB-DEF-CHECK
095520     IF JOB-DEF-LOCAL-ASGN
095530        MOVE JOB-DEF-LOCAL-POOL         TO AHK1-POOL
095540     ELSE
095550        MOVE JOB-DEF-YARD-POOL          TO AHK1-POOL
095560     END-IF
095570     MOVE WK-JSK2-ASGN-DIST             TO AHK1-DIST
095580     MOVE WK-JSK2-ASGN-SUB-DIST         TO AHK1-SUB-DIST
095590     MOVE P08S-TSQ-CALL-DATE     (SUB2) TO AHK1-EFF-DATE
095600     MOVE WK-JSK2-ASGN-START-TIME       TO AHK1-EFF-TIME
095610     MOVE P08S-TSQ-JSKEY2(SUB2)         TO WORK-JS-KEY2
095620     MOVE TASK-CALL-TRAIN               TO AHK1-AJOB-ASGN
095630     MOVE AH-KEY-1                      TO VRUINFO-AHIST-KEY
095640                                           AHM-AHIST-KEY
095650*
095660*    NOW CHECK THE AHM FOR EMPLOYEES ALREADY PROCESSED
095670*    THIS INCLUDES THE EMPLOYEE BEING PROCESSED NOW
095680*
095690     SET  AHM-EMPLOYEE-RECORD           TO TRUE
095700     MOVE AHM-KEY                       TO AHMKEY
095710     MOVE '0'                           TO WS-AHMISC-FOUND-FLAG
095720     MOVE ZEROES                        TO I
095730     PERFORM P8700-STRTBR-AHMFILE
095740     IF SUCCESS
095750        SET  AHMISC-NOT-DONE            TO TRUE
095760        PERFORM UNTIL AHMISC-DONE
095770           PERFORM P8710-READNXT-AHMFILE
095780           IF  SUCCESS
095790           AND AHM-AHIST-KEY             = VRUINFO-AHIST-KEY
095800           AND AHM-EMPLOYEE-RECORD
095810              IF  AHM-EMPLOYEE-NUMBER    > SPACES
095820              AND NOT AHM-VACANT-TURN
095830*                FIND THE EQUAL CRAFT IN THE VRUINFO
095840*                SET I TO THE SUBSCRIPT VALUE
095850                 PERFORM VARYING SUB8 FROM 1 BY 1
095860                   UNTIL SUB8 > VRUINFO-CC-MAX
095870                    IF AHM-CRAFT-CODE    = VRUINFO-CRAFT(SUB8)
095880                       MOVE SUB8           TO I
095890                       MOVE VRUINFO-CC-MAX TO SUB8
095900                    END-IF
095910                 END-PERFORM
095920                 MOVE AHM-EMPLOYEE-NUMBER
095930                                        TO VRUINFO-EMP-NBR(I)
095940                                           MSTRNBRK
095950                 IF AHM-EMPLOYEE-NUMBER  = P08S-TSQ-EMP-SEL(SUB2)
095960                    MOVE P08S-TSQ-DECKEY(SUB2)
095970                                        TO VRUINFO-DECISION-KEY(I)
095980                    IF P08S-TSQ-SAVE-CBCK-FLAG(SUB2) = 'Y'
095990                       SET VRU-SHORT-CALLED(I) TO TRUE
096000                    END-IF
096010                 END-IF
096020                 MOVE AHM-OD-FUNCTION   TO WS-FUNCTION-CODE
096030                 EVALUATE AHM-OD-FUNCTION
096040                    WHEN 'CAO*'
096050                    WHEN 'SCO*'
096060                         SET CALL-FUNCTION      TO TRUE
096070                    WHEN 'VAO*'
096080                         SET IVR-CALL-FUNCTION  TO TRUE
096090                 END-EVALUATE
096100                 IF IVR-CALL-FUNCTION
096110                    PERFORM P8505-READ-MASTER-ONLY
096120                    IF LAYOFF-CODE       = 'B8' OR 'B1'
096130                       SET  EMP-UNDER-VRU-CONTROL(I) TO TRUE
096140                    END-IF
096150                    IF WS-AHM-KEY NOT > SPACES
096160                       MOVE AHM-KEY     TO WS-AHM-KEY
096170                    END-IF
096180                 END-IF
096190                 SET  VRU-REQUIRED-CRAFT(I)     TO TRUE
096200                 IF CALL-FUNCTION
096210                 OR VACATE-FUNCTION
096220                    SET  CMA-CALL-SUCCESSFUL(I) TO TRUE
096230                 END-IF
096240                 MOVE SCR08S-DIST       TO VRUINFO-XFER-DD(I)
096250                 MOVE SCR08S-SUB-DIST   TO VRUINFO-XFER-SD(I)
096260              END-IF
096270           ELSE
096280              SET  AHMISC-DONE          TO TRUE
096290           END-IF
096300        END-PERFORM
096310        PERFORM P8720-ENDBR-AHMFILE
096320     END-IF
096330*
096340*    NOW SEARCH THE P08S-TSQ AREA FOR ANY EQUAL ASSIGNMENTS AND
096350*    ADD TO THE VRUINFO AS REQUIRED CRAFTS.
096360*
096370     MOVE P08S-TSQ-JSKEY2(SUB2)   TO WORK-JS-KEY2
096380     MOVE WK-JSK2-ASGN            TO WS-SAVE-JSK2-ASGN
096390     MOVE WK-JSK2-ASGN-START-TIME TO WS-SAVE-JSK2-STIME
096400     PERFORM VARYING SUBQ FROM 1 BY 1
096410       UNTIL SUBQ > P08S-TSQ-MAX
096420        IF SUBQ NOT = SUB2
096430           MOVE P08S-TSQ-JSKEY2(SUBQ)   TO WORK-JS-KEY2
096440           IF  WK-JSK2-ASGN              = WS-SAVE-JSK2-ASGN
096450           AND WK-JSK2-ASGN-START-TIME   = WS-SAVE-JSK2-STIME
096460*             BEFORE WE UPDATE THE VRUINFO, CHECK TO SEE IF THIS
096470*             EMP IS ALREADY ON THE VRUINFO (EARLIER OCCURANCE)
096480              IF P08S-TSQ-EMP-SEL(SUBQ) > SPACES
096490                 MOVE SPACE                TO WS-ALREADY-VINFOD-FL
096500                 PERFORM VARYING SUB8 FROM 1 BY 1
096510                   UNTIL SUB8 > VRUINFO-CC-MAX
096520                    IF P08S-TSQ-EMP-SEL(SUBQ)
096530                                           = VRUINFO-EMP-NBR(SUB8)
096540                       SET WS-ALREADY-VINFOD      TO TRUE
096550                       MOVE VRUINFO-CC-MAX TO SUB8
096560                    END-IF
096570                 END-PERFORM
096580              END-IF
096590              IF NOT WS-ALREADY-VINFOD
096600*                FIND THE EQUAL CRAFT IN THE VRUINFO
096610*                SET I TO THE SUBSCRIPT VALUE
096620                 PERFORM VARYING SUB8 FROM 1 BY 1
096630                   UNTIL SUB8 > VRUINFO-CC-MAX
096640                    IF WK-JSK2-ASGN-CC     = VRUINFO-CRAFT(SUB8)
096650                       MOVE SUB8           TO I
096660                       MOVE VRUINFO-CC-MAX TO SUB8
096670                    END-IF
096680                 END-PERFORM
096690                 MOVE P08S-TSQ-CALLED-CODE(SUBQ)
096700                                        TO WS-FUNCTION-CODE
096710                 IF IVR-CALL-FUNCTION
096720                    SET EMP-UNDER-VRU-CONTROL(I) TO TRUE
096730                 END-IF
096740                 IF CALL-FUNCTION
096750                 OR VACATE-FUNCTION
096760                    SET  CMA-CALL-SUCCESSFUL(I) TO TRUE
096770                 END-IF
096780                 MOVE P08S-TSQ-EMP-SEL(SUBQ)
096790                                        TO VRUINFO-EMP-NBR(I)
096800                 MOVE P08S-TSQ-DECKEY(SUBQ)
096810                                        TO VRUINFO-DECISION-KEY(I)
096820                 MOVE WK-JSK2-ASGN-CC   TO VRUINFO-CRAFT  (I)
096830                 IF P08S-TSQ-SAVE-CBCK-FLAG(SUBQ) = 'Y'
096840                    SET VRU-SHORT-CALLED(I) TO TRUE
096850                 END-IF
096860                 SET  VRU-REQUIRED-CRAFT(I)     TO TRUE
096870                 MOVE SCR08S-DIST       TO VRUINFO-XFER-DD(I)
096880                 MOVE SCR08S-SUB-DIST   TO VRUINFO-XFER-SD(I)
096890              END-IF
096900           END-IF
096910        END-IF
096920     END-PERFORM
096930*
096940*    NOW SEARCH THE TLB OF ANY REMAINING ASSIGNMENTS TO BE SCAL'D
096950*    AND ADD TO THE VRUINFO AS REQUIRED CRAFTS
096960*
096970     PERFORM VARYING SUBV FROM 1 BY 1
096980       UNTIL SUBV > 100
096990        IF WS-VRU-SCAL-PARM(SUBV) = SPACES
097000           MOVE 100 TO SUBV
097010        ELSE
097020           MOVE P08S-TSQ-JSKEY2(SUB2)     TO JSKEY2
097030           PERFORM P8600-READ-JSKEY2
097040           IF NOT SUCCESS
097050              MOVE 'P4010-1'              TO ERR-PARAGRAPH
097060              MOVE JSKEY2                 TO ERR-KEY
097070              PERFORM P9999-GOT-PROBLEM
097080           END-IF
097090           IF  WS-VRU-SCAL-DIST (SUBV) = JSK1-ASGN-DIST
097100           AND WS-VRU-SCAL-SDIST(SUBV) = JSK1-ASGN-SUB-DIST
097110           AND WS-VRU-SCAL-ASGN (SUBV) = JSK1-ASGN
097120              PERFORM VARYING SUB8 FROM 1 BY 1
097130                UNTIL SUB8 > VRUINFO-CC-MAX
097140                IF WS-VRU-SCAL-CC(SUBV) = VRUINFO-CRAFT(SUB8)
097150                   MOVE WS-VRU-SCAL-EMP(SUBV)
097160                                          TO VRUINFO-EMP-NBR(SUB8)
097170                   SET  VRU-REQUIRED-CRAFT(SUB8)  TO TRUE
097180                   SET  CMA-CALL-SUCCESSFUL(SUB8) TO TRUE
097190                   MOVE SCR08S-DIST       TO VRUINFO-XFER-DD(SUB8)
097200                   MOVE SCR08S-SUB-DIST   TO VRUINFO-XFER-SD(SUB8)
097210                   MOVE VRUINFO-CC-MAX    TO SUB8
097220                END-IF
097230              END-PERFORM
097240           END-IF
097250        END-IF
097260     END-PERFORM
097270
097280     SET VRUINFO-YARD-BUILT-NEW       TO TRUE
097290
097300     EXEC CICS WRITE
097310               DATASET(VRU-INFO-FILE)
097320               FROM   (VRUINFO-FILE)
097330               LENGTH (VRU-INFO-TERM-RLGTH)
097340               RIDFLD (VRUINFOKEY)
097350               RESP   (WS-RESPONSE)
097360     END-EXEC
097370     MOVE WS-RESPONSE                 TO FILE-STATUS
097380     IF NOT SUCCESS
097390        MOVE 'P6167-1'                TO ERR-PARAGRAPH
097400        MOVE VRUINFOKEY               TO ERR-KEY
097410        PERFORM P9999-GOT-PROBLEM
097420     END-IF
097430
097440     IF WS-AHM-KEY > SPACES
097450        PERFORM P6168-UPDATE-AHM3
097460     END-IF
097470     .
097480*
097490 P6168-UPDATE-AHM3.
097500*
097510     MOVE WS-AHM-KEY                  TO AHM-KEY
097520     SET AHM-EMPLOYEE-RECORD-3        TO TRUE
097530     MOVE AHM-KEY                     TO AHMKEY
097540     PERFORM P8810-READ-AHM
097550     IF SUCCESS
097560        PERFORM P8820-READ-AHM-UPDATE
097570        MOVE VRUINFO-TASK-KEY         TO AHM3-VRUINFO-TASK-KEY
097580        PERFORM P8830-REWRITE-AHM
097590     ELSE
097600        MOVE SPACES                   TO AHIST-EMPLOYEE-DATA-3
097610        MOVE VRUINFO-TASK-KEY         TO AHM3-VRUINFO-TASK-KEY
097620        PERFORM P8835-WRITE-AHM
097630     END-IF
097640     .
097650*
097660 P6170-SETUP-P0ESCOMM.
097670*
097680     MOVE HOLD-TASK-KEY               TO TASKTIME
097690     PERFORM P8661-READ-TASK
097700     IF NOT SUCCESS
097710        MOVE -1                       TO SCR08S-FUNC-CURSOR(SUB1)
097720        MOVE REV-VIDEO                TO SCR08S-FUNC-HI    (SUB1)
097730*            'NO TASK RECORD FOUND'
097740        MOVE 'N246'                   TO MSGLOG-CODE
097750        PERFORM P9000-SEND-MAP-AND-RETURN
097760     END-IF
097770     MOVE SPACES                      TO P0ES-COMMAREA-PARMS
097780     SET  P0ES-CALL-TO-SERVICE        TO TRUE
097790     SET  P0ES-FIRST-TIME-CALL        TO TRUE
097800     MOVE P08S-TSQ-EMP-SEL(SUB2)      TO P0ES-EMP-NBR
097810     MOVE HOLD-TASK-KEY               TO P0ES-TASK-KEY
097820     MOVE '0000'                      TO P0ES-ZEROES
097830     MOVE SCR08S-CC(SUB1)             TO WS-CRAFT-CODE-CHECK
097840     MOVE WS-IVR-CALL-PROF            TO P0ES-CALL-PROFILE
097850     MOVE WS-SHORT-CALL-REQ           TO P0ES-SHORT-CALL-REQ
097860     MOVE P08SCA-RESUB-FLAG(SUB1)     TO P0ES-RESUB-FLAG
097870*CNC0579B - BEG
097880     IF P08S-TSQ-DEC-RPT-LOCATION(SUB2) > SPACES
097890        MOVE P08S-TSQ-DEC-RPT-LOCATION(SUB2)
097900                                      TO P0ES-VCNY-RPT-LOCATION
097910     ELSE
097920        MOVE SPACES                   TO P0ES-VCNY-RPT-LOCATION
097930     END-IF
097940*CNC0579B - END
097950     .
097960*
097970 P6180-LINK-TO-0ES.
097980*
097990     EXEC CICS LINK
098000               PROGRAM (P0ES-PGM)
098010               COMMAREA(P0ES-COMMAREA-PARMS)
098020               LENGTH  (P0ES-LGTH)
098030               RESP    (WS-RESPONSE)
098040     END-EXEC
098050     MOVE WS-RESPONSE                 TO FILE-STATUS
098060     IF P0ES-IVR-ABEND
098070     OR NOT SUCCESS
098080        MOVE 'P6180-1'                TO ERR-PARAGRAPH
098090        MOVE 'P0ESLINK'               TO ERR-KEY
098100        PERFORM P9999-GOT-PROBLEM
098110     END-IF
098120     .
098130*
098140 P6200-DISPLAY-QUAL-INFO.
098150*
098160     IF P08S-TSQ-EMP-SEL(SUB2) > SPACES
098170*CNC0572 - BEG
098180        MOVE ZEROES TO SUB5
098190*CNC0572 - END
098200        MOVE P08S-TSQ-EMP-SEL(SUB2)  TO EMP-NBR OF WS-MSTR
098210                                        MSTRNBRK
098220        PERFORM P8505-READ-MASTER-ONLY
098230        MOVE PSTCA-VARIABLE-AREA    TO P08S-TSQ-SAVE-VARIABLE-AREA
098240        INITIALIZE P08D-COMMAREA-PARMS
098250        MOVE TRCN-DESCRIPTION        TO P08DCA-TRN-DESC
098260        MOVE P08S-TSQ-EMP-SEL(SUB2)  TO P08DCA-EMP-NBR
098270        MOVE TRCN-ORIGIN-STN         TO P08DCA-ORIGIN-STN
098280        MOVE TRCN-TRAIN-ASGN         TO P08DCA-TRAIN-ASGN
098290        MOVE TRCN-ROUTE-DESIGNATION  TO P08DCA-ROUTE-DSGN
098300        MOVE SCR08S-CC(SUB2)         TO WS-CRAFT-CODE-CHECK
098310        PERFORM PXXXX-GET-MASTER-CRAFT
098320        IF WS-CRAFT-CODE-CHECK > SPACES
098330           MOVE WS-CRAFT-CODE-CHECK  TO P08DCA-CRAFT
098340        ELSE
098350           MOVE SCR08S-CC(SUB2)      TO P08DCA-CRAFT
098360        END-IF
098370        MOVE WK-CALL-DATE            TO P08DCA-CALL-DATE
098380        MOVE SPACES                TO WORK-CNTLKEY
098390        MOVE '13'                  TO WK-CNTL-REC-TYPE
098400        MOVE TRCN-DIST             TO WK-CNTL-DIST
098410        MOVE TRCN-SDIST            TO WK-CNTL-SUB-DIST
098420        MOVE 'A'                   TO WK-CNTL-ASGN-QUAL-TYPE
098430        MOVE TRCN-TRAIN-ASGN       TO WK-CNTL-ASGN-QUAL
098440        MOVE SCR08S-CC(SUB2)       TO WK-CNTL-ASGN-QUAL-7-8
098450        MOVE WORK-CNTLKEY          TO CNTLKEY
098460        PERFORM P8610-READ-CNTLFILE
098470        IF NOT SUCCESS
098480           MOVE '**'               TO WK-CNTL-ASGN-QUAL-5-6
098490           MOVE WORK-CNTLKEY       TO CNTLKEY
098500           PERFORM P8610-READ-CNTLFILE
098510        END-IF
098520        IF SUCCESS
098530          PERFORM VARYING SUB3 FROM 1 BY 1
098540            UNTIL SUB3 > 5
098550            IF CNTL-QUALIFICATION-TABLE-ENTRY(SUB3) > SPACE
098560               ADD 1 TO SUB5
098570               MOVE CNTL-QUALIFICATION-TABLE-ENTRY(SUB3) TO
098580                    WS-ASGN-QUAL(SUB5)
098590            END-IF
098600          END-PERFORM
098610        END-IF
098620        MOVE WK-CNTL-ASGN-QUAL-7-8 TO WS-HOLD-CC
098630        IF WS-HOLD-CC = 'FI' OR 'SE'
098640           MOVE 'SE'               TO WS-HOLD-CC
098650        END-IF
098660        IF WS-HOLD-CC = 'S1' OR 'S2'
098670           MOVE 'SW'               TO WS-HOLD-CC
098680        END-IF
098690        IF WK-CNTL-ASGN-QUAL-7-8 = ('B1' OR 'B2')
098700           MOVE 'BK'               TO WS-HOLD-CC
098710        END-IF
098720        PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
098730           IF WS-HOLD-CC = TRCN-SPCL-QUAL-CC(SUBA)
098740              IF TRCN-SPCL-QUAL(SUBA) > SPACES
098750                ADD 1                     TO SUB5
098760                MOVE TRCN-SPCL-QUAL(SUBA) TO WS-ASGN-QUAL(SUB5)
098770              END-IF
098780           END-IF
098790        END-PERFORM
098800*CNC0572 - BEG
098810        PERFORM VARYING SUBA FROM 1 BY 1 UNTIL SUBA > 4
098820           IF WS-HOLD-CC = TRCN-SPCL-QUAL2-CC(SUBA)
098830              IF TRCN-SPCL-QUAL2(SUBA) > SPACES
098840                 ADD 1                     TO SUB5
098850                 MOVE TRCN-SPCL-QUAL(SUBA) TO WS-ASGN-QUAL(SUB5)
098860              END-IF
098870           END-IF
098880        END-PERFORM
098890*CNC0572 - END
098900        PERFORM VARYING QUAL FROM 1 BY 1
098910*CNC0572 - BEG
098920***                            UNTIL QUAL > 6
098930                               UNTIL QUAL > 8
098940*CNC0572 - END
098950           IF WS-ASGN-QUAL(QUAL) > SPACES
098960              ADD 1                     TO SUB6
098970              MOVE WS-ASGN-QUAL(QUAL)   TO P08DCA-QUAL(SUB6)
098980           END-IF
098990        END-PERFORM
099000        PERFORM P7000-WRITE-TSQUEUE
099010        MOVE P08S-PGM              TO PSTCA-FROM-PROGRAM
099020        EXEC CICS XCTL
099030                 PROGRAM(P08D-PGM)
099040                 COMMAREA(PSTCOMM-AREA)
099050                 LENGTH(PSTCOMM-LGTH)
099060                 RESP(WS-RESPONSE)
099070        END-EXEC
099080        MOVE WS-RESPONSE  TO FILE-STATUS
099090        IF NOT SUCCESS
099100           MOVE 'P6200-1' TO ERR-PARAGRAPH
099110           PERFORM P9999-GOT-PROBLEM
099120        END-IF
099130     ELSE
099140*       'FUNCTION NOT YET AVAILABLE'
099150        MOVE 'F003'       TO MSGLOG-CODE
099160        MOVE -1           TO SCR08S-FUNC-CURSOR(SUB2)
099170        PERFORM P9000-SEND-MAP-AND-RETURN
099180     END-IF.
099190*
099200 P6300-CHECK-TERR-QUAL.
099210*
099220     IF P08S-TSQ-EMP-SEL(SUB2) > SPACES
099230        SET QUAL-MSG-NO            TO TRUE
099240        MOVE 0                     TO COUNT3
099250        MOVE SPACES                TO WS-DATA-ARRAY
099260        MOVE P08S-TSQ-EMP-SEL(SUB2)  TO EMP-NBR OF WS-MSTR
099270                                        MSTRNBRK
099280                                        WS-TERR-EMP-NO
099290        PERFORM P8505-READ-MASTER-ONLY
099300        MOVE CRAFT IN WS-MSTR      TO WS-TERR-EMP-CC
099310        PERFORM P6301-GET-QUAL-LSP-LSD
099320        IF QUAL-MSG-YES
099330           IF PSTCA-SUB = 2
099340              STRING 'EMPLOYEE NOT QUALIFIED USE FUNCTION "QUAL"'
099350                     ' FOR DETAILS'
099360                     DELIMITED BY SIZE INTO SCR08S-PHONE(SUB1)
099370           ELSE
099380              STRING 'EMPLOYEE NOT QUALIFIED USE FUNCTION "QUAL"'
099390                     ' FOR DETAILS'
099400                     DELIMITED BY SIZE INTO SCR08S-PHONE(SUB1)
099410           END-IF
099420           MOVE YELLOW             TO SCR08S-PHONE-COL(SUB1)
099430           SET EMP-DISQUALIFIED    TO TRUE
099440           IF WS-CALL-NO
099450              SET P08S-TSQ-RESTRICTED(SUB2)      TO TRUE
099460           ELSE
099470              MOVE SPACES        TO P08S-TSQ-NO-CALL-CODE(SUB2)
099480           END-IF
099490        END-IF
099500     END-IF.
099510*
099520 P6301-GET-QUAL-LSP-LSD.
099530*
099540     MOVE SPACES                 TO  WS-TRR-STORAGE
099550     MOVE TRCN-ORIGIN-STN        TO  TRR-FROM-LOC
099560     MOVE TRCN-FNL-STN           TO  TRR-TO-LOC
099570     MOVE TRCN-ROUTE-DESIGNATION TO  TRR-ROUTE-DSGN
099580     PERFORM PSQL-TRROPN06
099590     IF SQL-SUCCESS
099600        MOVE 0                  TO COUNT1
099610        PERFORM UNTIL NO-ROW-FND
099620           PERFORM PSQL-TRRFET06
099630           IF SQL-SUCCESS
099640              ADD 1             TO COUNT1
099650              MOVE TRR-LINE-SEG TO WS-LINE-SEG(COUNT1)
099660           ELSE
099670              IF NO-ROW-FND
099680                 MOVE COUNT1    TO COUNTMAX
099690              END-IF
099700           END-IF
099710        END-PERFORM
099720        MOVE 0 TO COUNT2 COUNT1
099730        PERFORM UNTIL COUNT1 >= COUNTMAX
099740           ADD 1                TO COUNT1
099750           IF WS-LINE-SEG(COUNT1) > SPACES
099760              MOVE WS-LINE-SEG(COUNT1) TO LSP-LINE-SEG
099770              MOVE WK-CALL-DATE        TO LSP-EXP-TS-DT
099780              PERFORM PSQL-LSPSEL04
099790              IF SQL-SUCCESS
099800                 IF LSP-EXP-TS = C-MAX-TS
099810                    ADD 1              TO COUNT2
099820                    MOVE LSP-LINE-SEG  TO WK-LINE-SEG(COUNT2)
099830                    MOVE LSP-EXP-TS    TO WK-EXP-TS(COUNT2)
099840                 ELSE
099850                    SET DE-YYMMDD-FORMAT TO TRUE
099860                    MOVE LSP-EXP-TS-DT TO DE-YYMMDD
099870                    PERFORM P8998-DATEEDIT
099880                    MOVE DE-CCYYMMDD   TO DE-COMPARE1-DATE
099890
099900                    SET DE-YYMMDD-FORMAT TO TRUE
099910                    MOVE WK-CALL-DATE  TO DE-YYMMDD
099920                    PERFORM P8998-DATEEDIT
099930                    IF DE-COMPARE1-DATE > DE-CCYYMMDD
099940                       ADD 1           TO COUNT2
099950                       MOVE LSP-LINE-SEG TO WK-LINE-SEG(COUNT2)
099960                       MOVE LSP-EXP-TS TO WK-EXP-TS(COUNT2)
099970                    END-IF
099980                 END-IF
099990              END-IF
100000           END-IF
100010        END-PERFORM
100020        MOVE COUNT2                    TO COUNTMAX
100030        MOVE 0                         TO COUNT2
100040        PERFORM UNTIL COUNT2 >= COUNTMAX
100050           ADD 1 TO COUNT2
100060           IF WK-LINE-SEG(COUNT2) > SPACES
100070              MOVE WK-LINE-SEG(COUNT2) TO LSD-LINE-SEG
100080              MOVE WK-EXP-TS(COUNT2)   TO LSD-EXP-TS
100090              MOVE WS-TERR-EMP-CC      TO WS-CRAFT-CODE-CHECK
100100              PERFORM PXXXX-GET-MASTER-CRAFT
100110              IF WS-CRAFT-CODE-CHECK > SPACES
100120                 MOVE WS-CRAFT-CODE-CHECK  TO LSD-CRFT-CD
100130              ELSE
100140                 MOVE CRAFT IN WS-MSTR     TO LSD-CRFT-CD
100150              END-IF
100160              PERFORM PSQL-LSDOPN02
100170              PERFORM UNTIL NO-ROW-FND
100180                 ADD 1 TO COUNT3
100190                 PERFORM PSQL-LSDFET02
100200                 IF SQL-SUCCESS
100210                    MOVE LSD-QUAL-CD      TO WS-QUAL-CODE(COUNT3)
100220                    MOVE LSD-INIT-TRIPS   TO WS-LSD-TRIPS(COUNT3)
100230                    MOVE LSD-INIT-DAYS    TO
100240                         WS-LSD-INIT-DAYS(COUNT3)
100250                 END-IF
100260              END-PERFORM
100270              PERFORM PSQL-LSDCLO02
100280              COMPUTE COUNT3 = COUNT3 - 1
100290           END-IF
100300        END-PERFORM
100310        PERFORM PSQL-TRRCLO06
100320     END-IF
100330     MOVE 'N'                          TO DONE-CODE1
100340     SET WS-CALL-NO                    TO TRUE
100350     PERFORM VARYING SUB4 FROM 1 BY 1 UNTIL DONE1
100360        IF WS-QUAL-CODE(SUB4) > SPACES
100370           MOVE SPACES                 TO WS-QUAL-FILE
100380           MOVE P08S-TSQ-EMP-SEL(SUB2) TO QUAL-EMP-NO
100390           MOVE WS-QUAL-CODE(SUB4)     TO QUALIFICATION
100400                                          WS-TEMP-QUAL
100410           MOVE QUALEMP-KEY            TO QUALEMP
100420           EXEC CICS READ
100430                     DATASET(QUAL-FILE-VIA-QUALEMP)
100440                     INTO(WS-QUAL-FILE)
100450                     LENGTH(QUALEMP-RLGTH)
100460                     RIDFLD(QUALEMP)
100470                     KEYLENGTH(QUALEMP-KLGTH)
100480                     RESP(WS-RESPONSE)
100490           END-EXEC
100500           MOVE WS-RESPONSE            TO FILE-STATUS
100510           IF SUCCESS
100520              SET WS-CALL-YES          TO TRUE
100530           ELSE
100540              SET QUAL-MSG-YES         TO TRUE
100550           END-IF
100560        ELSE
100570           SET DONE1                   TO TRUE
100580        END-IF
100590     END-PERFORM.
100600*
100610*
100620*=================================================================
100630 P6500-CHECK-IVR-CALL-COMP.
100640*=================================================================
100650     IF  CNT-UNDER-VRU-CONTROL    > +0
100660     AND CNT-VRU-UNSUCCESSFUL NOT > +0
100670        MOVE TASK-LOCATION-KEY TO TASKTIME
100680        PERFORM P8660-READ-TASK-UPD
100690        SET TASK-VRU-CALL-INIT TO TRUE
100700        PERFORM P8662-REWRITE-TASK
100710     END-IF
100720
100730     IF CNT-UNDER-VRU-CONTROL > +0
100740     OR CNT-VRU-UNSUCCESSFUL  > +0
100750        CONTINUE
100760     ELSE
100770*       ANY EMPS NOT PROCESSED BY VRU OR CMA SET BACK
100780*       TO "ORDER - VRU FINISHED"
100790*       OTHERWISE DELETE THE TASK RECORD
100800        IF CNT-CMA-NOT-CALLED > +0
100810           MOVE TASK-LOCATION-KEY TO TASKTIME
100820           PERFORM P8660-READ-TASK-UPD
100830           IF SUCCESS
100840              MOVE SPACE             TO TASK-VRU-CALL-FLAG
100850              PERFORM P8662-REWRITE-TASK
100860           ELSE
100870              EXEC CICS UNLOCK
100880                        DATASET(TASK-VIA-EFF-TIME)
100890                        RESP(WS-RESPONSE)
100900              END-EXEC
100910           END-IF
100920        ELSE
100930           MOVE TASK-LOCATION-KEY TO TASKTIME
100940           EXEC CICS DELETE
100950                     DATASET(TASK-VIA-EFF-TIME)
100960                     RIDFLD(TASK-EFF-TIME-KEY)
100970                     RESP(WS-RESPONSE)
100980           END-EXEC
100990        END-IF
101000     END-IF
101010     .
101020*C1174-BEG
101030*=================================================================
101040 P6600-REMOVE-TRACKING.
101050*=================================================================
101060     PERFORM P9700-READ-TSQUEUE
101070     IF SUCCESS
101080        PERFORM VARYING SUB7 FROM 1 BY 1
101090           UNTIL SUB7 > TSQ-MAX
101100***        IF P08S-TSQ-EMP-SEL(SUB7) > SPACES
101110           IF P08S-TSQ-ARRAY(SUB7) > SPACES
101120              IF P08S-TSQ-EMP-SEL(SUB7) > '000000000'
101130                 MOVE P08S-TSQ-EMP-SEL(SUB7) TO MSTRNBRK WS-MSTR
101140                 EXEC CICS READ
101150                           DATASET(MSTR-VIA-EMP-NBR)
101160                           INTO(WS-MSTR)
101170                           LENGTH(MSTRENBR-RLGTH)
101180                           RIDFLD(MSTRNBRK)
101190                           KEYLENGTH(MSTRENBR-KLGTH)
101200                          RESP(WS-RESPONSE)
101210                 END-EXEC
101220                 MOVE WS-RESPONSE TO FILE-STATUS
101230                 IF NOT SUCCESS
101240                    MOVE 'P6600-1' TO ERR-PARAGRAPH
101250                    MOVE MSTRNBRK TO ERR-KEY
101260                    PERFORM P9999-GOT-PROBLEM
101270                 END-IF
101280                 IF LAYOFF-CODE-1 IN WS-MSTR NOT = 'B'
101290                  MOVE SPACES              TO P938-COMMAREA-PARMS
101300                  SET P938-CANCEL-FUNCTION  TO TRUE
101310                  SET P938-ASGN-AJ          TO TRUE
101320*C1184 - BEG
101330***               MOVE P08S-TSQ-JSKEY2(SUB7) TO W-JS-KEY2
101340***               MOVE W-JSK2-ASGN-DIST     TO P938-ASGN-DIST
101350***               MOVE W-JSK2-ASGN-SUB-DIST TO P938-ASGN-SUB-DIST
101360***               MOVE W-JSK2-ASGN          TO P938-ASGN-ASGN
101370***               MOVE W-JSK2-ASGN-CC       TO P938-ASGN-CC
101380                  MOVE P08S-TSQ-JSKEY2(SUB7) TO JSKEY2
101390                  PERFORM P8600-READ-JSKEY2
101400                  IF NOT SUCCESS
101410                     MOVE 'P6600-2'        TO ERR-PARAGRAPH
101420                     MOVE JSKEY2           TO ERR-KEY
101430                     PERFORM P9999-GOT-PROBLEM
101440                  END-IF
101450                  MOVE JSK1-ASGN-DIST      TO P938-ASGN-DIST
101460                  MOVE JSK1-ASGN-SUB-DIST  TO P938-ASGN-SUB-DIST
101470                  MOVE JSK1-ASGN           TO P938-ASGN-ASGN
101480                  MOVE JSK1-ASGN-CC        TO P938-ASGN-CC
101490*C1184 - END
101500                  MOVE P08S-TSQ-CALL-DATE-TIME(SUB7)
101510                                      TO P938-EFF-DATE-TIME
101520                  MOVE PSTCA-TIME-ZONE      TO P938-TIME-ZONE
101530                  SET P938-ASGN-IS-HOME     TO TRUE
101540                  SET P938-CAN-ANNULL-JOB   TO TRUE
101550                  SET P938-CANCEL-IN-TOWN   TO TRUE
101560*C1184 - BEG
101570***               SET AJ-ASGN               TO TRUE
101580*C1184 - END
101590                  EXEC CICS LINK
101600                            PROGRAM(P938-PGM)
101610                            COMMAREA(P938-COMMAREA-PARMS)
101620                            LENGTH(P938-LGTH)
101630                            RESP(WS-RESPONSE)
101640                  END-EXEC
101650                  MOVE WS-RESPONSE          TO FILE-STATUS
101660                  IF NOT SUCCESS
101670                     MOVE 'P6600-3'         TO ERR-PARAGRAPH
101680                     MOVE 'P938LINK'        TO ERR-KEY
101690                     PERFORM P9999-GOT-PROBLEM
101700                  END-IF
101710                 END-IF
101720              END-IF
101730           ELSE
101740              MOVE TSQ-MAX                 TO SUB7
101750           END-IF
101760        END-PERFORM
101770     END-IF
101780     .
101790*C1174-END
101800*=================================================================
101810 P7000-FIND-ASGN-CLASS.
101820*=================================================================
101830     IF SUB1 > ARRAY-MAX
101840        MOVE 1 TO SUB1
101850     END-IF
101860
101870     MOVE SPACES                       TO WS-ASGN-MERG-CLASS
101880                                          WORK-CNTLKEY
101890     MOVE SCR08S-ASGN(SUB1)            TO WS-MERG-KEY
101900     IF UFP-ASGN
101910        MOVE '3A'                      TO WK-CNTL-REC-TYPE
101920        MOVE WS-MERG-DIST              TO WK-CNTL-DIST
101930        MOVE WS-MERG-SUB               TO WK-CNTL-SUB-DIST
101940        MOVE WS-MERG-POOL              TO WK-CNTL-POOL
101950        MOVE 'F'                       TO WK-CNTL-POOL-TYPE
101960        MOVE WORK-CNTLKEY              TO CNTLKEY
101970        PERFORM P8610-READ-CNTLFILE
101980        IF SUCCESS
101990           MOVE CNTL-POOL-MERGER-CLASS TO WS-ASGN-MERG-CLASS
102000        END-IF
102010     ELSE
102020        IF AJ-ASGN
102030           MOVE WS-TRCN-FILE           TO SAVE-TRCN-FILE
102040           MOVE SPACES                 TO TRCN-KEY3
102050           MOVE WS-MERG-DIST           TO TRCN-DIST3
102060           MOVE WS-MERG-SUB            TO TRCN-SDIST3
102070           MOVE WS-MERG-REST           TO TRCN-ASSIGNMENT
102080           MOVE TRCN-KEY3              TO TRCNKEY3
102090           PERFORM P8630-READ-TRCNKEY3
102100           IF SUCCESS
102110              MOVE TRCN-MERGER-CLASS   TO WS-ASGN-MERG-CLASS
102120           END-IF
102130           MOVE SAVE-TRCN-FILE         TO WS-TRCN-FILE
102140        END-IF
102150     END-IF
102160     .
102170*
102180 P7000-WRITE-TSQUEUE.
102190*
102200*      WRITE MAP TSQUEUE
102210*
102220     EXEC CICS ASSIGN
102230          EXTDS(WS-CICS-EXTDS-CODE)
102240     END-EXEC
102250
102260     IF SCREEN-HAS-EXT-ATTR
102270        EXEC CICS SEND STRFIELD
102280                  FROM(WS-STRFIELD)
102290                  LENGTH(WS-STRFIELD-LGTH)
102300                  RESP(WS-RESPONSE)
102310        END-EXEC
102320        MOVE WS-RESPONSE           TO FILE-STATUS
102330        IF NOT SUCCESS
102340           MOVE 'P7000-1'          TO ERR-PARAGRAPH
102350           MOVE 'SEND STRFIELD'    TO ERR-KEY
102360           PERFORM P9999-GOT-PROBLEM
102370        END-IF
102380     END-IF
102390
102400     MOVE LENGTH OF WS-BUFFER-DATA TO WS-BUFFER-LGTH
102410     EXEC CICS RECEIVE BUFFER
102420               INTO(WS-BUFFER-DATA)
102430               LENGTH(WS-BUFFER-LGTH)
102440               RESP(WS-RESPONSE)
102450     END-EXEC
102460     MOVE WS-RESPONSE              TO FILE-STATUS
102470     IF NOT SUCCESS AND NOT EOC
102480        MOVE 'P7000-2'             TO ERR-PARAGRAPH
102490        MOVE 'RECEIVE BUFFER'      TO ERR-KEY
102500        PERFORM P9999-GOT-PROBLEM
102510     END-IF
102520     MOVE EIBCPOSN                 TO WS-BUFFER-CURSOR
102530
102540
102550     MOVE LENGTH OF WS-BUFFER-AREA TO P08STSQ-QLGTH
102560     MOVE EIBTRMID                 TO P08STSQ-MAP-TERM-ID
102570     EXEC CICS WRITEQ TS
102580               QUEUE(P08STSQ-MAP-QUEUE-ID)
102590               FROM(WS-BUFFER-AREA)
102600               LENGTH(P08STSQ-QLGTH)
102610               RESP(WS-RESPONSE)
102620     END-EXEC
102630     MOVE WS-RESPONSE              TO FILE-STATUS
102640     IF NOT SUCCESS
102650        MOVE 'P7000-3'             TO ERR-PARAGRAPH
102660        PERFORM P9999-GOT-PROBLEM
102670     END-IF
102680     MOVE EIBTRMID TO P08STSQ-CA-TERM-ID
102690     EXEC CICS WRITEQ TS
102700               QUEUE(P08STSQ-CA-QUEUE-ID)
102710               FROM(PSTCOMM-AREA)
102720               LENGTH(PSTCOMM-LGTH)
102730               ITEM(P08STSQ-QUEUE-ITEM)
102740               RESP(WS-RESPONSE)
102750     END-EXEC
102760     MOVE WS-RESPONSE              TO FILE-STATUS
102770     IF NOT SUCCESS
102780        MOVE 'P7000-4'             TO ERR-PARAGRAPH
102790        PERFORM P9999-GOT-PROBLEM
102800     END-IF.
102810*
102820 P7010-READ-TSQUEUE.
102830*
102840*              READ THE MAPS TSQUEUE
102850*
102860     MOVE LENGTH OF WS-BUFFER-AREA TO P08STSQ-QLGTH
102870     MOVE EIBTRMID                 TO P08STSQ-MAP-TERM-ID
102880     EXEC CICS READQ TS
102890               QUEUE(P08STSQ-MAP-QUEUE-ID)
102900               INTO(WS-BUFFER-AREA)
102910               LENGTH(P08STSQ-QLGTH)
102920               ITEM(P08STSQ-QUEUE-ITEM)
102930               RESP(WS-RESPONSE)
102940     END-EXEC
102950     MOVE WS-RESPONSE              TO FILE-STATUS
102960     IF SUCCESS
102970        SET SEND-BUFFER            TO TRUE
102980     ELSE
102990        SET CREATE-SCREEN          TO TRUE
103000        MOVE LOW-VALUES            TO PSTS08S
103010     END-IF
103020     MOVE EIBTRMID TO P08STSQ-CA-TERM-ID
103030     EXEC CICS READQ TS
103040               QUEUE(P08STSQ-CA-QUEUE-ID)
103050               INTO(PSTCOMM-AREA)
103060               LENGTH(PSTCOMM-LGTH)
103070               ITEM(P08STSQ-QUEUE-ITEM)
103080               RESP(WS-RESPONSE)
103090     END-EXEC
103100     MOVE WS-RESPONSE TO FILE-STATUS
103110     IF NOT SUCCESS
103120        MOVE SPACES TO PSTCOMM-AREA
103130     END-IF
103140     PERFORM P7020-DELETE-TSQUEUE.
103150*
103160 P7020-DELETE-TSQUEUE.
103170*
103180     MOVE EIBTRMID TO P08STSQ-MAP-TERM-ID
103190     EXEC CICS DELETEQ TS
103200               QUEUE(P08STSQ-MAP-QUEUE-ID)
103210               RESP(WS-RESPONSE)
103220     END-EXEC
103230     MOVE EIBTRMID TO P08STSQ-CA-TERM-ID
103240     EXEC CICS DELETEQ TS
103250               QUEUE(P08STSQ-CA-QUEUE-ID)
103260               RESP(WS-RESPONSE)
103270     END-EXEC.
103280*
103290 P7200-CONVERT-MASTER-DATES.
103300*
103310     SET NOT-HOS-EXEMPT        TO TRUE
103320     MOVE AJ-JOB-ASSIGNMENT    TO JOB-DEF-CHECK
103330     MOVE CRAFT OF WS-MSTR     TO WS-CRAFT-CODE-CHECK
103340     IF JOB-DEF-YARDMASTER-ASGN
064160*CNLD-246 B
103350*       AND YARDMASTER-CRAFT
064160        AND (YARDMASTER-CRAFT OR YARDMASTER-TRAINEE)
064160*CNLD-246 E
103360        PERFORM P3031-DETERMINE-IF-HOS-EXEMPT
103370     END-IF
103380
103390     SET DE-YYMMDD-FORMAT      TO TRUE
103400     MOVE EMP-MTOD-DATE        TO DE-YYMMDD
103410     PERFORM P8998-DATEEDIT
103420     MOVE DE-CCYYMMDD          TO WS-EMP-MTOD-DATE-CENT
103430     MOVE EMP-MTOD-TIME        TO WS-EMP-MTOD-HRMN
103440
103450     SET DE-YYMMDD-FORMAT      TO TRUE
103460     MOVE EMP-US-RSTD-DATE     TO DE-YYMMDD
103470     PERFORM P8998-DATEEDIT
103480     MOVE DE-CCYYMMDD          TO WS-EMP-US-RSTD-DATE-CENT
103490     MOVE EMP-US-RSTD-TIME     TO WS-EMP-US-RSTD-HRMN
103500
103510     SET DE-YYMMDD-FORMAT      TO TRUE
103520     MOVE EMP-PERS-REST-DATE   TO DE-YYMMDD
103530     PERFORM P8998-DATEEDIT
103540     MOVE DE-CCYYMMDD          TO WS-EMP-PERS-REST-DATE-CENT
103550     MOVE EMP-PERS-REST-TIME   TO WS-EMP-PERS-REST-HRMN
103560
103570     SET DE-YYMMDD-FORMAT      TO TRUE
103580     MOVE TUT-DATE             TO DE-YYMMDD
103590     PERFORM P8998-DATEEDIT
103600     MOVE DE-CCYYMMDD          TO WS-LAST-TUT-DATE-CENT
103610     MOVE TUT-TIME-OF-DAY      TO WS-LAST-TUT-HRMN.
103620*
103630 P7300-CONVERT-WK-CALL-DATES.
103640*
103650
103660     SET DE-YYMMDD-FORMAT      TO TRUE
103670     MOVE WK-CALL-DATE         TO DE-YYMMDD
103680     PERFORM P8998-DATEEDIT
103690     MOVE DE-YYMMDD-CE         TO WK-CALL-CENT.
103700*
103710 P8000-START-REPORT.
103720*
103730*    LOCATE ANY "PRINTER" DESTINATIONS
103740*
103750     MOVE SPACES               TO P906-COMMAREA-PARMS
103760                                  WORK-CNTLKEY
103770     MOVE '80'                 TO WK-CNTL-REC-TYPE
103780     MOVE SCR08S-DIST          TO WK-CNTL-DIST
103790     MOVE SCR08S-SUB-DIST      TO WK-CNTL-SUB-DIST
103800     MOVE 'S'                  TO WK-CNTL-MR-TYPE
103810     MOVE '00'                 TO WK-CNTL-MR-SEQ
103820     MOVE WORK-CNTLKEY         TO CNTLKEY
103830     PERFORM P8610-READ-CNTLFILE
103840     IF SUCCESS AND CNTL-HOST-PRNTR-ACTIVE
103850        MOVE CNTL-HOST-PRNTR-FL      TO WS-CNTL-PRNTR-FL
103860        MOVE 'P1'                    TO WK-CNTL-MR-SEQ
103870        MOVE WORK-CNTLKEY            TO CNTLKEY
103880        PERFORM P8610-READ-CNTLFILE
103890        IF SUCCESS
103900           MOVE CNTL-HOST-ADDR-ARRAY TO P906-PRINTER-ARRAY
103910        END-IF
103920     END-IF
103930     IF P906-PRINTER-ARRAY NOT > SPACES
103940        MOVE SPACES               TO CNTLKEY-AREA
103950        MOVE '02'                 TO CNTL-REC-TYPE
103960        MOVE SCR08S-DIST          TO CNTL-DIST
103970        MOVE SCR08S-SUB-DIST      TO CNTL-SUB-DIST
103980        MOVE CNTLKEY-AREA         TO CNTLKEY
103990        PERFORM P8610-READ-CNTLFILE
104000        IF SUCCESS
104010           MOVE CNTL-DEFAULT-PR-CLASS TO P906-PRINTER-ID
104020        ELSE
104030*               'PRINT CANCEL, NO PRINTERS HAVE BEEN DEFINED'
104040           MOVE 'P025' TO MSGLOG-CODE
104050           MOVE -1        TO SCR08S-DATE-CURSOR
104060           PERFORM P9000-SEND-MAP-AND-RETURN
104070        END-IF
104080     END-IF
104090     MOVE P906-PRINTER(1)      TO WS-PRINTER-ID
104100     IF WS-CNTL-PRNTR-FL = 'Y' AND
104110        WS-INVALID-PRNTR-ID
104120*       PROCESSING COMPLETE
104130        MOVE 'P081'             TO MSGLOG-CODE
104140     ELSE
104150        MOVE SCR08S-DIST     TO JCL-IC-DIST
104160                                P906-DIST
104170        MOVE SCR08S-SUB-DIST TO JCL-IC-SUB-DIST
104180                                P906-SUB-DIST
104190        MOVE WS-CALL-AREA-1  TO JCL-IC-CALL-AREA-1
104200        MOVE WS-CALL-AREA-2  TO JCL-IC-CALL-AREA-2
104210        MOVE P738-PGM        TO P906-PROGRAM-ID
104220        MOVE JCL-INPUT-CARD  TO P906-PARM-CARD-1
104230        SET P906-MULT-OUTPUT TO TRUE
104240        EXEC CICS LINK
104250                  PROGRAM(P906-PGM)
104260                  COMMAREA(P906-COMMAREA-PARMS)
104270                  LENGTH(P906-LGTH)
104280                  RESP(WS-RESPONSE)
104290        END-EXEC
104300        MOVE WS-RESPONSE TO FILE-STATUS
104310        IF NOT SUCCESS
104320           MOVE 'P8000-1' TO ERR-PARAGRAPH
104330           PERFORM P9999-GOT-PROBLEM
104340        END-IF
104350        SET P08SCA-REPORT-NOT-READY TO TRUE
104360
104370        IF P906-PRINTER-ERROR
104380*            NO-DEFAULT-PRINTER-MSG
104390           MOVE 'D003' TO MSGLOG-CODE
104400        ELSE
104410           IF P906-JCL-ERROR
104420*               NO-JCL-CNTL-MSG
104430              MOVE 'J005' TO MSGLOG-CODE
104440           ELSE
104450*               REPORT-GEN-MSG
104460              MOVE 'R001' TO MSGLOG-CODE
104470           END-IF
104480        END-IF
104490     END-IF.
104500*
104510 P8070-LINK-942-SEN-ANAL.
104520*
104530     EXEC CICS LINK
104540               PROGRAM(P942-PGM)
104550               COMMAREA(P942-COMMAREA-PARMS)
104560               LENGTH(P942-LGTH)
104570               RESP(WS-RESPONSE)
104580     END-EXEC
104590     MOVE WS-RESPONSE     TO FILE-STATUS
104600     IF NOT SUCCESS
104610        MOVE 'P8070-1'    TO ERR-PARAGRAPH
104620        MOVE 'P942LINK'   TO ERR-KEY
104630        PERFORM P9999-GOT-PROBLEM
104640     END-IF.
104650*
104660 P8080-INITIATE-TRACKING.
104670*
104680     MOVE SPACES                         TO P938-COMMAREA-PARMS
104690     MOVE TRCN-EN-REST-REQ               TO P938-INIT-EN-REST-REQ
104700     MOVE TRCN-TR-REST-REQ               TO P938-INIT-TR-REST-REQ
104710     SET P938-INITIATE-FUNCTION          TO TRUE
104720     SET P938-ASGN-AJ                    TO TRUE
104730     MOVE JSK1-ASGN-DIST                 TO P938-ASGN-DIST
104740     MOVE JSK1-ASGN-SUB-DIST             TO P938-ASGN-SUB-DIST
104750     MOVE JSK1-ASSIGNMENT                TO P938-ASGN-ASSIGN
104760     MOVE P08S-TSQ-CALL-DATE-TIME(SUB2)
104770                                         TO P938-EFF-DATE-TIME
104780     MOVE PSTCA-TIME-ZONE                TO P938-TIME-ZONE
104790     MOVE SCR08S-ASGN(SUB1)              TO P938-INIT-TRAIN
104800     MOVE WS-TRK-REASON                  TO P938-INIT-REASON
104810     IF P938-INIT-RJH
104820        IF P08S-TSQ-OWNER(SUB2) = P08S-TSQ-EMP-SEL(SUB2)
104830           SET P938-INIT-REG-MAN         TO TRUE
104840           MOVE P08S-TSQ-OWNER(SUB2)     TO P938-INIT-EMP-NO
104850           SET P08S-TSQ-TRK-RJH(SUB2)    TO TRUE
104860        ELSE
104870           SET P938-INIT-VCNY-FILL       TO TRUE
104880           MOVE P08S-TSQ-EMP-SEL(SUB2)   TO P938-INIT-EMP-NO
104890        END-IF
104900     ELSE
104910        MOVE P08S-TSQ-OWNER(SUB2)        TO P938-INIT-EMP-NO
104920        SET P938-INIT-REG-MAN            TO TRUE
104930     END-IF
104940     EXEC CICS LINK
104950               PROGRAM(P938-PGM)
104960               COMMAREA(P938-COMMAREA-PARMS)
104970               LENGTH(P938-LGTH)
104980               RESP(WS-RESPONSE)
104990     END-EXEC
105000     MOVE WS-RESPONSE                    TO FILE-STATUS
105010     IF NOT SUCCESS
105020        MOVE 'P8080-1'                   TO ERR-PARAGRAPH
105030        MOVE 'P938LINK'                  TO ERR-KEY
105040        PERFORM P9999-GOT-PROBLEM
105050     END-IF.
105060*
105070 P8500-READ-MASTER.
105080*
105090     SET NOT-OUT-OF-TOWN TO TRUE
105100     EXEC CICS READ
105110               DATASET(MSTR-VIA-EMP-NBR)
105120               INTO(WS-MSTR)
105130               LENGTH(MSTRENBR-RLGTH)
105140               RIDFLD(MSTRNBRK)
105150               KEYLENGTH(MSTRENBR-KLGTH)
105160               RESP(WS-RESPONSE)
105170     END-EXEC
105180     MOVE WS-RESPONSE TO FILE-STATUS
105190     IF NOT SUCCESS
105200       MOVE 'P8500-1'  TO ERR-PARAGRAPH
105210       MOVE MSTRNBRK   TO ERR-KEY
105220       PERFORM P9999-GOT-PROBLEM
105230     END-IF
105240     PERFORM P8510-READ-MASTER-INFO.
105250*-----------------------------------------------------------*
105260*             CONVERT TO PSTCA-TIME-ZONE                    *
105270*-----------------------------------------------------------*
105280     IF DIST        OF WS-MSTR NOT = SCR08S-DIST
105290        OR SUB-DIST OF WS-MSTR NOT = SCR08S-SUB-DIST
105300        PERFORM P8506-CONVERT-MASTER-INFO
105310     ELSE
105320        MOVE PSTCA-TIME-ZONE      TO WS-EMP-TIME-ZONE
105330     END-IF.
105340*
105350 P8505-READ-MASTER-ONLY.
105360*
105370     EXEC CICS READ
105380               DATASET(MSTR-VIA-EMP-NBR)
105390               INTO(WS-MSTR)
105400               LENGTH(MSTRENBR-RLGTH)
105410               RIDFLD(MSTRNBRK)
105420               KEYLENGTH(MSTRENBR-KLGTH)
105430               RESP(WS-RESPONSE)
105440     END-EXEC
105450     MOVE WS-RESPONSE TO FILE-STATUS
105460     IF NOT SUCCESS
105470        MOVE 'P8505-1'  TO ERR-PARAGRAPH
105480        MOVE MSTRNBRK   TO ERR-KEY
105490        PERFORM P9999-GOT-PROBLEM
105500     END-IF
105510     IF DIST        OF WS-MSTR NOT = SCR08S-DIST
105520        OR SUB-DIST OF WS-MSTR NOT = SCR08S-SUB-DIST
105530        PERFORM P8506-CONVERT-MASTER-INFO
105540     END-IF
105550     .
105560*
105570 P8506-CONVERT-MASTER-INFO.
105580*
105590     MOVE SPACES                     TO WORK-CNTLKEY
105600     MOVE '02'                       TO WK-CNTL-REC-TYPE
105610     MOVE DIST     OF WS-MSTR        TO WK-CNTL-DIST
105620     MOVE SUB-DIST OF WS-MSTR        TO WK-CNTL-SUB-DIST
105630     MOVE WORK-CNTLKEY               TO CNTLKEY
105640     PERFORM P8610-READ-CNTLFILE
105650     IF NOT SUCCESS
105660        MOVE 'P8506-1'               TO ERR-PARAGRAPH
105670        MOVE CNTLKEY                 TO ERR-KEY
105680        PERFORM P9999-GOT-PROBLEM
105690     END-IF
105700     MOVE SPACES                     TO TZ-PARAMETERS
105710     MOVE CNTL-TIME-ZONE             TO TZ-IN-ZONE
105720                                        WS-EMP-TIME-ZONE
105730     MOVE PSTCA-TIME-ZONE            TO TZ-OUT-ZONE
105740
105750     IF EMP-MTOD > '0000000000'
105760        MOVE EMP-MTOD                TO TZ-IN-DATE-TIME
105770        PERFORM P8996-TIMEZONE
105780        IF TZ-INVALID-PARAMETERS
105790           MOVE 'P8506-2'            TO ERR-PARAGRAPH
105800           PERFORM P8996-TZERROR
105810        END-IF
105820        MOVE TZ-OUT-DATE-TIME        TO EMP-MTOD
105830     END-IF
105840
105850     IF EMP-US-RSTD > '0000000000'
105860        MOVE EMP-US-RSTD             TO TZ-IN-DATE-TIME
105870        PERFORM P8996-TIMEZONE
105880        IF TZ-INVALID-PARAMETERS
105890           MOVE 'P8506-3'            TO ERR-PARAGRAPH
105900           PERFORM P8996-TZERROR
105910        END-IF
105920        MOVE TZ-OUT-DATE-TIME        TO EMP-US-RSTD
105930     END-IF
105940
105950     IF EMP-PERS-REST > '0000000000'
105960        MOVE EMP-PERS-REST           TO TZ-IN-DATE-TIME
105970        PERFORM P8996-TIMEZONE
105980        IF TZ-INVALID-PARAMETERS
105990           MOVE 'P8506-4'            TO ERR-PARAGRAPH
106000           PERFORM P8996-TZERROR
106010        END-IF
106020        MOVE TZ-OUT-DATE-TIME        TO EMP-PERS-REST
106030     END-IF
106040     .
106050*
106060 P8510-READ-MASTER-INFO.
106070*
106080     MOVE SPACES             TO WS-ASGN-FILE
106090     MOVE EMP-NBR OF WS-MSTR TO ASGN-EMP-NO
106100     PERFORM PXXXX-JOB-OWNED
106110     MOVE ASGN-JOB-TYPE   TO NORMAL-ASGNMT-FLAG
106120     MOVE ASGN-ASSIGNMENT TO NORMAL-ASGNMT
106130     MOVE SPACES             TO WS-ASGN-FILE
106140     MOVE EMP-NBR OF WS-MSTR TO ASGN-EMP-NO
106150     PERFORM PXXXX-LATEST-TEMP-JOB
106160     MOVE ASGN-JOB-TYPE   TO TEMPORARY-ASGNMT-FLAG
106170     MOVE ASGN-ASSIGNMENT TO TEMPORARY-ASGNMT
106180     SET NOT-OUT-OF-TOWN  TO TRUE
106190     MOVE SPACES             TO WS-ASGN-FILE
106200     MOVE EMP-NBR OF WS-MSTR TO ASGN-EMP-NO
106210     PERFORM PXXXX-JOB-ON-DUTY
106220     IF ASGN-ASSIGNMENT > SPACE
106230       MOVE ASGN-JOB-TYPE          TO ON-DUTY-ASGNMT-FLAG
106240       MOVE ASGN-ASSIGNMENT        TO ON-DUTY-ASGNMT
106250       MOVE ASGN-ON-DUTY-DATE-TIME TO ON-DUTY-OUT-TOWN-CODE
106260     END-IF.
106270*
106280 P8520-LINK-TO-PS52.
106290*
106300     EXEC CICS LINK
106310               PROGRAM(PS52-PGM)
106320               COMMAREA(PS52-COMMAREA-PARMS)
106330               LENGTH(PS52-LGTH)
106340               RESP(WS-RESPONSE)
106350     END-EXEC
106360     MOVE WS-RESPONSE        TO FILE-STATUS
106370     .
106380*
106390 P8600-READ-JSKEY2.
106400*
106410     EXEC CICS READ
106420               DATASET(JS-VIA-JSKEY2)
106430               INTO(WS-JOB-SCHEDULE)
106440               LENGTH(JSKEY2-RLGTH)
106450               RIDFLD(JSKEY2)
106460               KEYLENGTH(JSKEY2-KLGTH)
106470               RESP(WS-RESPONSE)
106480     END-EXEC
106490     MOVE WS-RESPONSE               TO FILE-STATUS.
106500*
106510 P8610-READ-CNTLFILE.
106520*
106530     EXEC CICS READ
106540               DATASET(CNTL-FILE-VIA-CNTLKEY)
106550               INTO(WS-CNTL-FILE)
106560               LENGTH(CNTLFILE-RLGTH)
106570               RIDFLD(CNTLKEY)
106580               KEYLENGTH(CNTLFILE-KLGTH)
106590               RESP(WS-RESPONSE)
106600     END-EXEC
106610     MOVE WS-RESPONSE               TO FILE-STATUS.
106620*
106630 P8620-READ-TRCNKEY1.
106640*
106650     EXEC CICS READ
106660               DATASET(TRAIN-CN-VIA-TRCNKEY1)
106670               INTO(WS-TRCN-FILE)
106680               LENGTH(TRAINCN-RLGTH)
106690               RIDFLD(TRCNKEY1)
106700               KEYLENGTH(TRAINCN-KLGTH)
106710               RESP(WS-RESPONSE)
106720     END-EXEC
106730     MOVE WS-RESPONSE               TO FILE-STATUS.
106740*
106750 P8630-READ-TRCNKEY3.
106760*
106770     EXEC CICS READ
106780               DATASET(TRAIN-CN-VIA-DSD-ASGN)
106790               INTO(WS-TRCN-FILE)
106800               LENGTH(TRAINCN-DSD-RLGTH)
106810               RIDFLD(TRCNKEY3)
106820               KEYLENGTH(TRAINCN-DSD-KLGTH)
106830               RESP(WS-RESPONSE)
106840     END-EXEC
106850     MOVE WS-RESPONSE               TO FILE-STATUS.
106860*
106870 P8640-READ-AJJOBKEY.
106880*
106890     EXEC CICS READ
106900               DATASET(AJ-VIA-JNAME-JCRAFT)
106910               INTO(WS-ASGNED-JOBS)
106920               LENGTH(AJNAMECR-RLGTH)
106930               RIDFLD(AJJOBKEY)
106940               KEYLENGTH(AJNAMECR-KLGTH)
106950               RESP(WS-RESPONSE)
106960     END-EXEC
106970     MOVE WS-RESPONSE TO FILE-STATUS.
106980*
106990 P8650-READ-SENKEY2.
107000*
107010     EXEC CICS READ
107020               DATASET(SENFILE-VIA-EMP-NO)
107030               INTO(WS-SENIORITY)
107040               LENGTH(SENENBR-RLGTH)
107050               RIDFLD(SENKEY2)
107060               KEYLENGTH(SENENBR-KLGTH)
107070               RESP(WS-RESPONSE)
107080     END-EXEC
107090     MOVE WS-RESPONSE           TO FILE-STATUS.
107100*
107110 P8660-READ-TASK-UPD.
107120*
107130     EXEC CICS READ
107140               UPDATE
107150               DATASET(TASK-VIA-EFF-TIME)
107160               INTO(WS-TASK)
107170               LENGTH(TASKEFTM-RLGTH)
107180               RIDFLD(TASKTIME)
107190               KEYLENGTH(TASKEFTM-KLGTH)
107200               RESP(WS-RESPONSE)
107210     END-EXEC
107220     MOVE WS-RESPONSE     TO FILE-STATUS.
107230     IF NOT SUCCESS
107240        MOVE 'P8660-1'    TO ERR-PARAGRAPH
107250        MOVE TASKTIME     TO ERR-KEY
107260        PERFORM P9999-GOT-PROBLEM
107270     END-IF.
107280*
107290 P8661-READ-TASK.
107300*
107310     EXEC CICS READ
107320               DATASET(TASK-VIA-EFF-TIME)
107330               INTO(WS-TASK)
107340               LENGTH(TASKEFTM-RLGTH)
107350               RIDFLD(TASKTIME)
107360               KEYLENGTH(TASKEFTM-KLGTH)
107370               RESP(WS-RESPONSE)
107380     END-EXEC
107390     MOVE WS-RESPONSE     TO FILE-STATUS.
107400     IF NOT SUCCESS
107410        MOVE 'P8661-1'    TO ERR-PARAGRAPH
107420        MOVE TASKTIME     TO ERR-KEY
107430        PERFORM P9999-GOT-PROBLEM
107440     END-IF.
107450*
107460 P8662-REWRITE-TASK.
107470*
107480     EXEC CICS REWRITE
107490               DATASET(TASK-VIA-EFF-TIME)
107500               FROM(WS-TASK)
107510               LENGTH(TASKEFTM-RLGTH)
107520               RESP(WS-RESPONSE)
107530     END-EXEC
107540     MOVE WS-RESPONSE TO FILE-STATUS
107550     IF NOT SUCCESS
107560        MOVE 'P8662-1'    TO ERR-PARAGRAPH
107570        MOVE TASKTIME     TO ERR-KEY
107580        PERFORM P9999-GOT-PROBLEM
107590     END-IF.
107600*CNC0579B - BEG
107610*
107620 P8670-READ-DECISION.
107630*
107640     EXEC CICS READ
107650               DATASET(DEC-TABLE-VIA-DECKEY)
107660               INTO(WORK-DEC-TABLE)
107670               LENGTH(DECTABLE-RLGTH)
107680               RIDFLD(DECKEY)
107690               KEYLENGTH(DECTABLE-KLGTH)
107700               RESP(WS-RESPONSE)
107710     END-EXEC
107720     MOVE WS-RESPONSE TO FILE-STATUS
107730     IF NOT SUCCESS
107740        IF NOT (NO-RECORD-FND OR END-OF-FILE)
107750           MOVE 'P8670-1' TO ERR-PARAGRAPH
107760           MOVE DECKEY    TO ERR-KEY
107770           PERFORM P9999-GOT-PROBLEM
107780        END-IF
107790     END-IF.
107800*CNC0579B - END
107810*
107820 P8700-STRTBR-AHMFILE.
107830*
107840     EXEC CICS STARTBR
107850               DATASET(AHM-DATASET)
107860               RIDFLD(AHMKEY)
107870               GTEQ
107880               RESP(WS-RESPONSE)
107890     END-EXEC
107900     MOVE WS-RESPONSE           TO FILE-STATUS.
107910*
107920 P8710-READNXT-AHMFILE.
107930*
107940     EXEC CICS READNEXT
107950               DATASET(AHM-DATASET)
107960               INTO(WS-AHIST-MISC)
107970               LENGTH(AHM-RLGTH)
107980               RIDFLD(AHMKEY)
107990               KEYLENGTH(AHM-KLGTH)
108000               RESP(WS-RESPONSE)
108010     END-EXEC
108020     MOVE WS-RESPONSE           TO FILE-STATUS.
108030*
108040 P8720-ENDBR-AHMFILE.
108050*
108060     EXEC CICS ENDBR
108070               DATASET(AHM-DATASET)
108080               RESP(WS-RESPONSE)
108090     END-EXEC
108100     MOVE WS-RESPONSE           TO FILE-STATUS.
108110*=======================================================
108120 P8810-READ-AHM.
108130*=======================================================
108140      EXEC CICS READ
108150                DATASET   (AHM-FILE)
108160                INTO      (WS-AHIST-MISC)
108170                LENGTH    (AHM-RLGTH)
108180                RIDFLD    (AHMKEY)
108190                KEYLENGTH (AHM-KLGTH)
108200                RESP      (WS-RESPONSE)
108210      END-EXEC
108220      MOVE WS-RESPONSE            TO FILE-STATUS
108230      .
108240*===================================================
108250 P8820-READ-AHM-UPDATE.
108260*===================================================
108270      EXEC CICS READ
108280                UPDATE
108290                DATASET   (AHM-FILE)
108300                INTO      (WS-AHIST-MISC)
108310                LENGTH    (AHM-RLGTH)
108320                RIDFLD    (AHMKEY)
108330                KEYLENGTH (AHM-KLGTH)
108340                RESP      (WS-RESPONSE)
108350      END-EXEC
108360      MOVE WS-RESPONSE            TO FILE-STATUS
108370      IF NOT SUCCESS
108380         MOVE 'P8820-1'           TO ERR-PARAGRAPH
108390         MOVE AHMKEY              TO ERR-KEY
108400         PERFORM P9999-GOT-PROBLEM
108410      END-IF
108420      .
108430*===================================================
108440 P8830-REWRITE-AHM.
108450*===================================================
108460      EXEC CICS REWRITE
108470                DATASET   (AHM-FILE)
108480                FROM      (WS-AHIST-MISC)
108490                LENGTH    (AHM-RLGTH)
108500                RESP      (WS-RESPONSE)
108510      END-EXEC
108520      MOVE WS-RESPONSE            TO FILE-STATUS
108530      IF NOT SUCCESS
108540         MOVE 'P8830-1'           TO ERR-PARAGRAPH
108550         MOVE AHMKEY              TO ERR-KEY
108560         PERFORM P9999-GOT-PROBLEM
108570      END-IF
108580      .
108590*===================================================
108600 P8835-WRITE-AHM.
108610*===================================================
108620      EXEC CICS WRITE
108630                DATASET   (AHM-FILE)
108640                FROM      (WS-AHIST-MISC)
108650                LENGTH    (AHM-RLGTH)
108660                RIDFLD    (AHMKEY)
108670                RESP      (WS-RESPONSE)
108680      END-EXEC
108690      MOVE WS-RESPONSE            TO FILE-STATUS
108700      IF NOT SUCCESS
108710         MOVE 'P8835-1'           TO ERR-PARAGRAPH
108720         MOVE AHMKEY              TO ERR-KEY
108730         PERFORM P9999-GOT-PROBLEM
108740      END-IF
108750      .
108760*
108770 P8900-WRITE-HISTORY.
108780*
108790     MOVE WORK-HIST-TIME TO P943-CLOCK-TIME
108800     IF P943-EFF-DATE-TIME NOT > SPACES
108810        MOVE WS-LOCAL-DATE-TIME TO P943-EFF-DATE-TIME
108820     END-IF
108830     SET P943-EMPLOYEE-FUNCTION TO TRUE
108840
108850     EXEC CICS ASSIGN
108860               USERID(P943-USERID)
108870     END-EXEC
108880
108890     SET NO-RECORD-FND TO TRUE
108900     MOVE WORK-HIST-TIMEX TO P943-CLOCK-TIME
108910
108920     EXEC CICS LINK
108930               PROGRAM(P943-PGM)
108940               COMMAREA(P943-COMMAREA-PARMS)
108950               LENGTH(P943-LGTH)
108960               RESP(WS-RESPONSE)
108970     END-EXEC
108980
108990     MOVE WS-RESPONSE TO FILE-STATUS
109000     IF NOT SUCCESS
109010        MOVE 'P8900' TO ERR-PARAGRAPH
109020        MOVE 'P943'  TO ERR-KEY
109030        PERFORM P9999-GOT-PROBLEM
109040     END-IF.
109050*
109060 PXXXX-JOB-OWNER.
109070*
109080     SET ASGN-OWNER-REC TO TRUE
109090     MOVE ZERO     TO ASGN-DATE-TIME
109100     MOVE ASGNKEY1 TO ASGNJOB
109110     EXEC CICS READ
109120               DATASET(ASGN-VIA-ASGNJOB)
109130               INTO(ASGN-AREA)
109140               LENGTH(ASGNJOB-RLGTH)
109150               RIDFLD(ASGNJOB)
109160               KEYLENGTH(ASGNJOB-KLGTH)
109170               RESP(WS-RESPONSE)
109180     END-EXEC
109190     MOVE WS-RESPONSE TO FILE-STATUS
109200     IF NOT SUCCESS
109210       IF NO-RECORD-FND
109220         MOVE ZEROS  TO ASGN-EMP-NO
109230       ELSE
109240         MOVE 'PXXXX-A'  TO ERR-PARAGRAPH
109250         MOVE ASGNJOB    TO ERR-KEY
109260         PERFORM P9999-GOT-PROBLEM
109270       END-IF
109280     END-IF.
109290*
109300 PXXXX-LATEST-TEMP.
109310*
109320     MOVE SPACES       TO SAVE-ASGN-AREA
109330     SET ASGN-TEMP-REC TO TRUE
109340     MOVE ZEROS        TO ASGN-DATE-TIME
109350     MOVE ASGNKEY1     TO ASGNJOB XXXX-ASGNKEY1
109360     EXEC CICS STARTBR
109370               DATASET(ASGN-VIA-ASGNJOB)
109380               RIDFLD(ASGNJOB)
109390               GTEQ
109400               RESP(WS-RESPONSE)
109410     END-EXEC
109420     MOVE WS-RESPONSE TO FILE-STATUS
109430     IF NOT SUCCESS
109440       IF NO-RECORD-FND
109450         MOVE ZEROS TO ASGN-EMP-NO
109460       ELSE
109470         MOVE 'PXXXX-B1' TO ERR-PARAGRAPH
109480         MOVE ASGNJOB    TO ERR-KEY
109490         PERFORM P9999-GOT-PROBLEM
109500       END-IF
109510     ELSE
109520       MOVE 0 TO ASGN-DONE-CODE
109530       PERFORM UNTIL ASGN-DONE
109540         EXEC CICS READNEXT
109550                   DATASET(ASGN-VIA-ASGNJOB)
109560                   INTO(ASGN-AREA)
109570                   LENGTH(ASGNJOB-RLGTH)
109580                   RIDFLD(ASGNJOB)
109590                   KEYLENGTH(ASGNJOB-KLGTH)
109600                   RESP(WS-RESPONSE)
109610         END-EXEC
109620         MOVE WS-RESPONSE TO FILE-STATUS
109630         IF NOT SUCCESS
109640           IF NOT END-OF-FILE
109650             MOVE 'PXXXX-B2' TO ERR-PARAGRAPH
109660             MOVE ASGNJOB    TO ERR-KEY
109670             PERFORM P9999-GOT-PROBLEM
109680           END-IF
109690           SET ASGN-DONE TO TRUE
109700         ELSE
109710           IF ASGN-TEMP-REC
109720              AND XX-ASGN-DIST     = ASGN-DIST
109730              AND XX-ASGN-SUB-DIST = ASGN-SUB-DIST
109740              AND XX-ASGN-JOB      = ASGN-AJ-JOB
109750                                     OF ASGN-ASSIGNMENT
109760             MOVE ASGN-AREA TO SAVE-ASGN-AREA
109770             IF GET-FIRST-TEMP-EMP
109780               SET ASGN-DONE TO TRUE
109790             END-IF
109800           ELSE
109810             SET ASGN-DONE TO TRUE
109820           END-IF
109830         END-IF
109840       END-PERFORM
109850       EXEC CICS ENDBR
109860                 DATASET(ASGN-VIA-ASGNJOB)
109870                 RESP(WS-RESPONSE)
109880       END-EXEC
109890     END-IF
109900     IF SAVE-ASGN-AREA > SPACE
109910       MOVE SAVE-ASGN-AREA TO ASGN-AREA
109920     ELSE
109930       MOVE ZERO TO ASGN-EMP-NO
109940     END-IF.
109950*
109960 PXXXX-ON-DUTY-EMP.
109970*
109980     SET ASGN-ON-DUTY-REC TO TRUE
109990     MOVE ZERO            TO ASGN-DATE-TIME
110000     MOVE ASGNKEY1        TO ASGNJOB
110010     EXEC CICS READ
110020               DATASET(ASGN-VIA-ASGNJOB)
110030               INTO(ASGN-AREA)
110040               LENGTH(ASGNJOB-RLGTH)
110050               RIDFLD(ASGNJOB)
110060               KEYLENGTH(ASGNJOB-KLGTH)
110070               RESP(WS-RESPONSE)
110080     END-EXEC
110090     MOVE WS-RESPONSE TO FILE-STATUS
110100     IF NOT SUCCESS
110110       IF NO-RECORD-FND
110120         MOVE ZERO TO ASGN-EMP-NO
110130       ELSE
110140         MOVE 'PXXXX-C' TO ERR-PARAGRAPH
110150         MOVE ASGNJOB   TO ERR-KEY
110160         PERFORM P9999-GOT-PROBLEM
110170       END-IF
110180     END-IF.
110190*
110200 PXXXX-JOB-OWNED.
110210*
110220     MOVE SPACES   TO NORMAL-ASGNMT-FLAG NORMAL-ASGNMT
110230     MOVE '1'      TO ASGN-EMP-NO-REC-TYPE
110240     MOVE ZEROS    TO ASGN-EMP-DATE-TIME
110250     MOVE ASGNKEY2 TO ASGNEMP
110260     EXEC CICS READ
110270               DATASET(ASGN-VIA-ASGNEMP)
110280               INTO(WS-ASGN-FILE)
110290               LENGTH(ASGNEMP-RLGTH)
110300               RIDFLD(ASGNEMP)
110310               KEYLENGTH(ASGNEMP-KLGTH)
110320               RESP(WS-RESPONSE)
110330     END-EXEC
110340     MOVE WS-RESPONSE TO FILE-STATUS
110350     IF NOT SUCCESS
110360       IF NO-RECORD-FND
110370         MOVE SPACE TO ASGN-ASSIGNMENT
110380       ELSE
110390         MOVE 'PXXXX-D' TO ERR-PARAGRAPH
110400         MOVE ASGNEMP   TO ERR-KEY
110410         PERFORM P9999-GOT-PROBLEM
110420       END-IF
110430     END-IF.
110440*
110450 PXXXX-LATEST-TEMP-JOB.
110460*
110470     MOVE SPACES   TO SAVE-ASGN-AREA
110480     MOVE SPACES   TO TEMPORARY-ASGNMT-FLAG TEMPORARY-ASGNMT
110490     MOVE '2'      TO ASGN-EMP-NO-REC-TYPE
110500     MOVE ZEROS    TO ASGN-EMP-DATE-TIME
110510     MOVE ASGNKEY2 TO ASGNEMP XXXX-ASGNKEY2
110520     EXEC CICS STARTBR
110530               DATASET(ASGN-VIA-ASGNEMP)
110540               RIDFLD(ASGNEMP)
110550               GTEQ
110560               RESP(WS-RESPONSE)
110570     END-EXEC
110580     MOVE WS-RESPONSE TO FILE-STATUS
110590     IF NOT SUCCESS
110600       IF NOT NO-RECORD-FND
110610         MOVE 'PXXXX-E1' TO ERR-PARAGRAPH
110620         MOVE ASGNEMP    TO ERR-KEY
110630         PERFORM P9999-GOT-PROBLEM
110640       END-IF
110650       MOVE SPACES TO ASGN-ASSIGNMENT
110660     ELSE
110670       MOVE 0 TO ASGN-DONE-CODE
110680       PERFORM UNTIL ASGN-DONE
110690         EXEC CICS READNEXT
110700                   DATASET(ASGN-VIA-ASGNEMP)
110710                   INTO(ASGN-AREA)
110720                   LENGTH(ASGNEMP-RLGTH)
110730                   RIDFLD(ASGNEMP)
110740                   KEYLENGTH(ASGNEMP-KLGTH)
110750                   RESP(WS-RESPONSE)
110760         END-EXEC
110770         MOVE WS-RESPONSE TO FILE-STATUS
110780         IF NOT SUCCESS
110790           IF NOT END-OF-FILE
110800             MOVE 'PXXXX-E2' TO ERR-PARAGRAPH
110810             MOVE ASGNEMP    TO ERR-KEY
110820             PERFORM P9999-GOT-PROBLEM
110830           END-IF
110840           MOVE SPACES   TO ASGN-ASSIGNMENT
110850           SET ASGN-DONE TO TRUE
110860         ELSE
110870           IF ASGN-EMP-NO = XX-ASGN-EMP
110880              AND ASGN-EMP-NO-REC-TYPE = '2'
110890             MOVE ASGN-AREA TO SAVE-ASGN-AREA
110900           ELSE
110910             SET ASGN-DONE TO TRUE
110920           END-IF
110930         END-IF
110940       END-PERFORM
110950       EXEC CICS ENDBR
110960                 DATASET(ASGN-VIA-ASGNEMP)
110970                 RESP(WS-RESPONSE)
110980       END-EXEC
110990       MOVE SAVE-ASGN-AREA TO ASGN-AREA
111000     END-IF.
111010*
111020 PXXXX-JOB-ON-DUTY.
111030*
111040     MOVE SPACES       TO ON-DUTY-ASGNMT-FLAG
111050                          ON-DUTY-ASGNMT
111060     MOVE '3'          TO ASGN-EMP-NO-REC-TYPE
111070     MOVE '9999999999' TO ON-DUTY-OUT-TOWN-CODE
111080     MOVE ZEROS        TO ASGN-EMP-DATE-TIME
111090     MOVE ASGNKEY2     TO ASGNEMP
111100     EXEC CICS READ
111110               DATASET(ASGN-VIA-ASGNEMP)
111120               INTO(ASGN-AREA)
111130               LENGTH(ASGNEMP-RLGTH)
111140               RIDFLD(ASGNEMP)
111150               KEYLENGTH(ASGNEMP-KLGTH)
111160               RESP(WS-RESPONSE)
111170     END-EXEC
111180     MOVE WS-RESPONSE TO FILE-STATUS
111190     IF NOT SUCCESS
111200       IF NOT NO-RECORD-FND
111210         MOVE 'PXXXX-F' TO ERR-PARAGRAPH
111220         MOVE ASGNEMP   TO ERR-KEY
111230         PERFORM P9999-GOT-PROBLEM
111240       END-IF
111250       MOVE SPACES TO ASGN-ASSIGNMENT
111260     END-IF.
111270 COPY CHEKCRFT.
111280*
111290 P8994-CONVERT-HIST-TIMEZONE.
111300****************************************************************
111310*  CONVERT WS-LOCAL-DATE-TIME  FROM PSTCA-TIME-ZONE
111320*  TO CNTL-TIME-ZONE, PRIOR TO WRITING HISTORY RECORD.
111330*  ONLY IF  HIST DIST & SUB-DIST ARE  NOT = TO PSTCA DIST & SUB-
111340****************************************************************
111350     MOVE SPACES                   TO WORK-CNTLKEY
111360     MOVE '02'                     TO WK-CNTL-REC-TYPE
111370     MOVE P943-DIST                TO WK-CNTL-DIST
111380     MOVE P943-SDIST               TO WK-CNTL-SUB-DIST
111390     MOVE WORK-CNTLKEY             TO CNTLKEY
111400     PERFORM P8610-READ-CNTLFILE
111410     IF NOT SUCCESS
111420        MOVE 'P8994'   TO ERR-PARAGRAPH
111430        MOVE CNTLKEY   TO ERR-KEY
111440        PERFORM P9999-GOT-PROBLEM
111450     END-IF
111460     MOVE SPACES                   TO TZ-PARAMETERS
111470     MOVE CNTL-TIME-ZONE           TO TZ-OUT-ZONE
111480     MOVE WS-LOCAL-DATE-TIME       TO TZ-IN-DATE-TIME
111490     MOVE PSTCA-TIME-ZONE          TO TZ-IN-ZONE
111500     PERFORM P8996-TIMEZONE
111510     MOVE TZ-OUT-DATE-TIME         TO P943-EFF-DATE-TIME
111520     MOVE TZ-IN-ZONE               TO P943-EMP-TIME-ZONE.
111530
111540 COPY TIMEZONE.
111550
111560 COPY TZERROR.
111570
111580 COPY DATEEDIT.
111590
111600 COPY TIMEEDIT.
111610*
111620 P9000-SEND-MAP-AND-RETURN.
111630*
111640     IF MSGLOG-CODE > SPACES
111650         PERFORM P9030-GET-MESSAGE
111660         MOVE MSGLOG-MESSAGE-AREA TO SCR08S-ERRORMSG
111670*CNLD-230-B
111680         IF MSGLOG-CODE           = 'E378'
111690                                    OR 'E379'
111700                                    OR 'E380'
      *CNLD-257-DXC-B
111700                                    OR 'E383'
      *CNLD-257-DXC-E
111710            MOVE WS-RFR-MSG       TO SCR08S-ERRORMSG(05:4)
111720*           'ERT=XXXX <<MESSAGE>>'
111730         END-IF
111740*CNLD-230-E
111750     END-IF
111760
111770     MOVE P08S-MAP-VERSION(PSTCA-SUB) TO P08S-MAP
111780     IF CREATE-SCREEN
111790        PERFORM P9010-SEND-PHYSICAL-MAP
111800        PERFORM P9500-WRITE-TSQUEUE3
111810        PERFORM P9500-WRITE-TSQUEUE
111820        PERFORM P9780-WRITE-TSQUEUE4
111830     ELSE
111840        IF CONTINUE-SCREEN
111850           PERFORM P9020-SEND-DATAONLY-MAP
111860           MOVE PSTS08S TO P08S-TSQ3-DATA
111870           PERFORM P9550-REWRITE-TSQUEUE3
111880           PERFORM P9600-REWRITE-TSQUEUE
111890           PERFORM P9790-REWRITE-TSQUEUE4
111900        ELSE
111910           PERFORM P9035-SEND-BUFFER
111920        END-IF
111930     END-IF
111940     EXEC CICS RETURN
111950               TRANSID(P08S-TRAN)
111960               COMMAREA(PSTCOMM-AREA)
111970               LENGTH(PSTCOMM-LGTH)
111980     END-EXEC.
111990*
112000 P9010-SEND-PHYSICAL-MAP.
112010*
112020     EXEC CICS SEND MAP(P08S-MAP)
112030                    MAPSET(P08S-SET)
112040                    FROM(PSTS08S)
112050                    CURSOR
112060                    ERASE
112070                    RESP(WS-RESPONSE)
112080     END-EXEC
112090     MOVE WS-RESPONSE TO FILE-STATUS
112100     IF NOT SUCCESS
112110        MOVE 'P9010'   TO ERR-PARAGRAPH
112120        PERFORM P9999-GOT-PROBLEM
112130     END-IF.
112140*
112150 P9020-SEND-DATAONLY-MAP.
112160*
112170     EXEC CICS SEND MAP(P08S-MAP)
112180                    MAPSET(P08S-SET)
112190                    FROM(PSTS08S)
112200                    DATAONLY
112210                    CURSOR
112220                    RESP(WS-RESPONSE)
112230     END-EXEC
112240     MOVE WS-RESPONSE TO FILE-STATUS
112250     IF NOT SUCCESS
112260        MOVE 'P9020' TO ERR-PARAGRAPH
112270        PERFORM P9999-GOT-PROBLEM
112280     END-IF.
112290*
112300 P9030-GET-MESSAGE.
112310*
112320     MOVE PSTCA-SUB TO MSGLOG-SUB-CODE
112330     EXEC CICS READ
112340               DATASET(MSGLOG-VIA-CODE)
112350               INTO(MSGLOG-AREA)
112360               LENGTH(MSGLOG-RLGTH)
112370               RIDFLD(MSGLOG-KEY)
112380               KEYLENGTH(MSGLOG-KLGTH)
112390               RESP(WS-RESPONSE)
112400     END-EXEC
112410     MOVE WS-RESPONSE TO FILE-STATUS
112420     IF NOT SUCCESS
112430        IF PSTCA-SUB = 1
112440           MOVE 'NO MESSAGE ON FILE' TO MSGLOG-MESSAGE
112450        ELSE
112460           MOVE 'AUCUN MESSAGE'      TO MSGLOG-MESSAGE
112470        END-IF
112480     END-IF
112490     MOVE MSGLOG-CODE     TO MSGLOG-MSG-CODE
112500     MOVE '-'             TO MSGLOG-MSG-SEP
112510     MOVE MSGLOG-SUB-CODE TO MSGLOG-MSG-SUB-CODE.
112520*
112530 P9035-SEND-BUFFER.
112540*
112550     EXEC CICS SEND
112560               FROM(WS-BUFFER-DATA)
112570               LENGTH(WS-BUFFER-LGTH)
112580               ERASE
112590               RESP(WS-RESPONSE)
112600     END-EXEC
112610     MOVE WS-RESPONSE       TO FILE-STATUS
112620     IF NOT SUCCESS
112630        MOVE 'P9035-1'      TO ERR-PARAGRAPH
112640        MOVE 'SEND BUFFER'  TO ERR-KEY
112650        PERFORM P9999-GOT-PROBLEM
112660     END-IF
112670     EXEC CICS SEND
112680               CONTROL
112690               CURSOR(WS-BUFFER-CURSOR)
112700               RESP(WS-RESPONSE)
112710     END-EXEC
112720     MOVE WS-RESPONSE       TO FILE-STATUS
112730     IF NOT SUCCESS
112740        MOVE 'P9035-2'      TO ERR-PARAGRAPH
112750        MOVE 'SEND CURSOR'  TO ERR-KEY
112760        PERFORM P9999-GOT-PROBLEM
112770     END-IF.
112780*=================================================================
112790 P9050-RESET-ATTRIBUTES.
112800*=================================================================
112810     PERFORM UNTIL DONE
112820        ADD 1                       TO SUB1
112830        IF SUB1 NOT > ARRAY-MAX
112840           ADD 1                    TO SUB2
112850           IF SUB2 NOT > P08S-TSQ-CNT
112860              PERFORM P4010-DISPLAY-VACANCY
112870           ELSE
112880              SET DONE              TO TRUE
112890           END-IF
112900        ELSE
112910           SET DONE                 TO TRUE
112920        END-IF
112930     END-PERFORM.
112940*=================================================================
112950 P9100-SETUP-MAIN-MENU.
112960*=================================================================
112970     EXEC CICS XCTL
112980               PROGRAM(P03-PGM)
112990               COMMAREA(PSTCOMM-AREA)
113000               LENGTH(PSTCOMM-LGTH)
113010               RESP(WS-RESPONSE)
113020     END-EXEC
113030     MOVE WS-RESPONSE TO FILE-STATUS
113040     IF NOT SUCCESS
113050         MOVE 'P9100' TO ERR-PARAGRAPH
113060         PERFORM P9999-GOT-PROBLEM
113070     END-IF.
113080*=================================================================
113090 P9150-SETUP-SCR997.
113100*=================================================================
113110     MOVE SPACES                        TO PSTCA-VARIABLE-AREA
113120     MOVE P08S-PGM                      TO P997CA-FROM-PROGRAM
113130     SET P997CA-MSTR-BY-NAME            TO TRUE
113140     MOVE MSTR-VIA-EMP-NAME             TO P997CA-FILE
113150     MOVE MSTRENAM-RLGTH                TO P997CA-RLGTH
113160     MOVE MSTRENAM-KLGTH                TO P997CA-KLGTH
113170     MOVE SCR08S-NAME(FUNC-SUB)         TO P997CA-PASS-KEY
113180     EXEC CICS XCTL
113190               PROGRAM(P997-PGM)
113200               COMMAREA(PSTCOMM-AREA)
113210               LENGTH(PSTCOMM-LGTH)
113220               RESP(WS-RESPONSE)
113230     END-EXEC
113240     MOVE WS-RESPONSE                   TO FILE-STATUS
113250     IF NOT SUCCESS
113260        MOVE 'P9150-01'                 TO ERR-PARAGRAPH
113270        PERFORM P9999-GOT-PROBLEM
113280     END-IF.
113290*
113300 P9300-READ-TASK-BY-STATION.
113310*
113320     MOVE SPACES                        TO WS-TASK
113330     MOVE TRCN-ORIGIN-STN               TO TASK-CALL-ORIGIN-STN
113340     MOVE WS-9300-CALL-DATE-TIME        TO
113350                                      TASK-CALL-ORDER-DATE-TIME
113360     MOVE '20'                          TO TASK-CALL-ORDER-CE
113370     MOVE TRCN-TRAIN-ASGN               TO TASK-CALL-TRAIN2
113380
113390     MOVE TASK-STATION-KEY              TO TASKSTNKEY
113400     MOVE SPACE                         TO WS-TASK-FOUND-FLAG
113410     SET TASK-NOT-DONE                  TO TRUE
113420     EXEC CICS STARTBR
113430               DATASET(TASK-VIA-STATION)
113440               RIDFLD(TASKSTNKEY)
113450               GTEQ
113460               RESP(WS-RESPONSE)
113470     END-EXEC
113480     MOVE WS-RESPONSE                   TO FILE-STATUS
113490     IF NOT SUCCESS
113500        IF NOT (NO-RECORD-FND      OR END-OF-FILE)
113510           MOVE 'P9300-1'               TO ERR-PARAGRAPH
113520           MOVE TASKSTNKEY              TO ERR-KEY
113530           PERFORM P9999-GOT-PROBLEM
113540        ELSE
113550           SET TASK-DONE                TO TRUE
113560        END-IF
113570     END-IF
113580     PERFORM UNTIL TASK-DONE
113590        EXEC CICS READNEXT
113600                  DATASET(TASK-VIA-STATION)
113610                  INTO(WS-TASK)
113620                  LENGTH(TASKSTN-RLGTH)
113630                  RIDFLD(TASKSTNKEY)
113640                  KEYLENGTH(TASKSTN-KLGTH)
113650                  RESP(WS-RESPONSE)
113660        END-EXEC
113670        MOVE WS-RESPONSE            TO FILE-STATUS
113680        IF SUCCESS
113690           IF TASK-CALL
113700              IF TASK-CALL-ORIGIN-STN = TRCN-ORIGIN-STN AND
113710                 TASK-CALL-ORDER-DATE-TIME =
113720                                   WS-9300-CALL-DATE-TIME AND
113730                 TASK-CALL-TRAIN2  = TRCN-TRAIN-ASGN   AND
113740                 TASK-DIST         = SCR08S-DIST       AND
113750                 TASK-SUB-DIST     = SCR08S-SUB-DIST
113760                 SET TASK-DONE   TO TRUE
113770                 SET WS-TASK-FOUND TO TRUE
113780              ELSE
113790                 IF TASK-CALL-ORIGIN-STN = TRCN-ORIGIN-STN AND
113800                    TASK-CALL-ORDER-DATE-TIME =
113810                                      WS-9300-CALL-DATE-TIME AND
113820                    TASK-CALL-TRAIN2  = TRCN-TRAIN-ASGN
113830                    CONTINUE
113840                 ELSE
113850                    SET TASK-DONE TO TRUE
113860                 END-IF
113870              END-IF
113880           END-IF
113890        ELSE
113900           SET TASK-DONE   TO TRUE
113910        END-IF
113920     END-PERFORM
113930     EXEC CICS ENDBR
113940               DATASET(TASK-VIA-STATION)
113950               RESP(WS-RESPONSE)
113960     END-EXEC
113970     .
113980*
113990 P9310-READ-UPDATE-VRUINFO.
114000*
114010     MOVE VRUINFO-KEY-AREA              TO VRUINFOKEY
114020     EXEC CICS READ
114030               UPDATE
114040               DATASET  (VRU-INFO-FILE)
114050               INTO     (VRUINFO-FILE)
114060               LENGTH   (VRU-INFO-TERM-RLGTH)
114070               RIDFLD   (VRUINFOKEY)
114080               KEYLENGTH(VRU-INFO-TERM-KLGTH)
114090               RESP     (WS-RESPONSE)
114100     END-EXEC
114110     MOVE WS-RESPONSE                   TO FILE-STATUS
114120     IF  NOT SUCCESS
114130     AND NOT NO-RECORD-FND
114140        MOVE 'P9310-1'                  TO ERR-PARAGRAPH
114150        MOVE VRUINFOKEY                 TO ERR-KEY
114160        PERFORM P9999-GOT-PROBLEM
114170     END-IF
114180     .
114190*
114200 P9320-REWRITE-VRUINFO.
114210*
114220     EXEC CICS REWRITE
114230               DATASET(VRU-INFO-FILE)
114240               FROM   (VRUINFO-FILE)
114250               LENGTH (VRU-INFO-TERM-RLGTH)
114260               RESP   (WS-RESPONSE)
114270     END-EXEC
114280     MOVE WS-RESPONSE                   TO FILE-STATUS
114290     IF NOT SUCCESS
114300        MOVE 'P9320-1'                  TO ERR-PARAGRAPH
114310        MOVE VRUINFOKEY                 TO ERR-KEY
114320        PERFORM P9999-GOT-PROBLEM
114330     END-IF
114340     .
114350*
114360 P9330-READ-VRUINFO.
114370*
114380     MOVE VRUINFO-KEY-AREA              TO VRUINFOKEY
114390     EXEC CICS READ
114400               DATASET  (VRU-INFO-FILE)
114410               INTO     (VRUINFO-FILE)
114420               LENGTH   (VRU-INFO-TERM-RLGTH)
114430               RIDFLD   (VRUINFOKEY)
114440               KEYLENGTH(VRU-INFO-TERM-KLGTH)
114450               RESP     (WS-RESPONSE)
114460     END-EXEC
114470     MOVE WS-RESPONSE                   TO FILE-STATUS
114480     IF  NOT SUCCESS
114490     AND NOT NO-RECORD-FND
114500        MOVE 'P9330-1'                  TO ERR-PARAGRAPH
114510        MOVE VRUINFOKEY                 TO ERR-KEY
114520        PERFORM P9999-GOT-PROBLEM
114530     END-IF
114540     .
114550*=================================================================
114560 P9500-WRITE-TSQUEUE3.
114570*=================================================================
114580     MOVE EIBTRMID                      TO P08S-TSQ3-TERM
114590     EXEC CICS DELETEQ TS
114600               QUEUE(P08S-TSQ3-ID)
114610               RESP(WS-RESPONSE)
114620     END-EXEC
114630     MOVE WS-RESPONSE                   TO FILE-STATUS
114640     MOVE SPACES                        TO P08S-TSQ3-DATA
114650     MOVE PSTS08S                       TO P08S-TSQ3-DATA
114660     MOVE PSTCOMM-AREA                  TO P08S-TSQ3-PSTCOMM-AREA
114670     EXEC CICS WRITEQ TS
114680               QUEUE(P08S-TSQ3-ID)
114690               FROM(P08S-TSQ3-AREA)
114700               LENGTH(P08S-TSQ3-LGTH)
114710               ITEM(P08S-TSQ3-ITEM)
114720               RESP(WS-RESPONSE)
114730     END-EXEC
114740     MOVE WS-RESPONSE                   TO FILE-STATUS
114750     IF NOT SUCCESS
114760        MOVE 'P9500-01'                 TO ERR-PARAGRAPH
114770        PERFORM P9999-GOT-PROBLEM
114780     END-IF.
114790*=================================================================
114800 P9400-DELETE-TSQUEUE.
114810*=================================================================
114820     EXEC CICS DELETEQ TS
114830               QUEUE(P08S-TSQ-ID)
114840               RESP(WS-RESPONSE)
114850     END-EXEC
114860     MOVE WS-RESPONSE               TO FILE-STATUS.
114870*
114880 P9500-WRITE-TSQUEUE.
114890*
114900     EXEC CICS WRITEQ TS
114910               QUEUE(P08S-TSQ-ID)
114920               FROM(P08S-TSQ-AREA)
114930               LENGTH(P08S-TSQ-LGTH)
114940               ITEM(P08S-TSQ-ITEM)
114950               RESP(WS-RESPONSE)
114960     END-EXEC
114970     MOVE WS-RESPONSE               TO FILE-STATUS
114980     IF NOT SUCCESS
114990         MOVE 'P9500-1'             TO ERR-PARAGRAPH
115000         PERFORM P9999-GOT-PROBLEM
115010     END-IF.
115020*
115030 P9510-WRITE-TSQUEUE-2.
115040*
115050     EXEC CICS WRITEQ TS
115060               QUEUE(P08S-TSQ-ID)
115070               FROM(P08S-TSQ2-AREA)
115080               LENGTH(P08S-TSQ2-LGTH)
115090               ITEM(P08S-TSQ2-ITEM)
115100               RESP(WS-RESPONSE)
115110     END-EXEC
115120     MOVE WS-RESPONSE               TO FILE-STATUS
115130     IF NOT SUCCESS
115140         MOVE 'P9510-1'             TO ERR-PARAGRAPH
115150         PERFORM P9999-GOT-PROBLEM
115160     END-IF.
115170*=================================================================
115180 P9550-REWRITE-TSQUEUE3.
115190*=================================================================
115200     MOVE EIBTRMID                      TO P08S-TSQ3-TERM
115210     MOVE PSTCOMM-AREA                  TO P08S-TSQ3-PSTCOMM-AREA
115220     EXEC CICS WRITEQ TS
115230               QUEUE(P08S-TSQ3-ID)
115240               FROM(P08S-TSQ3-AREA)
115250               LENGTH(P08S-TSQ3-LGTH)
115260               ITEM(P08S-TSQ3-ITEM)
115270               REWRITE
115280               RESP(WS-RESPONSE)
115290     END-EXEC
115300     MOVE WS-RESPONSE                   TO FILE-STATUS
115310     IF NOT SUCCESS
115320        MOVE 'P9550-01'                 TO ERR-PARAGRAPH
115330        PERFORM P9999-GOT-PROBLEM
115340     END-IF.
115350*=================================================================
115360 P9600-READ-TSQUEUE3.
115370*=================================================================
115380     MOVE EIBTRMID                      TO P08S-TSQ3-TERM
115390     EXEC CICS READQ TS
115400               QUEUE(P08S-TSQ3-ID)
115410               INTO(P08S-TSQ3-AREA)
115420               LENGTH(P08S-TSQ3-LGTH)
115430               ITEM(P08S-TSQ3-ITEM)
115440               RESP(WS-RESPONSE)
115450     END-EXEC
115460     MOVE WS-RESPONSE                   TO FILE-STATUS
115470     IF NOT SUCCESS
115480        MOVE 'P9600-01'                 TO ERR-PARAGRAPH
115490        PERFORM P9999-GOT-PROBLEM
115500     END-IF.
115510*=================================================================
115520 P9600-REWRITE-TSQUEUE.
115530*
115540     EXEC CICS WRITEQ TS
115550               QUEUE(P08S-TSQ-ID)
115560               FROM(P08S-TSQ-AREA)
115570               LENGTH(P08S-TSQ-LGTH)
115580               ITEM(P08S-TSQ-ITEM)
115590               REWRITE
115600               RESP(WS-RESPONSE)
115610     END-EXEC
115620     MOVE WS-RESPONSE               TO FILE-STATUS
115630     IF NOT SUCCESS
115640         MOVE 'P9600-1'             TO ERR-PARAGRAPH
115650         PERFORM P9999-GOT-PROBLEM
115660     END-IF.
115670*
115680 P9700-READ-TSQUEUE.
115690*
115700     EXEC CICS READQ TS
115710               QUEUE(P08S-TSQ-ID)
115720               INTO(P08S-TSQ-AREA)
115730               LENGTH(P08S-TSQ-LGTH)
115740               ITEM(P08S-TSQ-ITEM)
115750               RESP(WS-RESPONSE)
115760     END-EXEC
115770     MOVE WS-RESPONSE               TO FILE-STATUS
115780     IF NOT SUCCESS
115790         MOVE 'P9700-1'             TO ERR-PARAGRAPH
115800         PERFORM P9999-GOT-PROBLEM
115810     END-IF.
115820*
115830 P9710-READ-TSQUEUE-2.
115840*
115850     EXEC CICS READQ TS
115860               QUEUE(P08S-TSQ-ID)
115870               INTO(P08S-TSQ2-AREA)
115880               LENGTH(P08S-TSQ2-LGTH)
115890               ITEM(P08S-TSQ2-ITEM)
115900               RESP(WS-RESPONSE)
115910     END-EXEC
115920     MOVE WS-RESPONSE               TO FILE-STATUS.
115930*
115940 P9750-SETUP-SCR998.
115950*
115960     MOVE SPACES            TO P998COMM-AREA
115970     MOVE P08S-PGM          TO P998CA-FROM-PROGRAM
115980     MOVE P08S-MAP          TO P998CA-SCREEN-ID
115990     MOVE EIBCPOSN          TO P998CA-CURSOR-POS
116000     EXEC CICS XCTL
116010               PROGRAM(P998-PGM)
116020               COMMAREA(PSTCOMM-AREA)
116030               LENGTH(PSTCOMM-LGTH)
116040               RESP(WS-RESPONSE)
116050     END-EXEC
116060     MOVE WS-RESPONSE       TO FILE-STATUS
116070     IF NOT SUCCESS
116080        MOVE 'P9750'        TO ERR-PARAGRAPH
116090        PERFORM P9999-GOT-PROBLEM
116100     END-IF
116110     .
116120*=================================================================
116130 P9770-READ-TSQUEUE4.
116140*=================================================================
116150     EXEC CICS READQ TS
116160               QUEUE(P08S-TSQ4-ID)
116170               INTO(P08S-TSQ4-AREA)
116180               LENGTH(P08S-TSQ4-LGTH)
116190               ITEM(P08S-TSQ4-ITEM)
116200               RESP(WS-RESPONSE)
116210     END-EXEC
116220     MOVE WS-RESPONSE               TO FILE-STATUS
116230     IF NOT SUCCESS
116240         MOVE 'P9770-1'             TO ERR-PARAGRAPH
116250         PERFORM P9999-GOT-PROBLEM
116260     END-IF
116270     MOVE P08S-TSQ4-DATA            TO WS-VRU-SCAL-TABLE
116280     .
116290*=================================================================
116300 P9780-WRITE-TSQUEUE4.
116310*=================================================================
116320     EXEC CICS DELETEQ TS
116330               QUEUE(P08S-TSQ4-ID)
116340               RESP(WS-RESPONSE)
116350     END-EXEC
116360     MOVE WS-RESPONSE                   TO FILE-STATUS
116370     MOVE WS-VRU-SCAL-TABLE             TO P08S-TSQ4-DATA
116380     EXEC CICS WRITEQ TS
116390               QUEUE(P08S-TSQ4-ID)
116400               FROM(P08S-TSQ4-AREA)
116410               LENGTH(P08S-TSQ4-LGTH)
116420               ITEM(P08S-TSQ4-ITEM)
116430               RESP(WS-RESPONSE)
116440     END-EXEC
116450     MOVE WS-RESPONSE                   TO FILE-STATUS
116460     IF NOT SUCCESS
116470        MOVE 'P9780-01'                 TO ERR-PARAGRAPH
116480        PERFORM P9999-GOT-PROBLEM
116490     END-IF
116500     .
116510*=================================================================
116520 P9790-REWRITE-TSQUEUE4.
116530*=================================================================
116540     MOVE WS-VRU-SCAL-TABLE             TO P08S-TSQ4-DATA
116550     EXEC CICS WRITEQ TS
116560               QUEUE(P08S-TSQ4-ID)
116570               FROM(P08S-TSQ4-AREA)
116580               LENGTH(P08S-TSQ4-LGTH)
116590               ITEM(P08S-TSQ4-ITEM)
116600               REWRITE
116610               RESP(WS-RESPONSE)
116620     END-EXEC
116630     MOVE WS-RESPONSE                   TO FILE-STATUS
116640     IF NOT SUCCESS
116650        MOVE 'P9790-01'                 TO ERR-PARAGRAPH
116660        PERFORM P9999-GOT-PROBLEM
116670     END-IF
116680     .
116690*=================================================================
116700 P9795-DELETE-TSQUEUE4.
116710*=================================================================
116720     EXEC CICS DELETEQ TS
116730               QUEUE(P08S-TSQ4-ID)
116740               RESP(WS-RESPONSE)
116750     END-EXEC
116760     MOVE WS-RESPONSE               TO FILE-STATUS
116770     .
116780*
116790 P9810-PROCESS-OFFSET.
116800*
116810     MOVE PSTCA-DT-OS-FUN       TO PARM-CONV-TYPE
116820     MOVE PSTCA-DT-OS-DAYS      TO PARM-SEC-JULIAN-DAY
116830     MOVE PSTCA-DT-OS-HRMN      TO PARM-SEC-HRMN
116840     EXEC CICS LINK
116850               PROGRAM(P903-PGM)
116860               COMMAREA(DATE-CONVERSION-PARMS)
116870               LENGTH(P903-LGTH)
116880               RESP(WS-RESPONSE)
116890     END-EXEC
116900     MOVE WS-RESPONSE           TO FILE-STATUS
116910     IF NOT SUCCESS
116920        MOVE 'P9810-1'          TO ERR-PARAGRAPH
116930        MOVE 'P903'             TO ERR-KEY
116940        PERFORM P9999-GOT-PROBLEM
116950     END-IF.
116960*
116970 P9811-FORMAT-CENTURY.
116980*
116990     MOVE SPACES                  TO WS-CENT-QUAL-DATE
117000                                     WS-CENT-CALL-DATE
117010     MOVE QUAL-DATE               TO DE-YYMMDD
117020     SET DE-YYMMDD-FORMAT         TO TRUE
117030     SET DE-REFORMAT-ONLY         TO TRUE
117040     MOVE SPACE                   TO DE-STATUS-FLAG
117050     PERFORM P8998-DATEEDIT
117060     MOVE DE-CCYYMMDD             TO WS-CENT-QUAL-DATE
117070
117080     MOVE WK-CALL-DATE            TO DE-YYMMDD
117090     SET DE-YYMMDD-FORMAT         TO TRUE
117100     SET DE-REFORMAT-ONLY         TO TRUE
117110     MOVE SPACE                   TO DE-STATUS-FLAG
117120     PERFORM P8998-DATEEDIT
117130     MOVE DE-CCYYMMDD             TO WS-CENT-CALL-DATE.
117140*
117150 P9990-CLEAR-SCREEN.
117160*
117170     EXEC CICS SEND CONTROL
117180                    ERASE
117190                    FREEKB
117200     END-EXEC
117210     EXEC CICS RETURN END-EXEC.
117220*
117230 P9993-CALL-PS94-PROGRAM.
117240*
117250     EXEC CICS LINK
117260               PROGRAM  (PS94-PGM)
117270               COMMAREA (PS94-COMMAREA-PARMS)
117280               LENGTH   (PS94-LGTH)
117290               RESP     (WS-RESPONSE)
117300     END-EXEC
117310
117320     MOVE WS-RESPONSE                   TO FILE-STATUS
117330     IF NOT SUCCESS
117340        MOVE 'P9993-S94'                TO ERR-PARAGRAPH
117350        PERFORM P9999-GOT-PROBLEM
117360     ELSE
117370        MOVE PS94-FILE-STATUS           TO FILE-STATUS
117380     END-IF
117390
117400     IF PS94-ERROR-ENCOUNTERED
117410        MOVE PS94-COMMAREA-PARMS(1:PSTCOMM-LGTH)
117420                  TO DFHCOMMAREA(1:PSTCOMM-LGTH)
117430        EXEC CICS RETURN END-EXEC
117440     END-IF.
117450 X9999-GOBACK.
117460     GOBACK.
117470******************************************************************
117480     EXEC SQL INCLUDE GOTPROB  END-EXEC.
117490******************************************************************
117500     EXEC SQL INCLUDE LVPSEL01 END-EXEC.
117510
117520     EXEC SQL INCLUDE HSTCUR02 END-EXEC.
117530     EXEC SQL INCLUDE HSTSHARE END-EXEC.
117540
117550     EXEC SQL INCLUDE LSPSEL04 END-EXEC.
117560     EXEC SQL INCLUDE LSPSHARE END-EXEC.
117570
117580     EXEC SQL INCLUDE LSDCUR02 END-EXEC.
117590     EXEC SQL INCLUDE LSDSHARE END-EXEC.
117600
117610     EXEC SQL INCLUDE TRRSHARE END-EXEC.
117620     EXEC SQL INCLUDE TRRCUR06 END-EXEC.
