000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CNP0DV.
000300
000400*****************************************************************
000500*  BACKGROUND TRANSACTION/AUTOMATIC UPDATE SYSTEM RESET BREAK   *
000600*****************************************************************
000700*  DATE   INITIAL  LOG#   DESCRIPTION
000800*-------- ------- ------- ---------------------------------------
000900*04/04/23   SXM   CNC0600 CREATE.
001000*05/16/23   RJA  CNLD-214 INCLUDE NON-TRAIN HOS RECS (INCIDENTALS,
001100*                         MANUAL, COMMUTE) IN THE EVALUATION.
001200*05/27/23   RJA  CNC0600  CHANGE MARKED WITH 05/27/23
001300*06/12/23   DXC  CNLD-246 RESTRICT SYS FORCED RESET FOR YARDMASTER
001400*06/12/23   RJA  CNC0600  CHANGE MARKED WITH 06/12/23
001500*****************************************************************
001600
001700 ENVIRONMENT DIVISION.
001800
001900 DATA DIVISION.
002000 WORKING-STORAGE SECTION.
002100 01  FILLER                         PIC X(10) VALUE 'PGM0DV W/S'.
002200
002300
002400 01  WS-SUBSCRIPTS.
002500     05  I                          PIC 99    VALUE ZERO.
002600*
002700 01  WS-FLAGS.
002800     05  WS-CNTL-READ-FLAG          PIC X(01) VALUE SPACE.
002900         88  CNTL-READ-DONE                   VALUE 'Y'.
003000     05  WS-MSTR-READ-FLAG          PIC X(01) VALUE SPACE.
003100         88  MSTR-READ-DONE                   VALUE 'Y'.
003200     05  WS-TOD-DONE-CODE           PIC X(01) VALUE 'N'.
003300         88  TOD-DONE                         VALUE 'Y'.
003400         88  TOD-NOT-DONE                     VALUE 'N'.
003500     05  WS-RESET-FLAG              PIC X(01) VALUE SPACE.
003600         88  WS-NATURAL-RESET                 VALUE 'N'.
003700         88  WS-FORCED-RESET                  VALUE 'F'.
003800     05  WS-TIE-UP-FLAG             PIC X(01) VALUE SPACE.
003900         88  TIED-UP-HOME                     VALUE 'Y'.
004000     05  FILLER                     PIC X(01) VALUE SPACE.
004100         88  WS-MY-TRAN-FOUND                 VALUE 'Y'.
004200         88  WS-MY-TRAN-NOT-FOUND             VALUE 'N'.
004300     05  WS-ZZ-CNTL-REC-FLAG        PIC X(01) VALUE SPACE.
004400         88  WS-ZZ-CNTL-REC-YES               VALUE 'Y'.
004500         88  WS-ZZ-CNTL-REC-NO                VALUE 'N'.
004600* 5/20/23 RJA CNLD-214 B
004700     05  WS-SYNCPOINT-FLAG          PIC X(01) VALUE SPACE.
004800         88  SYNCPOINT-ISSUED                 VALUE 'Y'.
004900         88  NO-SYNCPOINT-ISSUED              VALUE SPACE.
005000     05  WS-E95-REQUIRED-FLAG       PIC X(01) VALUE SPACE.
005100         88  E95-REQUIRED                     VALUE 'Y'.
005200         88  E95-NOT-REQUIRED                 VALUE 'N'.
005300     05  WS-UPDATE-FLAG             PIC X(01) VALUE SPACE.
005400         88  UPDATE-MODE                      VALUE 'Y'.
005500         88  EVALUATE-MODE                    VALUE SPACE.
005600     05  WS-READ-DONE-CODE          PIC X(01) VALUE SPACE.
005700         88  READ-DONE                        VALUE 'Y'.
005800* 5/20/23 RJA CNLD-214 E
005900
006000 01  WS-MISC.
006100     05  WS-SPACE                   PIC X(01) VALUE SPACE.
006200     05  WS-START-DATE-TIME-CENT.
006300         10  WS-START-CENT          PIC X(02).
006400         10  WS-START-DATE-TIME     PIC X(10).
006500     05  WS-FINISH-DATE-TIME-CENT.
006600         10  WS-FINISH-CENT         PIC X(02).
006700         10  WS-FINISH-DATE-TIME    PIC X(10).
006800* 5/20/23 RJA CNLD-214 B
006900     05  WS-LAST-PROCESSED-DIST     PIC X(02) VALUE SPACES.
007000     05  WS-LAST-PROCESSED-SDIST    PIC X(02) VALUE SPACES.
007100 01  WS-WRR-START-DTTM.
007200     05  WS-WRR-START-DATE          PIC X(06) VALUE SPACES.
007300     05  WS-WRR-START-TIME          PIC X(04) VALUE SPACES.
007400 01  WS-8DAYS-AGO-DTTM.
007500     05  WS-8DAYS-AGO-DATE          PIC X(06) VALUE SPACES.
007600     05  WS-8DAYS-AGO-TIME          PIC X(04) VALUE SPACES.
007700 01  WS-00-WRR-BEGINNING-DTTM.
007800     05  WS-00-WRR-BEGINNING-DT     PIC X(06) VALUE SPACES.
007900     05  WS-00-WRR-BEGINNING-TM     PIC X(04) VALUE SPACES.
008000 01  WS-7DAY-START-DTTM.
008100     05  WS-7DAY-START-DATE         PIC X(06)  VALUE SPACES.
008200     05  WS-7DAY-START-TIME         PIC X(04)  VALUE SPACES.
008300 01  WS-WRR-7DAY-END.
008400     05  WS-WRR-7DAY-END-DATE       PIC X(06)  VALUE SPACES.
008500     05  WS-WRR-7DAY-END-TIME       PIC X(04)  VALUE SPACES.
008600 01  WS-EMP-EFF-DATE-TIME.
008700     05  WS-EMP-EFF-DATE            PIC X(06)  VALUE SPACES.
008800     05  WS-EMP-EFF-TIME            PIC X(04)  VALUE SPACES.
008900 01  WS-PREV-7DAY-START-DTTM.
009000     05  WS-PREV-7DAY-START-DT      PIC X(06)  VALUE SPACES.
009100     05  WS-PREV-7DAY-START-TM      PIC X(04)  VALUE SPACES.
009200 01  LAST-EMPTOD-OFFD-YYMMDD-HRMN.
009300     05  LAST-EMPTOD-OFFD-DATE      PIC X(06)  VALUE SPACES.
009400     05  LAST-EMPTOD-OFFD-TIME      PIC X(04)  VALUE SPACES.
009500 01  NATURAL-RESET-AT-0DVRUN-FL     PIC X(01)  VALUE SPACES.
009600     88  NATURAL-RESET-AT-0DVRUN               VALUE 'A'.
009700 01  NATURAL-RESET-BEFORE-0DVRUN-FL PIC X(01)  VALUE SPACES.
009800     88  NATURAL-RESET-BEFORE-0DVRUN           VALUE 'B'.
009900 01  ENDED-BROWSE-FL                PIC X(01)  VALUE SPACES.
010000     88  ENDED-BROWSE-AT-THIS-REC              VALUE 'Y'.
010100* 5/20/23 RJA CNLD-214 E
010200
010300 01  WS-HOLD-CNTL-REC-ZZ.
010400     04  FILLER                    PIC X(020).
010500     04  DIST-TYPE-REC1-ZZ         PIC X(002).
010600     04  SUB-DIST-TYPE-REC1-ZZ     PIC X(002).
010700     04  FILLER                    PIC X(001).
010800     04  DIST-TYPE-REC2-ZZ         PIC X(002).
010900     04  SUB-DIST-TYPE-REC2-ZZ     PIC X(002).
011000     04  FILLER                    PIC X(001).
011100     04  DIST-TYPE-REC3-ZZ         PIC X(002).
011200     04  SUB-DIST-TYPE-REC3-ZZ     PIC X(002).
011300     04  FILLER                    PIC X(001).
011400     04  EMP-NBR1-ZZ               PIC X(009).
011500     04  FILLER                    PIC X(001).
011600     04  EMP-NBR2-ZZ               PIC X(009).
011700     04  FILLER                    PIC X(202).
011800 01  WS-HOLD-CNTL-FILE              PIC X(256) VALUE SPACE.
011900 01  WS-HOLD-CNTL-KEY               PIC X(020) VALUE SPACE.
012000 01  WS-TUT-DATE-TIME.
012100     05  WS-TUT-DATE-CENT.
012200         10  WS-TUT-CENT                PIC X(02) VALUE SPACE.
012300         10  WS-TUT-DATE                PIC X(06) VALUE SPACE.
012400     05  WS-TUT-TIME                    PIC X(04) VALUE SPACE.
012500 01  WS-NATURAL-RESET-ST-DTTM.
012600     05  WS-NATURAL-RESET-ST-DATE       PIC X(06) VALUE SPACE.
012700     05  WS-NATURAL-RESET-ST-TIME       PIC X(04) VALUE SPACE.
012800 01  WS-7DAY-END-MINUS6HR-DTTM.
012900     05  WS-7DAY-END-MINUS6HR-DATE      PIC X(06) VALUE SPACE.
013000     05  WS-7DAY-END-MINUS6HR-TIME      PIC X(04) VALUE SPACE.
013100 01  WS-DAY1-DATE-TIME.
013200     05  WS-DAY1-DATE                   PIC X(06) VALUE SPACE.
013300     05  WS-DAY1-TIME                   PIC X(04) VALUE SPACE.
013400 01  WS-DAY2-DATE-TIME.
013500     05  WS-DAY2-DATE                   PIC X(06) VALUE SPACE.
013600     05  WS-DAY2-TIME                   PIC X(04) VALUE SPACE.
013700 01  WS-DAY3-DATE-TIME.
013800     05  WS-DAY3-DATE                   PIC X(06) VALUE SPACE.
013900     05  WS-DAY3-TIME                   PIC X(04) VALUE SPACE.
014000 01  WS-UPD-PROJ-DATE-TIME.
014100     05  WS-UPD-PROJ-DATE               PIC X(06) VALUE SPACE.
014200     05  WS-UPD-PROJ-TIME               PIC X(04) VALUE SPACE.
014300 01  WS-UPD-PROJ-UD1-FROM               PIC X(10) VALUE SPACE.
014400 01  WS-UPD-PROJ-UD1-TO                 PIC X(10) VALUE SPACE.
014500 01  WS-UPD-PROJ-UD2-FROM               PIC X(10) VALUE SPACE.
014600 01  WS-UPD-PROJ-UD2-TO                 PIC X(10) VALUE SPACE.
014700*
014800 01  ON-DUTY-ASGN-AREA.
014900     02  ON-DUTY-ASGNMT-FLAG            PIC X(01) VALUE SPACE.
015000         88  ON-DUTY-UFP                          VALUE 'U'.
015100         88  ON-DUTY-AJ                           VALUE 'A'.
015200         88  ON-DUTY-XB                           VALUE 'X'.
015300     02  ON-DUTY-ASGNMT.
015400         03  OD-DIST                    PIC X(02) VALUE SPACE.
015500         03  OD-SUB-DIST                PIC X(02) VALUE SPACE.
015600         03  OD-AREA.
015700             04  OD-1                   PIC X(06).
015800             04  OD-2 REDEFINES OD-1.
015900                 06  OD-POOL            PIC X(02).
016000                 06  OD-TURN            PIC X(04).
016100             04  OD-CC                  PIC X(02) VALUE SPACE.
016200     02  FILLER                         PIC X(11).
016300
016400 01  ON-DUTY-OUT-TOWN-CODE              PIC X(10)
016500                                               VALUE '9999999999'.
016600     88  OUT-TOWN                              VALUE '0000000000'.
016700
016800
016900
017000*****************************************************************
017100***                 I/O STATUS CHECK FIELDS
017200*****************************************************************
017300 01  WS-RESPONSE                    PIC S9(8) COMP VALUE ZEROES.
017400 01  FILE-STATUS                    PIC 9(4)  VALUE ZEROES.
017500     COPY IOCODES.
017600*****************************************************************
017700***                  PROGRAM NAME COPYBOOKS
017800*****************************************************************
017900     COPY PSTCB0DV.
018000*****************************************************************
018100***                    COMMAREA COPYBOOKS
018200*****************************************************************
018300     COPY P917COMM.
018400     COPY P943COMM.
018500     COPY PS08COMM.
018600     COPY P903COMM.
018700     COPY P901COMM.
018800*****************************************************************
018900***                     FILE COPYBOOKS
019000*****************************************************************
019100     COPY WSCNTL.
019200     COPY WSMSTR.
019300     COPY WSMSTR3.
019400     COPY WSEMPTOD.
019500     COPY WSASGN.
019600     COPY WSUFP.
019700*****************************************************************
019800***                     MISC. COPYBOOKS
019900*****************************************************************
020000     COPY PSTERAR.
020100     COPY WSEDDATE.
020200     COPY WSEDTIME.
020300     COPY WSSYDTTM.
020400     COPY WSZONE.
020500     COPY WSOFFSET.
020600*CNLD-246-DXC-B
020700     COPY PSTCCRFT.
020800*CNLD-246-DXC-E
020900
021000 PROCEDURE DIVISION.
021100*
021200 P0000-MAINLINE.
021300*
021400     EXEC CICS IGNORE CONDITION
021500               ERROR
021600     END-EXEC
021700     EXEC CICS HANDLE ABEND
021800               LABEL(P9999-GOT-PROBLEM)
021900     END-EXEC
022000     COPY ABSTIME.
022100     EXEC CICS ASKTIME
022200               ABSTIME(WS-ABSTIME)
022300     END-EXEC
022400     ADD WS-ABSTIME-OFFSET TO WS-ABSTIME
022500     EXEC CICS FORMATTIME
022600               ABSTIME(WS-ABSTIME)
022700               YYYYMMDD(WS-SYSTEM-DATE-CENT)
022800               TIME(WS-SYSTEM-TIME-AREA)
022900     END-EXEC
023000     MOVE WS-PRESENT-TIME  TO WS-START-DATE-TIME
023100     PERFORM P9800-GET-TIME-OFFSET
023200
023300* 5/20/23 RJA CNLD-214 B
023400     MOVE SPACES                    TO CNTLKEY-AREA
023500     SET COMPANY-TYPE-REC           TO TRUE
023600     MOVE CNTLKEY-AREA              TO CNTLKEY
023700     PERFORM P8015-READ-CNTL-FILE
023800     IF SUCCESS
023900        IF CNTL-00-WRR-BEGINNING-DTTM > SPACES
024000           MOVE CNTL-00-WRR-BEGINNING-DTTM
024100                             TO WS-00-WRR-BEGINNING-DTTM
024200        END-IF
024300     END-IF
024400* 5/20/23 RJA CNLD-214 E
024500
024600     MOVE SPACES                    TO WS-CNTL-FILE
024700                                       WS-HOLD-CNTL-REC-ZZ
024800                                       CNTLKEY
024900     SET  WS-ZZ-CNTL-REC-NO         TO TRUE
025000     SET  SYSTEM-RESET-BREAK        TO TRUE
025100     MOVE CNTLKEY-AREA              TO CNTLKEY
025200     PERFORM P8015-READ-CNTL-FILE
025300     IF SUCCESS
025400        SET WS-ZZ-CNTL-REC-YES TO TRUE
025500        MOVE WS-CNTL-FILE           TO WS-HOLD-CNTL-REC-ZZ
025600     END-IF
025700     PERFORM P1000-PROCESS-SUBDIST
025800     PERFORM P9000-RETURN.
025900*
026000 P1000-PROCESS-SUBDIST.
000400*****************************************************************
000500*  LOOP ALL SUB-DIST, PICK ONES WHOSE CAN WRR = NEW, FOR EACH:  *
      *     LOOP ALL EMPLOYEES IN MSTR,                               *
      *     - FOR EACH EVALUATE IF ELIGIBLE FOR RESET                 *
000600*****************************************************************
026100*
026200     MOVE SPACES                    TO WS-CNTL-FILE
026300                                       WS-CNTL-READ-FLAG
026400                                       CNTLKEY
026500
026600     SET  SUB-DIST-TYPE-REC         TO TRUE
026700     MOVE CNTLKEY-AREA              TO CNTLKEY
026800     PERFORM P8000-STARTBR-CNTL-FILE
026900     IF SUCCESS
027000        PERFORM UNTIL CNTL-READ-DONE
027100
027200* 5/20/23 RJA CNLD-214 B
027300           IF SYNCPOINT-ISSUED
027400
027500              MOVE SPACES TO CNTLKEY-AREA
027600              SET  SUB-DIST-TYPE-REC TO TRUE
027700              MOVE WS-LAST-PROCESSED-DIST  TO CNTL-DIST
027800              MOVE WS-LAST-PROCESSED-SDIST TO CNTL-SUB-DIST
027900              MOVE CNTLKEY-AREA TO CNTLKEY
028000              PERFORM P8000-STARTBR-CNTL-FILE
028100
028200              SET NO-SYNCPOINT-ISSUED TO TRUE
028300
028400           END-IF
028500
028600           IF SUCCESS
028700* 5/20/23 RJA CNLD-214 E
028800
028900              PERFORM P8010-READNEXT-CNTL-FILE
029000
029100* 5/20/23 RJA CNLD-214 B
029200           END-IF
029300* 5/20/23 RJA CNLD-214 E
029400
029500           IF SUCCESS
029600              IF  SUB-DIST-TYPE-REC
029700
029800* 5/20/23 RJA CNLD-214 B
029900                 IF NOT (CNTL-DIST     = WS-LAST-PROCESSED-DIST
030000                     AND CNTL-SUB-DIST = WS-LAST-PROCESSED-SDIST)
030100
030200                    MOVE CNTL-DIST     TO WS-LAST-PROCESSED-DIST
030300                    MOVE CNTL-SUB-DIST TO WS-LAST-PROCESSED-SDIST
030400* 5/20/23 RJA CNLD-214 E
030500
030600                    IF CNTL-CAN-WRR-NEW
030700* 5/20/23 RJA CNLD-214 B
030800                       MOVE CNTL-02-WRR-BEGINNING-DTTM
030900                                       TO WS-WRR-START-DTTM
031000                       PERFORM P1132A-FETCH-WRR-START
031100* 5/20/23 RJA CNLD-214 E
031200                       PERFORM P8999-GET-CURR-TIME
031300* 5/20/23 RJA CNLD-214 B
031400                       MOVE WS-LOCAL-DATE TO WS-EMP-EFF-DATE
031500                       MOVE WS-LOCAL-TIME TO WS-EMP-EFF-TIME
031600                       PERFORM P1132D-COMP-8DAYS-AGO
031700* 5/20/23 RJA CNLD-214 E
031800                       PERFORM P2000-PROCESS-MSTR
031900* 5/20/23 RJA CNLD-214 B
032000                       EXEC CICS SYNCPOINT
032100                                 RESP(WS-RESPONSE)
032200                       END-EXEC
032300
032400                       SET SYNCPOINT-ISSUED TO TRUE
032500* 5/20/23 RJA CNLD-214 E
032600                    END-IF
032700* 5/20/23 RJA CNLD-214 B
032800                 END-IF
032900* 5/20/23 RJA CNLD-214 E
033000              ELSE
033100                 SET CNTL-READ-DONE TO TRUE
033200              END-IF
033300           ELSE
033400              SET CNTL-READ-DONE TO TRUE
033500           END-IF
033600        END-PERFORM
033700        PERFORM P8020-ENDBR-CNTL-FILE
033800     END-IF.
033900*
034000 P2000-PROCESS-MSTR.
000400*****************************************************************
000500*  LOOP ALL SUB-DIST, PICK ONES WHOSE CAN WRR = NEW, FOR EACH:  *
      *     LOOP ALL EMPLOYEES IN MSTR,                               *
      *     - FOR EACH EVALUATE FOR RESET:                            *
      *       + NOT IN WORKING STATUS                                 *
      *       + NOT YARDMASTER / YARDMASTER-TRAINEE                   *
      *       + TIE-UP HOME                                           *
      *       + PASS RESET ELIGIBILITY:                               *
      *         * 7DAY-END WITHIN 6HRS (READ MSTR3)                   * 
      *     - THEN:                                                   *
      *       + IF NATURAL RESET:                                     *
      *         * FOR SHOW-JOB:                                       *
      *           - CALC UD1, UD2, RESET-END & UPDATE MSTR3           *      
      *         * OTHERS:                                             *
      *           - SAME LOGIC FOR SYSTEM FORCE RESET                 *
      *       + IF SYSTEM FORCE RESET:                                *
      *         * UPDATE STATUS TO E95 (IF CURRENT IS A / C)          *
      *         * CALC NEW 7DAY & UPDATE MSTR3 <-- ??? ONLY CALL ?    * 
      *         * CALC UD1, UD2, RESET-END & UPDATE MSTR3             *
      *         * CALL CNP943 TO WRITE HISTORY                        *
      *         * CALL CNPS08 TO WRITE ETOD(EMPLOYEE TIME ON DUTY LOG)*
000600*****************************************************************
034100*
034200     MOVE SPACES                    TO WS-MSTR
034300                                       WS-MSTR-READ-FLAG
034400                                       MSTRDSCEK
034500     MOVE CNTL-DIST                 TO DIST     OF WS-MSTR
034600     MOVE CNTL-SUB-DIST             TO SUB-DIST OF WS-MSTR
034700     MOVE DIST-SDIST-CRFT-EMPNO-KEY TO MSTRDSCEK
034800     PERFORM P8100-STARTBR-MSTR-FILE
034900     IF SUCCESS
035000        PERFORM UNTIL MSTR-READ-DONE
035100           PERFORM P8110-READNEXT-MSTR-FILE
035200           IF SUCCESS
035300              IF DIST     OF WS-MSTR = CNTL-DIST AND
035400                 SUB-DIST OF WS-MSTR = CNTL-SUB-DIST
035500*CNLD-246-DXC-B
035600                 MOVE CRAFT OF WS-MSTR TO WS-CRAFT-CODE-CHECK
035700*CNLD-246-DXC-E
035800                 IF WS-ZZ-CNTL-REC-YES
035900                    IF (DIST OF WS-MSTR = DIST-TYPE-REC1-ZZ AND
036000                      SUB-DIST OF WS-MSTR = SUB-DIST-TYPE-REC1-ZZ)
036100                        OR
036200                       (DIST OF WS-MSTR = DIST-TYPE-REC2-ZZ AND
036300                      SUB-DIST OF WS-MSTR = SUB-DIST-TYPE-REC2-ZZ)
036400                        OR
036500                       (DIST OF WS-MSTR = DIST-TYPE-REC3-ZZ AND
036600                      SUB-DIST OF WS-MSTR = SUB-DIST-TYPE-REC3-ZZ)
036700                        OR (EMP-NBR OF WS-MSTR = EMP-NBR1-ZZ)
036800                        OR (EMP-NBR OF WS-MSTR = EMP-NBR2-ZZ)
036900                        IF LAYOFF-CODE-1 NOT = 'B'
037000*CNLD-246-DXC-B
037100                        AND NOT (YARDMASTER-CRAFT
037200                                 OR YARDMASTER-TRAINEE)
037300*CNLD-246-DXC-E
037400                           PERFORM P2100-EVALUATE-EMPLOYEE
037500                        END-IF
037600                    END-IF
037700                 ELSE
037800                    IF LAYOFF-CODE-1 NOT = 'B'
037900*CNLD-246-DXC-B
038000                    AND NOT (YARDMASTER-CRAFT
038100                             OR YARDMASTER-TRAINEE)
038200*CNLD-246-DXC-E
038300                       PERFORM P2100-EVALUATE-EMPLOYEE
038400                    END-IF
038500                 END-IF
038600              ELSE
038700                 SET MSTR-READ-DONE TO TRUE
038800              END-IF
038900           ELSE
039000              IF NO-RECORD-FND OR END-OF-FILE
039100                 SET MSTR-READ-DONE TO TRUE
039200              END-IF
039300           END-IF
039400        END-PERFORM
039500        PERFORM P8120-ENDBR-MSTR-FILE
039600     END-IF
039700     .
039800*
039900 P2100-EVALUATE-EMPLOYEE.
040000*
040100*    CHECK IF EMP IS AT HOME
040200     PERFORM P2110-VERIFY-TIE-UP-HOME
040300
040400     IF TIED-UP-HOME
040500
040600*       EVALUATE IF EMPLOYEE SHOULD BE FORCED INTO E95
040700        MOVE SPACE TO WS-E95-REQUIRED-FLAG
040800        SET EVALUATE-MODE TO TRUE
040900        PERFORM P2120-CHECK-RESET-ELIGIBILITY
041000
041100        IF E95-REQUIRED
041200*          UPDATE EMPLOYEE'S HOS WITH RESETS AND FORCE INTO E95
041300           MOVE SPACE TO WS-E95-REQUIRED-FLAG
041400           SET UPDATE-MODE TO TRUE
041500           PERFORM P2130-UPDATE-RESETS-AND-E95
041600        END-IF
041700
041800     END-IF
041900     .
042000*
042100 P2110-VERIFY-TIE-UP-HOME.
042200*
042300     MOVE SPACES                     TO WS-TIE-UP-FLAG
042400     PERFORM P8600-GET-ON-DUTY-JOB
042500     IF ON-DUTY-ASGNMT > SPACES
042600        IF ON-DUTY-UFP
042700* 06/12/23 RJA B EMPLOYEE TIED UP AT AWAY ON AN INTERDIVISIONAL
042800*                POOL (22 PG PP) IS MISINTERPRETED AS TIED-UP-HOME
042900*
043000*           MOVE SPACES               TO WS-UFP
043100*           MOVE OD-DIST              TO DIST2
043200*           MOVE OD-SUB-DIST          TO SUB-DIST2
043300*           MOVE OD-POOL              TO POOL-NAME2
043400*           MOVE OD-TURN              TO TURN-NBR OF UFPTURN-AREA
043500*           MOVE OD-CC                TO POOL-CRAFT-CODE2
043600*           MOVE UFPTURN-AREA         TO UFPTURN
043700*           PERFORM P8700-READ-UFPTURN
043800*           IF NOT SUCCESS
043900*              IF NO-RECORD-FND
044000** 5/20/23 RJA CNLD-214 B
044100**                SET IN-TOWN         TO TRUE
044200*                 CONTINUE
044300** 5/20/23 RJA CNLD-214 E
044400*              ELSE
044500*                 MOVE 'P2110-1'      TO ERR-PARAGRAPH
044600*                 MOVE UFPTURN        TO ERR-KEY
044700*                 PERFORM P9999-GOT-PROBLEM
044800*              END-IF
044900*           END-IF
045000*           IF IN-TOWN
045100*              SET TIED-UP-HOME       TO TRUE
045200*           END-IF
045300            CONTINUE
045400* 06/12/23 RJA E
045500        ELSE
045600           SET TIED-UP-HOME          TO TRUE
045700        END-IF
045800     ELSE
045900        SET TIED-UP-HOME             TO TRUE
046000     END-IF
046100     .
046200*
046300 P2120-CHECK-RESET-ELIGIBILITY.
046400*
046500     MOVE SPACES TO NATURAL-RESET-AT-0DVRUN-FL
046600                    NATURAL-RESET-BEFORE-0DVRUN-FL
046700
046800     MOVE EMP-NBR IN WS-MSTR       TO MSTR3NBRK
046900
047000     EXEC CICS READ
047100               DATASET(MSTR3-VIA-EMP-NBR)
047200               INTO(WS-MSTR3)
047300               LENGTH(MSTR3ENBR-RLGTH)
047400               RIDFLD(MSTR3NBRK)
047500               KEYLENGTH(MSTR3ENBR-KLGTH)
047600               RESP(WS-RESPONSE)
047700     END-EXEC
047800     MOVE WS-RESPONSE              TO FILE-STATUS.
047900     IF NOT SUCCESS
048000        IF NOT (NO-RECORD-FND OR END-OF-FILE)
048100           MOVE 'P2120-1'          TO ERR-PARAGRAPH
048200           MOVE MSTR3NBRK          TO ERR-KEY
048300           PERFORM P9999-GOT-PROBLEM
048400        END-IF
048500     ELSE
048600
048700        IF (MSTR3-PROJ-RESET-BRK = MSTR3-SYSTEM-RESET-BRK)
048800        AND MSTR3-SYSTEM-RESET-BRK > '0000000000'
048900*
049000*          CHECK FOR ANY HOS B/W SYSTEM-RESET AND NOW
049100*
049200           PERFORM P1131-PRV-HOS-AFTER-SYS-RESET
049300        ELSE
049400*
049500*          CHECK FOR ALL HOS B/W 7DAY-START AND NOW
049600*
049700           PERFORM P1132-RECALC-PROJ-RESET-BRK
049800        END-IF
049900*
050000        IF NOT E95-NOT-REQUIRED
050100           PERFORM P1140-CALCULATE-7DAY-END
050200           PERFORM P1150-FURTHER-VALIDATE-E95-REQ
050300        END-IF
050400*
050500     END-IF
050600     .
050700
050800*
050900 P2130-UPDATE-RESETS-AND-E95.
051000*
051100     IF NATURAL-RESET-BEFORE-0DVRUN
051200*       UPDATE NATURAL-RESETS AND FORCE E95
051300        PERFORM P2140-NATURAL-RESETS-AND-E95
051400     ELSE
051500*       FORCE E95
051600        MOVE EMP-NBR IN WS-MSTR       TO MSTR3NBRK
051700*
051800        PERFORM P8210-READ-UPD-MSTR3-FILE
051900*
052000        MOVE WS-WRR-7DAY-END       TO MSTR3-7DAY-END-PERIOD
052100        MOVE SPACES                TO MSTR3-PREV-7DAY-END
052200        MOVE WS-UPD-PROJ-DATE-TIME TO MSTR3-PROJ-RESET-BRK
052300                                      MSTR3-SYSTEM-RESET-BRK
052400        MOVE WS-UPD-PROJ-UD1-FROM  TO MSTR3-PROJ-UD1-FROM
052500                                      MSTR3-SYSTEM-UD1-FROM
052600        MOVE WS-UPD-PROJ-UD1-TO    TO MSTR3-PROJ-UD1-TO
052700                                      MSTR3-SYSTEM-UD1-TO
052800        MOVE WS-UPD-PROJ-UD2-FROM  TO MSTR3-PROJ-UD2-FROM
052900                                      MSTR3-SYSTEM-UD2-FROM
053000        MOVE WS-UPD-PROJ-UD2-TO    TO MSTR3-PROJ-UD2-TO
053100                                      MSTR3-SYSTEM-UD2-TO
053200*
053300        PERFORM P8220-REWRITE-MSTR3-FILE
053400*
053500        SET WS-FORCED-RESET   TO TRUE
053600        PERFORM P1137-WRITE-RESET-BRK-WRK-HIST
053700        PERFORM P1138-WRITE-ETOD-RESET-BREAK
053800        IF LAYOFF-CODE-1 IN WS-MSTR = ('A' OR 'C')
053900           PERFORM P2700-CHANGE-TO-E95-STATUS
054000        END-IF
054100     END-IF
054200     .
054300
054400*
054500 P2140-NATURAL-RESETS-AND-E95.
054600*
054700     MOVE SPACES TO NATURAL-RESET-AT-0DVRUN-FL
054800                    NATURAL-RESET-BEFORE-0DVRUN-FL
054900
055000     IF MSTR3-PROJ-RESET-BRK = MSTR3-SYSTEM-RESET-BRK
055100        AND MSTR3-SYSTEM-RESET-BRK > '0000000000'
055200*
055300*       CHECK FOR ANY HOS B/W SYSTEM-RESET AND NOW
055400*
055500        PERFORM P1131-PRV-HOS-AFTER-SYS-RESET
055600     ELSE
055700*
055800*          CHECK FOR ALL HOS B/W 7DAY-START AND NOW
055900*
056000        PERFORM P1132-RECALC-PROJ-RESET-BRK
056100     END-IF
056200
056300     IF WS-7DAY-START-DTTM > '0000000000'
056400        PERFORM P1140-CALCULATE-7DAY-END
056500        MOVE EMP-NBR OF WS-MSTR   TO MSTR3NBRK
056600        PERFORM P8210-READ-UPD-MSTR3-FILE
056700*
056800        IF NATURAL-RESET-AT-0DVRUN
056900*
057000*          THIS CAN BE TRUE VERY RARELY IF EMPTOD RECS WERE
057100*          DELETED BETWEEN THE EMPTOD-BROWSES IN P1133 AND P1133U
057200*
057300           CONTINUE
057400        ELSE
057500           PERFORM P1150-FURTHER-VALIDATE-E95-REQ
057600        END-IF
057700        IF E95-REQUIRED
057800           MOVE WS-UPD-PROJ-DATE-TIME
057900                                  TO MSTR3-PROJ-RESET-BRK
058000                                     MSTR3-SYSTEM-RESET-BRK
058100           MOVE WS-UPD-PROJ-UD1-FROM
058200                                  TO MSTR3-PROJ-UD1-FROM
058300                                     MSTR3-SYSTEM-UD1-FROM
058400           MOVE WS-UPD-PROJ-UD1-TO
058500                                  TO MSTR3-PROJ-UD1-TO
058600                                     MSTR3-SYSTEM-UD1-TO
058700           MOVE WS-UPD-PROJ-UD2-FROM
058800                                  TO MSTR3-PROJ-UD2-FROM
058900                                     MSTR3-SYSTEM-UD2-FROM
059000           MOVE WS-UPD-PROJ-UD2-TO
059100                                  TO MSTR3-PROJ-UD2-TO
059200                                     MSTR3-SYSTEM-UD2-TO
059300        ELSE
059400           MOVE WS-UPD-PROJ-DATE-TIME
059500                                  TO MSTR3-PROJ-RESET-BRK
059600           MOVE WS-UPD-PROJ-UD1-FROM
059700                                  TO MSTR3-PROJ-UD1-FROM
059800           MOVE WS-UPD-PROJ-UD1-TO
059900                                  TO MSTR3-PROJ-UD1-TO
060000           MOVE WS-UPD-PROJ-UD2-FROM
060100                                  TO MSTR3-PROJ-UD2-FROM
060200           MOVE WS-UPD-PROJ-UD2-TO
060300                                  TO MSTR3-PROJ-UD2-TO
060400        END-IF
060500*
060600        MOVE WS-WRR-7DAY-END      TO MSTR3-7DAY-END-PERIOD
060700        MOVE SPACES               TO MSTR3-PREV-7DAY-END
060800        PERFORM P8220-REWRITE-MSTR3-FILE
060900*
061000        IF E95-REQUIRED
061100           SET WS-FORCED-RESET   TO TRUE
061200           PERFORM P1137-WRITE-RESET-BRK-WRK-HIST
061300           PERFORM P1138-WRITE-ETOD-RESET-BREAK
061400           IF LAYOFF-CODE-1 IN WS-MSTR = ('A' OR 'C')
061500              PERFORM P2700-CHANGE-TO-E95-STATUS
061600           END-IF
061700        END-IF
061800     END-IF
061900     .
062000*
062100 P1131-PRV-HOS-AFTER-SYS-RESET.
062200*
062300*    CONTROL REACHES HERE ONLY IF :
062400*    MSTR3-SYSTEM-RESET-BRK = MSTR3-PROJ-RESET-BRK
062500
062600     IF MSTR3-PROJ-RESET-BRK > WS-WRR-START-DTTM
062700        MOVE MSTR3-PROJ-RESET-BRK TO WS-7DAY-START-DTTM
062800     ELSE
062900
063000        IF WS-8DAYS-AGO-DTTM > WS-WRR-START-DTTM
063100           MOVE WS-8DAYS-AGO-DTTM  TO WS-7DAY-START-DTTM
063200        ELSE
063300*          START READING FROM (WS-WRR-START-DTTM - 1 DAY)
063400           PERFORM P1132B-WRR-START-MINUS-1DY
063500        END-IF
063600     END-IF
063700
063800     IF UPDATE-MODE
063900        PERFORM P1133U-PRV-HOS-AFTER-7DY-START
064000     ELSE
064100        PERFORM P1133-PRV-HOS-AFTER-7DY-START
064200     END-IF
064300     .
064400
064500*
064600 P1132-RECALC-PROJ-RESET-BRK.
064700*
064800     IF MSTR3-7DAY-END-PERIOD > '0000000000'
064900*       COMPUTE 7DAY START FROM MSTR3
065000        PERFORM P1140A-CALCULATE-7DAY-START
065100
065200        IF WS-7DAY-START-DTTM > WS-WRR-START-DTTM
065300           CONTINUE
065400        ELSE
065500
065600           IF WS-8DAYS-AGO-DTTM > WS-WRR-START-DTTM
065700              MOVE WS-8DAYS-AGO-DTTM  TO WS-7DAY-START-DTTM
065800           ELSE
065900*             START READING FROM (WS-WRR-START-DTTM - 1 DAY)
066000              PERFORM P1132B-WRR-START-MINUS-1DY
066100           END-IF
066200        END-IF
066300     ELSE
066400
066500        IF WS-8DAYS-AGO-DTTM > WS-WRR-START-DTTM
066600           MOVE WS-8DAYS-AGO-DTTM  TO WS-7DAY-START-DTTM
066700        ELSE
066800*          START READING FROM (WS-WRR-START-DTTM - 1 DAY)
066900           PERFORM P1132B-WRR-START-MINUS-1DY
067000        END-IF
067100     END-IF
067200
067300     IF UPDATE-MODE
067400        PERFORM P1133U-PRV-HOS-AFTER-7DY-START
067500     ELSE
067600        PERFORM P1133-PRV-HOS-AFTER-7DY-START
067700     END-IF
067800     .
067900*
068000 P1132A-FETCH-WRR-START.
068100*
068200     IF WS-WRR-START-DTTM > SPACES
068300* VALIDATE DTTM ON SUB-DISTRICT CONTROL
068400        PERFORM P1132C-CHECK-WRR-START
068500     END-IF
068600
068700     IF WS-WRR-START-DTTM NOT > SPACES
068800* NO DTTM/VALID-DTTM ON SUB-DISTRICT CHECK THE CORPORATE CONTROL
068900        IF WS-00-WRR-BEGINNING-DTTM > SPACES
069000           MOVE WS-00-WRR-BEGINNING-DTTM
069100                                TO WS-WRR-START-DTTM
069200           PERFORM P1132C-CHECK-WRR-START
069300        END-IF
069400     END-IF
069500
069600     IF WS-WRR-START-DTTM NOT > SPACES
069700* NO DATE ON CONTROLS DEFAULT TO GOVERNMENT DATE
069800        MOVE '2305250001' TO WS-WRR-START-DTTM
069900     END-IF
070000     .
070100
070200*
070300 P1132B-WRR-START-MINUS-1DY.
070400*
070500* SUBTRACT 1 DAY FROM THE ABOVE RESULT - THE THEORY HERE IS THAT
070600*  NO TOUR OF DUTY WILL HAVE LASTED MORE THAN 24 HOURS, SO WE CAN
070700*  GO BACK 1 DAY AND START LOOKING AT THE RECORDS STARTING FROM
070800*  THAT DATE TO DETERMINE IF ANY OF THE TIME ON DUTY SHOULD COUNT
070900     MOVE ZEROS              TO DATE-CONVERSION-PARMS
071000     SET PARM-SUBTRACT       TO TRUE
071100     MOVE WS-WRR-START-DATE  TO PARM-PRI-DATE-GREG
071200     MOVE 01                 TO PARM-SEC-GREG-DAY
071300     EXEC CICS LINK
071400               PROGRAM(P903-PGM)
071500               COMMAREA(DATE-CONVERSION-PARMS)
071600               LENGTH(P903-LGTH)
071700               RESP(WS-RESPONSE)
071800     END-EXEC
071900     MOVE WS-RESPONSE             TO FILE-STATUS
072000     IF NOT SUCCESS
072100        MOVE 'P1132B-1'           TO ERR-PARAGRAPH
072200        MOVE 'P903LINK'           TO ERR-KEY
072300        PERFORM P9999-GOT-PROBLEM
072400     END-IF
072500
072600     MOVE PARM-RES-DATE-GREG TO WS-7DAY-START-DATE
072700     MOVE WS-WRR-START-TIME  TO WS-7DAY-START-TIME
072800     .
072900
073000*
073100 P1132C-CHECK-WRR-START.
073200*
073300     MOVE WS-WRR-START-DATE   TO DE-YYMMDD
073400     SET DE-YYMMDD-FORMAT     TO TRUE
073500     PERFORM P8998-DATEEDIT
073600     IF DE-INVALID-DATE
073700        MOVE SPACES TO WS-WRR-START-DTTM
073800     END-IF
073900     IF WS-WRR-START-TIME > SPACES
074000        MOVE WS-WRR-START-TIME   TO TE-MILITARY-TIME
074100        SET TE-MILITARY-FORMAT   TO TRUE
074200        PERFORM P8997-TIMEEDIT
074300        IF TE-INVALID-TIME
074400           MOVE SPACES TO WS-WRR-START-DTTM
074500        END-IF
074600     END-IF
074700     .
074800*
074900 P1132D-COMP-8DAYS-AGO.
075000*
075100* COMPUTE THE DATE 8 DAYS AGO FROM CURRENT ON-DUTY TIME
075200     MOVE ZEROS                 TO DATE-CONVERSION-PARMS
075300     SET PARM-SUBTRACT          TO TRUE
075400     MOVE WS-EMP-EFF-DATE       TO PARM-PRI-DATE-GREG
075500     MOVE WS-EMP-EFF-TIME       TO PARM-PRI-HRMN
075600     MOVE 000008                TO PARM-SEC-GREG-DAY
075700     EXEC CICS LINK
075800               PROGRAM(P903-PGM)
075900               COMMAREA(DATE-CONVERSION-PARMS)
076000               LENGTH(P903-LGTH)
076100               RESP(WS-RESPONSE)
076200     END-EXEC
076300     MOVE WS-RESPONSE           TO FILE-STATUS
076400     IF NOT SUCCESS
076500        MOVE 'P1132D-1'         TO ERR-PARAGRAPH
076600        MOVE 'P903LINK'         TO ERR-KEY
076700        PERFORM P9999-GOT-PROBLEM
076800     END-IF
076900     MOVE PARM-RES-DATE-GREG    TO WS-8DAYS-AGO-DATE
077000     MOVE PARM-RES-HRMN         TO WS-8DAYS-AGO-TIME
077100     .
077200*
077300 P1133-PRV-HOS-AFTER-7DY-START.
077400*
077500     MOVE SPACES                     TO WS-EMP-TOD
077600     MOVE MSTR3-EMP-NBR              TO EMPTOD-EMP-NBR
077700     MOVE WS-7DAY-START-DATE         TO DE-YYMMDD
077800     SET DE-YYMMDD-FORMAT            TO TRUE
077900     PERFORM P8998-DATEEDIT
078000     MOVE DE-CCYYMMDD                TO EMPTOD-OD-DATE-CENT
078100     MOVE WS-7DAY-START-TIME         TO EMPTOD-OD-TIME
078200     MOVE EMPTOD-KEY-AREA            TO EMPTOD-EMPKEY
078300
078400     PERFORM P8060-EMPTOD-STARTBR
078500     MOVE 'N'                        TO WS-READ-DONE-CODE
078600     MOVE '0000000000'               TO WS-7DAY-START-DTTM
078700     PERFORM UNTIL READ-DONE
078800        PERFORM P8070-EMPTOD-READNEXT
078900        IF SUCCESS
079000           IF EMPTOD-EMP-NBR = MSTR3-EMP-NBR
079100              AND WS-EMP-EFF-DATE-TIME > EMPTOD-OD-YYMMDD-HRMN
079200              IF (NOT EMPTOD-VIRTUAL-ENTRY) AND
079300*
079400                 (NOT EMPTOD-IMPOSED-REST-REC)
079500*             EMPTOD-IMPOSED-REST-REC IS UNLIKELY TO BE FOUND WITH
079600*             NEW CAN WRR, ADDING THIS AS A SANITY CHECK
079700                 IF EMPTOD-RESET-BREAK-REC
079800                    MOVE '0000000000'   TO WS-7DAY-START-DTTM
079900                 END-IF
080000                 IF EMPTOD-CONTACT AND
080100                    WS-7DAY-START-DTTM > '0000000000'
080200                    IF ((EMPTOD-OD-YYMMDD-HRMN >=
080300                             WS-UPD-PROJ-UD1-FROM
080400                          AND EMPTOD-OD-YYMMDD-HRMN <=
080500                               WS-UPD-PROJ-UD1-TO)
080600                        OR
080700                        (EMPTOD-OD-YYMMDD-HRMN >=
080800                            WS-UPD-PROJ-UD2-FROM
080900                          AND EMPTOD-OD-YYMMDD-HRMN <=
081000                                  WS-UPD-PROJ-UD2-TO)
081100                        OR
081200                        (EMPTOD-OFFD-YYMMDD-HRMN >=
081300                            WS-UPD-PROJ-UD1-FROM
081400                          AND EMPTOD-OFFD-YYMMDD-HRMN <=
081500                                  WS-UPD-PROJ-UD1-TO)
081600                        OR
081700                        (EMPTOD-OFFD-YYMMDD-HRMN >=
081800                            WS-UPD-PROJ-UD2-FROM
081900                          AND EMPTOD-OFFD-YYMMDD-HRMN <=
082000                                  WS-UPD-PROJ-UD2-TO))
082100                       PERFORM P1136E-EXTEND-PROJ-RESET
082200                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
082300                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
082400                    END-IF
082500                 END-IF
082600                 IF (NOT EMPTOD-CONTACT) AND
082700                    (NOT EMPTOD-RESET-BREAK-REC)
082800
082900                    IF WS-7DAY-START-DTTM NOT > '0000000000'
083000                    AND EMPTOD-OD-YYMMDD-HRMN <=
083100                                              WS-WRR-START-DTTM
083200                    AND EMPTOD-OFFD-YYMMDD-HRMN >
083300                                              WS-WRR-START-DTTM
083400                       MOVE WS-WRR-START-DTTM
083500                                   TO WS-7DAY-START-DTTM
083600                       PERFORM P1136-COMPUTE-PROJ-RESET
083700                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
083800                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
083900                    END-IF
084000
084100                    IF WS-7DAY-START-DTTM NOT > '0000000000'
084200                    AND EMPTOD-OD-YYMMDD-HRMN >=
084300                                              WS-WRR-START-DTTM
084400                       MOVE EMPTOD-OD-YYMMDD-HRMN
084500                                        TO WS-7DAY-START-DTTM
084600                       PERFORM P1136-COMPUTE-PROJ-RESET
084700                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
084800                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
084900                    END-IF
085000
085100                    IF WS-7DAY-START-DTTM  > '0000000000'
085200                    AND EMPTOD-OD-YYMMDD-HRMN >
085300                        WS-UPD-PROJ-DATE-TIME
085400                    AND (EMPTOD-HOS-CLAIM OR
085500                         EMPTOD-COMMUTE   OR
085600                         EMPTOD-MANUAL)
085700*                      TRAIN RELATED HOS SHOULD BE SKIPPED :
085800*                      WITH TRAINS, (ORDER - LEAD) SHOULD BE MORE
085900*                      THAN PROJECTED-RESET WHICH IS CHECKED AT
086000*                      THE TIME OF CALL; HERE, WE ONLY LOOK FOR
086100*                      RESETS BEFORE HOS-CLAIMS AND MANUAL/COMMUTE
086200*
086300                       SET NATURAL-RESET-BEFORE-0DVRUN TO TRUE
086400*
086500                       MOVE EMPTOD-OD-YYMMDD-HRMN
086600                                        TO WS-7DAY-START-DTTM
086700                       PERFORM P1136-COMPUTE-PROJ-RESET
086800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
086900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
087000                    END-IF
087100
087200                    IF WS-7DAY-START-DTTM  > '0000000000'
087300                    AND EMPTOD-OFFD-YYMMDD-HRMN >
087400                        LAST-EMPTOD-OFFD-YYMMDD-HRMN
087500*                      THERE CAN BE OVERLAPPING HOS, PROJ-RESET
087600*                      SHOULD BE COMPUTED W/ GREATEST OFFD READ
087700                       PERFORM P1136-COMPUTE-PROJ-RESET
087800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
087900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
088000                    END-IF
088100                 END-IF
088200              END-IF
088300           ELSE
088400              SET READ-DONE          TO TRUE
088500           END-IF
088600        ELSE
088700           SET READ-DONE             TO TRUE
088800        END-IF
088900     END-PERFORM
089000     PERFORM P8090-EMPTOD-ENDBR
089100
089200     IF (WS-7DAY-START-DTTM NOT > '0000000000')
089300*       NO HOS RECORDS WERE FOUND;
089400        SET E95-NOT-REQUIRED         TO TRUE
089500     ELSE
089600*      ?WILL HAVE TO RETHINK THE IVR-NOTIFY-PROJECTED-RESET
089700*      ?EXTENSION PROCESS
089800        IF MSTR3-PROJ-RESET-BRK-CH > WS-UPD-PROJ-DATE-TIME
089900           MOVE MSTR3-PROJ-RESET-BRK-CH TO WS-UPD-PROJ-DATE-TIME
090000        END-IF
090100*
090200        IF WS-EMP-EFF-DATE-TIME     > WS-UPD-PROJ-DATE-TIME
090300           SET NATURAL-RESET-AT-0DVRUN TO TRUE
090400
090500           SET E95-NOT-REQUIRED      TO TRUE
090600        END-IF
090700     END-IF
090800     .
090900*
091000 P1133U-PRV-HOS-AFTER-7DY-START.
091100*
091200     MOVE SPACES                     TO WS-PREV-7DAY-START-DTTM
091300                                     LAST-EMPTOD-OFFD-YYMMDD-HRMN
091400
091500     MOVE SPACES                     TO WS-EMP-TOD
091600     MOVE MSTR3-EMP-NBR              TO EMPTOD-EMP-NBR
091700     MOVE WS-7DAY-START-DATE         TO DE-YYMMDD
091800     SET DE-YYMMDD-FORMAT            TO TRUE
091900     PERFORM P8998-DATEEDIT
092000     MOVE DE-CCYYMMDD                TO EMPTOD-OD-DATE-CENT
092100     MOVE WS-7DAY-START-TIME         TO EMPTOD-OD-TIME
092200     MOVE EMPTOD-KEY-AREA            TO EMPTOD-EMPKEY
092300
092400     PERFORM P8060-EMPTOD-STARTBR
092500     MOVE 'N'                        TO WS-READ-DONE-CODE
092600     MOVE '0000000000'               TO WS-7DAY-START-DTTM
092700     PERFORM UNTIL READ-DONE
092800        PERFORM P8070-EMPTOD-READNEXT
092900        IF SUCCESS
093000           IF EMPTOD-EMP-NBR = MSTR3-EMP-NBR
093100              AND WS-EMP-EFF-DATE-TIME > EMPTOD-OD-YYMMDD-HRMN
093200              IF (NOT EMPTOD-VIRTUAL-ENTRY) AND
093300*
093400                 (NOT EMPTOD-IMPOSED-REST-REC)
093500*             EMPTOD-IMPOSED-REST-REC IS UNLIKELY TO BE FOUND WITH
093600*             NEW CAN WRR, ADDING THIS AS A SANITY CHECK
093700                 IF EMPTOD-RESET-BREAK-REC
093800                    MOVE '0000000000'   TO WS-7DAY-START-DTTM
093900                 END-IF
094000                 IF EMPTOD-CONTACT AND
094100                    WS-7DAY-START-DTTM > '0000000000'
094200                    IF ((EMPTOD-OD-YYMMDD-HRMN >=
094300                             WS-UPD-PROJ-UD1-FROM
094400                          AND EMPTOD-OD-YYMMDD-HRMN <=
094500                               WS-UPD-PROJ-UD1-TO)
094600                        OR
094700                        (EMPTOD-OD-YYMMDD-HRMN >=
094800                            WS-UPD-PROJ-UD2-FROM
094900                          AND EMPTOD-OD-YYMMDD-HRMN <=
095000                                  WS-UPD-PROJ-UD2-TO)
095100                        OR
095200                        (EMPTOD-OFFD-YYMMDD-HRMN >=
095300                            WS-UPD-PROJ-UD1-FROM
095400                          AND EMPTOD-OFFD-YYMMDD-HRMN <=
095500                                  WS-UPD-PROJ-UD1-TO)
095600                        OR
095700                        (EMPTOD-OFFD-YYMMDD-HRMN >=
095800                            WS-UPD-PROJ-UD2-FROM
095900                          AND EMPTOD-OFFD-YYMMDD-HRMN <=
096000                                  WS-UPD-PROJ-UD2-TO))
096100                       PERFORM P1136E-EXTEND-PROJ-RESET
096200                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
096300                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
096400                    END-IF
096500                 END-IF
096600                 IF (NOT EMPTOD-CONTACT) AND
096700                    (NOT EMPTOD-RESET-BREAK-REC)
096800
096900                    IF WS-7DAY-START-DTTM NOT > '0000000000'
097000                    AND EMPTOD-OD-YYMMDD-HRMN <=
097100                                              WS-WRR-START-DTTM
097200                    AND EMPTOD-OFFD-YYMMDD-HRMN >
097300                                              WS-WRR-START-DTTM
097400                       MOVE WS-WRR-START-DTTM
097500                                   TO WS-7DAY-START-DTTM
097600                                      WS-PREV-7DAY-START-DTTM
097700                       PERFORM P1136-COMPUTE-PROJ-RESET
097800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
097900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
098000                    END-IF
098100
098200                    IF WS-7DAY-START-DTTM NOT > '0000000000'
098300                    AND EMPTOD-OD-YYMMDD-HRMN >=
098400                                              WS-WRR-START-DTTM
098500                       MOVE EMPTOD-OD-YYMMDD-HRMN
098600                                        TO WS-7DAY-START-DTTM
098700                                      WS-PREV-7DAY-START-DTTM
098800                       PERFORM P1136-COMPUTE-PROJ-RESET
098900                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
099000                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
099100                    END-IF
099200
099300                    IF WS-7DAY-START-DTTM  > '0000000000'
099400                    AND EMPTOD-OD-YYMMDD-HRMN >
099500                        WS-UPD-PROJ-DATE-TIME
099600                    AND (EMPTOD-HOS-CLAIM OR
099700                         EMPTOD-COMMUTE   OR
099800                         EMPTOD-MANUAL)
099900*                      TRAIN RELATED HOS SHOULD BE SKIPPED :
100000*                      WITH TRAINS, (ORDER - LEAD) SHOULD BE MORE
100100*                      THAN PROJECTED-RESET WHICH IS CHECKED AT
100200*                      THE TIME OF CALL; HERE, WE ONLY LOOK FOR
100300*                      RESETS BEFORE HOS-CLAIMS AND MANUAL/COMMUTE
100400*
100500                       SET NATURAL-RESET-BEFORE-0DVRUN TO TRUE
100600
100700                       PERFORM P8090-EMPTOD-ENDBR
100800                       SET ENDED-BROWSE-AT-THIS-REC TO TRUE
100900
101000                       SET WS-NATURAL-RESET TO TRUE
101100                       PERFORM P1137-WRITE-RESET-BRK-WRK-HIST
101200                       PERFORM P1138-WRITE-ETOD-RESET-BREAK
101300
101400                       MOVE EMPTOD-OD-YYMMDD-HRMN
101500                                        TO WS-7DAY-START-DTTM
101600                                      WS-PREV-7DAY-START-DTTM
101700                       PERFORM P1136-COMPUTE-PROJ-RESET
101800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
101900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
102000                    END-IF
102100
102200                    IF WS-7DAY-START-DTTM  > '0000000000'
102300                    AND EMPTOD-OFFD-YYMMDD-HRMN >
102400                        LAST-EMPTOD-OFFD-YYMMDD-HRMN
102500*                      THERE CAN BE OVERLAPPING HOS, PROJ-RESET
102600*                      SHOULD BE COMPUTED W/ GREATEST OFFD READ
102700                       PERFORM P1136-COMPUTE-PROJ-RESET
102800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
102900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
103000                    END-IF
103100                 END-IF
103200              END-IF
103300           ELSE
103400              SET READ-DONE          TO TRUE
103500           END-IF
103600        ELSE
103700           SET READ-DONE             TO TRUE
103800        END-IF
103900*
104000        IF ENDED-BROWSE-AT-THIS-REC
104100           MOVE EMPTOD-KEY-AREA TO EMPTOD-EMPKEY
104200           PERFORM P8060-EMPTOD-STARTBR
104300*          IF THIS LOOP IS MODIFIED, PLEASE NOTE THIS SAME RECORD
104400*          WE JUST PROCESSED WILL BE READ AGAIN AT READNEXT.
104500
104600           MOVE SPACES          TO ENDED-BROWSE-FL
104700        END-IF
104800*
104900     END-PERFORM
105000     PERFORM P8090-EMPTOD-ENDBR
105100
105200     IF (WS-7DAY-START-DTTM NOT > '0000000000')
105300*       NO HOS RECORDS WERE FOUND;
105400        SET E95-NOT-REQUIRED         TO TRUE
105500     ELSE
105600*      ?WILL HAVE TO RETHINK THE IVR-NOTIFY-PROJECTED-RESET
105700*      ?EXTENSION PROCESS
105800        IF MSTR3-PROJ-RESET-BRK-CH > WS-UPD-PROJ-DATE-TIME
105900           MOVE MSTR3-PROJ-RESET-BRK-CH TO WS-UPD-PROJ-DATE-TIME
106000        END-IF
106100*
106200        IF WS-EMP-EFF-DATE-TIME     > WS-UPD-PROJ-DATE-TIME
106300           SET NATURAL-RESET-AT-0DVRUN TO TRUE
106400
106500           SET E95-NOT-REQUIRED      TO TRUE
106600        END-IF
106700     END-IF
106800     .
106900*
107000 P1136-COMPUTE-PROJ-RESET.
107100*
107200     MOVE EMPTOD-OFFD-DATE        TO WS-DAY1-DATE
107300     MOVE EMPTOD-OFFD-TIME        TO WS-DAY1-TIME
107400
107500     MOVE ZEROS                   TO DATE-CONVERSION-PARMS
107600     SET PARM-ADD                 TO TRUE
107700     MOVE WS-DAY1-DATE            TO PARM-PRI-DATE-GREG
107800     MOVE 01                      TO PARM-SEC-GREG-DAY
107900     EXEC CICS LINK
108000               PROGRAM(P903-PGM)
108100               COMMAREA(DATE-CONVERSION-PARMS)
108200               LENGTH(P903-LGTH)
108300               RESP(WS-RESPONSE)
108400     END-EXEC
108500     MOVE WS-RESPONSE             TO FILE-STATUS
108600     IF NOT SUCCESS
108700        MOVE 'P1136-1'            TO ERR-PARAGRAPH
108800        MOVE 'P903LINK'           TO ERR-KEY
108900        PERFORM P9999-GOT-PROBLEM
109000     END-IF
109100     MOVE PARM-RES-DATE-GREG      TO WS-DAY2-DATE
109200
109300     MOVE ZEROS                   TO DATE-CONVERSION-PARMS
109400     SET PARM-ADD                 TO TRUE
109500     MOVE WS-DAY1-DATE            TO PARM-PRI-DATE-GREG
109600     MOVE 02                      TO PARM-SEC-GREG-DAY
109700     EXEC CICS LINK
109800               PROGRAM(P903-PGM)
109900               COMMAREA(DATE-CONVERSION-PARMS)
110000               LENGTH(P903-LGTH)
110100               RESP(WS-RESPONSE)
110200     END-EXEC
110300     MOVE WS-RESPONSE             TO FILE-STATUS
110400     IF NOT SUCCESS
110500        MOVE 'P1136-2'            TO ERR-PARAGRAPH
110600        MOVE 'P903LINK'           TO ERR-KEY
110700        PERFORM P9999-GOT-PROBLEM
110800     END-IF
110900     MOVE PARM-RES-DATE-GREG      TO WS-DAY3-DATE
111000
111100     IF WS-DAY1-TIME >= 2200 AND WS-DAY1-TIME <= 2359
111200        MOVE ZEROS                TO DATE-CONVERSION-PARMS
111300        SET PARM-ADD              TO TRUE
111400        MOVE WS-DAY1-DATE         TO PARM-PRI-DATE-GREG
111500        MOVE WS-DAY1-TIME         TO PARM-PRI-HRMN
111600        MOVE 3200                 TO PARM-SEC-HRMN
111700        EXEC CICS LINK
111800                  PROGRAM(P903-PGM)
111900                  COMMAREA(DATE-CONVERSION-PARMS)
112000                  LENGTH(P903-LGTH)
112100                  RESP(WS-RESPONSE)
112200        END-EXEC
112300        MOVE WS-RESPONSE          TO FILE-STATUS
112400        IF NOT SUCCESS
112500           MOVE 'P1136-3'         TO ERR-PARAGRAPH
112600           MOVE 'P903LINK'        TO ERR-KEY
112700           PERFORM P9999-GOT-PROBLEM
112800        END-IF
112900        MOVE PARM-RES-DATE-GREG   TO WS-UPD-PROJ-DATE
113000        MOVE PARM-RES-HRMN        TO WS-UPD-PROJ-TIME
113100        MOVE WS-DAY1-DATE-TIME    TO WS-UPD-PROJ-UD1-FROM
113200        MOVE WS-DAY2-DATE         TO WS-UPD-PROJ-UD1-TO(1:6)
113300        MOVE '0800'               TO WS-UPD-PROJ-UD1-TO(7:4)
113400        MOVE WS-DAY2-DATE         TO WS-UPD-PROJ-UD2-FROM(1:6)
113500        MOVE '2200'               TO WS-UPD-PROJ-UD2-FROM(7:4)
113600        MOVE WS-DAY3-DATE         TO WS-UPD-PROJ-UD2-TO(1:6)
113700        MOVE '0559'               TO WS-UPD-PROJ-UD2-TO(7:4)
113800     ELSE
113900        IF WS-DAY1-TIME = 0000
114000           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-DATE
114100           MOVE '0800'            TO WS-UPD-PROJ-TIME
114200           MOVE WS-DAY1-DATE      TO WS-UPD-PROJ-UD1-FROM(1:6)
114300                                    WS-UPD-PROJ-UD1-TO(1:6)
114400                                    WS-UPD-PROJ-UD2-FROM(1:6)
114500           MOVE '0000'            TO WS-UPD-PROJ-UD1-FROM(7:4)
114600           MOVE '0800'            TO WS-UPD-PROJ-UD1-TO(7:4)
114700           MOVE '2200'            TO WS-UPD-PROJ-UD2-FROM(7:4)
114800           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-UD2-TO(1:6)
114900           MOVE '0559'            TO WS-UPD-PROJ-UD2-TO(7:4)
115000        ELSE
115100           MOVE WS-DAY3-DATE      TO WS-UPD-PROJ-DATE
115200           MOVE '0559'            TO WS-UPD-PROJ-TIME
115300           MOVE WS-DAY1-DATE      TO WS-UPD-PROJ-UD1-FROM(1:6)
115400           MOVE '2200'            TO WS-UPD-PROJ-UD1-FROM(7:4)
115500           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-UD1-TO(1:6)
115600           MOVE '0800'            TO WS-UPD-PROJ-UD1-TO(7:4)
115700           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-UD2-FROM(1:6)
115800           MOVE '2200'            TO WS-UPD-PROJ-UD2-FROM(7:4)
115900           MOVE WS-DAY3-DATE      TO WS-UPD-PROJ-UD2-TO(1:6)
116000           MOVE '0559'            TO WS-UPD-PROJ-UD2-TO(7:4)
116100        END-IF
116200     END-IF
116300     .
116400
116500*
116600 P1136E-EXTEND-PROJ-RESET.
116700*
116800        MOVE ZEROS                   TO DATE-CONVERSION-PARMS
116900        SET PARM-ADD                 TO TRUE
117000        MOVE WS-UPD-PROJ-DATE        TO PARM-PRI-DATE-GREG
117100        MOVE 01                      TO PARM-SEC-GREG-DAY
117200        EXEC CICS LINK
117300                  PROGRAM(P903-PGM)
117400                  COMMAREA(DATE-CONVERSION-PARMS)
117500                  LENGTH(P903-LGTH)
117600                  RESP(WS-RESPONSE)
117700        END-EXEC
117800        MOVE WS-RESPONSE             TO FILE-STATUS
117900        IF NOT SUCCESS
118000           MOVE 'P1136-1'            TO ERR-PARAGRAPH
118100           MOVE 'P903LINK'           TO ERR-KEY
118200           PERFORM P9999-GOT-PROBLEM
118300        END-IF
118400        MOVE PARM-RES-DATE-GREG      TO WS-UPD-PROJ-DATE
118500        MOVE '0559'                  TO WS-UPD-PROJ-TIME
118600     .
118700*
118800 P1137-WRITE-RESET-BRK-WRK-HIST.
118900*
119000
119100     PERFORM VARYING I FROM 1 BY 1
119200     UNTIL I > 3
119300        MOVE SPACES                TO P943-COMMAREA-PARMS
119400        IF I = 1
119500           IF WS-NATURAL-RESET
119600              STRING 'NATURAL RESET BREAK END: '
119700              WS-UPD-PROJ-DATE-TIME DELIMITED
119800              BY SIZE INTO P943-FUN27-COMMENTS
119900           END-IF
120000           IF WS-FORCED-RESET
120100              STRING 'FORCED RESET BREAK END: '
120200              WS-UPD-PROJ-DATE-TIME DELIMITED
120300              BY SIZE INTO P943-FUN27-COMMENTS
120400           END-IF
120500        END-IF
120600        IF I = 2
120700           STRING 'UNDISTURB-1: ' WS-UPD-PROJ-UD1-FROM
120800           ' TO ' WS-UPD-PROJ-UD1-TO   DELIMITED
120900           BY SIZE INTO P943-FUN27-COMMENTS
121000        END-IF
121100        IF I = 3
121200           STRING 'UNDISTURB-2: ' WS-UPD-PROJ-UD2-FROM
121300           ' TO ' WS-UPD-PROJ-UD2-TO   DELIMITED
121400           BY SIZE INTO P943-FUN27-COMMENTS
121500        END-IF
121600        MOVE EMP-NBR OF WS-MSTR    TO P943-EMP-NBR
121700*05/27/23 -  WRITE RESET BRK HISTORY EFF TIME IN EMP TZ ALWAYS
121800        MOVE WS-LOCAL-DATE-TIME    TO P943-EFF-DATE-TIME
121900        SET P943-RESET-BREAK-FUN   TO TRUE
122000        MOVE DIST OF WS-MSTR       TO P943-DIST
122100        MOVE SUB-DIST OF WS-MSTR   TO P943-SDIST
122200        PERFORM P8900-WRITE-HISTORY
122300     END-PERFORM
122400     .
122500*
122600 P1138-WRITE-ETOD-RESET-BREAK.
122700*
122800     INITIALIZE PS08-COMMAREA-PARMS
122900     SET PS08-RESET-BREAK-FUN      TO TRUE
123000
123100     EVALUATE TRUE
123200        WHEN WS-NATURAL-RESET
123300           SET PS08-RESET-BREAK-NAT TO TRUE
123400        WHEN WS-FORCED-RESET
123500           SET PS08-RESET-BREAK-SYS TO TRUE
123600     END-EVALUATE
123700
123800     MOVE EMP-NBR OF WS-MSTR       TO PS08-EMP-NBR
123900     MOVE WS-UPD-PROJ-DATE-TIME    TO PS08-OD-DATE-TIME
124000                                      PS08-ORDER-DATE-TIME
124100     SET DE-YYMMDD-FORMAT          TO TRUE
124200     MOVE PS08-OD-DATE             TO DE-YYMMDD
124300     PERFORM P8998-DATEEDIT
124400     MOVE DE-CCYYMMDD              TO PS08-OD-DATE-CENT
124500                                      PS08-ORDER-DATE-CENT
124600     MOVE PS08-OD-DATE-TIME-CENT   TO PS08-OFFD-DATE-TIME-CENT
124700     MOVE CNTL-DIST                TO PS08-DIST
124800     MOVE CNTL-SUB-DIST            TO PS08-SDIST
124900     MOVE 'CNP0DV  '               TO PS08-TRAIN-ASGN
125000     MOVE 'G'                      TO PS08-EMP-REST-STATUS
125100     MOVE CRAFT OF WS-MSTR         TO PS08-CRAFT
125200
125300
125400     EXEC CICS LINK
125500               PROGRAM(PS08-PGM)
125600               COMMAREA(PS08-COMMAREA-PARMS)
125700               LENGTH(PS08-LGTH)
125800               RESP(WS-RESPONSE)
125900     END-EXEC
126000     MOVE WS-RESPONSE           TO FILE-STATUS
126100     IF NOT SUCCESS
126200        MOVE 'P1138-1'          TO ERR-PARAGRAPH
126300        STRING EMP-NBR OF WS-MSTR
126400               WS-SPACE
126500               PS08-OD-DATE-TIME-CENT
126600               WS-SPACE
126700               PS08-RETURN-ERRORS
126800               DELIMITED BY SIZE
126900               INTO ERR-KEY
127000        MOVE 'S08 LINK ERROR'
127100                                TO ERR-SENTENCE
127200        PERFORM P9999-GOT-PROBLEM
127300     END-IF
127400     IF NOT PS08-NO-ERRORS
127500        MOVE 'P1138-2'          TO ERR-PARAGRAPH
127600        STRING EMP-NBR OF WS-MSTR
127700               WS-SPACE
127800               PS08-OD-DATE-TIME-CENT
127900               WS-SPACE
128000               PS08-RETURN-ERRORS
128100               DELIMITED BY SIZE
128200               INTO ERR-KEY
128300        MOVE 'CHECK S08 INPUT PARAMETERS'
128400                                TO ERR-SENTENCE
128500        PERFORM P9999-GOT-PROBLEM
128600     END-IF.
128700*
128800 P1140-CALCULATE-7DAY-END.
128900*
129000     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
129100     SET PARM-ADD                  TO TRUE
129200     MOVE WS-7DAY-START-DATE       TO PARM-PRI-DATE-GREG
129300     MOVE WS-7DAY-START-TIME       TO PARM-PRI-HRMN
129400     MOVE 000006                   TO PARM-SEC-GREG-DAY
129500     MOVE 2359                     TO PARM-SEC-HRMN
129600     EXEC CICS LINK
129700               PROGRAM(P903-PGM)
129800               COMMAREA(DATE-CONVERSION-PARMS)
129900               LENGTH(P903-LGTH)
130000               RESP(WS-RESPONSE)
130100     END-EXEC
130200     MOVE WS-RESPONSE              TO FILE-STATUS
130300     IF NOT SUCCESS
130400        MOVE 'P1140-1'             TO ERR-PARAGRAPH
130500        MOVE 'P903LINK'            TO ERR-KEY
130600        PERFORM P9999-GOT-PROBLEM
130700     END-IF
130800     MOVE PARM-RES-DATE-GREG       TO WS-WRR-7DAY-END-DATE
130900     MOVE PARM-RES-HRMN            TO WS-WRR-7DAY-END-TIME
131000     .
131100*
131200 P1140A-CALCULATE-7DAY-START.
131300*
131400     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
131500     SET PARM-SUBTRACT             TO TRUE
131600     MOVE MSTR3-7DAY-END-DATE      TO PARM-PRI-DATE-GREG
131700     MOVE MSTR3-7DAY-END-TIME      TO PARM-PRI-HRMN
131800     MOVE 000006                   TO PARM-SEC-GREG-DAY
131900     MOVE 2359                     TO PARM-SEC-HRMN
132000     EXEC CICS LINK
132100               PROGRAM(P903-PGM)
132200               COMMAREA(DATE-CONVERSION-PARMS)
132300               LENGTH(P903-LGTH)
132400               RESP(WS-RESPONSE)
132500     END-EXEC
132600     MOVE WS-RESPONSE              TO FILE-STATUS
132700     IF NOT SUCCESS
132800        MOVE 'P1140A-1'            TO ERR-PARAGRAPH
132900        MOVE 'P903LINK'            TO ERR-KEY
133000        PERFORM P9999-GOT-PROBLEM
133100     END-IF
133200     MOVE PARM-RES-DATE-GREG       TO WS-7DAY-START-DATE
133300     MOVE PARM-RES-HRMN            TO WS-7DAY-START-TIME
133400     .
133500*
133600 P1150-FURTHER-VALIDATE-E95-REQ.
133700*
133800*    CALCULATE 7DAY-END MINUS 0600
133900     MOVE ZEROES                  TO DATE-CONVERSION-PARMS
134000     SET  PARM-SUBTRACT           TO TRUE
134100     MOVE WS-WRR-7DAY-END-DATE    TO PARM-PRI-DATE-GREG
134200     MOVE WS-WRR-7DAY-END-TIME    TO PARM-PRI-HRMN
134300     MOVE 0600                    TO PARM-SEC-HRMN
134400     PERFORM P8400-CALL-DATE-ROUTINE
134500     MOVE PARM-RES-DATE-GREG      TO WS-7DAY-END-MINUS6HR-DATE
134600     MOVE PARM-RES-HRMN           TO WS-7DAY-END-MINUS6HR-TIME
134700
134800     IF WS-LOCAL-DATE-TIME >= WS-7DAY-END-MINUS6HR-DTTM
134900        SET E95-REQUIRED TO TRUE
135000     END-IF
135100     .
135200*
135300 P2700-CHANGE-TO-E95-STATUS.
135400*
135500     MOVE SPACES                        TO P917-COMMAREA-PARMS
135600     MOVE EMP-NBR OF WS-MSTR            TO P917-EMP-NO
135700     MOVE 'E'                           TO P917-STATUS-CODE
135800     MOVE '95'                          TO P917-EDB-CODE
135900     MOVE 'PODV'                        TO P917-SUPV-INIT
136000     MOVE CNTL-TIME-ZONE                TO P917-TIME-ZONE
136100*
136200*    'BOOKON/BOOKOFF CANNOT BE OVER 24 HRS IN PAST' P917 EDIT
136300     MOVE WS-LOCAL-DATE                 TO P917-EFF-DATE
136400     MOVE WS-LOCAL-TIME                 TO P917-EFF-TIME
136500*
136600     MOVE MSTR3-SYSTEM-RESET-DATE       TO P917-EXP-DATE
136700     MOVE MSTR3-SYSTEM-RESET-TIME       TO P917-EXP-TIME
136800*
136900     EXEC CICS LINK
137000               PROGRAM(P917-PGM)
137100               COMMAREA(P917-COMMAREA-PARMS)
137200               LENGTH(P917-LGTH)
137300               RESP(WS-RESPONSE)
137400     END-EXEC
137500     MOVE WS-RESPONSE TO FILE-STATUS
137600     IF NOT SUCCESS
137700        MOVE 'P2700-1' TO ERR-PARAGRAPH
137800        PERFORM P9999-GOT-PROBLEM
137900     END-IF
138000     IF P917-ERROR-FOUND
138100        PERFORM P2700C-COMMENTS-HISTORY
138200     END-IF.
138300*
138400 P2700C-COMMENTS-HISTORY.
138500*
138600     MOVE SPACES                        TO P943-COMMAREA-PARMS
138700     SET  P943-MISC-COMMENTS-FUN        TO TRUE
138800     MOVE P917-EMP-NO                   TO P943-EMP-NBR
138900*05/27/23 -  WRITE COMMENTS HISTORY EFF TIME IN EMP TZ
139000     MOVE WS-LOCAL-DATE-TIME            TO P943-EFF-DATE-TIME
139100     MOVE DIST     OF WS-MSTR           TO P943-DIST
139200     MOVE SUB-DIST OF WS-MSTR           TO P943-SDIST
139300     MOVE CRAFT    OF WS-MSTR           TO P943-CRAFT
139400     STRING 'MA0V|E95|ERR:'
139500            P917-ERRORCODE '|'
139600            P917-EFF-DATE-TIME '|'
139700            P917-EXP-DATE-TIME '|'
139800            LAST-EMPTOD-OFFD-YYMMDD-HRMN
139900       DELIMITED BY SIZE INTO  P943-FUN58-COMMENTS
140000     PERFORM P8900-WRITE-HISTORY.
140100*
140200 P8000-STARTBR-CNTL-FILE.
140300*
140400     EXEC CICS STARTBR
140500               DATASET(CNTL-FILE-VIA-CNTLKEY)
140600               RIDFLD(CNTLKEY)
140700               GTEQ
140800               RESP(WS-RESPONSE)
140900     END-EXEC
141000     MOVE WS-RESPONSE TO FILE-STATUS
141100     IF NOT SUCCESS
141200        IF NOT (NO-RECORD-FND OR END-OF-FILE)
141300           MOVE 'P8000-1'                 TO ERR-PARAGRAPH
141400           MOVE CNTLKEY                   TO ERR-KEY
141500           PERFORM P9999-GOT-PROBLEM
141600        END-IF
141700     END-IF.
141800*
141900 P8010-READNEXT-CNTL-FILE.
142000*
142100     EXEC CICS READNEXT
142200               DATASET(CNTL-FILE-VIA-CNTLKEY)
142300               INTO(WS-CNTL-FILE)
142400               LENGTH(CNTLFILE-RLGTH)
142500               RIDFLD(CNTLKEY)
142600               KEYLENGTH(CNTLFILE-KLGTH)
142700               RESP(WS-RESPONSE)
142800     END-EXEC
142900     MOVE WS-RESPONSE TO FILE-STATUS
143000     IF NOT SUCCESS
143100        IF NOT (NO-RECORD-FND OR END-OF-FILE)
143200           MOVE 'P8010-1'                 TO ERR-PARAGRAPH
143300           MOVE CNTLKEY                   TO ERR-KEY
143400           PERFORM P9999-GOT-PROBLEM
143500        END-IF
143600     END-IF.
143700*
143800 P8015-READ-CNTL-FILE.
143900*
144000     EXEC CICS READ
144100               DATASET(CNTL-FILE-VIA-CNTLKEY)
144200               INTO(WS-CNTL-FILE)
144300               LENGTH(CNTLFILE-RLGTH)
144400               RIDFLD(CNTLKEY)
144500               KEYLENGTH(CNTLFILE-KLGTH)
144600               RESP(WS-RESPONSE)
144700     END-EXEC
144800     MOVE WS-RESPONSE TO FILE-STATUS
144900     IF NOT SUCCESS
145000        IF NOT (NO-RECORD-FND OR END-OF-FILE)
145100           MOVE 'P8015-1'                 TO ERR-PARAGRAPH
145200           MOVE CNTLKEY                   TO ERR-KEY
145300           PERFORM P9999-GOT-PROBLEM
145400        END-IF
145500     END-IF.
145600*
145700 P8020-ENDBR-CNTL-FILE.
145800*
145900     EXEC CICS ENDBR
146000               DATASET(CNTL-FILE-VIA-CNTLKEY)
146100               RESP(WS-RESPONSE)
146200     END-EXEC
146300     .
146400*=================================================================
146500 P8060-EMPTOD-STARTBR.
146600*=================================================================
146700     EXEC CICS STARTBR
146800               DATASET(EMPTOD-VIA-EMP)
146900               RIDFLD(EMPTOD-EMPKEY)
147000               GTEQ
147100               RESP(WS-RESPONSE)
147200     END-EXEC
147300     MOVE WS-RESPONSE                   TO FILE-STATUS
147400     IF NOT SUCCESS
147500        MOVE 'P8060-1'                  TO ERR-PARAGRAPH
147600        MOVE EMPTOD-EMPKEY              TO ERR-KEY
147700        PERFORM P9999-GOT-PROBLEM
147800     END-IF
147900     .
148000*=================================================================
148100 P8070-EMPTOD-READNEXT.
148200*=================================================================
148300     EXEC CICS READNEXT
148400               DATASET(EMPTOD-VIA-EMP)
148500               INTO(WS-EMP-TOD)
148600               LENGTH(EMPTOD-EMP-RLGTH)
148700               RIDFLD(EMPTOD-EMPKEY)
148800               KEYLENGTH(EMPTOD-EMP-KLGTH)
148900               RESP(WS-RESPONSE)
149000     END-EXEC
149100     MOVE WS-RESPONSE                   TO FILE-STATUS
149200     IF NOT (SUCCESS OR NO-RECORD-FND OR END-OF-FILE)
149300        MOVE 'P8070-1'                  TO ERR-PARAGRAPH
149400        MOVE EMPTOD-EMPKEY              TO ERR-KEY
149500        PERFORM P9999-GOT-PROBLEM
149600     END-IF
149700     .
149800*=================================================================
149900 P8090-EMPTOD-ENDBR.
150000*=================================================================
150100     EXEC CICS ENDBR
150200               DATASET(EMPTOD-VIA-EMP)
150300               RESP(WS-RESPONSE)
150400     END-EXEC
150500     .
150600*
150700 P8100-STARTBR-MSTR-FILE.
150800*
150900     EXEC CICS STARTBR
151000               DATASET(MSTR-VIA-DIST-SDIST-CRFT-EMP)
151100               RIDFLD(MSTRDSCEK)
151200               GTEQ
151300               RESP(WS-RESPONSE)
151400     END-EXEC
151500     MOVE WS-RESPONSE TO FILE-STATUS
151600     IF NOT SUCCESS
151700        IF NOT (NO-RECORD-FND OR END-OF-FILE)
151800           MOVE 'P8100-1'                 TO ERR-PARAGRAPH
151900           MOVE MSTRDSCEK                 TO ERR-KEY
152000           PERFORM P9999-GOT-PROBLEM
152100        END-IF
152200     END-IF.
152300*
152400 P8110-READNEXT-MSTR-FILE.
152500*
152600     EXEC CICS READNEXT
152700               DATASET(MSTR-VIA-DIST-SDIST-CRFT-EMP)
152800               INTO(WS-MSTR)
152900               LENGTH(MSTRDSCE-RLGTH)
153000               RIDFLD(MSTRDSCEK)
153100               KEYLENGTH(MSTRDSCE-KLGTH)
153200               RESP(WS-RESPONSE)
153300     END-EXEC
153400     MOVE WS-RESPONSE TO FILE-STATUS
153500     IF NOT SUCCESS
153600        IF NOT (NO-RECORD-FND OR END-OF-FILE)
153700           MOVE 'P8110-1'                 TO ERR-PARAGRAPH
153800           MOVE MSTRDSCEK                 TO ERR-KEY
153900           PERFORM P9999-GOT-PROBLEM
154000        END-IF
154100     END-IF.
154200*
154300 P8120-ENDBR-MSTR-FILE.
154400*
154500     EXEC CICS ENDBR
154600               DATASET(MSTR-VIA-DIST-SDIST-CRFT-EMP)
154700               RESP(WS-RESPONSE)
154800     END-EXEC
154900     .
155000*
155100 P8210-READ-UPD-MSTR3-FILE.
155200*
155300     EXEC CICS READ
155400               UPDATE
155500               DATASET(MSTR3-VIA-EMP-NBR)
155600               INTO(WS-MSTR3)
155700               LENGTH(MSTR3ENBR-RLGTH)
155800               RIDFLD(MSTR3NBRK)
155900               KEYLENGTH(MSTR3ENBR-KLGTH)
156000               RESP(WS-RESPONSE)
156100     END-EXEC
156200     MOVE WS-RESPONSE           TO FILE-STATUS
156300     IF NOT SUCCESS AND NOT(NO-RECORD-FND OR END-OF-FILE)
156400        MOVE 'P8210-1'          TO ERR-PARAGRAPH
156500        MOVE MSTR3NBRK          TO ERR-KEY
156600        PERFORM P9999-GOT-PROBLEM
156700     END-IF.
156800*
156900 P8220-REWRITE-MSTR3-FILE.
157000*
157100     MOVE MSTR3-EMP-NBR        TO MSTR3NBRK
157200     EXEC CICS REWRITE
157300               DATASET(MSTR3-VIA-EMP-NBR)
157400               FROM(WS-MSTR3)
157500               LENGTH(MSTR3ENBR-RLGTH)
157600               RESP(WS-RESPONSE)
157700     END-EXEC
157800     MOVE WS-RESPONSE           TO FILE-STATUS
157900     IF NOT SUCCESS
158000        MOVE 'P8220-1'          TO ERR-PARAGRAPH
158100        MOVE MSTR3NBRK          TO ERR-KEY
158200        PERFORM P9999-GOT-PROBLEM
158300     END-IF.
158400*
158500 P8400-CALL-DATE-ROUTINE.
158600*
158700     EXEC CICS LINK
158800               PROGRAM(P903-PGM)
158900               COMMAREA(DATE-CONVERSION-PARMS)
159000               LENGTH(P903-LGTH)
159100               RESP(WS-RESPONSE)
159200     END-EXEC
159300     MOVE WS-RESPONSE      TO FILE-STATUS
159400     IF NOT SUCCESS
159500        MOVE 'P8400'       TO ERR-PARAGRAPH
159600        MOVE 'P903LINK'    TO ERR-KEY
159700        PERFORM P9999-GOT-PROBLEM
159800     END-IF
159900     .
160000*
160100 P8500-UPDATE-TRANS-STATS.
160200*
160300     EXEC CICS ASKTIME ABSTIME(WS-ABSTIME) END-EXEC
160400     ADD WS-ABSTIME-OFFSET TO WS-ABSTIME
160500     EXEC CICS FORMATTIME
160600               ABSTIME(WS-ABSTIME)
160700               YYYYMMDD(WS-SYSTEM-DATE-CENT)
160800               TIME(WS-SYSTEM-TIME-AREA)
160900     END-EXEC
161000
161100     MOVE WS-PRESENT-TIME           TO WS-FINISH-DATE-TIME
161200
161300     MOVE SPACES                    TO WS-CNTL-FILE
161400     SET TRANS-INITIATION-REC       TO TRUE
161500     MOVE P0DV-TRAN                 TO CNTL-TRANSACTION
161600     MOVE CNTLKEY-AREA              TO CNTLKEY
161700     EXEC CICS READ UPDATE
161800               DATASET(CNTL-FILE-VIA-CNTLKEY)
161900               INTO(WS-CNTL-FILE)
162000               LENGTH(CNTLFILE-RLGTH)
162100               RIDFLD(CNTLKEY)
162200               KEYLENGTH(CNTLFILE-KLGTH)
162300               RESP(WS-RESPONSE)
162400     END-EXEC
162500     MOVE WS-RESPONSE               TO FILE-STATUS
162600     IF SUCCESS
162700        SET WS-MY-TRAN-FOUND        TO TRUE
162800     ELSE
162900        IF NO-RECORD-FND
163000           SET WS-MY-TRAN-NOT-FOUND TO TRUE
163100        ELSE
163200           MOVE 'P8500-1'           TO ERR-PARAGRAPH
163300           MOVE CNTLKEY             TO ERR-KEY
163400           PERFORM P9999-GOT-PROBLEM
163500        END-IF
163600     END-IF
163700*
163800     MOVE WS-START-DATE-TIME        TO CNTL-TRANS-LAST-RUN-START
163900     MOVE WS-FINISH-DATE-TIME       TO CNTL-TRANS-LAST-RUN-FINISH
164000*
164100     IF  WS-MY-TRAN-FOUND
164200         EXEC CICS REWRITE
164300                   DATASET(CNTL-FILE-VIA-CNTLKEY)
164400                   FROM   (WS-CNTL-FILE)
164500                   LENGTH (CNTLFILE-RLGTH)
164600                   RESP   (WS-RESPONSE)
164700         END-EXEC
164800     ELSE
164900         EXEC CICS WRITE
165000                   DATASET  (CNTL-FILE-VIA-CNTLKEY)
165100                   FROM     (WS-CNTL-FILE)
165200                   LENGTH   (CNTLFILE-RLGTH)
165300                   RIDFLD   (CNTLKEY)
165400                   KEYLENGTH(CNTLFILE-KLGTH)
165500                   RESP     (WS-RESPONSE)
165600         END-EXEC
165700     END-IF
165800     MOVE WS-RESPONSE               TO FILE-STATUS
165900     IF NOT SUCCESS
166000        MOVE 'P8500-2'              TO ERR-PARAGRAPH
166100        MOVE CNTLKEY                TO ERR-KEY
166200        PERFORM P9999-GOT-PROBLEM
166300     END-IF.
166400*
166500 P8600-GET-ON-DUTY-JOB.
166600*
166700     MOVE SPACES                    TO WS-ASSIGN-COMMAREA
166800                                       WS-ASGN-FILE
166900                                       ON-DUTY-ASGN-AREA
167000     MOVE EMP-NBR IN WS-MSTR        TO ASGN-EMP-NO
167100     SET  GET-JOB-ON-DUTY           TO TRUE
167200     MOVE WS-ASGN-FILE              TO WS-ASSIGN-RECORD
167300     EXEC CICS LINK
167400               PROGRAM(P901-PGM)
167500               COMMAREA(WS-ASSIGN-COMMAREA)
167600               LENGTH(P901-LGTH)
167700               RESP(WS-RESPONSE)
167800     END-EXEC
167900     MOVE WS-RESPONSE               TO FILE-STATUS
168000     IF NOT SUCCESS
168100        MOVE 'P8600-1'              TO ERR-PARAGRAPH
168200        MOVE 'P901LINK'             TO ERR-KEY
168300        MOVE 'LINK TO 901 FAILED'   TO ERR-SENTENCE
168400        PERFORM P9999-GOT-PROBLEM
168500     END-IF
168600     MOVE WS-ASSIGN-RECORD          TO WS-ASGN-FILE
168700     IF ASGN-ASSIGNMENT              > SPACES
168800        MOVE ASGNKEY1               TO ON-DUTY-ASGN-AREA
168900        MOVE ASGN-JOB-TYPE          TO ON-DUTY-ASGNMT-FLAG
169000     END-IF
169100     .
169200*
169300 P8700-READ-UFPTURN.
169400*
169500     EXEC CICS READ
169600               DATASET(UFP-VIA-TURN-NBR)
169700               INTO(WS-UFP)
169800               LENGTH(UFPTURN-RLGTH)
169900               RIDFLD(UFPTURN)
170000               KEYLENGTH(UFPTURN-KLGTH)
170100               RESP(WS-RESPONSE)
170200     END-EXEC
170300     MOVE WS-RESPONSE                TO FILE-STATUS
170400     .
170500*
170600 P8900-WRITE-HISTORY.
170700*
170800     SET P943-EMPLOYEE-FUNCTION      TO TRUE
170900     EXEC CICS ASSIGN
171000               USERID(P943-USERID)
171100     END-EXEC
171200
171300     EXEC CICS LINK
171400               PROGRAM(P943-PGM)
171500               COMMAREA(P943-COMMAREA-PARMS)
171600               LENGTH(P943-LGTH)
171700               RESP(WS-RESPONSE)
171800     END-EXEC
171900     MOVE WS-RESPONSE                TO FILE-STATUS
172000     IF NOT SUCCESS
172100        MOVE 'P8900-1'               TO ERR-PARAGRAPH
172200        MOVE 'P943'                  TO ERR-KEY
172300        PERFORM P9999-GOT-PROBLEM
172400     END-IF
172500     IF P943-RETURN-STATUS > ZEROES
172600        MOVE 'P8900-2'               TO ERR-PARAGRAPH
172700        MOVE P943-RETURN-STATUS      TO FILE-STATUS
172800        MOVE 'P943-RETURN'           TO ERR-KEY
172900        PERFORM P9999-GOT-PROBLEM
173000     END-IF.
173100
173200*
173300 COPY DATEEDIT.
173400*
173500 COPY TIMEEDIT.
173600*
173700 COPY TIMEZONE.
173800*
173900 COPY TZERROR.
174000*
174100*
174200 P8999-GET-CURR-TIME.
174300*
174400     EXEC CICS ASKTIME
174500               ABSTIME(WS-ABSTIME)
174600     END-EXEC
174700     ADD  WS-ABSTIME-OFFSET             TO WS-ABSTIME
174800     EXEC CICS FORMATTIME
174900               ABSTIME(WS-ABSTIME)
175000               YYYYMMDD(WS-SYSTEM-DATE-CENT)
175100               TIME(WS-SYSTEM-TIME-AREA)
175200     END-EXEC
175300*
175400*    INSTALL APPLICATION DATE/TIME
175500*
175600     PERFORM P9800-GET-TIME-OFFSET
175700     IF WS-DATE-TIME-OFFSET              > SPACES
175800        MOVE ZEROS                      TO DATE-CONVERSION-PARMS
175900        MOVE WS-SYSTEM-DATE             TO PARM-PRI-DATE-GREG
176000        MOVE WS-SYSTEM-TIME             TO PARM-PRI-HRMN
176100        PERFORM P9810-PROCESS-OFFSET
176200        MOVE PARM-RES-GREG-CENT         TO WS-SYSTEM-CENT
176300        MOVE PARM-RES-DATE-GREG         TO WS-SYSTEM-DATE
176400        MOVE PARM-RES-HRMN              TO WS-SYSTEM-TIME
176500     END-IF
176600*
176700*    CONVERT SYSTEM DATE/TIME TO LOCAL DATE/TIME
176800*
176900     MOVE SPACES                        TO TZ-PARAMETERS
177000     SET  TZ-IN-SYSTEM-ZONE             TO TRUE
177100     MOVE WS-PRESENT-TIME               TO TZ-IN-DATE-TIME
177200     MOVE CNTL-TIME-ZONE                TO TZ-OUT-ZONE
177300     PERFORM P8996-TIMEZONE
177400     MOVE TZ-OUT-DATE-TIME-CENT         TO WS-LOCAL-DATE-TIME-CENT
177500     .
177600*
177700 P9000-RETURN.
177800*
177900     PERFORM P8500-UPDATE-TRANS-STATS
178000
178100     EXEC CICS RETURN END-EXEC.
178200*
178300 P9800-GET-TIME-OFFSET.
178400*
178500     MOVE WS-CNTL-FILE                  TO WS-HOLD-CNTL-FILE
178600     MOVE CNTLKEY-AREA                  TO WS-HOLD-CNTL-KEY
178700     MOVE SPACES                        TO CNTLKEY-AREA
178800     SET  DATE-TIME-OFFSET-REC          TO TRUE
178900     EXEC CICS ASSIGN
179000               USERID(CNTL-DT-USERID)
179100     END-EXEC
179200     MOVE CNTLKEY-AREA                  TO CNTLKEY
179300     PERFORM P8015-READ-CNTL-FILE
179400     IF NOT SUCCESS
179500        IF NO-RECORD-FND
179600           MOVE '********'              TO CNTL-DT-USERID
179700           PERFORM P8015-READ-CNTL-FILE
179800        ELSE
179900           MOVE 'P9800-1'               TO ERR-PARAGRAPH
180000           MOVE CNTLKEY                 TO ERR-KEY
180100           PERFORM P9999-GOT-PROBLEM
180200        END-IF
180300     END-IF
180400     IF SUCCESS
180500        MOVE CNTL-DT-ADD-SUBTRACT       TO WS-DT-OS-FUN
180600        MOVE CNTL-DT-DAYS-OFFSET        TO WS-DT-OS-DAYS
180700        MOVE CNTL-DT-HRMN-OFFSET        TO WS-DT-OS-HRMN
180800     ELSE
180900        MOVE SPACES                     TO WS-DATE-TIME-OFFSET
181000        IF NOT NO-RECORD-FND
181100           MOVE 'P9800-2'               TO ERR-PARAGRAPH
181200           MOVE CNTLKEY                 TO ERR-KEY
181300           PERFORM P9999-GOT-PROBLEM
181400        END-IF
181500     END-IF
181600     MOVE WS-HOLD-CNTL-FILE             TO WS-CNTL-FILE
181700     MOVE WS-HOLD-CNTL-KEY              TO CNTLKEY-AREA
181800                                           CNTLKEY
181900     .
182000*
182100 P9810-PROCESS-OFFSET.
182200*
182300     MOVE WS-DT-OS-FUN                  TO PARM-CONV-TYPE
182400     MOVE WS-DT-OS-DAYS                 TO PARM-SEC-JULIAN-DAY
182500     MOVE WS-DT-OS-HRMN                 TO PARM-SEC-HRMN
182600     EXEC CICS LINK
182700               PROGRAM(P903-PGM)
182800               COMMAREA(DATE-CONVERSION-PARMS)
182900               LENGTH(P903-LGTH)
183000               RESP(WS-RESPONSE)
183100     END-EXEC
183200     MOVE WS-RESPONSE                   TO FILE-STATUS
183300     IF NOT SUCCESS
183400        MOVE 'P9810-1'                  TO ERR-PARAGRAPH
183500        MOVE 'P903'                     TO ERR-KEY
183600        MOVE 'LINK TO 903 FAILED'       TO ERR-SENTENCE
183700        PERFORM P9999-GOT-PROBLEM
183800     END-IF
183900     .
184000*
184100 P9999-GOT-PROBLEM.
184200*
184300     MOVE P0DV-PGM  TO ERR-PROGRAM
184400     SET ERR-NO-MAP TO TRUE
184500     MOVE DFHEIBLK  TO ERR-EIBLK
184600     EXEC CICS XCTL
184700               PROGRAM(PSTERR-PGM)
184800               COMMAREA(PSTERAR-AREA)
184900               LENGTH(PSTERAR-LGTH)
185000               RESP(WS-RESPONSE)
185100     END-EXEC
185200     EXEC CICS ABEND
185300               ABCODE(PSTERR-ABCODE)
185400               CANCEL
185500     END-EXEC.
185600*
185700 X9999-GOBACK.
185800     GOBACK.
