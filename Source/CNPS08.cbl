000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CNPS08.
000300*****************************************************************
000400*     SUBROUTINE TO UPDATE EMPLOYEE TIME ON DUTY LOG
000500*****************************************************************
000600*  DATE   INITIAL  LOG#    DESCRIPTION
000700*-------- ------- ------   --------------------------------------
000800*02/14/01   SWS   CNC0331  ORIGINAL.
000900*03/07/03   JAJ   CNC0331B MAND REST/HOS ADDENDUM B.
001000*04/14/04   NRL   CNC0331G ADD SUPPORT FOR RESPITE.
001100*                          REMOVE ACTUAL TIME CALC, IT'S NOW IN
001200*                          CNP925.
001300*05/03/04   NRL   CNC0331G ADD TIME ZONE TO THE EMPTOD FILE.
001400*05/05/05   MET   CNC0331H HOURS OF SERVICE CLAIMS.
001500*12/13/06   JES   CNC0413  TRACKING VS LOST WORK HOS - VIA
001600*                          VIRTUAL TEMP TRACKING.
001700*07/20/07   RAL   CNC0331E IF FROM 05Y, TRACK TIME TO CUTOFF TIME
001800*08/30/07   RAL   CNC0331E IF FROM 05Y, TRACK TIME FROM START TIME
001900*02/02/11   SXK   C848     FIXED ISSUE WHICH INVOLVED RESPITE TIME
002000*                          AND LOST WORK (TRACK). NOW, WRITING
002100*                          SECOND ENTRY HOS RECORD FOR THE WORKED
002200*                          EMPLOYEE INSTEAD OF TRACKED EMPLOYEE.
002300*07/27/11   BXS   CNC0481  FIXED ISSUE WHICH INVOLVED RESPITE FLAG
002400*                          NOW P1200 WILL UPDATE CORRECT STATUS AS
002500*                          PROVIDED IN PS08COMM AREA.
002600*12/01/11   BLD   C929     CREATED FUNCTION PS08-INQUIRY-FROM-07Z.
002700*                          THIS FUNCTION WILL RETURN TIME EVEN
002800*                          WHEN TRIPS OVERLAP.
002900*08/13/13   AXK   CNC0531   == RECOMPILE ONLY ==
003000*                          ADDED YARD CRAFTS Y0-Y9 AND YT
003010*11/02/17   MFO   C1177   WHEN PROCESSING HOS VIOLATION VALIDATION
003020*                         FROM 07Z, RESET COUNT ONCE AN EMPLOYEE
003021*                         IS 'GREENED UP'.
003022*11/27/17   MFO   C1180   RESET FOR 07Z REVISIONS
003023*02/16/18   MFO   C1186   FACTOR IN RESPITE TIME
003023*03/10/23   SXP   CNC0600 WRR PHASE 2: WHEN CALCULATING TTOD,
      *                         DONT CHECK 24HOUR GAP BETWEEN TOURS.
      *                         PROCESS INSERT PS08-RESET-BREAK-FUN
      *05/08/23   RKW   CNC0600 CNLD-214 INDICATE NATURAL OR SYS RESET
      *05/13/23   RKW   CNC0600 CNLD-231 ADD START UP PROCESSING
      *06/03/23   RPV   CNLD-242 NEW PS08-RESET-BREAK-COS ADDED
      *06/19/23   RPV   CNLD-248 NEW PS08-RESET-BRK-7DAY-END-FUN ADDED
      *08/24/23   RJA   CNLD-259 IF RESET STARTS AT 0000, IT SHOULD END
      *                          AT 0759 THE NEXT DAY - DURATION IN THIS
      *                          CASE WILL BE 31 HR 59 MIN INSTEAD OF A
      *                          32 HR MINIMUM.
      *08/24/23   RJA   CNLD-267 MANUAL E95 DUE-BACK SHOULD BE COMPUTED
      *                          BASED ON LAST HOS END TIME
      *10/26/23   RJA   CNLD-266 ADD PS08-SJOWNER-AT-0DV-RESET
      *01/17/24   RJA   CNLD-303 FIX : RESPITE HOS RECORDS ARE UPDATED
      *                          WITH WRONG EMPTOD-RESET-BREAK-FLAG.
      *01/17/24   RJA   CNLD-304 MINIMUM RESET DURATION IS CHANGED FROM
      *                          3200 TO 3159.
      *07/22/24   RJA   CNLD-302 IMPOSED REST IS OBSOLETE PER NEW WRR.
      *                          REMOVING RELATED CODE.
003024*****************************************************************
003025 ENVIRONMENT DIVISION.
003026 CONFIGURATION SECTION.
003027 SOURCE-COMPUTER. IBM-9370.
003028 OBJECT-COMPUTER. IBM-9370.
003029 DATA DIVISION.
003030 WORKING-STORAGE SECTION.
003040 01  WS-SUBSCRIPTS.
003050     05  SUB1                        PIC 9(002) VALUE ZEROS.
003060     05  SUB2                        PIC 9(002) VALUE ZEROS.
003070
003080 01  WS-FLAGS.
003090     05  DONE-CODE                   PIC 9(001) VALUE ZEROS.
003100         88  RECORDS-REMAINING                  VALUE ZERO.
003200         88  NOT-DONE                           VALUE ZERO.
003300         88  DONE                               VALUE 1.
003400     05  CREATE-RECORD-CODE          PIC 9(001) VALUE ZEROS.
003500         88  CREATE-RECORD                      VALUE 1.
003600     05  WS-ACTUAL-TIME-FLAG         PIC X(001) VALUE SPACE.
003700         88  WS-USE-ORDER-TIME                  VALUE 'O'.
003800         88  WS-ADD-15-TO-TTOD                  VALUE 'A'.
003900     05  WS-SUBSEQUENT-FLAG          PIC X(001) VALUE SPACE.
004000         88  SUBSEQUENT-FOUND                   VALUE 'Y'.
      *CNC0600-B
024600     05  WS-CAN-WRR-FLAG             PIC X(001) VALUE ' '.
024700         88  WS-CAN-WRR-NEW                     VALUE 'N'.
024600     05  WS-WRR-START-DTTM.
024700         10 WS-WRR-START-DATE        PIC X(6) VALUE SPACES.
               10 WS-WRR-START-TIME        PIC X(4) VALUE SPACES.
      *CNC0600-E
      *CPLD-248-B
005600     05  WS-READ-DONE-CODE          PIC X(01) VALUE SPACE.
005700         88  READ-DONE                        VALUE 'Y'.
      *CPLD-248-E
004100
004200 01  WS-SAVE-EMP-TOD                 PIC X(100) VALUE SPACES.
004300
004400 01  WS-TOTALS.
004500     02  WS-TOTAL-HRS-ON-DUTY        PIC 9(005) VALUE ZEROS.
004600     02  WS-TOTAL-DAYS               PIC 9(003) VALUE ZEROS.
004700     02  WS-TOTAL-HR                 PIC 9(005) VALUE ZEROS.
004800     02  WS-TOTAL-MN                 PIC 9(003) VALUE ZEROS.
004900
005000 01 WS-SAVE-START-DATE-TIME.
005100    02 WS-SAVE-START-DATE-CENT.
005200       03 WS-SAVE-START-CE           PIC X(002) VALUE SPACES.
005300       03 WS-SAVE-START-DATE.
005400          04 WS-SAVE-START-YR        PIC X(002) VALUE SPACES.
005500          04 WS-SAVE-START-MO        PIC X(002) VALUE SPACES.
005600          04 WS-SAVE-START-DY        PIC X(002) VALUE SPACES.
005700    02 WS-SAVE-START-TIME.
005800       03 WS-SAVE-START-HR           PIC X(002) VALUE SPACES.
005900       03 WS-SAVE-START-MN           PIC X(002) VALUE SPACES.
006000
006100 01 WS-START-CALC-DATE-TIME.
006200    02 WS-START-CALC-DATE-CENT.
006300       03 WS-START-CALC-CE           PIC X(002) VALUE SPACES.
006400       03 WS-START-CALC-DATE.
006500          04 WS-START-CALC-YR        PIC X(002) VALUE SPACES.
006600          04 WS-START-CALC-MO        PIC X(002) VALUE SPACES.
006700          04 WS-START-CALC-DY        PIC X(002) VALUE SPACES.
006800    02 WS-START-CALC-TIME.
006900       03 WS-START-CALC-HR           PIC X(002) VALUE SPACES.
007000       03 WS-START-CALC-MN           PIC X(002) VALUE SPACES.
       01 WS-START-CALC-CEDTTM REDEFINES WS-START-CALC-DATE-TIME.
          02 FILLER                        PIC X(02).
          02 WS-START-CALC-DTTM            PIC X(10).
007100
      *CNLD-248-B
008300 01  WS-WRR-7DAY-END.
008400     05  WS-WRR-7DAY-END-DATE       PIC X(06)  VALUE SPACES.
008500     05  WS-WRR-7DAY-END-TIME       PIC X(04)  VALUE SPACES.
008000 01  WS-7DAY-START-DTTM.
008100     05  WS-7DAY-START-DATE         PIC X(06)  VALUE SPACES.
008200     05  WS-7DAY-START-TIME         PIC X(04)  VALUE SPACES.
007400 01  WS-8DAYS-AGO-DTTM.
007500     05  WS-8DAYS-AGO-DATE          PIC X(06) VALUE SPACES.
007600     05  WS-8DAYS-AGO-TIME          PIC X(04) VALUE SPACES.
008600 01  WS-EMP-EFF-DATE-TIME.
008700     05  WS-EMP-EFF-DATE            PIC X(06)  VALUE SPACES.
008800     05  WS-EMP-EFF-TIME            PIC X(04)  VALUE SPACES.
008900 01  WS-PREV-7DAY-START-DTTM.
009000     05  WS-PREV-7DAY-START-DT      PIC X(06)  VALUE SPACES.
009100     05  WS-PREV-7DAY-START-TM      PIC X(04)  VALUE SPACES.
009200 01  LAST-EMPTOD-OFFD-YYMMDD-HRMN.
009300     05  LAST-EMPTOD-OFFD-DATE      PIC X(06)  VALUE SPACES.
009400     05  LAST-EMPTOD-OFFD-TIME      PIC X(04)  VALUE SPACES.
012500 01  WS-NATURAL-RESET-ST-DTTM.
012600     05  WS-NATURAL-RESET-ST-DATE       PIC X(06) VALUE SPACE.
012700     05  WS-NATURAL-RESET-ST-TIME       PIC X(04) VALUE SPACE.
012800 01  WS-7DAY-END-MINUS6HR-DTTM.
012900     05  WS-7DAY-END-MINUS6HR-DATE      PIC X(06) VALUE SPACE.
013000     05  WS-7DAY-END-MINUS6HR-TIME      PIC X(04) VALUE SPACE.
013100 01  WS-DAY1-DATE-TIME.
013200     05  WS-DAY1-DATE                   PIC X(06) VALUE SPACE.
013300     05  WS-DAY1-TIME                   PIC X(04) VALUE SPACE.
013400 01  WS-DAY2-DATE-TIME.
013500     05  WS-DAY2-DATE                   PIC X(06) VALUE SPACE.
013600     05  WS-DAY2-TIME                   PIC X(04) VALUE SPACE.
013700 01  WS-DAY3-DATE-TIME.
013800     05  WS-DAY3-DATE                   PIC X(06) VALUE SPACE.
013900     05  WS-DAY3-TIME                   PIC X(04) VALUE SPACE.
014000 01  WS-UPD-PROJ-DATE-TIME.
014100     05  WS-UPD-PROJ-DATE               PIC X(06) VALUE SPACE.
014200     05  WS-UPD-PROJ-TIME               PIC X(04) VALUE SPACE.
014300 01  WS-UPD-PROJ-UD1-FROM               PIC X(10) VALUE SPACE.
014400 01  WS-UPD-PROJ-UD1-TO                 PIC X(10) VALUE SPACE.
014500 01  WS-UPD-PROJ-UD2-FROM               PIC X(10) VALUE SPACE.
014600 01  WS-UPD-PROJ-UD2-TO                 PIC X(10) VALUE SPACE.
      *CNLD-248-E
007200 01 WS-START-DATE-TIME.
007300    02 WS-START-DATE-CENT.
007400       03 WS-START-CE                PIC X(002) VALUE SPACES.
007500       03 WS-START-DATE.
007600          04 WS-START-YR             PIC X(002) VALUE SPACES.
007700          04 WS-START-MO             PIC X(002) VALUE SPACES.
007800          04 WS-START-DY             PIC X(002) VALUE SPACES.
007900    02 WS-START-TIME.
008000       03 WS-START-HR                PIC X(002) VALUE SPACES.
008100       03 WS-START-MN                PIC X(002) VALUE SPACES.
008200
008300
008400 01 WS-SUB-DATE-TIME.
008500    02 WS-SUB-DATE-CENT.
008600       03 WS-SUB-CE                  PIC X(002) VALUE SPACES.
008700       03 WS-SUB-DATE.
008800          04 WS-SUB-YR               PIC X(002) VALUE SPACES.
008900          04 WS-SUB-MO               PIC X(002) VALUE SPACES.
009000          04 WS-SUB-DY               PIC X(002) VALUE SPACES.
009100    02 WS-SUB-TIME.
009200       03 WS-SUB-HR                  PIC X(002) VALUE SPACES.
009300       03 WS-SUB-MN                  PIC X(002) VALUE SPACES.
009400
009500 01  WS-MISC.
009600     05 WS-EMP-TIME-ZONE           PIC  X(001) VALUE SPACES.
009700     05 WORK-TIME.
009800        10  WORK-HR                PIC  9(002) VALUE ZEROS.
009900            88 VALID-HR                        VALUE 0 THRU 23.
010000        10  WORK-MN                PIC  9(002) VALUE ZEROS.
010100            88 VALID-MN                        VALUE 0 THRU 59.
010200     05 WORK-TIME-NUM REDEFINES
010300        WORK-TIME                  PIC  9(004).
010400     05 WS-EMP-OFFD-DATE-TIME.
010500        10 WS-EMP-OFFD-DATE-CE       PIC X(002) VALUE SPACES.
010600        10 WS-EMP-OFFD-DATE          PIC X(006) VALUE SPACES.
010700        10 WS-EMP-OFFD-TIME          PIC X(004) VALUE SPACES.
010800     05 WS-EMP-OFFD-DATE-TIME-NUM REDEFINES
010900        WS-EMP-OFFD-DATE-TIME        PIC 9(012).
011000     05 WS-EMP-OD-DATE-TIME.
011100        10 WS-EMP-OD-DATE-CE         PIC X(002) VALUE SPACES.
011200        10 WS-EMP-OD-DATE            PIC X(006) VALUE SPACES.
011300        10 WS-EMP-OD-TIME            PIC X(004) VALUE SPACES.
011400     05 WS-EMP-OD-DATE-TIME-NUM REDEFINES
011500        WS-EMP-OD-DATE-TIME          PIC 9(012).
011600     05 WS-TTA-MAX                   PIC 9(002) VALUE 50.
011700     05 WS-CALC-DATE-TIME.
011800        10 WS-CALC-DATE-CE           PIC X(002) VALUE SPACES.
011900        10 WS-CALC-DATE              PIC X(006) VALUE SPACES.
012000        10 WS-CALC-TIME              PIC X(004) VALUE SPACES.
012100     05 WS-CALC-DATE-TIME-NUM REDEFINES
012200        WS-CALC-DATE-TIME            PIC 9(012).
012300
012400 01 WS-TTA-EMPLOYEE-TABLE.
012500     02 WS-TTA-EMP-TABLE      OCCURS 50 TIMES.
012600         03  WS-TTA-EMP              PIC X(009).
012700         03  WS-TTA-REST-STAT        PIC X(001).
012800             88  WS-TTA-REST-GREEN              VALUE 'G'.
012900
013000 01 WS-REST-STATUS-CHECK             PIC X(001) VALUE SPACES.
013100    88  WS-VALID-REST-STATUS                    VALUE 'R' 'Y' 'G'.
013200
013300 01 WS-LAST-OFFD-CENT-DATE-TIME.
013400    02 WS-LAST-OFFD-DATE-CENT.
013500       03 WS-LAST-OFFD-CE            PIC X(002) VALUE SPACES.
013600       03 WS-LAST-OFFD-DATE.
013700          04 WS-LAST-OFFD-YR         PIC X(002) VALUE SPACES.
013800          04 WS-LAST-OFFD-MO         PIC X(002) VALUE SPACES.
013900          04 WS-LAST-OFFD-DY         PIC X(002) VALUE SPACES.
014000    02 WS-LAST-OFFD-TIME.
014100       03 WS-LAST-OFFD-HR            PIC X(002) VALUE SPACES.
014200       03 WS-LAST-OFFD-MN            PIC X(002) VALUE SPACES.
014300
014400*****************************************************************
014500***                 I/O STATUS CHECK FIELDS
014600*****************************************************************
014700 01  WS-RESPONSE                     PIC S9(008) COMP VALUE ZEROS.
014800 01  FILE-STATUS                     PIC  9(004) VALUE ZEROS.
014900     EXEC SQL INCLUDE IOCODES END-EXEC.
015000 01  DISPLAY-FILE-STATUS           PIC S9(8) VALUE ZERO.
015100     EXEC SQL INCLUDE WSCOMMON END-EXEC.
015200*****************************************************************
015300***                   COMMAREA COPYBOOKS
015400*****************************************************************
015500     EXEC SQL INCLUDE PS08COMM END-EXEC.
015600*****************************************************************
015700***                  CALLED ROUTINES COPYBOOKS
015800*****************************************************************
015900     EXEC SQL INCLUDE P903COMM END-EXEC.
016000*****************************************************************
016100***                       FILE COPYBOOKS
016200*****************************************************************
016300     EXEC SQL INCLUDE TTAV     END-EXEC.
016400     EXEC SQL INCLUDE TTAWS    END-EXEC.
016500     EXEC SQL INCLUDE WSCNTL   END-EXEC.
016600     EXEC SQL INCLUDE WSEMPTOD END-EXEC.
016700     EXEC SQL INCLUDE WSMSTR   END-EXEC.
      *CPLD-248-B
016700     EXEC SQL INCLUDE WSMSTR3  END-EXEC.
      *CPLD-248-E
016800*****************************************************************
016900***                       MISC COPYBOOKS
017000*****************************************************************
017100     EXEC SQL INCLUDE PSTCCRFT END-EXEC.
017200     EXEC SQL INCLUDE WSBIF    END-EXEC.
017300     EXEC SQL INCLUDE WSOFFSET END-EXEC.
017400     EXEC SQL INCLUDE WSSYDTTM END-EXEC.
017500     EXEC SQL INCLUDE WSEDDATE END-EXEC.
017600     EXEC SQL INCLUDE WSEDTIME END-EXEC.
017700     EXEC SQL INCLUDE WSZONE   END-EXEC.
017800     EXEC SQL INCLUDE PSTERAR  END-EXEC.
017900
018000     EXEC SQL INCLUDE SQLCA    END-EXEC.
018100*****************************************************************
018200 LINKAGE SECTION.
018300*****************************************************************
018400 01  DFHCOMMAREA.
018500     05  FILLER                      PIC  X(150).
018600*****************************************************************
018700 PROCEDURE DIVISION.
018800******************************************************************
018900 P0000-MAINLINE.
019000******************************************************************
019100     EXEC CICS IGNORE CONDITION ERROR END-EXEC
019200     EXEC CICS HANDLE ABEND LABEL(P9999-GOT-PROBLEM) END-EXEC
019300
019400     EXEC SQL INCLUDE ABSTIME                        END-EXEC
019500
019600     IF EIBCALEN NOT > ZERO
019700        PERFORM P9999-GOT-PROBLEM
019800     END-IF
019900     MOVE DFHCOMMAREA       TO PS08-COMMAREA-PARMS
020000     SET PS08-NO-ERRORS     TO TRUE
020100
020200     EXEC CICS ASKTIME
020300               ABSTIME(WS-ABSTIME)
020400     END-EXEC
020500     ADD WS-ABSTIME-OFFSET  TO WS-ABSTIME
020600     EXEC CICS FORMATTIME
020700               ABSTIME(WS-ABSTIME)
020800               YYYYMMDD(WS-SYSTEM-DATE-CENT)
020900               TIME(WS-SYSTEM-TIME-AREA)
021000     END-EXEC
021100***---------------------------------------------------------------
021200***  INSTALL APPLICATION DATE/TIME
021300***---------------------------------------------------------------
021400     PERFORM P9800-GET-TIME-OFFSET
021500     IF WS-DATE-TIME-OFFSET              > SPACES
021600        MOVE ZEROS                   TO DATE-CONVERSION-PARMS
021700        MOVE WS-SYSTEM-DATE          TO PARM-PRI-DATE-GREG
021800        MOVE WS-SYSTEM-TIME          TO PARM-PRI-HRMN
021900        PERFORM P9810-PROCESS-OFFSET
022000        MOVE PARM-RES-GREG-CENT      TO WS-SYSTEM-CENT
022100        MOVE PARM-RES-DATE-GREG      TO WS-SYSTEM-DATE
022200        MOVE PARM-RES-HRMN           TO WS-SYSTEM-TIME
022300     END-IF
022400
022500     IF PS08-EMP-NBR > SPACES
022600        MOVE PS08-EMP-NBR            TO MSTRNBRK
022700     ELSE
022800        SET PS08-NO-SUCH-EMP-NBR     TO TRUE
022900        PERFORM P9000-RETURN
023000     END-IF
023100***---------------------------------------------------------------
023200***  FIND EMPLOYEE'S TIME ZONE
023300***---------------------------------------------------------------
023400     PERFORM P8500-READ-MASTER
023500     MOVE SPACES                     TO CNTLKEY-AREA
023600     SET SUB-DIST-TYPE-REC           TO TRUE
023700     MOVE DIST     IN WS-MSTR        TO CNTL-DIST
023800     MOVE SUB-DIST IN WS-MSTR        TO CNTL-SUB-DIST
023900     PERFORM P8100-READ-CNTL
024000     IF NOT SUCCESS
024100        MOVE 'P0000-2'            TO ERR-PARAGRAPH
024200        MOVE CNTLKEY              TO ERR-KEY
024300        PERFORM P9999-GOT-PROBLEM
024400     END-IF
024500     MOVE CNTL-TIME-ZONE             TO WS-EMP-TIME-ZONE
      *CNC0600-B
           MOVE CNTL-CAN-WRR-FLAG          TO WS-CAN-WRR-FLAG
           MOVE CNTL-02-WRR-BEGINNING-DTTM TO WS-WRR-START-DTTM
      *CNC0600-E
024600***---------------------------------------------------------------
024700***  CONVERT SYSTEM DATE/TIME TO LOCAL DATE/TIME
024800***---------------------------------------------------------------
024900     MOVE SPACES                     TO TZ-PARAMETERS
025000     MOVE TZ-SYSTEM-TIME-ZONE        TO TZ-IN-ZONE
025100     MOVE WS-PRESENT-TIME            TO TZ-IN-DATE-TIME
025200     MOVE CNTL-TIME-ZONE             TO TZ-OUT-ZONE
025300     PERFORM P8996-TIMEZONE
025400     IF TZ-INVALID-PARAMETERS
025500        MOVE 'A0000TZ'               TO ERR-PARAGRAPH
025600        PERFORM P8996-TZERROR
025700     END-IF
025800     MOVE TZ-OUT-DATE-TIME-CENT      TO WS-LOCAL-DATE-TIME-CENT
025900
026000
026100     EVALUATE TRUE
026200        WHEN PS08-INSERT-FUN
026300           PERFORM P5000-CHECK-ETOD-DATA
026400           PERFORM P5500-SAVE-WS-FROM-TTA
026500           PERFORM P1000-INSERT
026600        WHEN PS08-UPDATE-FUN
026700           PERFORM P5000-CHECK-ETOD-DATA
026800           PERFORM P5500-SAVE-WS-FROM-TTA
026900           PERFORM P2000-UPDATE
027000        WHEN PS08-DELETE-FUN
027100           PERFORM P3000-DELETE
027200        WHEN PS08-INQUIRY-FUN
027300        WHEN PS08-INQUIRY-FROM-05Y
027400        WHEN PS08-INQUIRY-FROM-07Z
027500*C1180 - BEG
027600        WHEN PS08-INQUIRY-07Z-REVISE
027700*C1180 - END
027800           PERFORM P4000-INQUIRY
027900*CNLD-302 B COMMENTING
027900*       WHEN PS08-IMPOSED-REST-FUN
028000*          PERFORM P5000-CHECK-ETOD-DATA
028100*          PERFORM P4500-IMPOSED-REST
027900*CNLD-302 E
      *CNC0600-B
027900        WHEN PS08-RESET-BREAK-FUN
028000           PERFORM P5000-CHECK-ETOD-DATA
028100           PERFORM P4600-RESET-BREAK
      *CNC0600-E
      *CNLD-248-B
027900        WHEN PS08-RESET-BRK-7DAY-END-FUN
044400           PERFORM P4210-WWR-START-DATE
      * MOVE THE WRR START DATE DETAILS
031400           MOVE WS-SUB-DATE   TO WS-WRR-START-DATE
031400           MOVE WS-SUB-TIME   TO WS-WRR-START-TIME
031500           MOVE WS-LOCAL-DATE TO WS-EMP-EFF-DATE
031500           MOVE WS-LOCAL-TIME TO WS-EMP-EFF-TIME
031600           PERFORM P7400-COMP-8DAYS-AGO
                 PERFORM P7000-CHECK-RESET-ELIGIBILITY
      *CNLD-248-E
028200        WHEN OTHER
028300           MOVE 'P0000-3'            TO ERR-PARAGRAPH
028400           MOVE 'BADFUNC'            TO ERR-KEY
028500           PERFORM P9999-GOT-PROBLEM
028600     END-EVALUATE
028700     PERFORM P9000-RETURN.
028800******************************************************************
028900 P1000-INSERT.
029000******************************************************************
029100     MOVE SPACES                   TO WS-EMP-TOD
029200     PERFORM P5100-LOAD-EMPTOD
029300     PERFORM P1100-WRITE-EMPTOD.
029400******************************************************************
029500 P1100-WRITE-EMPTOD.
029600******************************************************************
029700*    IF THERE IS RESPITE TIME WITH A BREAK OF 3 OR MORE HOURS THEN
029800*    BREAK THE TOUR OF DUTY INTO 2 ENTRIES.  THE FIRST ENTRY IS
029900*    FROM THE OD TIME TO THE RESPITE FROM AND THE SECOND ENTRY IS
030000*    THE RESPITE TO TIME TO THE OFFD.
030100
030200     IF PS08-RESPITE-FR-DATE-TIME-CENT > SPACES
030300        AND PS08-RESPITE-TO-DATE-TIME-CENT > SPACES
030400        MOVE ZEROS                 TO DATE-CONVERSION-PARMS
030500        SET PARM-DIFF              TO TRUE
030600        MOVE PS08-RESPITE-FR-DATE  TO PARM-PRI-DATE-GREG
030700        MOVE PS08-RESPITE-FR-TIME  TO PARM-PRI-HRMN
030800        MOVE PS08-RESPITE-TO-DATE  TO PARM-SEC-DATE-GREG
030900        MOVE PS08-RESPITE-TO-TIME  TO PARM-SEC-HRMN
031000        PERFORM P6000-LINK-TO-903
031100        IF PARM-RES-TOT-DAYS > 0
031200           OR PARM-RES-HRMN > 0259
031300           PERFORM P1200-WRITE-RESPITE-EMPTOD
031400        ELSE
031500           PERFORM P8050-WRITE-ETOD
031600           PERFORM P5600-UPDATE-TOD-FROM-WS-TTA
031700        END-IF
031800     ELSE
031900        PERFORM P8050-WRITE-ETOD
032000        PERFORM P5600-UPDATE-TOD-FROM-WS-TTA
032100     END-IF.
032200******************************************************************
032300 P1200-WRITE-RESPITE-EMPTOD.
032400******************************************************************
032500     MOVE PS08-RESPITE-FR-DATE-TIME-CENT
032600                                TO EMPTOD-OFFD-DATE-TIME
032700     MOVE PS08-RESPITE-TO-DATE-TIME-CENT
032800                                TO EMPTOD-NEXT-OD-DATE-TIME
032900     SET EMPTOD-RESPITE-1          TO TRUE
033000*C481
033100     MOVE PS08-EMP-REST-STATUS  TO EMPTOD-REST-STATUS-CODE
033200*
033300     PERFORM P8050-WRITE-ETOD
033400     PERFORM P5600-UPDATE-TOD-FROM-WS-TTA
033500
033500
033600*    C848 - WRITE SECOND ENTRY FOR EMPLOYEE WORKED
033500
015310*CNLD-303 B
100600     EVALUATE TRUE
100600     WHEN PS08-INSERT-FUN
100600          PERFORM P5100-LOAD-EMPTOD
026600     WHEN PS08-UPDATE-FUN
035600          PERFORM P2100-GET-DATA-FOR-UPDATE
100600     END-EVALUATE
015310*CNLD-303 E
033700     MOVE PS08-EMP-NBR          TO EMPTOD-EMP-NBR
033800*C481 MOVE PS08-EMP-REST-STATUS  TO EMPTOD-REST-STATUS-CODE
033900     MOVE SPACES                TO EMPTOD-VIRTUAL-ENTRY-IND
034000*    C848 - END
034100     MOVE PS08-RESPITE-TO-DATE-TIME-CENT
034200                                TO EMPTOD-OD-DATE-TIME
034300     MOVE PS08-OFFD-DATE-TIME-CENT
034400                                TO EMPTOD-OFFD-DATE-TIME
034500     MOVE SPACES                   TO EMPTOD-NEXT-OD-DATE-TIME
034600     SET EMPTOD-RESPITE-2          TO TRUE
034700     SET EMPTOD-REST-RESPITE       TO TRUE
034800     MOVE EMPTOD-KEY-AREA          TO EMPTOD-EMPKEY
034900     PERFORM P8050-WRITE-ETOD
035000     PERFORM P5600-UPDATE-TOD-FROM-WS-TTA.
035100******************************************************************
035200 P2000-UPDATE.
035300******************************************************************
035400     PERFORM P3000-DELETE
035500     IF PS08-NO-ERRORS
035600        PERFORM P2100-GET-DATA-FOR-UPDATE
035700        PERFORM P1100-WRITE-EMPTOD
035800     END-IF
035900     .
036000******************************************************************
036100 P2100-GET-DATA-FOR-UPDATE.
036200******************************************************************
036300     IF PS08-OD-DATE-TIME > SPACES
036400        MOVE PS08-OD-DATE-TIME-CENT   TO EMPTOD-OD-DATE-TIME
036500     END-IF
036600     IF PS08-DIST-SDIST    > SPACES
036700        MOVE PS08-DIST-SDIST          TO EMPTOD-DIST-SDIST
036800     END-IF
036900     IF PS08-TRAIN-ASGN    > SPACES
037000        MOVE PS08-TRAIN-ASGN          TO EMPTOD-TRAIN-ASGN
037100     END-IF
037200     IF PS08-CRAFT         > SPACES
037300        MOVE PS08-CRAFT               TO EMPTOD-CRAFT
037400     END-IF
037500     IF PS08-TIME-ZONE     > SPACES
037600        MOVE PS08-TIME-ZONE           TO EMPTOD-TIME-ZONE
037700     END-IF
037800     IF PS08-OFFD-DATE-TIME > SPACES
037900        MOVE PS08-OFFD-DATE-TIME-CENT TO EMPTOD-OFFD-DATE-TIME
038000     END-IF.
038100******************************************************************
038200 P3000-DELETE.
038300******************************************************************
038400     MOVE SPACES                         TO WS-EMP-TOD
038500     MOVE PS08-EMP-NBR                   TO EMPTOD-EMP-NBR
038600     IF PS08-PREV-OD-DATE-TIME-CENT > SPACES
038700        MOVE PS08-PREV-OD-DATE-TIME-CENT TO EMPTOD-OD-DATE-TIME
038800        MOVE EMPTOD-KEY-AREA             TO EMPTOD-EMPKEY
038900        PERFORM P8070-READ-ETOD
039000        IF SUCCESS
039100           PERFORM P8060-DELETE-ETOD
039200           PERFORM P3100-DELETE-RESPITE
039300        END-IF
039400     END-IF
039500     IF PS08-OD-DATE-TIME-CENT > SPACES
039600        MOVE PS08-OD-DATE-TIME-CENT      TO EMPTOD-OD-DATE-TIME
039700        MOVE EMPTOD-KEY-AREA             TO EMPTOD-EMPKEY
039800        PERFORM P8070-READ-ETOD
039900        IF SUCCESS
040000           PERFORM P8060-DELETE-ETOD
040100           PERFORM P3100-DELETE-RESPITE
040200        END-IF
040300     END-IF
040400     IF PS08-ORDER-DATE-TIME-CENT > SPACES
040500        MOVE PS08-ORDER-DATE-TIME-CENT   TO EMPTOD-OD-DATE-TIME
040600        MOVE EMPTOD-KEY-AREA             TO EMPTOD-EMPKEY
040700        PERFORM P8070-READ-ETOD
040800        IF SUCCESS
040900           PERFORM P8060-DELETE-ETOD
041000           PERFORM P3100-DELETE-RESPITE
041100        END-IF
041200     END-IF
041300
041400     IF PS08-UPDATE-FUN
041500        MOVE SPACES                     TO WS-TTA-STORAGE
041600        MOVE PS08-EMP-NBR               TO TTA-ON-DUTY-EMP-NBR
041700        MOVE PS08-VIRT-ORDER-DATE-TIME  TO TTA-ON-DUTY-TS-DTTM
041800        MOVE PS08-DIST                  TO TTA-DIST
041900        MOVE PS08-SDIST                 TO TTA-SUB-DIST
042000        MOVE PS08-TRAIN-ASGN            TO TTA-ASGN-ID
042100        MOVE PS08-CRAFT                 TO TTA-CRAFT-CD
042200        PERFORM PSQL-TTADEL
042300     END-IF
042400     .
042500******************************************************************
042600 P3100-DELETE-RESPITE.
042700******************************************************************
042800*    CHECK TO SEE IF THE ENTRY IS THE FIRST PART OF A TOUR OF DUTY
042900*    THAT CONTAINS RESPITE.  IF IT IS THEN ALSO DELETE THE SECOND
043000*    PART OF THE TOUR OF DUTY.
043100
043200     IF EMPTOD-RESPITE-1
043300        MOVE EMPTOD-NEXT-OD-DATE-TIME    TO EMPTOD-OD-DATE-TIME
043400        MOVE EMPTOD-KEY-AREA             TO EMPTOD-EMPKEY
043500        PERFORM P8070-READ-ETOD
043600        IF SUCCESS
043700           PERFORM P8060-DELETE-ETOD
043800        END-IF
043900     END-IF.
044000******************************************************************
044100 P4000-INQUIRY.
044200******************************************************************
044300     PERFORM P4100-CHECK-CALC-DATA
044400     PERFORM P4200-GET-START-DATE
044500     PERFORM P4300-CALC-TIME-ON-DUTY.
044600******************************************************************
044700 P4100-CHECK-CALC-DATA.
044800******************************************************************
044900     IF PS08-CALC-DATE > SPACES
045000        SET DE-CCYYMMDD-FORMAT   TO TRUE
045100        MOVE PS08-CALC-DATE-CENT TO DE-CCYYMMDD
045200        PERFORM P8998-DATEEDIT
045300        IF DE-INVALID-DATE
045400           SET PS08-BAD-DATE-CALC TO TRUE
045500           PERFORM P9000-RETURN
045600        END-IF
045700     ELSE
045800        SET PS08-BAD-DATE-CALC   TO TRUE
045900        PERFORM P9000-RETURN
046000     END-IF
046100     IF PS08-CALC-TIME > SPACES
046200        SET TE-MILITARY-FORMAT   TO TRUE
046300        MOVE PS08-CALC-TIME      TO TE-MILITARY-TIME
046400        PERFORM P8997-TIMEEDIT
046500        IF TE-INVALID-TIME
046600           IF TE-MILITARY-TIME = ZEROES
046700              CONTINUE
046800           ELSE
046900              SET PS08-BAD-TIME-CALC TO TRUE
047000              PERFORM P9000-RETURN
047100           END-IF
047200        END-IF
047300     ELSE
047400        SET PS08-BAD-TIME-CALC   TO TRUE
047500        PERFORM P9000-RETURN
047600     END-IF
047700*** 331E - IF FROM 05Y - SAVE START DATE AND TIME
047800     IF PS08-INQUIRY-FROM-05Y
047900        AND PS08-OD-DATE-TIME-CENT NUMERIC
048000        AND PS08-OD-DATE-TIME-CENT > ZERO
048100        MOVE PS08-OD-DATE-TIME-CENT TO WS-START-CALC-DATE-TIME
048200                                       WS-SAVE-START-DATE-TIME
048300        MOVE SPACES                 TO PS08-OD-DATE-TIME-CENT
048400     END-IF.
048500******************************************************************
048600 P4200-GET-START-DATE.
048700******************************************************************
048800* USE 7 AS THE DEFAULT NBR OF DAYS TO CALCULATE TIME ON DUTY
048900     IF PS08-CALC-NBR-DAYS NOT > ZEROS
049000        MOVE 07              TO PS08-CALC-NBR-DAYS
049100     END-IF
049200
049300* GET THE RESULTING DATE FROM SUBTRACTING A CERTAIN NBR OF DAYS
049400     MOVE ZEROS              TO DATE-CONVERSION-PARMS
049500     SET PARM-SUBTRACT       TO TRUE
049600     MOVE PS08-CALC-DATE     TO PARM-PRI-DATE-GREG
049700     MOVE PS08-CALC-NBR-DAYS TO PARM-SEC-GREG-DAY
049800     PERFORM P6000-LINK-TO-903
049900
050000     SET DE-YYMMDD-FORMAT    TO TRUE
050100     MOVE PARM-RES-DATE-GREG TO DE-YYMMDD
050200     PERFORM P8998-DATEEDIT
050300     MOVE DE-CCYYMMDD        TO WS-SUB-DATE-CENT
050400     MOVE PS08-CALC-TIME     TO WS-SUB-TIME
      *CNC0660-CNLD-231
           IF WS-CAN-WRR-NEW
              PERFORM P4210-WWR-START-DATE
           END-IF
050500
050600* SUBTRACT 1 DAY FROM THE ABOVE RESULT - THE THEORY HERE IS THAT
050700*  NO TOUR OF DUTY WILL HAVE LASTED MORE THAN 24 HOURS, SO WE CAN
050800*  GO BACK 1 DAY AND START LOOKING AT THE RECORDS STARTING FROM
050900*  THAT DATE TO DETERMINE IF ANY OF THE TIME ON DUTY SHOULD COUNT
051000*  TOWARDS THE TOTAL TIME ON DUTY
051100     MOVE ZEROS              TO DATE-CONVERSION-PARMS
051200     SET PARM-SUBTRACT       TO TRUE
051300     MOVE WS-SUB-DATE        TO PARM-PRI-DATE-GREG
051400     MOVE 01                 TO PARM-SEC-GREG-DAY
051500     PERFORM P6000-LINK-TO-903
051600
051700     SET DE-YYMMDD-FORMAT    TO TRUE
051800     MOVE PARM-RES-DATE-GREG TO DE-YYMMDD
051900     PERFORM P8998-DATEEDIT
052000     MOVE DE-CCYYMMDD        TO WS-START-DATE-CENT
052100     MOVE WS-SUB-TIME        TO WS-START-TIME.
      *CNC0600-CNLD-231B
052200******************************************************************
052300 P4210-WWR-START-DATE.
052400******************************************************************
      * CHECK CONTROL RECORDS TO GET THE WORK REST RULES START DATE
      * IF ANY, DO NOT COUNT HOS BEFORE THAT DATE.
      * TEST FOR DATE ON SUB-DISTRICT FIRST
           IF WS-WRR-START-DTTM > SPACES
              IF WS-SUB-DATE < WS-WRR-START-DATE
              OR (   WS-SUB-DATE = WS-WRR-START-DATE
                 AND WS-SUB-TIME < WS-WRR-START-TIME
                 )
                 MOVE WS-WRR-START-DATE TO WS-SUB-DATE
                 MOVE WS-WRR-START-TIME TO WS-SUB-TIME
              ELSE
                 CONTINUE
           ELSE
      * NO DATE ON SUB-DISTRICT CHECK THE CORPORATE CONTROL
              MOVE SPACES          TO CNTLKEY-AREA
              SET COMPANY-TYPE-REC TO TRUE
              EXEC CICS READ
                        DATASET(CNTL-FILE-VIA-CNTLKEY)
                        INTO(WS-CNTL-FILE)
                        LENGTH(CNTLFILE-RLGTH)
                        RIDFLD(CNTLKEY-AREA)
                        KEYLENGTH(CNTLFILE-KLGTH)
                        RESP(WS-RESPONSE)
              END-EXEC
              MOVE WS-RESPONSE TO FILE-STATUS
              IF NOT SUCCESS
                MOVE 'P4210' TO ERR-PARAGRAPH
                PERFORM P9999-GOT-PROBLEM
              END-IF
              IF CNTL-00-WRR-BEGINNING-DTTM > SPACES
                 IF WS-SUB-DATE < CNTL-00-WRR-BEGINNING-DATE
                 OR (   WS-SUB-DATE = CNTL-00-WRR-BEGINNING-DATE
                    AND WS-SUB-TIME < CNTL-00-WRR-BEGINNING-TIME
                    )
                    MOVE CNTL-00-WRR-BEGINNING-DATE TO WS-SUB-DATE
                    MOVE CNTL-00-WRR-BEGINNING-TIME TO WS-SUB-TIME
                 ELSE
                    CONTINUE
                 END-IF
              ELSE
      * NO DATE ON CONTROL DEFAULT TO GOVERNMENT DATE
                 IF WS-SUB-DATE-TIME < '202305250001'
                    MOVE '202305250001' TO WS-SUB-DATE-TIME
                 ELSE
                    CONTINUE
              END-IF
           END-IF
           .
      *CNC0600-CNLD-231E
052200******************************************************************
052300 P4300-CALC-TIME-ON-DUTY.
052400******************************************************************
052500     MOVE ZEROS                    TO WS-TOTALS
052600     MOVE SPACES                   TO WS-EMP-TOD
052700     MOVE PS08-EMP-NBR             TO EMPTOD-EMP-NBR
052800     MOVE WS-START-DATE-TIME       TO EMPTOD-OD-DATE-TIME
052900     MOVE EMPTOD-KEY-AREA          TO EMPTOD-EMPKEY
053000     PERFORM P8000-STARTBR-ETOD
053100     IF SUCCESS
053200        SET NOT-DONE               TO TRUE
053300        PERFORM UNTIL DONE
053400           PERFORM P8010-READNEXT-ETOD
053500           IF SUCCESS AND EMPTOD-EMP-NBR = PS08-EMP-NBR
053600              PERFORM P4320-CHECK-TIME-ZONE
053700*** 331E - ALLOW TIMES > INPUT TIME TO PASS THROUGH TO P4400 IF
053800***        ONDUTY < CALC TIME INPUT IF FROM CNP05Y.
053900***      - BYPASS IF RECORD OUTSIDE OF RANGE AND OFFD > START
054000             IF PS08-INQUIRY-FROM-05Y AND
054100              WS-EMP-OFFD-DATE-TIME-NUM < WS-START-CALC-DATE-TIME
054200              CONTINUE
054300             ELSE
054400              IF WS-EMP-OFFD-DATE-TIME-NUM >
054500                 PS08-CALC-DATE-TIME-NUM AND
054600                (PS08-INQUIRY-FUN OR
054700*C1180 - BEG
054800                 PS08-INQUIRY-07Z-REVISE OR
054900*C1180 - END
055000                (PS08-INQUIRY-FROM-05Y AND
055100***              WS-EMP-OD-DATE-TIME >= PS08-CALC-DATE-TIME-NUM))
055200                 WS-EMP-OD-DATE-TIME >= WS-CALC-DATE-TIME))
055300                 SET DONE          TO TRUE
055400              ELSE
055500                 IF EMPTOD-IMPOSED-REST-REC
      *CNC0600-B
                          OR EMPTOD-RESET-BREAK-REC
                          IF WS-CAN-WRR-NEW
                             CONTINUE
                          ELSE
      *CNC0600-E
055600                       MOVE ZEROS  TO WS-TOTALS
055700                       MOVE SPACES TO WS-LAST-OFFD-CENT-DATE-TIME
      *CNC0600-B
                          END-IF
      *CNC0600-E
055800                 ELSE
055900                    IF  PS08-CLEAR-QUICK-TIE
056000                    AND PS08-TRAIN-ASGN = EMPTOD-TRAIN-ASGN
056100                    AND PS08-OD-DATE-TIME-CENT
056200                                        = EMPTOD-OD-DATE-TIME
056300                       CONTINUE
056400                    ELSE
056500                       IF EMPTOD-VIRTUAL-ENTRY
056600                          IF PS08-INCLUDE-VIRTUALS
056700                             PERFORM P4400-CALC-TIME-ON-DUTY
056800                          END-IF
056900                       ELSE
057000                          PERFORM P4400-CALC-TIME-ON-DUTY
057100                       END-IF
057200                    END-IF
057300                 END-IF
057400              END-IF
057500             END-IF
057600           ELSE
057700              SET DONE             TO TRUE
057800           END-IF
057900        END-PERFORM
058000        PERFORM P8020-ENDBR-ETOD
058100     END-IF
058200     IF  PS08-OD-DATE-TIME-CENT      > SPACES
058300     AND WS-LAST-OFFD-CENT-DATE-TIME > SPACES
      *CNC0600-B
      *DONT CHECK GAP BETWEEN THE TRIPS FOR THE NEW CANADIAN REST RULES
                 AND NOT WS-CAN-WRR-NEW
      *CNC0600-E
058400        PERFORM P4415-TIME-FROM-LAST-TRIP
058500     END-IF
058600     MULTIPLY WS-TOTAL-DAYS BY 24 GIVING WS-TOTAL-HRS-ON-DUTY
058700     ADD WS-TOTAL-HR               TO WS-TOTAL-HRS-ON-DUTY
058800     MOVE WS-TOTAL-HRS-ON-DUTY     TO PS08-TOTAL-HRS-ON-DUTY
058900     MOVE WS-TOTAL-MN              TO PS08-TOTAL-MNS-ON-DUTY
059000     .
059100******************************************************************
059200 P4320-CHECK-TIME-ZONE.
059300******************************************************************
059400     MOVE EMPTOD-OD-DATE-TIME        TO WS-EMP-OD-DATE-TIME
059500     MOVE EMPTOD-OFFD-DATE-TIME      TO WS-EMP-OFFD-DATE-TIME
059600     MOVE PS08-CALC-DATE-TIME        TO WS-CALC-DATE-TIME
059700     MOVE WS-SAVE-START-DATE-TIME    TO WS-START-CALC-DATE-TIME
059800     MOVE SPACES                     TO WS-CNTL-FILE
059900     SET SUB-DIST-TYPE-REC           TO TRUE
060000     MOVE EMPTOD-DIST                TO CNTL-DIST
060100     MOVE EMPTOD-SDIST               TO CNTL-SUB-DIST
060200     PERFORM P8100-READ-CNTL
060300     IF SUCCESS
060400        AND CNTL-TIME-ZONE NOT = WS-EMP-TIME-ZONE
060500        PERFORM P4340-CONVERT-TIMES
060600     END-IF.
060700******************************************************************
060800 P4340-CONVERT-TIMES.
060900******************************************************************
061000     MOVE SPACES                     TO TZ-PARAMETERS
061100     MOVE CNTL-TIME-ZONE             TO TZ-IN-ZONE
061200     MOVE EMPTOD-OD-DATE             TO TZ-IN-DATE
061300     MOVE EMPTOD-OD-TIME             TO TZ-IN-TIME
061400     MOVE WS-EMP-TIME-ZONE           TO TZ-OUT-ZONE
061500     PERFORM P8996-TIMEZONE
061600     IF TZ-INVALID-PARAMETERS
061700        MOVE 'A0000TZ'               TO ERR-PARAGRAPH
061800        PERFORM P8996-TZERROR
061900     END-IF
062000     MOVE TZ-OUT-DATE-TIME-CENT      TO WS-EMP-OD-DATE-TIME
062100
062200     MOVE SPACES                     TO TZ-PARAMETERS
062300     MOVE CNTL-TIME-ZONE             TO TZ-IN-ZONE
062400     MOVE EMPTOD-OFFD-DATE           TO TZ-IN-DATE
062500     MOVE EMPTOD-OFFD-TIME           TO TZ-IN-TIME
062600     MOVE WS-EMP-TIME-ZONE           TO TZ-OUT-ZONE
062700     PERFORM P8996-TIMEZONE
062800     IF TZ-INVALID-PARAMETERS
062900        MOVE 'A0000TZ'               TO ERR-PARAGRAPH
063000        PERFORM P8996-TZERROR
063100     END-IF
063200     MOVE TZ-OUT-DATE-TIME-CENT      TO WS-EMP-OFFD-DATE-TIME
063300*** CONVERT CALC TIME TO SAME TIME ZONE
063400     MOVE SPACES                     TO TZ-PARAMETERS
063500     MOVE CNTL-TIME-ZONE             TO TZ-IN-ZONE
063600     MOVE PS08-CALC-DATE             TO TZ-IN-DATE
063700     MOVE PS08-CALC-TIME             TO TZ-IN-TIME
063800     MOVE WS-EMP-TIME-ZONE           TO TZ-OUT-ZONE
063900     PERFORM P8996-TIMEZONE
064000     IF TZ-INVALID-PARAMETERS
064100        MOVE 'A0000TZ'               TO ERR-PARAGRAPH
064200        PERFORM P8996-TZERROR
064300     END-IF
064400     MOVE TZ-OUT-DATE-TIME-CENT      TO WS-CALC-DATE-TIME
064500*** CONVERT START CALC TIME TO SAME TIME ZONE
064600     IF PS08-INQUIRY-FROM-05Y
064700        AND WS-SAVE-START-DATE-TIME NUMERIC
064800        AND WS-SAVE-START-DATE-TIME > ZERO
064900        MOVE SPACES                  TO TZ-PARAMETERS
065000        MOVE CNTL-TIME-ZONE          TO TZ-IN-ZONE
065100        MOVE WS-SAVE-START-DATE      TO TZ-IN-DATE
065200        MOVE WS-SAVE-START-TIME      TO TZ-IN-TIME
065300        MOVE WS-EMP-TIME-ZONE        TO TZ-OUT-ZONE
065400        PERFORM P8996-TIMEZONE
065500        IF TZ-INVALID-PARAMETERS
065600           MOVE 'A0000TZ'            TO ERR-PARAGRAPH
065700           PERFORM P8996-TZERROR
065800        END-IF
065900        MOVE TZ-OUT-DATE-TIME-CENT   TO WS-START-DATE-TIME
066000     END-IF.
066100******************************************************************
066200 P4400-CALC-TIME-ON-DUTY.
066300******************************************************************
066400*** 331E - IF FROM CNP05Y AND OFFDUTY IS > INPUT TIME, CALCULATE
066500***        TIMES ONLY UP TO THE INPUT TIME
066600***        ALSO CALCULATE ONLY FROM THE START DATE
066700     IF PS08-INQUIRY-FROM-05Y
066800        IF WS-EMP-OFFD-DATE-TIME-NUM > WS-CALC-DATE-TIME
066900           MOVE WS-CALC-DATE-TIME TO WS-EMP-OFFD-DATE-TIME
067000        END-IF
067100        IF WS-EMP-OD-DATE-TIME-NUM   < WS-START-CALC-DATE-TIME
067200           MOVE WS-START-CALC-DATE-TIME TO WS-EMP-OD-DATE-TIME
067300        END-IF
067400     END-IF
067500
067600     IF WS-EMP-OFFD-DATE-TIME > WS-SUB-DATE-TIME
067700        IF WS-LAST-OFFD-CENT-DATE-TIME > SPACES
      *CNC0600-B
      *DONT CHECK GAP BETWEEN THE TRIPS FOR THE NEW CANADIAN REST RULES
                 AND NOT WS-CAN-WRR-NEW
      *CNC0600-E
067800           PERFORM P4410-TIME-BETWEEN-TRIPS
067900        END-IF
068000        PERFORM P4420-ADD-TIME-ON-DUTY
068100     END-IF.
068200******************************************************************
068300 P4410-TIME-BETWEEN-TRIPS.
068400******************************************************************
068500*   CALCULATE THE TIME BETWEEN THE OFF DUTY OF THE LAST TRIP AND
068600*   THE ON DUTY TIME OF THE CURRENT RECORD.  WHEN 24 HOURS OR
068700*   MORE HAS ELAPSED, BETWEEN THE TRIPS THE WS-TOTALS VARIABLE
068800*   IS RESET TO ZERO.
068900*
069000*   NOTE:  THE ON DUTY TIME OF THE CURRENT RECORD MAY BE ADJUSTED
069100*          FOR ACTUAL TIME.
069200*-----------------------------------------------------------------
069300     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
069400     SET PARM-DIFF                 TO TRUE
069500     MOVE WS-EMP-OD-DATE           TO PARM-PRI-DATE-GREG
069600     MOVE WS-EMP-OD-TIME           TO PARM-PRI-HRMN
069700     MOVE WS-LAST-OFFD-DATE        TO PARM-SEC-DATE-GREG
069800     MOVE WS-LAST-OFFD-TIME        TO PARM-SEC-HRMN
069900     PERFORM P6000-LINK-TO-903
070000     MULTIPLY PARM-RES-TOT-DAYS BY 24 GIVING WORK-HR
070100     ADD PARM-RES-HR               TO WORK-HR
070200     MOVE PARM-RES-MN              TO WORK-MN
070300     IF WORK-TIME-NUM >= 2400
070300*2023/09/11 RJA B
070300*    AS WORK-HR IS 9(02), IT CAN GET COMPUTED TO LESS THAN 24 IN
070300*    SOME CASES WHEN DIFF > 24, FOR EX: IF DIFF IS 4 DAYS 4 HOURS
070300     OR PARM-RES-TOT-DAYS >= 1
070300*2023/09/11 RJA E
070400        MOVE ZEROS                 TO WS-TOTALS
070500        MOVE SPACES                TO WS-LAST-OFFD-CENT-DATE-TIME
070600*C1177 - BEG
070700*C1180 - ADD PS08-INQUIRY-07Z-REVISE
070800     ELSE
070900        IF PS08-INQUIRY-FROM-07Z OR PS08-INQUIRY-07Z-REVISE
071000           IF EMPTOD-REST-GREEN
071100*C1186 - BEG TESTING...
071200           OR EMPTOD-REST-RESPITE
071210*C1186 - END TESTING...
071220              MOVE ZEROS           TO WS-TOTALS
071230              MOVE SPACES          TO WS-LAST-OFFD-CENT-DATE-TIME
071240           END-IF
071250        END-IF
071260*C1177 - END
071270     END-IF.
071280******************************************************************
071290 P4415-TIME-FROM-LAST-TRIP.
071300******************************************************************
071400*   WHEN COMING FROM P925 DURING A CALL, DETERMINE IF MORE THAN
071500*   24 HOURS HAS ELAPSED BETWEEN THE OFF DUTY TIME OF THE LAST
071600*   TRIP AND THE ON DUTY TIME OF THE TRIP BEING CALLED.  IF SO,
071700*   RESET THE TOTAL TIME TO ZERO.
071800*
071900*   NOTE:  THE ON DUTY TIME OF THE CURRENT RECORD MAY BE ADJUSTED
072000*          FOR ACTUAL TIME.
072100*-----------------------------------------------------------------
072200     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
072300     SET PARM-DIFF                 TO TRUE
072400     MOVE PS08-OD-DATE             TO PARM-PRI-DATE-GREG
072500     MOVE PS08-OD-TIME             TO PARM-PRI-HRMN
072600     MOVE WS-LAST-OFFD-DATE        TO PARM-SEC-DATE-GREG
072700     MOVE WS-LAST-OFFD-TIME        TO PARM-SEC-HRMN
072800     PERFORM P6000-LINK-TO-903
072900     MULTIPLY PARM-RES-TOT-DAYS BY 24 GIVING WORK-HR
073000     ADD PARM-RES-HR               TO WORK-HR
073100     MOVE PARM-RES-MN              TO WORK-MN
073200     MOVE WORK-TIME-NUM            TO PS08-TM-FROM-LST-TRIP
070300*2023/09/11 RJA B
070300*    AS WORK-HR IS 9(02), IT CAN GET COMPUTED TO LESS THAN 24 IN
070300*    SOME CASES WHEN DIFF > 24, FOR EX: IF DIFF IS 4 DAYS 4 HOURS
070300     IF WORK-TIME-NUM <  2400
070300     AND PARM-RES-TOT-DAYS >= 4
070300*       NOTE: PS08-TM-FROM-LST-TRIP IS NOT GETTING USED ANYWHERE
070300*       AS OF TODAY. POPULATING THIS WITH 9959 INSTEAD OF A VALUE
070300*       LESS THAN 2400 FOR CONSISTENCY.
073500        MOVE 9959                  TO PS08-TM-FROM-LST-TRIP
073500     END-IF
070300*2023/09/11 RJA E
073300     IF WORK-TIME-NUM >= 2400
070300*2023/09/11 RJA B
070300*    AS WORK-HR IS 9(02), IT CAN GET COMPUTED TO LESS THAN 24 IN
070300*    SOME CASES WHEN DIFF > 24, FOR EX: IF DIFF IS 4 DAYS 4 HOURS
070300     OR PARM-RES-TOT-DAYS >= 1
070300*2023/09/11 RJA E
073400        MOVE ZEROS                 TO WS-TOTALS
073500     END-IF.
073600******************************************************************
073700 P4420-ADD-TIME-ON-DUTY.
073800******************************************************************
073900     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
074000     SET PARM-DIFF                 TO TRUE
074100     MOVE WS-EMP-OFFD-DATE         TO PARM-PRI-DATE-GREG
074200     MOVE WS-EMP-OFFD-TIME         TO PARM-PRI-HRMN
074300     IF WS-EMP-OD-DATE-TIME < WS-SUB-DATE-TIME
074400        MOVE WS-SUB-DATE           TO PARM-SEC-DATE-GREG
074500        MOVE WS-SUB-TIME           TO PARM-SEC-HRMN
074600     ELSE
074700        MOVE WS-EMP-OD-DATE        TO PARM-SEC-DATE-GREG
074800        MOVE WS-EMP-OD-TIME        TO PARM-SEC-HRMN
074900     END-IF
075000     PERFORM P6000-LINK-TO-903
075100     ADD PARM-RES-TOT-DAYS         TO WS-TOTAL-DAYS
075200     ADD PARM-RES-HR               TO WS-TOTAL-HR
075300     ADD PARM-RES-MN               TO WS-TOTAL-MN
075400     IF WS-TOTAL-MN >= 60
075500        ADD 1                      TO WS-TOTAL-HR
075600        SUBTRACT 60              FROM WS-TOTAL-MN
075700     END-IF
075800     MOVE WS-EMP-OFFD-DATE-TIME    TO WS-LAST-OFFD-CENT-DATE-TIME.
027900
027900*CNLD-302 B COMMENTING
075900******************************************************************
076000*P4500-IMPOSED-REST.
076100******************************************************************
076200*    MOVE SPACES                   TO WS-EMP-TOD
076300*    SET  EMPTOD-IMPOSED-REST-REC  TO TRUE
076400*    PERFORM P5100-LOAD-EMPTOD
076500*    PERFORM P1100-WRITE-EMPTOD.
027900*CNLD-302 E
027900
      *CNC0600-B
075900******************************************************************
076000 P4600-RESET-BREAK.
076100******************************************************************
076200     MOVE SPACES                   TO WS-EMP-TOD
      *CNLD-214
076300*    SET  EMPTOD-RESET-BREAK-REC   TO TRUE
           EVALUATE TRUE
             WHEN PS08-RESET-BREAK-SYS
                SET EMPTOD-RESET-BREAK-SYS TO TRUE
015310*CNLD-266 B
015310          IF PS08-SJOWNER-AT-0DV-RESET
015310             SET EMPTOD-SJOWNER-AT-0DV-RESET TO TRUE
015310          END-IF
015310*CNLD-266 E
             WHEN PS08-RESET-BREAK-NAT
                SET EMPTOD-RESET-BREAK-NAT TO TRUE
      *CNLD-242-RPV-B MANUAL UPDATE FROM CHANGE OF STATUS SCREEN
      *           PS08-RESET-BREAK-MNL WAS PS08-RESET-BREAK-COS
             WHEN PS08-RESET-BREAK-MNL
      *           EMPTOD-RESET-BREAK-MNL WAS EMPTOD-RESET-BREAK-COS
                SET EMPTOD-RESET-BREAK-MNL TO TRUE
      *CNLD-242-RPV-E
      *      WHEN OTHER
      *         MOVE 'Y' TO EMPTOD-RESET-BREAK-FLAG
           END-EVALUATE
      *CNLD-214
076400     PERFORM P5100-LOAD-EMPTOD
076500     PERFORM P1100-WRITE-EMPTOD.
      *CNC0600-E
076600******************************************************************
076700 P5000-CHECK-ETOD-DATA.
076800******************************************************************
076900* THE EMP-NBR WAS CHECKED IN P0000-MAINLINE
077000
077100     IF PS08-OD-DATE > SPACES
077200        SET DE-CCYYMMDD-FORMAT  TO TRUE
077300        MOVE PS08-OD-DATE-CENT  TO DE-CCYYMMDD
077400        PERFORM P8998-DATEEDIT
077500        IF DE-INVALID-DATE
077600           SET PS08-BAD-DATE-OD TO TRUE
077700           PERFORM P9000-RETURN
077800        END-IF
077900     ELSE
078000        SET PS08-BAD-DATE-OD    TO TRUE
078100        PERFORM P9000-RETURN
078200     END-IF
078300
078400     IF PS08-OD-TIME > SPACES
078500        SET TE-MILITARY-FORMAT  TO TRUE
078600        MOVE PS08-OD-TIME       TO TE-MILITARY-TIME
078700        PERFORM P8997-TIMEEDIT
078800        IF TE-INVALID-TIME
078900           IF TE-MILITARY-TIME = ZEROES
079000              CONTINUE
079100           ELSE
079200              SET PS08-BAD-TIME-OD  TO TRUE
079300              PERFORM P9000-RETURN
079400           END-IF
079500        END-IF
079600     ELSE
079700        SET PS08-BAD-TIME-OD       TO TRUE
079800        PERFORM P9000-RETURN
079900     END-IF
080000
080100* THIS CHECK WILL NEED TO GO AGAINST THE DB EVENTUALLY
080200     IF PS08-TRAIN-ASGN > SPACES
      *CNC0600-B
              OR EMPTOD-RESET-BREAK-REC
      *CNC0600-E
080300        CONTINUE
080400     ELSE
080500        SET PS08-NO-SUCH-TRAIN-ASGN TO TRUE
080600        PERFORM P9000-RETURN
080700     END-IF
080800
080900     IF PS08-TRAIN-ASGN = '    HOME  '
081000        CONTINUE
081100     ELSE
081200        IF PS08-CRAFT > SPACES
081300           MOVE PS08-CRAFT          TO WS-CRAFT-CODE-CHECK
081400           IF NOT VALID-ASGN-CRAFT
081500              SET PS08-NO-SUCH-CRAFT TO TRUE
081600              PERFORM P9000-RETURN
081700           END-IF
081800        ELSE
081900           SET PS08-NO-SUCH-CRAFT   TO TRUE
082000           PERFORM P9000-RETURN
082100        END-IF
082200     END-IF
082300
082400     IF PS08-OFFD-DATE > SPACES
082500        SET DE-CCYYMMDD-FORMAT      TO TRUE
082600        MOVE PS08-OFFD-DATE-CENT    TO DE-CCYYMMDD
082700        PERFORM P8998-DATEEDIT
082800        IF DE-INVALID-DATE
082900           SET PS08-BAD-DATE-OFFD   TO TRUE
083000           PERFORM P9000-RETURN
083100        END-IF
083200     ELSE
083300        SET PS08-BAD-DATE-OFFD      TO TRUE
083400        PERFORM P9000-RETURN
083500     END-IF
083600
083700     IF PS08-OFFD-TIME > SPACES
083800        SET TE-MILITARY-FORMAT  TO TRUE
083900        MOVE PS08-OFFD-TIME     TO TE-MILITARY-TIME
084000        PERFORM P8997-TIMEEDIT
084100        IF TE-INVALID-TIME
084200           IF TE-MILITARY-TIME = ZEROES
084300              CONTINUE
084400           ELSE
084500              SET PS08-BAD-TIME-OFFD TO TRUE
084600              PERFORM P9000-RETURN
084700           END-IF
084800        END-IF
084900     ELSE
085000        SET PS08-BAD-TIME-OFFD  TO TRUE
085100        PERFORM P9000-RETURN
085200     END-IF
085300
085400     IF PS08-ORDER-DATE > SPACES
085500        SET DE-CCYYMMDD-FORMAT     TO TRUE
085600        MOVE PS08-ORDER-DATE-CENT  TO DE-CCYYMMDD
085700        PERFORM P8998-DATEEDIT
085800        IF DE-INVALID-DATE
085900           SET PS08-BAD-DATE-ORDER TO TRUE
086000           PERFORM P9000-RETURN
086100        END-IF
086200     END-IF
086300
086400     IF PS08-ORDER-TIME > SPACES
086500        SET TE-MILITARY-FORMAT     TO TRUE
086600        MOVE PS08-ORDER-TIME       TO TE-MILITARY-TIME
086700        PERFORM P8997-TIMEEDIT
086800        IF TE-INVALID-TIME
086900           IF TE-MILITARY-TIME = ZEROES
087000              CONTINUE
087100           ELSE
087200              SET PS08-BAD-TIME-ORDER  TO TRUE
087300              PERFORM P9000-RETURN
087400           END-IF
087500        END-IF
087600     END-IF
087700
087800     IF PS08-RESPITE-FR-DATE > SPACES
087900        SET DE-CCYYMMDD-FORMAT          TO TRUE
088000        MOVE PS08-RESPITE-FR-DATE-CENT  TO DE-CCYYMMDD
088100        PERFORM P8998-DATEEDIT
088200        IF DE-INVALID-DATE
088300           SET PS08-BAD-DATE-RESP-FR    TO TRUE
088400           PERFORM P9000-RETURN
088500        END-IF
088600     END-IF
088700
088800     IF PS08-RESPITE-FR-TIME > SPACES
088900        SET TE-MILITARY-FORMAT          TO TRUE
089000        MOVE PS08-RESPITE-FR-TIME       TO TE-MILITARY-TIME
089100        PERFORM P8997-TIMEEDIT
089200        IF TE-INVALID-TIME
089300           IF TE-MILITARY-TIME = ZEROES
089400              CONTINUE
089500           ELSE
089600              SET PS08-BAD-TIME-RESP-FR TO TRUE
089700              PERFORM P9000-RETURN
089800           END-IF
089900        END-IF
090000     END-IF
090100
090200     IF PS08-RESPITE-TO-DATE > SPACES
090300        SET DE-CCYYMMDD-FORMAT          TO TRUE
090400        MOVE PS08-RESPITE-TO-DATE-CENT  TO DE-CCYYMMDD
090500        PERFORM P8998-DATEEDIT
090600        IF DE-INVALID-DATE
090700           SET PS08-BAD-DATE-RESP-TO    TO TRUE
090800           PERFORM P9000-RETURN
090900        END-IF
091000     END-IF
091100     IF PS08-RESPITE-TO-TIME > SPACES
091200        SET TE-MILITARY-FORMAT          TO TRUE
091300        MOVE PS08-RESPITE-TO-TIME       TO TE-MILITARY-TIME
091400        PERFORM P8997-TIMEEDIT
091500        IF TE-INVALID-TIME
091600           IF TE-MILITARY-TIME = ZEROES
091700              CONTINUE
091800           ELSE
091900              SET PS08-BAD-TIME-RESP-TO TO TRUE
092000              PERFORM P9000-RETURN
092100           END-IF
092200        END-IF
092300     END-IF.
092400******************************************************************
092500 P5100-LOAD-EMPTOD.
092600******************************************************************
092700     MOVE PS08-EMP-NBR             TO EMPTOD-EMP-NBR
092800     IF PS08-TIME-ZONE > SPACES
092900        MOVE PS08-TIME-ZONE        TO EMPTOD-TIME-ZONE
093000     ELSE
093100        MOVE WS-EMP-TIME-ZONE      TO EMPTOD-TIME-ZONE
093200     END-IF
093300     MOVE PS08-OD-DATE-TIME-CENT   TO EMPTOD-OD-DATE-TIME
093400     MOVE EMPTOD-KEY-AREA          TO EMPTOD-EMPKEY
093500     MOVE PS08-DIST-SDIST          TO EMPTOD-DIST-SDIST
093600     MOVE PS08-TRAIN-ASGN          TO EMPTOD-TRAIN-ASGN
093700     MOVE PS08-CRAFT               TO EMPTOD-CRAFT
093800     MOVE PS08-OFFD-DATE-TIME-CENT TO EMPTOD-OFFD-DATE-TIME
093900     MOVE PS08-EMP-REST-STATUS     TO EMPTOD-REST-STATUS-CODE
094000     MOVE WS-ACTUAL-TIME-FLAG      TO EMPTOD-ACTUAL-TIME-FLAG
094100     IF PS08-HOS-MC-ENTRY
094200        SET EMPTOD-HOS-CLAIM       TO TRUE
094300     END-IF
094400     MOVE PS08-JOB-TYPE            TO EMPTOD-JOB-TYPE
094500     MOVE PS08-TERM                TO EMPTOD-TERM
094600     MOVE PS08-TIE-UP-SERVICE      TO EMPTOD-TIE-UP-SERVICE
094700     .
094800******************************************************************
094900 P5500-SAVE-WS-FROM-TTA.
095000******************************************************************
095100     INITIALIZE WS-TTA-EMPLOYEE-TABLE
095200     MOVE 1                             TO SUB1
095300     MOVE PS08-EMP-NBR                  TO TTA-ON-DUTY-EMP-NBR
095400     MOVE PS08-VIRT-ORDER-DATE-TIME     TO TTA-ON-DUTY-TS-DTTM
095500     MOVE PS08-DIST                     TO TTA-DIST
095600     MOVE PS08-SDIST                    TO TTA-SUB-DIST
095700     MOVE PS08-TRAIN-ASGN               TO TTA-ASGN-ID
095800     MOVE PS08-CRAFT                    TO TTA-CRAFT-CD
095900     PERFORM PSQL-TTAOPN01
096000     PERFORM PSQL-TTAFET01
096100     PERFORM UNTIL NOT SQL-SUCCESS  OR  SUB1 > WS-TTA-MAX
096200        MOVE TTA-TRACKED-EMP            TO WS-TTA-EMP(SUB1)
096300        MOVE TTA-REST-STATUS-CD         TO WS-REST-STATUS-CHECK
096400        IF WS-VALID-REST-STATUS
096500           MOVE TTA-REST-STATUS-CD      TO WS-TTA-REST-STAT(SUB1)
096600        ELSE
096700           SET WS-TTA-REST-GREEN(SUB1)  TO TRUE
096800        END-IF
096900        PERFORM PSQL-TTAFET01
097000        ADD 1                           TO SUB1
097100     END-PERFORM
097200     PERFORM PSQL-TTACLO01
097300     .
097400******************************************************************
097500 P5600-UPDATE-TOD-FROM-WS-TTA.
097600******************************************************************
097700     PERFORM VARYING SUB1 FROM 1 BY 1
097800       UNTIL SUB1 > WS-TTA-MAX
097900        IF WS-TTA-EMP(SUB1) NOT > SPACES
098000           MOVE WS-TTA-MAX             TO SUB1
098100        ELSE
098200*
098300*          IF THE EMPLOYEE IS BEING TRACKED ON THE ASSIGNMENT
098400*          HE'S WORKING, FOR SOME REASON, DON'T WRITE A VIRTUAL
098500*          TOD ENTRY.
098600*
098700           IF WS-TTA-EMP(SUB1) = PS08-EMP-NBR
098800              CONTINUE
098900           ELSE
099000             MOVE WS-TTA-EMP(SUB1)       TO EMPTOD-EMP-NBR
099100             MOVE PS08-OD-DATE-TIME-CENT TO EMPTOD-OD-DATE-TIME
099200             MOVE EMPTOD-KEY-AREA        TO EMPTOD-EMPKEY
099300*            CHECK FOR ANY SUBSEQUENT TOD RECORDS
099400*            IF FOUND, DO NOT UPDATE EXISTING VIRTUAL TOD RECORD
099500             PERFORM P5610-CHECK-FOR-SUBSEQUENTS
099600             IF SUBSEQUENT-FOUND
099700                CONTINUE
099800             ELSE
099900                MOVE WS-TTA-EMP(SUB1)       TO EMPTOD-EMP-NBR
100000                MOVE PS08-OD-DATE-TIME-CENT TO EMPTOD-OD-DATE-TIME
100100                MOVE EMPTOD-KEY-AREA        TO EMPTOD-EMPKEY
100200                PERFORM P8070-READ-ETOD
100300                IF SUCCESS
100400                   CONTINUE
100500                ELSE
100600                   PERFORM P5100-LOAD-EMPTOD
100700                   MOVE WS-TTA-EMP(SUB1)     TO EMPTOD-EMP-NBR
100800                   MOVE WS-TTA-REST-STAT(SUB1)
100900                                        TO EMPTOD-REST-STATUS-CODE
101000                   SET EMPTOD-VIRTUAL-ENTRY  TO TRUE
101100                   PERFORM P8050-WRITE-ETOD
101200                END-IF
101300             END-IF
101400           END-IF
101500        END-IF
101600     END-PERFORM
101700     .
101800******************************************************************
101900 P5610-CHECK-FOR-SUBSEQUENTS.
102000******************************************************************
102100     MOVE SPACE                          TO WS-SUBSEQUENT-FLAG
102200     PERFORM P8000-STARTBR-ETOD
102300     IF SUCCESS
102400        PERFORM P8010-READNEXT-ETOD
102500        IF SUCCESS
102600           IF EMPTOD-EMP-NBR  = WS-TTA-EMP(SUB1)
102700              SET SUBSEQUENT-FOUND       TO TRUE
102800           END-IF
102900        END-IF
103000        PERFORM P8020-ENDBR-ETOD
103100     END-IF
015310*CNLD-303 B
029100     MOVE SPACES TO WS-EMP-TOD
015310*CNLD-303 E
103200     .
103300******************************************************************
103400 P6000-LINK-TO-903.
103500******************************************************************
103600     EXEC CICS LINK
103700               PROGRAM(P903-PGM)
103800               COMMAREA(DATE-CONVERSION-PARMS)
103900               LENGTH(P903-LGTH)
104000               RESP(WS-RESPONSE)
104100     END-EXEC
104200     MOVE WS-RESPONSE        TO FILE-STATUS
104300     IF NOT SUCCESS
104400        MOVE 'P6000-1'       TO ERR-PARAGRAPH
104500        MOVE 'P903LINK'      TO ERR-KEY
104600        PERFORM P9999-GOT-PROBLEM
104700     END-IF.
      *CNLD-248-B
       P7000-CHECK-RESET-ELIGIBILITY.
497400     MOVE EMP-NBR OF WS-MSTR   TO MSTR3NBRK
           PERFORM P8600-READ-MSTR3-RECORD
047800*    MOVE WS-RESPONSE              TO FILE-STATUS.
047900     IF NOT SUCCESS
048000        IF NOT (NO-RECORD-FND OR END-OF-FILE)
048100           MOVE 'P7000-1'          TO ERR-PARAGRAPH
048200           MOVE MSTR3NBRK          TO ERR-KEY
048300           PERFORM P9999-GOT-PROBLEM
      *CNLD-267-RJA-B
048400        ELSE
770500           MOVE SPACES                     TO WS-MSTR3
770600           MOVE MSTR3NBRK                  TO MSTR3-KEY
770700           EXEC CICS WRITE
770800                     DATASET(MSTR3-VIA-EMP-NBR)
770900                     FROM(WS-MSTR3)
771000                     LENGTH(MSTR3ENBR-RLGTH)
771100                     RIDFLD(MSTR3NBRK)
771200                     RESP(WS-RESPONSE)
771300           END-EXEC
771400           MOVE WS-RESPONSE                TO FILE-STATUS
771500           IF NOT SUCCESS
771600              MOVE 'P7000-2' TO ERR-PARAGRAPH
771700              PERFORM P9999-GOT-PROBLEM
771800           END-IF
                 PERFORM P8600-READ-MSTR3-RECORD
771500           IF NOT SUCCESS
771600              MOVE 'P7000-3' TO ERR-PARAGRAPH
771700              PERFORM P9999-GOT-PROBLEM
771800           END-IF
      *CNLD-267-RJA-E
048400        END-IF
      *CNLD-267-RJA-B
048500*    ELSE
048500     END-IF
048500     IF SUCCESS
      *CNLD-267-RJA-E
048600
048700        IF (MSTR3-PROJ-RESET-BRK = MSTR3-SYSTEM-RESET-BRK)
048800        AND MSTR3-SYSTEM-RESET-BRK > '0000000000'
048900*
049000*          CHECK FOR ANY HOS B/W SYSTEM-RESET AND NOW
049100*
049200           PERFORM P1131-PRV-HOS-AFTER-SYS-RESET
049300        ELSE
049400*
049500*          CHECK FOR ALL HOS B/W 7DAY-START AND NOW
049600*
049700           PERFORM P1132-RECALC-PROJ-RESET-BRK
049800        END-IF
049900*
      *CNLD-267-RJA-B
              MOVE '0000000000'          TO PS08-PROJ-RESET-DTTM
                                            PS08-PROJ-UD1-FROM
                                            PS08-PROJ-UD1-TO
                                            PS08-PROJ-UD2-FROM
                                            PS08-PROJ-UD2-TO
      *CNLD-267-RJA-E
050000        IF WS-7DAY-START-DTTM > '0000000000'
050100           PERFORM P1140-CALCULATE-7DAY-END
      *CNLD-267-RJA-B
                 IF WS-EMP-EFF-DATE-TIME <= WS-UPD-PROJ-DATE-TIME
      * 8/30/23  WS-EMP-EFF-DATE-TIME <= WS-UPD-PROJ-DATE-TIME WILL
      *          ALWAYS BE TRUE WHEN WS-7DAY-START-DTTM > ZEROS
                    IF WS-EMP-EFF-DATE-TIME = WS-UPD-PROJ-DATE-TIME
                       CONTINUE
                    ELSE
                       MOVE WS-UPD-PROJ-DATE-TIME
                                         TO PS08-PROJ-RESET-DTTM
                       MOVE WS-UPD-PROJ-UD1-FROM
                                         TO PS08-PROJ-UD1-FROM
                       MOVE WS-UPD-PROJ-UD1-TO
                                         TO PS08-PROJ-UD1-TO
                       MOVE WS-UPD-PROJ-UD2-FROM
                                         TO PS08-PROJ-UD2-FROM
                       MOVE WS-UPD-PROJ-UD2-TO
                                         TO PS08-PROJ-UD2-TO
                    END-IF
                 END-IF
      *CNLD-267-RJA-E
              ELSE
089100           MOVE WS-7DAY-START-DATE TO PS08-7DAY-END-DATE-YYMMDD
08910            MOVE WS-7DAY-START-TIME TO PS08-7DAY-END-TIME
050300        END-IF
050300     END-IF
435200     .
435200*
062100 P1131-PRV-HOS-AFTER-SYS-RESET.
062200*
062300*    CONTROL REACHES HERE ONLY IF :
062400*    MSTR3-SYSTEM-RESET-BRK = MSTR3-PROJ-RESET-BRK
062500
062600     IF MSTR3-PROJ-RESET-BRK > WS-WRR-START-DTTM
062700        MOVE MSTR3-PROJ-RESET-BRK TO WS-7DAY-START-DTTM
062800     ELSE
062900
063000        IF WS-8DAYS-AGO-DTTM > WS-WRR-START-DTTM
063100           MOVE WS-8DAYS-AGO-DTTM  TO WS-7DAY-START-DTTM
063200        ELSE
063300*          START READING FROM (WS-WRR-START-DTTM - 1 DAY)
063400           PERFORM P7110-WRR-START-MINUS-1DY
063500        END-IF
063600     END-IF
063700
064100     PERFORM P1133-PRV-HOS-AFTER-7DY-START
064300     .
070300 P7110-WRR-START-MINUS-1DY.
070400*
070500* SUBTRACT 1 DAY FROM THE ABOVE RESULT - THE THEORY HERE IS THAT
070600*  NO TOUR OF DUTY WILL HAVE LASTED MORE THAN 24 HOURS, SO WE CAN
070700*  GO BACK 1 DAY AND START LOOKING AT THE RECORDS STARTING FROM
070800*  THAT DATE TO DETERMINE IF ANY OF THE TIME ON DUTY SHOULD COUNT
070900     MOVE ZEROS              TO DATE-CONVERSION-PARMS
071000     SET PARM-SUBTRACT       TO TRUE
071100     MOVE WS-WRR-START-DATE  TO PARM-PRI-DATE-GREG
071200     MOVE 01                 TO PARM-SEC-GREG-DAY
071300     EXEC CICS LINK
071400               PROGRAM(P903-PGM)
071500               COMMAREA(DATE-CONVERSION-PARMS)
071600               LENGTH(P903-LGTH)
071700               RESP(WS-RESPONSE)
071800     END-EXEC
071900     MOVE WS-RESPONSE             TO FILE-STATUS
072000     IF NOT SUCCESS
072100        MOVE 'P7110-1'           TO ERR-PARAGRAPH
072200        MOVE 'P903LINK'           TO ERR-KEY
072300        PERFORM P9999-GOT-PROBLEM
072400     END-IF
072500
072600     MOVE PARM-RES-DATE-GREG TO WS-7DAY-START-DATE
072700     MOVE WS-WRR-START-TIME  TO WS-7DAY-START-TIME
072800     .
077300 P1133-PRV-HOS-AFTER-7DY-START.
077400*
077500     MOVE SPACES                     TO WS-EMP-TOD
077600     MOVE MSTR3-EMP-NBR              TO EMPTOD-EMP-NBR
077700     MOVE WS-7DAY-START-DATE         TO DE-YYMMDD
077800     SET DE-YYMMDD-FORMAT            TO TRUE
077900     PERFORM P8998-DATEEDIT
078000     MOVE DE-CCYYMMDD                TO EMPTOD-OD-DATE-CENT
078100     MOVE WS-7DAY-START-TIME         TO EMPTOD-OD-TIME
078200     MOVE EMPTOD-KEY-AREA            TO EMPTOD-EMPKEY
078300
078400     PERFORM P8000-STARTBR-ETOD
078500     MOVE 'N'                        TO WS-READ-DONE-CODE
078600     MOVE '0000000000'               TO WS-7DAY-START-DTTM
078700     PERFORM UNTIL READ-DONE
078800        PERFORM P8010-READNEXT-ETOD
078900        IF SUCCESS
079000           IF EMPTOD-EMP-NBR = MSTR3-EMP-NBR
079100              AND WS-EMP-EFF-DATE-TIME > EMPTOD-OD-YYMMDD-HRMN
079200              IF (NOT EMPTOD-VIRTUAL-ENTRY) AND
079300*
079400                 (NOT EMPTOD-IMPOSED-REST-REC)
079500*             EMPTOD-IMPOSED-REST-REC IS UNLIKELY TO BE FOUND WITH
079600*             NEW CAN WRR, ADDING THIS AS A SANITY CHECK
079700                 IF EMPTOD-RESET-BREAK-REC
079800                    MOVE '0000000000'   TO WS-7DAY-START-DTTM
079900                 END-IF
080000                 IF EMPTOD-CONTACT AND
080100                    WS-7DAY-START-DTTM > '0000000000'
080200                    IF ((EMPTOD-OD-YYMMDD-HRMN >=
080300                             WS-UPD-PROJ-UD1-FROM
080400                          AND EMPTOD-OD-YYMMDD-HRMN <=
080500                               WS-UPD-PROJ-UD1-TO)
080600                        OR
080700                        (EMPTOD-OD-YYMMDD-HRMN >=
080800                            WS-UPD-PROJ-UD2-FROM
080900                          AND EMPTOD-OD-YYMMDD-HRMN <=
081000                                  WS-UPD-PROJ-UD2-TO)
081100                        OR
081200                        (EMPTOD-OFFD-YYMMDD-HRMN >=
081300                            WS-UPD-PROJ-UD1-FROM
081400                          AND EMPTOD-OFFD-YYMMDD-HRMN <=
081500                                  WS-UPD-PROJ-UD1-TO)
081600                        OR
081700                        (EMPTOD-OFFD-YYMMDD-HRMN >=
081800                            WS-UPD-PROJ-UD2-FROM
081900                          AND EMPTOD-OFFD-YYMMDD-HRMN <=
082000                                  WS-UPD-PROJ-UD2-TO))
082100                       PERFORM P7125-EXTEND-PROJ-RESET
082200                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
082300                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
082400                    END-IF
082500                 END-IF
082600                 IF (NOT EMPTOD-CONTACT) AND
082700                    (NOT EMPTOD-RESET-BREAK-REC)
082800
082900                    IF WS-7DAY-START-DTTM NOT > '0000000000'
083000                    AND EMPTOD-OD-YYMMDD-HRMN <=
083100                                              WS-WRR-START-DTTM
083200                    AND EMPTOD-OFFD-YYMMDD-HRMN >
083300                                              WS-WRR-START-DTTM
083400                       MOVE WS-WRR-START-DTTM
083500                                   TO WS-7DAY-START-DTTM
083600                       PERFORM P7126-COMPUTE-PROJ-RESET
083700                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
083800                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
083900                    END-IF
084000
084100                    IF WS-7DAY-START-DTTM NOT > '0000000000'
084200                    AND EMPTOD-OD-YYMMDD-HRMN >=
084300                                              WS-WRR-START-DTTM
084400                       MOVE EMPTOD-OD-YYMMDD-HRMN
084500                                        TO WS-7DAY-START-DTTM
084600                       PERFORM P7126-COMPUTE-PROJ-RESET
084700                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
084800                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
084900                    END-IF
085000
085100                    IF WS-7DAY-START-DTTM  > '0000000000'
085200                    AND EMPTOD-OD-YYMMDD-HRMN >
085300                        WS-UPD-PROJ-DATE-TIME
085400                    AND (EMPTOD-HOS-CLAIM OR
085500                         EMPTOD-COMMUTE   OR
085600                         EMPTOD-MANUAL)
085700*                      TRAIN RELATED HOS SHOULD BE SKIPPED :
085800*                      WITH TRAINS, (ORDER - LEAD) SHOULD BE MORE
085900*                      THAN PROJECTED-RESET WHICH IS CHECKED AT
086000*                      THE TIME OF CALL; HERE, WE ONLY LOOK FOR
086100*                      RESETS BEFORE HOS-CLAIMS AND MANUAL/COMMUTE
086200*
086300*                      SET NATURAL-RESET-BEFORE-0DVRUN TO TRUE
086400*
086500                       MOVE EMPTOD-OD-YYMMDD-HRMN
086600                                        TO WS-7DAY-START-DTTM
086700                       PERFORM P7126-COMPUTE-PROJ-RESET
086800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
086900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
087000                    END-IF
087100
087200                    IF WS-7DAY-START-DTTM  > '0000000000'
087300                    AND EMPTOD-OFFD-YYMMDD-HRMN >
087400                        LAST-EMPTOD-OFFD-YYMMDD-HRMN
087500*                      THERE CAN BE OVERLAPPING HOS, PROJ-RESET
087600*                      SHOULD BE COMPUTED W/ GREATEST OFFD READ
087700                       PERFORM P7126-COMPUTE-PROJ-RESET
087800                       MOVE EMPTOD-OFFD-YYMMDD-HRMN
087900                                   TO LAST-EMPTOD-OFFD-YYMMDD-HRMN
088000                    END-IF
088100                 END-IF
088200              END-IF
088300           ELSE
088400              SET READ-DONE          TO TRUE
088500           END-IF
088600        ELSE
088700           SET READ-DONE             TO TRUE
088800        END-IF
088900     END-PERFORM
089000     PERFORM P8020-ENDBR-ETOD
089200     IF (WS-7DAY-START-DTTM NOT > '0000000000')
089300*       NO HOS RECORDS WERE FOUND;
089400        SET PS08-NO-HOS-REC-FOUND    TO TRUE
089500     ELSE
089600*      ?WILL HAVE TO RETHINK THE IVR-NOTIFY-PROJECTED-RESET
089700*      ?EXTENSION PROCESS
089800        IF MSTR3-PROJ-RESET-BRK-CH > WS-UPD-PROJ-DATE-TIME
089900           MOVE MSTR3-PROJ-RESET-BRK-CH TO WS-UPD-PROJ-DATE-TIME
090000        END-IF
090100*
090200        IF WS-EMP-EFF-DATE-TIME     > WS-UPD-PROJ-DATE-TIME
090300*      EMPLOYEE HAS GOT NATURAL RESET HERE
 90500           SET PS08-NATURAL-RESET    TO TRUE
      *      IF EMPLOYEE HAS GOT NATURAL RESET THEN NEXT CALL WILL
      *      DETERMIN HIS 7 DAY START AND END DATE PERIOD
                 MOVE '0000000000'         TO WS-7DAY-START-DTTM
090600        END-IF
090700     END-IF
090800     .
090900*
116500*
116600 P7125-EXTEND-PROJ-RESET.
116700*
116800        MOVE ZEROS                   TO DATE-CONVERSION-PARMS
116900        SET PARM-ADD                 TO TRUE
117000        MOVE WS-UPD-PROJ-DATE        TO PARM-PRI-DATE-GREG
117100        MOVE 01                      TO PARM-SEC-GREG-DAY
117200        EXEC CICS LINK
117300                  PROGRAM(P903-PGM)
117400                  COMMAREA(DATE-CONVERSION-PARMS)
117500                  LENGTH(P903-LGTH)
117600                  RESP(WS-RESPONSE)
117700        END-EXEC
117800        MOVE WS-RESPONSE             TO FILE-STATUS
117900        IF NOT SUCCESS
118000           MOVE 'P7125-1'            TO ERR-PARAGRAPH
118100           MOVE 'P903LINK'           TO ERR-KEY
118200           PERFORM P9999-GOT-PROBLEM
118300        END-IF
118400        MOVE PARM-RES-DATE-GREG      TO WS-UPD-PROJ-DATE
118500        MOVE '0559'                  TO WS-UPD-PROJ-TIME
118600     .
118700*
077200*
107000 P7126-COMPUTE-PROJ-RESET.
107100*
107200     MOVE EMPTOD-OFFD-DATE        TO WS-DAY1-DATE
107300     MOVE EMPTOD-OFFD-TIME        TO WS-DAY1-TIME
107400
107500     MOVE ZEROS                   TO DATE-CONVERSION-PARMS
107600     SET PARM-ADD                 TO TRUE
107700     MOVE WS-DAY1-DATE            TO PARM-PRI-DATE-GREG
107800     MOVE 01                      TO PARM-SEC-GREG-DAY
107900     EXEC CICS LINK
108000               PROGRAM(P903-PGM)
108100               COMMAREA(DATE-CONVERSION-PARMS)
108200               LENGTH(P903-LGTH)
108300               RESP(WS-RESPONSE)
108400     END-EXEC
108500     MOVE WS-RESPONSE             TO FILE-STATUS
108600     IF NOT SUCCESS
108700        MOVE 'P7126-1'            TO ERR-PARAGRAPH
108800        MOVE 'P903LINK'           TO ERR-KEY
108900        PERFORM P9999-GOT-PROBLEM
109000     END-IF
109100     MOVE PARM-RES-DATE-GREG      TO WS-DAY2-DATE
109200
109300     MOVE ZEROS                   TO DATE-CONVERSION-PARMS
109400     SET PARM-ADD                 TO TRUE
109500     MOVE WS-DAY1-DATE            TO PARM-PRI-DATE-GREG
109600     MOVE 02                      TO PARM-SEC-GREG-DAY
109700     EXEC CICS LINK
109800               PROGRAM(P903-PGM)
109900               COMMAREA(DATE-CONVERSION-PARMS)
110000               LENGTH(P903-LGTH)
110100               RESP(WS-RESPONSE)
110200     END-EXEC
110300     MOVE WS-RESPONSE             TO FILE-STATUS
110400     IF NOT SUCCESS
110500        MOVE 'P7126-2'            TO ERR-PARAGRAPH
110600        MOVE 'P903LINK'           TO ERR-KEY
110700        PERFORM P9999-GOT-PROBLEM
110800     END-IF
110900     MOVE PARM-RES-DATE-GREG      TO WS-DAY3-DATE
111000
111100     IF WS-DAY1-TIME >= 2200 AND WS-DAY1-TIME <= 2359
111200        MOVE ZEROS                TO DATE-CONVERSION-PARMS
111300        SET PARM-ADD              TO TRUE
111400        MOVE WS-DAY1-DATE         TO PARM-PRI-DATE-GREG
111500        MOVE WS-DAY1-TIME         TO PARM-PRI-HRMN
015310*CNLD-304 B
111600*       MOVE 3200                 TO PARM-SEC-HRMN
111600        MOVE 3159                 TO PARM-SEC-HRMN
015310*CNLD-304 E
111700        EXEC CICS LINK
111800                  PROGRAM(P903-PGM)
111900                  COMMAREA(DATE-CONVERSION-PARMS)
112000                  LENGTH(P903-LGTH)
112100                  RESP(WS-RESPONSE)
112200        END-EXEC
112300        MOVE WS-RESPONSE          TO FILE-STATUS
112400        IF NOT SUCCESS
112500           MOVE 'P7126-3'         TO ERR-PARAGRAPH
112600           MOVE 'P903LINK'        TO ERR-KEY
112700           PERFORM P9999-GOT-PROBLEM
112800        END-IF
112900        MOVE PARM-RES-DATE-GREG   TO WS-UPD-PROJ-DATE
113000        MOVE PARM-RES-HRMN        TO WS-UPD-PROJ-TIME
113100        MOVE WS-DAY1-DATE-TIME    TO WS-UPD-PROJ-UD1-FROM
113200        MOVE WS-DAY2-DATE         TO WS-UPD-PROJ-UD1-TO(1:6)
113300        MOVE '0800'               TO WS-UPD-PROJ-UD1-TO(7:4)
113400        MOVE WS-DAY2-DATE         TO WS-UPD-PROJ-UD2-FROM(1:6)
113500        MOVE '2200'               TO WS-UPD-PROJ-UD2-FROM(7:4)
113600        MOVE WS-DAY3-DATE         TO WS-UPD-PROJ-UD2-TO(1:6)
113700        MOVE '0559'               TO WS-UPD-PROJ-UD2-TO(7:4)
113800     ELSE
113900        IF WS-DAY1-TIME = 0000
114000           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-DATE
114000*CNLD-259 B CNC0600
114000*          RESET DURATION WILL BE LESS THAN THE REQUIRED MINIMUM
114000*          OF 32 HOURS IN THIS CASE; WILL BE 31H 59M.
114100*          MOVE '0800'            TO WS-UPD-PROJ-TIME
114100           MOVE '0759'            TO WS-UPD-PROJ-TIME
114000*CNLD-259 E CNC0600
114200           MOVE WS-DAY1-DATE      TO WS-UPD-PROJ-UD1-FROM(1:6)
114300                                    WS-UPD-PROJ-UD1-TO(1:6)
114400                                    WS-UPD-PROJ-UD2-FROM(1:6)
114500           MOVE '0000'            TO WS-UPD-PROJ-UD1-FROM(7:4)
114600           MOVE '0800'            TO WS-UPD-PROJ-UD1-TO(7:4)
114700           MOVE '2200'            TO WS-UPD-PROJ-UD2-FROM(7:4)
114800           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-UD2-TO(1:6)
114900           MOVE '0559'            TO WS-UPD-PROJ-UD2-TO(7:4)
115000        ELSE
115100           MOVE WS-DAY3-DATE      TO WS-UPD-PROJ-DATE
115200           MOVE '0559'            TO WS-UPD-PROJ-TIME
115300           MOVE WS-DAY1-DATE      TO WS-UPD-PROJ-UD1-FROM(1:6)
115400           MOVE '2200'            TO WS-UPD-PROJ-UD1-FROM(7:4)
115500           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-UD1-TO(1:6)
115600           MOVE '0800'            TO WS-UPD-PROJ-UD1-TO(7:4)
115700           MOVE WS-DAY2-DATE      TO WS-UPD-PROJ-UD2-FROM(1:6)
115800           MOVE '2200'            TO WS-UPD-PROJ-UD2-FROM(7:4)
115900           MOVE WS-DAY3-DATE      TO WS-UPD-PROJ-UD2-TO(1:6)
116000           MOVE '0559'            TO WS-UPD-PROJ-UD2-TO(7:4)
116100        END-IF
116200     END-IF
116300     .
116500*
070200*
064600 P1132-RECALC-PROJ-RESET-BRK.
064700*
064800     IF MSTR3-7DAY-END-PERIOD > '0000000000'
064900*       COMPUTE 7DAY START FROM MSTR3
065000        PERFORM P7210-CALCULATE-7DAY-START
065100
065200        IF WS-7DAY-START-DTTM > WS-WRR-START-DTTM
065300           CONTINUE
065400        ELSE
065500
065600           IF WS-8DAYS-AGO-DTTM > WS-WRR-START-DTTM
065700              MOVE WS-8DAYS-AGO-DTTM  TO WS-7DAY-START-DTTM
065800           ELSE
065900*             START READING FROM (WS-WRR-START-DTTM - 1 DAY)
066000              PERFORM P7110-WRR-START-MINUS-1DY
066100           END-IF
066200        END-IF
066300     ELSE
066500        IF WS-8DAYS-AGO-DTTM > WS-WRR-START-DTTM
066600           MOVE WS-8DAYS-AGO-DTTM  TO WS-7DAY-START-DTTM
066700        ELSE
066800*          START READING FROM (WS-WRR-START-DTTM - 1 DAY)
066900           PERFORM P7110-WRR-START-MINUS-1DY
067000        END-IF
067100     END-IF
067200
067600     PERFORM P1133-PRV-HOS-AFTER-7DY-START
067800     .
131100*
131200 P7210-CALCULATE-7DAY-START.
131300*
131400     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
131500     SET PARM-SUBTRACT             TO TRUE
131600     MOVE MSTR3-7DAY-END-DATE      TO PARM-PRI-DATE-GREG
131700     MOVE MSTR3-7DAY-END-TIME      TO PARM-PRI-HRMN
131800     MOVE 000006                   TO PARM-SEC-GREG-DAY
131900     MOVE 2359                     TO PARM-SEC-HRMN
132000     EXEC CICS LINK
132100               PROGRAM(P903-PGM)
132200               COMMAREA(DATE-CONVERSION-PARMS)
132300               LENGTH(P903-LGTH)
132400               RESP(WS-RESPONSE)
132500     END-EXEC
132600     MOVE WS-RESPONSE              TO FILE-STATUS
132700     IF NOT SUCCESS
132800        MOVE 'P7210-1'             TO ERR-PARAGRAPH
132900        MOVE 'P903LINK'            TO ERR-KEY
133000        PERFORM P9999-GOT-PROBLEM
133100     END-IF
133200     MOVE PARM-RES-DATE-GREG       TO WS-7DAY-START-DATE
133300     MOVE PARM-RES-HRMN            TO WS-7DAY-START-TIME
133400     .
133500*
128700*
128800 P1140-CALCULATE-7DAY-END.
128900*
129000     MOVE ZEROS                    TO DATE-CONVERSION-PARMS
129100     SET PARM-ADD                  TO TRUE
129200     MOVE WS-7DAY-START-DATE       TO PARM-PRI-DATE-GREG
129300     MOVE WS-7DAY-START-TIME       TO PARM-PRI-HRMN
129400     MOVE 000006                   TO PARM-SEC-GREG-DAY
129500     MOVE 2359                     TO PARM-SEC-HRMN
129600     EXEC CICS LINK
129700               PROGRAM(P903-PGM)
129800               COMMAREA(DATE-CONVERSION-PARMS)
129900               LENGTH(P903-LGTH)
130000               RESP(WS-RESPONSE)
130100     END-EXEC
130200     MOVE WS-RESPONSE              TO FILE-STATUS
130300     IF NOT SUCCESS
130400        MOVE 'P1140-1'             TO ERR-PARAGRAPH
130500        MOVE 'P903LINK'            TO ERR-KEY
130600        PERFORM P9999-GOT-PROBLEM
130700     END-IF
130800     MOVE PARM-RES-DATE-GREG       TO WS-WRR-7DAY-END-DATE
130800     MOVE PARM-RES-DATE-GREG       TO WS-WRR-7DAY-END-DATE
                                            PS08-7DAY-END-DATE-YYMMDD
130900     MOVE PARM-RES-HRMN            TO WS-WRR-7DAY-END-TIME
                                            PS08-7DAY-END-TIME
131000     .
073000*
074900 P7400-COMP-8DAYS-AGO.
075000*
075100* COMPUTE THE DATE 8 DAYS AGO FROM CURRENT ON-DUTY TIME
075200     MOVE ZEROS                 TO DATE-CONVERSION-PARMS
075300     SET PARM-SUBTRACT          TO TRUE
075400     MOVE WS-EMP-EFF-DATE       TO PARM-PRI-DATE-GREG
075500     MOVE WS-EMP-EFF-TIME       TO PARM-PRI-HRMN
075600     MOVE 000008                TO PARM-SEC-GREG-DAY
075700     EXEC CICS LINK
075800               PROGRAM(P903-PGM)
075900               COMMAREA(DATE-CONVERSION-PARMS)
076000               LENGTH(P903-LGTH)
076100               RESP(WS-RESPONSE)
076200     END-EXEC
076300     MOVE WS-RESPONSE           TO FILE-STATUS
076400     IF NOT SUCCESS
076500        MOVE 'P7400-1'          TO ERR-PARAGRAPH
076600        MOVE 'P903LINK'         TO ERR-KEY
076700        PERFORM P9999-GOT-PROBLEM
076800     END-IF
076900     MOVE PARM-RES-DATE-GREG    TO WS-8DAYS-AGO-DATE
077000     MOVE PARM-RES-HRMN         TO WS-8DAYS-AGO-TIME
077100     .
      *CNLD-248-E
104800******************************************************************
104900 P8000-STARTBR-ETOD.
105000******************************************************************
105100     EXEC CICS STARTBR
105200               DATASET(EMPTOD-VIA-EMP)
105300               RIDFLD(EMPTOD-EMPKEY)
105400               GTEQ
105500               RESP(WS-RESPONSE)
105600     END-EXEC
105700     MOVE WS-RESPONSE      TO FILE-STATUS
105800     IF NOT(SUCCESS OR NO-RECORD-FND)
105900       MOVE 'P8000-1'      TO ERR-PARAGRAPH
106000       MOVE EMPTOD-EMPKEY  TO ERR-KEY
106100       PERFORM P9999-GOT-PROBLEM
106200     END-IF.
106300******************************************************************
106400 P8010-READNEXT-ETOD.
106500******************************************************************
106600     EXEC CICS READNEXT
106700               DATASET(EMPTOD-VIA-EMP)
106800               INTO(WS-EMP-TOD)
106900               RIDFLD(EMPTOD-EMPKEY)
107000               LENGTH(EMPTOD-EMP-RLGTH)
107100               KEYLENGTH(EMPTOD-EMP-KLGTH)
107200               RESP(WS-RESPONSE)
107300     END-EXEC
107400     MOVE WS-RESPONSE      TO FILE-STATUS
107500     IF NOT(SUCCESS OR NO-RECORD-FND OR END-OF-FILE)
107600       MOVE 'P8010-1'      TO ERR-PARAGRAPH
107700       MOVE EMPTOD-EMPKEY  TO ERR-KEY
107800       PERFORM P9999-GOT-PROBLEM
107900     END-IF.
108000******************************************************************
108100 P8020-ENDBR-ETOD.
108200******************************************************************
108300     EXEC CICS ENDBR
108400               DATASET(EMPTOD-VIA-EMP)
108500               RESP(WS-RESPONSE)
108600     END-EXEC
108700     MOVE WS-RESPONSE      TO FILE-STATUS
108800     IF NOT SUCCESS
108900       MOVE 'P8020-1'      TO ERR-PARAGRAPH
109000       PERFORM P9999-GOT-PROBLEM
109100     END-IF.
109200******************************************************************
109300 P8030-READ-UPDATE-ETOD.
109400******************************************************************
109500     EXEC CICS READ
109600               UPDATE
109700               DATASET(EMPTOD-VIA-EMP)
109800               INTO(WS-EMP-TOD)
109900               LENGTH(EMPTOD-EMP-RLGTH)
110000               RIDFLD(EMPTOD-EMPKEY)
110100               KEYLENGTH(EMPTOD-EMP-KLGTH)
110200               RESP(WS-RESPONSE)
110300     END-EXEC
110400     MOVE WS-RESPONSE      TO FILE-STATUS
110500     IF NOT SUCCESS
110600       MOVE 'P8030-1'      TO ERR-PARAGRAPH
110700       MOVE EMPTOD-EMPKEY  TO ERR-KEY
110800       PERFORM P9999-GOT-PROBLEM
110900     END-IF.
111000******************************************************************
111100 P8040-REWRITE-ETOD.
111200******************************************************************
111300     EXEC CICS REWRITE
111400               DATASET(EMPTOD-VIA-EMP)
111500               FROM(WS-EMP-TOD)
111600               LENGTH(EMPTOD-EMP-RLGTH)
111700               RESP(WS-RESPONSE)
111800     END-EXEC
111900     MOVE WS-RESPONSE            TO FILE-STATUS
112000     IF DUP-KEY-ERR
112100        SET PS08-DUP-KEY-FND     TO TRUE
112200        PERFORM P9000-RETURN
112300     ELSE
112400        IF NOT SUCCESS
112500           MOVE 'P8040-1'        TO ERR-PARAGRAPH
112600           MOVE EMPTOD-EMPKEY    TO ERR-KEY
112700           PERFORM P9999-GOT-PROBLEM
112800        END-IF
112900     END-IF.
113000******************************************************************
113100 P8050-WRITE-ETOD.
113200******************************************************************
113300     MOVE WS-EMP-TOD             TO WS-SAVE-EMP-TOD
113400     MOVE EMPTOD-KEY-AREA        TO EMPTOD-EMPKEY
113500     EXEC CICS WRITE
113600               DATASET(EMPTOD-VIA-EMP)
113700               FROM(WS-EMP-TOD)
113800               LENGTH(EMPTOD-EMP-RLGTH)
113900               RIDFLD(EMPTOD-EMPKEY)
114000               RESP(WS-RESPONSE)
114100     END-EXEC
114200     MOVE WS-RESPONSE            TO FILE-STATUS
114300     IF DUP-KEY-ERR
114400        PERFORM P8030-READ-UPDATE-ETOD
114500        MOVE WS-SAVE-EMP-TOD     TO WS-EMP-TOD
114600        PERFORM P8040-REWRITE-ETOD
114700     ELSE
114800        IF NOT SUCCESS
114900           MOVE 'P8050-1'        TO ERR-PARAGRAPH
115000           MOVE EMPTOD-EMPKEY    TO ERR-KEY
115100           PERFORM P9999-GOT-PROBLEM
115200        END-IF
115300     END-IF.
115400******************************************************************
115500 P8060-DELETE-ETOD.
115600******************************************************************
115700     EXEC CICS DELETE
115800               DATASET(EMPTOD-VIA-EMP)
115900               RIDFLD(EMPTOD-EMPKEY)
116000               RESP(WS-RESPONSE)
116100     END-EXEC
116200     MOVE WS-RESPONSE         TO FILE-STATUS
116300     IF NOT SUCCESS
116400        MOVE 'P8060-1'      TO ERR-PARAGRAPH
116500        MOVE EMPTOD-EMPKEY  TO ERR-KEY
116600        PERFORM P9999-GOT-PROBLEM
116700     END-IF.
116800******************************************************************
116900 P8070-READ-ETOD.
117000******************************************************************
117100     EXEC CICS READ
117200               DATASET(EMPTOD-VIA-EMP)
117300               INTO(WS-EMP-TOD)
117400               LENGTH(EMPTOD-EMP-RLGTH)
117500               RIDFLD(EMPTOD-EMPKEY)
117600               KEYLENGTH(EMPTOD-EMP-KLGTH)
117700               RESP(WS-RESPONSE)
117800     END-EXEC
117900     MOVE WS-RESPONSE          TO FILE-STATUS
118000     IF NOT SUCCESS
118100        IF NOT NO-RECORD-FND
118200           MOVE 'P8070-1'      TO ERR-PARAGRAPH
118300           MOVE EMPTOD-EMPKEY  TO ERR-KEY
118400           PERFORM P9999-GOT-PROBLEM
118500        END-IF
118600     END-IF.
118700******************************************************************
118800 P8100-READ-CNTL.
118900******************************************************************
119000     MOVE CNTLKEY-AREA                  TO CNTLKEY
119100     EXEC CICS READ
119200               DATASET(CNTL-FILE-VIA-CNTLKEY)
119300               INTO(WS-CNTL-FILE)
119400               LENGTH(CNTLFILE-RLGTH)
119500               RIDFLD(CNTLKEY)
119600               KEYLENGTH(CNTLFILE-KLGTH)
119700               RESP(WS-RESPONSE)
119800     END-EXEC
119900     MOVE WS-RESPONSE                   TO FILE-STATUS
120000     IF NOT SUCCESS
120100        IF NOT NO-RECORD-FND
120200           MOVE 'P8100-1'               TO ERR-PARAGRAPH
120300           MOVE CNTLKEY                 TO ERR-KEY
120400           PERFORM P9999-GOT-PROBLEM
120500        END-IF
120600     END-IF.
120700******************************************************************
120800 P8500-READ-MASTER.
120900******************************************************************
121000     EXEC CICS READ
121100               DATASET(MSTR-VIA-EMP-NBR)
121200               INTO(WS-MSTR)
121300               LENGTH(MSTRENBR-RLGTH)
121400               RIDFLD(MSTRNBRK)
121500               KEYLENGTH(MSTRENBR-KLGTH)
121600               RESP(WS-RESPONSE)
121700     END-EXEC
121800     MOVE WS-RESPONSE                   TO FILE-STATUS
121900     IF NOT SUCCESS
122000        IF NO-RECORD-FND
122100           SET PS08-NO-SUCH-EMP-NBR     TO TRUE
122200           PERFORM P9000-RETURN
122300        ELSE
122400           MOVE 'P8500-1'                  TO ERR-PARAGRAPH
122500           MOVE MSTRNBRK                   TO ERR-KEY
122600           PERFORM P9999-GOT-PROBLEM
122700        END-IF
122800     END-IF.
122900******************************************************************
123000 P8510-READ-UPD-MASTER.
123100******************************************************************
123200     EXEC CICS READ
123300               UPDATE
123400               DATASET(MSTR-VIA-EMP-NBR)
123500               INTO(WS-MSTR)
123600               LENGTH(MSTRENBR-RLGTH)
123700               RIDFLD(MSTRNBRK)
123800               KEYLENGTH(MSTRENBR-KLGTH)
123900               RESP(WS-RESPONSE)
124000     END-EXEC
124100     MOVE WS-RESPONSE                   TO FILE-STATUS.
124200*
124300******************************************************************
124400 P8520-REWRITE-MASTER.
124500******************************************************************
124600     EXEC CICS REWRITE
124700               DATASET(MSTR-VIA-EMP-NBR)
124800               FROM(WS-MSTR)
124900               LENGTH(MSTRENBR-RLGTH)
125000               RESP(WS-RESPONSE)
125100     END-EXEC
125200     MOVE WS-RESPONSE                   TO FILE-STATUS.
125300*
      *CNLD-248-B
028000 P8600-READ-MSTR3-RECORD.
434300     EXEC CICS READ
434400               DATASET(MSTR3-VIA-EMP-NBR)
434500               INTO(WS-MSTR3)
434600               LENGTH(MSTR3ENBR-RLGTH)
434700               RIDFLD(MSTR3NBRK)
434800               KEYLENGTH(MSTR3ENBR-KLGTH)
434900               RESP(WS-RESPONSE)
435000     END-EXEC
435100     MOVE WS-RESPONSE TO FILE-STATUS.
435200*
146400*=================================================================
146500*P8610-EMPTOD-STARTBR.
146600*=================================================================
146700*    EXEC CICS STARTBR
146800*              DATASET(EMPTOD-VIA-EMP)
146900*              RIDFLD(EMPTOD-EMPKEY)
147000*              GTEQ
147100*              RESP(WS-RESPONSE)
147200*    END-EXEC
147300*    MOVE WS-RESPONSE                   TO FILE-STATUS
147400*    IF NOT SUCCESS
147500*       MOVE 'P8060-1'                  TO ERR-PARAGRAPH
147600*       MOVE EMPTOD-EMPKEY              TO ERR-KEY
147700*       PERFORM P9999-GOT-PROBLEM
147800*    END-IF
147900*    .
148000*=================================================================
148100*P8620-EMPTOD-READNEXT.
148200*=================================================================
148300*    EXEC CICS READNEXT
148400*              DATASET(EMPTOD-VIA-EMP)
148500*              INTO(WS-EMP-TOD)
148600*              LENGTH(EMPTOD-EMP-RLGTH)
148700*              RIDFLD(EMPTOD-EMPKEY)
148800*              KEYLENGTH(EMPTOD-EMP-KLGTH)
148900*              RESP(WS-RESPONSE)
149000*    END-EXEC
149100*    MOVE WS-RESPONSE                   TO FILE-STATUS
149200*    IF NOT (SUCCESS OR NO-RECORD-FND OR END-OF-FILE)
149300*       MOVE 'P8070-1'                  TO ERR-PARAGRAPH
149400*       MOVE EMPTOD-EMPKEY              TO ERR-KEY
149500*       PERFORM P9999-GOT-PROBLEM
149600*    END-IF
149700*    .
149800*=================================================================
149900*P8630-EMPTOD-ENDBR.
150000*=================================================================
150100*    EXEC CICS ENDBR
150200*              DATASET(EMPTOD-VIA-EMP)
150300*              RESP(WS-RESPONSE)
150400*    END-EXEC
150500*    .
      *CNLD-248-E
106900*
125400******************************************************************
125500     EXEC SQL INCLUDE DATEEDIT END-EXEC.
125600     EXEC SQL INCLUDE TIMEEDIT END-EXEC.
125700     EXEC SQL INCLUDE BIFEDIT  END-EXEC.
125800     EXEC SQL INCLUDE TIMEZONE END-EXEC.
125900     EXEC SQL INCLUDE TZERROR  END-EXEC.
126000*
126100******************************************************************
126200 P9000-RETURN.
126300******************************************************************
126400     MOVE PS08-COMMAREA-PARMS           TO DFHCOMMAREA
126500     EXEC CICS RETURN END-EXEC.
126600*
126700******************************************************************
126800 P9800-GET-TIME-OFFSET.
126900******************************************************************
127000     MOVE SPACES                        TO CNTLKEY-AREA
127100     SET DATE-TIME-OFFSET-REC           TO TRUE
127200     EXEC CICS ASSIGN
127300               USERID(CNTL-DT-USERID)
127400     END-EXEC
127500     MOVE CNTLKEY-AREA                  TO CNTLKEY
127600     EXEC CICS READ
127700               DATASET(CNTL-FILE-VIA-CNTLKEY)
127800               INTO(WS-CNTL-FILE)
127900               LENGTH(CNTLFILE-RLGTH)
128000               RIDFLD(CNTLKEY)
128100               KEYLENGTH(CNTLFILE-KLGTH)
128200               RESP(WS-RESPONSE)
128300     END-EXEC
128400     MOVE WS-RESPONSE                   TO FILE-STATUS
128500     IF NOT SUCCESS
128600        IF (NO-RECORD-FND OR END-OF-FILE)
128700           MOVE '********'              TO CNTL-DT-USERID
128800           MOVE CNTLKEY-AREA            TO CNTLKEY
128900           EXEC CICS READ
129000                     DATASET(CNTL-FILE-VIA-CNTLKEY)
129100                     INTO(WS-CNTL-FILE)
129200                     LENGTH(CNTLFILE-RLGTH)
129300                     RIDFLD(CNTLKEY)
129400                     KEYLENGTH(CNTLFILE-KLGTH)
129500                     RESP(WS-RESPONSE)
129600           END-EXEC
129700           MOVE WS-RESPONSE             TO FILE-STATUS
129800        ELSE
129900           MOVE 'P9800-1'               TO ERR-PARAGRAPH
130000           MOVE CNTLKEY                 TO ERR-KEY
130100           PERFORM P9999-GOT-PROBLEM
130200        END-IF
130300     END-IF
130400     IF SUCCESS
130500        MOVE CNTL-DT-ADD-SUBTRACT       TO WS-DT-OS-FUN
130600        MOVE CNTL-DT-DAYS-OFFSET        TO WS-DT-OS-DAYS
130700        MOVE CNTL-DT-HRMN-OFFSET        TO WS-DT-OS-HRMN
130800     ELSE
130900        MOVE SPACES                     TO WS-DATE-TIME-OFFSET
131000        IF NOT (NO-RECORD-FND OR END-OF-FILE)
131100           MOVE 'P9800-2'               TO ERR-PARAGRAPH
131200           MOVE CNTLKEY                 TO ERR-KEY
131300           PERFORM P9999-GOT-PROBLEM
131400        END-IF
131500     END-IF.
131600*
131700******************************************************************
131800 P9810-PROCESS-OFFSET.
131900******************************************************************
132000     MOVE WS-DT-OS-FUN                  TO PARM-CONV-TYPE
132100     MOVE WS-DT-OS-DAYS                 TO PARM-SEC-JULIAN-DAY
132200     MOVE WS-DT-OS-HRMN                 TO PARM-SEC-HRMN
132300     MOVE 'P9810-1'                     TO ERR-PARAGRAPH
132400     PERFORM P9850-DATE-CONVERSION.
132500*
132600******************************************************************
132700 P9850-DATE-CONVERSION.
132800******************************************************************
132900     EXEC CICS LINK
133000               PROGRAM(P903-PGM)
133100               COMMAREA(DATE-CONVERSION-PARMS)
133200               LENGTH(P903-LGTH)
133300               RESP(WS-RESPONSE)
133400     END-EXEC
133500     MOVE WS-RESPONSE                   TO FILE-STATUS
133600     IF NOT SUCCESS
133700        MOVE 'P903'                     TO ERR-KEY
133800        PERFORM P9999-GOT-PROBLEM
133900     END-IF
134000     MOVE SPACES                        TO ERR-PARAGRAPH.
134100*
134200******************************************************************
134300******************************************************************
134400     EXEC SQL INCLUDE TTACUR01 END-EXEC.
134500     EXEC SQL INCLUDE TTADEL   END-EXEC.
134600     EXEC SQL INCLUDE TTASHARE END-EXEC.
134700
134800     EXEC SQL INCLUDE GOTPROB  END-EXEC.
